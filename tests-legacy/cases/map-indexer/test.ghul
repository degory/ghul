
namespace Collections is
use System;  // String is in System
use Generic; // Map is in Generic
    
    class MyMapWithIndexer  is
        map: Generic.Map[String, String];

        init() is
            super.init();
            map = new Generic.Map[String, String](223);
        si

        // indexer for MyMapWithIndexer
        [s: String]: String public
            // indexer retrieve method
            is
                // attempt to retrieve value for key s
                // by convention we store keys upper case
                // so convert first
                
                var t = map[s.UpperCase];
        
                // test for not found
                if t? then
                    return t;  // Found OK? just return it

                else // take some action in case of not found
                    return "Key not found";
                fi    
            si, // NOTE the comma

            // insert method. Here we are converting to upper case 
            // before insertion. Also concat. new value with old
            = t is
                var current: String;
                if (map[s.UpperCase])? then
                    // key already in map
                    map[s.UpperCase] = t + " previously " + map[s.UpperCase];
                else    
                    map[s.UpperCase] = t;
                fi
            si

    si

    class Main is
        aMap: MyMapWithIndexer;

        init() is
            

            aMap = new MyMapWithIndexer();

            // insert some values with upper case keys   
            aMap["SMITH"]   = "020 555 1234";
            aMap["PATEL"] = "020 555 1235";

            // this key is lower case
            aMap["brown"]  = "020 555 1236";

            // attempt retrieve value that isn't present
            System.Console.write_line("FORSYTH " + aMap["FORSYTH"]);

            // retrieve with upper case key
            System.Console.write_line("SMITH " + aMap["SMITH"]);

            // using mixed case to retieve but should still work
            System.Console.write_line("PATEL " + aMap["Patel"]);
            
            // retrieve with upper case key
            System.Console.write_line("BROWN " + aMap["BROWN"]);   

            // attempt duplicate insertion
            aMap["brown"]  = "020 888 1236";
            System.Console.write_line("BROWN " + aMap["BROWN"]);   

  
            

        si      // init       
    si // class
  
si