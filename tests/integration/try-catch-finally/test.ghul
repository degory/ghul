namespace Test is
    @IF.not.v3()
    use Std = System.Console;
    @IF.not.v3()
    use object = System.Object;
    @IF.not.v3()
    use string = System.String;

    @IF.not.v3()
    use System.BoundsException;

    @IF.v3()
    use Std = IO.Std;

    @IF.v3()
    use BoundsException = System.IndexOutOfRangeException;

    use System.Exception;

    class Main is
        entry() static is
            @IL.entrypoint()

            test("expect finally only", null);

            test("expect my exception catch and finally", new MyException("my exception"));

            test("expect other exception catch and finally", new Exception("system exception"));

            test("expect other exception catch and finally", new BoundsException("bounds exception"));
        si

        test(message: string, ex: Exception) static is
            Std.out.write_line("before: " + message);

            try
                Std.out.write_line("start: " + message);

                if ex? then
                    Std.out.write_line("will throw: " + ex.get_type().name);

                    throw ex;

                    Std.out.write_line("should not be reachable");
                else
                    Std.out.write_line("will not throw");                    
                fi
                
            catch exa: MyException
                Std.out.write_line("caught my exception: " + exa.message);

            catch exb: Exception
                Std.out.write_line("caught other exception: " + exb.message);

            finally
                Std.out.write_line("finally: " + message);                
            yrt

            Std.out.write_line("after: " + message);
        si        
    si

    class MyException: Exception is
        init(message: string) is
            super.init(message);
        si
    si    
si