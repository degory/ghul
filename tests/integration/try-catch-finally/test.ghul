namespace Test is
    use System.String;
    use System.Console;
    use System.Exception;

    class Main is
        entry() static is
            @IL.entrypoint()

            test("expect finally only", null);

            test("expect my exception catch and finally", new MyException("my exception"));

            test("expect other exception catch and finally", new Exception("system exception"));

            test("expect other exception catch and finally", new System.BoundsException("bounds exception"));
        si

        test(message: String, ex: Exception) static is
            Console.write_line("before: " + message);

            try
                Console.write_line("start: " + message);

                if ex? then
                    Console.write_line("will throw: " + ex.get_type().name);

                    throw ex;

                    Console.write_line("should not be reachable");
                else
                    Console.write_line("will not throw");                    
                fi
                
            catch exa: MyException
                Console.write_line("caught my exception: " + exa.message);

            catch exb: Exception
                Console.write_line("caught other exception: " + exb.message);

            finally
                Console.write_line("finally: " + message);                
            yrt

            Console.write_line("after: " + message);
        si        
    si

    class MyException: Exception is
        init(message: String) is
            super.init(message);
        si
    si    
si