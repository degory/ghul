namespace Test is
    use System.String;
    use System.Console;

    class Main is
        entry() static is
            @IL.entrypoint()
            
            let rats = [new RAT(), new RAT(), new RAT()];

            let dog = new DOG("Spot");
            let cat = new CAT("Mr Tibbles");
            let chimera = new CHIMERA("Nigel");

            for r in rats do
                vocalise(r);
                digest(r);
            od
            
            vocalise(dog);
            vocalise(dog);
            digest(dog);

            vocalise(cat);
            vocalise(cat);
            digest(cat);

            vocalise(chimera);
            vocalise(chimera);
            rend(chimera);
            digest(chimera);
        si

        vocalise(v: Vocaliser) static is
            Console.write("" + v.name + " says ");
            v.vocalise();
        si

        rend(r: Render) static is
            Console.write("" + r.name + " roars ");
            r.rend();
        si

        digest(d: Digester) static is
            Console.write("" + d.name + " goes ");
            d.digest();
        si        
    si

    trait Named is
        name: String;
    si    

    trait Digester: Named is
        digest();
    si
    
    trait Vocaliser: Named is
        vocalise();
    si

    trait Render: Named is
        rend();
    si

    class WithName: Named is
        name: String;
        
        init(name: String) is
            self.name = name;
        si
    si

    class RAT: Vocaliser, Digester is
        name: String => "Shrug";

        init() is si

        vocalise() is
            Console.write_line("meep!");
        si

        digest() is
            Console.write_line("frrrp");
        si        
    si
        
    class DOG: WithName, Vocaliser, Digester is
        init(name: String) is
            super.init(name);
        si

        vocalise() is
            Console.write_line("bark!");
        si

        digest() is
            Console.write_line("burp!");
        si
        
    si

    class CAT: WithName, Digester, Named, Vocaliser is
        init(name: String) is
            super.init(name);
        si

        vocalise() is
            Console.write_line("meow!");
        si

        digest() is
            Console.write_line("purrrrrrp!");
        si
    si

    class CHIMERA: WithName, Render, Vocaliser, Digester is
        name: String;

        init(name: String) is
            self.name = name;
        si

        rend() is
            Console.write_line("grrrrargh!");
        si
        
        vocalise() is
            Console.write_line("baeow! meark!");                                
        si
        
        digest() is
            Console.write_line("burarkmeowp!");
        si        
    si
si