namespace Test is
    use System.Console;
    use System.Exception;

    class Main is
        entry() static is
            @IL.entrypoint()

            let v1 = finally_should_run_after_return_value(123);
            System.Console.write_line("result 1 is: " + v1);

            let v2 = finally_should_run_after_return_default_value(123);
            System.Console.write_line("result 2 is: " + v2);

            let v3 = return_from_finally_should_override_return_from_try_body(123);
            System.Console.write_line("result 3 is: " + v3);

            finally_should_run_after_return_void(123);

            let v5 = return_value_should_pass_through_nested_trys();
            System.Console.write_line("result 5 is: " + v5);

            let v6 = return_from_catch(333);
            System.Console.write_line("result 6 is: " + v6);

            let v7 = return_from_finally_should_override_return_from_catch(333);
            System.Console.write_line("result 7 is: " + v7);
        si

        finally_should_run_after_return_value(value: int) -> int static is
            try
                System.Console.write_line("will return: " + value * 2);
                return value * 2;
            finally
                System.Console.write_line("finally: " + value);
            yrt
        si
        
        finally_should_run_after_return_default_value(value: int) -> int static is
            try
                System.Console.write_line("will return: " + value * 2);
                return;
            finally
                System.Console.write_line("finally: " + value);
            yrt
        si

        return_from_finally_should_override_return_from_try_body(value: int) -> int static is
            try
                System.Console.write_line("will return: " + value * 2);
                return value * 2;
            finally
                System.Console.write_line("finally: " + value);
                return value * 3;
            yrt
        si        

        finally_should_run_after_return_void(value: int) static is
            try
                System.Console.write_line("will return: " + value * 2);
                return;
            finally
                System.Console.write_line("finally: " + value);
            yrt
        si

        return_value_should_pass_through_nested_trys() -> int static is
            try
                try
                    return 123;
                catch ex: Exception
                finally
                    System.Console.write_line("finally 1");
                yrt
                
            catch ex: Exception

            finally
                System.Console.write_line("finally 2");
            yrt                
        si
        
        return_from_catch(value: int) -> int static is
            try
                throw new Exception("oops");
            catch ex: Exception
                return value * 2;
            yrt            
        si

        return_from_finally_should_override_return_from_catch(value: int) -> int static is
            try
                throw new Exception("oops");
            catch ex: Exception
                return value * 2;
            finally
                return value * 3;
            yrt            
        si
    si
si