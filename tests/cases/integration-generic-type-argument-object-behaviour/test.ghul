namespace Test is
    use System.Object;
    use System.String;
    use System.Console;

    class Main is
        entry() static is
            @IL.entrypoint()

            let gi = new GENERIC[int](12345);

            gi.check_all();

            let gs = new GENERIC[String]("hello world");

            gs.check_all();
        si
    si

    class GENERIC[T] is
        value: T;

        init(value: T) is
            self.value = value;
        si

        check_all() is
            check_call_object_method();
            check_assign_to_variable_of_type_object();
            check_pass_to_function_accepting_argument_of_type_object();
            check_compare_against_object();
            check_compare_against_null();
        si        

        check_call_object_method() is
            let s: String = value.to_string();
            Console.write_line("call Object.to_string(): " + s); 
        si
        
        check_assign_to_variable_of_type_object() is
            let o: Object;

            o = value;

            Console.write_line("have assigned to object typed variable: " + o);
        si
        
        check_pass_to_function_accepting_argument_of_type_object() is
            check_object(value);
        si

        check_compare_against_object() is
            let o: Object = value;

            if o == value then
                Console.write_line("reference equal: " + o + " vs " + value);
            else
                Console.write_line("reference not equal: " + o + " vs " + value);
            fi
            
            if o != value then
                Console.write_line("reference not equal: " + o + " vs " + value);
            else
                Console.write_line("reference equal: " + o + " vs " + value);
            fi            
        si

        check_compare_against_null() is
            if value == null then
                Console.write_line("not expected: value equal to null: " + value + " vs " + value);
            else
                Console.write_line("have compared with == to null");
            fi
            
            if value != null then
                Console.write_line("have compared with != to null");
            else
                Console.write_line("not expected: value equal to null: " + value + " vs " + value);
            fi            
        si
                          
        check_object(o: Object) is            
            Console.write_line("have passed as object typed argument: " + o);
        si
    si
    
si