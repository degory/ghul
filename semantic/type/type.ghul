namespace Semantic.Type is
    use System;
    use Generic;

    class BASE: Object, Typed is
        type: BASE => self;

        init() is
        si

        =~(other: BASE) -> bool is
            return false;
        si

        specialize(specializer: GENERIC_SPECIALIZER) -> BASE
            => self;        

        specialize(specializer: GENERIC_SPECIALIZER, type_map: Dict[String,BASE]) -> BASE
            => self;        
    si

    class NAMED: BASE is
        name: String;
        arguments_string: String public;

        scope: Scope => symbol;

        symbol: Symbol.Scoped;

        init(name: String, symbol: Symbol.Scoped) is
            init(name, symbol, "");
        si

        init(name: String, symbol: Symbol.Scoped, arguments_string: String) is
            super.init();

            self.name = name;
            self.symbol = symbol;
            self.arguments_string = arguments_string;
        si

        =~(other: BASE) -> bool is
            if isa NAMED(other) then
                let other_symbol = cast NAMED(other);
                return symbol == other_symbol.symbol;
            fi
        si

        specialize(specializer: GENERIC_SPECIALIZER) -> NAMED
            => self;    

        specialize(specializer: GENERIC_SPECIALIZER, type_map: Dict[String,Type.BASE]) -> Type.BASE is
            let result = type_map[name];

            if result? then
                return result;
            fi

            return self;
        si

        hash() -> int => symbol.hash();

        toString() -> String => name + arguments_string;
    si

    class GENERIC: NAMED is
        _hash: int;

        arguments: List[BASE];

        init(
            name: String,
            symbol: Symbol.ScopedWithInheritance,
            arguments: List[BASE]
        ) is
            super.init(name, symbol);

            self.arguments = arguments;
        si

        specialize(specializer: GENERIC_SPECIALIZER) -> NAMED is
            return specialize(specializer, new Map[String,BASE]());
        si

        specialize(specializer: GENERIC_SPECIALIZER, type_map: Dict[String,BASE]) -> NAMED is
            IO.Std.err.println("attempt to specialize type: " + self);

            var result = specializer[self];

            if result? then
                IO.Std.err.println("existing specialization for type: " + self + " => " + result);
                return result;
            fi

            IO.Std.err.println("no existing specialization in " + specializer);

            assert(isa Symbol.ScopedWithInheritance(symbol), "can't specialize type that is neither class not trait:" + symbol);

            let specialized_symbol = cast Symbol.ScopedWithInheritance(symbol).copy_for_specialize();
            let specialized_type = new NAMED(name, specialized_symbol, "[" + arguments + "]");

            IO.Std.err.println("before start specializer is " + specializer);

            IO.Std.err.println("start to specialize symbol for type: " + specialized_symbol);

            let specialized_arguments = new Vector[BASE]();

            for a in arguments do
                specialized_arguments.add(a.specialize(specializer, type_map));
            od

            let self_with_specialized_arguments = new GENERIC(name, cast Symbol.ScopedWithInheritance(symbol), specialized_arguments);

            result = specializer[self_with_specialized_arguments];

            if result? then
                return result;
            fi

            specializer[self_with_specialized_arguments] = specialized_type;

            specialized_type.arguments_string = "[" + specialized_arguments + "]";

            IO.Std.err.println("specialize type with arguments: " + arguments);

            specialized_symbol.specialize_self(specializer, specialized_arguments);

            IO.Std.err.println("specialized generic type result is " + specialized_type);

            return specialized_type;
        si

        =~(other: GENERIC) -> bool is
            IO.Std.err.println("Compare " + self + " to " + other + "...");

            if symbol != other.symbol then
                IO.Std.err.println("symbols differ");
                return false;
            fi

            if arguments.Length != other.arguments.Length then
                IO.Std.err.println("argument list lengths differ");
                return false;
            fi

            for i in 0..arguments.Length do
                if arguments[i] !~ other.arguments[i] then
                    IO.Std.err.println("argument " + i + " differs");
                
                    return false;
                fi
            od

            IO.Std.err.println("generic types " + self + " and " + other + " are the same");
            return true;
        si

        hash() -> int is
            if _hash != 0 then
                return _hash;
            fi

            var result = super.hash();

            for a in arguments do
                result = result + a.hash();
            od

            if result == 0 then
                result = 1;
            fi

            IO.Std.err.println("hash " + self + " is " + result);

            _hash = result;

            return result;
        si        

        toString() -> String => name + "[" + arguments + "] {" + Object.dump(symbol) + "}";
    si    

    class FUNCTION_GROUP: BASE is
        name: String;

        function_group: Symbol.FUNCTION_GROUP;

        init(name: String, function_group: Symbol.FUNCTION_GROUP) is
            super.init();

            self.name = name;
            self.function_group = function_group;
        si

        =~(other: BASE) -> bool is
            if isa FUNCTION_GROUP(other) then
                let other_function_group = cast FUNCTION_GROUP(other);
                return function_group == other_function_group.function_group;
            fi
        si

        specialize(specializer: GENERIC_SPECIALIZER, arguments: Dict[String,Type.BASE]) -> FUNCTION_GROUP is
            return new FUNCTION_GROUP(
                name,
                function_group.specialize(specializer, arguments)
            );
        si

        toString() -> String => name + "(...)";
    si    
si