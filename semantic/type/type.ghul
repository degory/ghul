namespace Semantic.Type is
    use System;
    use Generic;

    class BASE: Object, Typed is
        type: BASE => self;

        init() is
        si

        =~(other: BASE) -> bool is
            return false;
        si

        specialize(specializer: GENERIC_SPECIALIZER) -> BASE
            => self;        

        specialize(specializer: GENERIC_SPECIALIZER, type_map: Dict[String,BASE]) -> BASE
            => self;        
    si

    class NAMED: BASE is
        name: String;

        scope: Scope => symbol;

        symbol: Symbol.Scoped;

        init(name: String, symbol: Symbol.Scoped) is
            super.init();

            self.name = name;
            self.symbol = symbol;
        si

        =~(other: BASE) -> bool is
            if isa NAMED(other) then
                let other_symbol = cast NAMED(other);
                return symbol == other_symbol.symbol;
            fi
        si

        specialize(specializer: GENERIC_SPECIALIZER) -> NAMED
            => self;    

        specialize(specializer: GENERIC_SPECIALIZER, type_map: Dict[String,Type.BASE]) -> Type.BASE is
            let result = type_map[name];

            if result? then
                return result;
            fi

            return self;
        si

        toString() -> String => name;
    si

    class GENERIC: NAMED is
        arguments: List[BASE];

        init(
            name: String,
            symbol: Symbol.ScopedWithInheritance,
            arguments: List[BASE]
        ) is
            super.init(name, symbol);

            self.arguments = arguments;
        si

        specialize(specializer: GENERIC_SPECIALIZER, type_map: Dict[String,BASE]) -> NAMED is
            return specialize(specializer);
        si

        specialize(specializer: GENERIC_SPECIALIZER) -> NAMED is
            let result = specializer[self];

            if result? then
                return result;
            fi

            assert(isa Symbol.ScopedWithInheritance(symbol), "can't specialize type that is neither class not trait:" + symbol);

            let specialized_symbol = cast Symbol.ScopedWithInheritance(symbol).copy_for_specialize();
            let specialized_type = new NAMED(name, specialized_symbol);

            specializer[self] = specialized_type;

            let specialized_arguments = new Vector[BASE]();

            for a in arguments do
                specialized_arguments.add(a.specialize(specializer));
            od

            specialized_symbol.specialize(specializer, specialized_arguments);
        si

        =~(other: GENERIC) -> bool is
            if symbol != other.symbol then
                return false;
            fi

            if arguments.Length != other.arguments.Length then
                return false;
            fi

            for i in 0..arguments.Length do
                if arguments[i] !~ other.arguments[i] then
                    return false;
                fi
            od

            return true;
        si

        hash() -> int is
            var result = super.hash();

            for a in arguments do
                result = result ^ a.hash();
            od
        si        

        toString() -> String => name;
    si    

    class FUNCTION_GROUP: BASE is
        name: String;

        function_group: Symbol.FUNCTION_GROUP;

        init(name: String, function_group: Symbol.FUNCTION_GROUP) is
            super.init();

            self.name = name;
            self.function_group = function_group;
        si

        =~(other: BASE) -> bool is
            if isa FUNCTION_GROUP(other) then
                let other_function_group = cast FUNCTION_GROUP(other);
                return function_group == other_function_group.function_group;
            fi
        si

        specialize(specializer: GENERIC_SPECIALIZER, arguments: Dict[String,Type.BASE]) -> FUNCTION_GROUP is
            return new FUNCTION_GROUP(
                name,
                function_group.specialize(specializer, arguments)
            );
        si

        toString() -> String => name + "(...)";
    si    
si