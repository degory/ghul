namespace Semantic is
    use System;

    use Logging;

    class SYMBOL_LOADER is
        _null_find_symbol: (String) -> Symbol.BASE static;
    
        _logger: LOGGER;
        _symbol_table: SYMBOL_TABLE;

        _function_caller: FUNCTION_CALLER;

        _int_type: Type.BASE;

        find_symbol: (String) -> Symbol.BASE public;
    
        init(
            logger: LOGGER,
            symbol_table: SYMBOL_TABLE,
            function_caller: FUNCTION_CALLER,
            int_type: Type.BASE
        ) is
            super.init();

            // calls to null anon functions don't always produce a sane stack trace
            if _null_find_symbol == null then
                _null_find_symbol = 
                    (name: String) -> Symbol.BASE is
                        throw new NotImplementedException("find_symbol is not set");
                    si;
            fi

            _logger = logger;
            _symbol_table = symbol_table;            
            _function_caller = function_caller;
            _int_type = int_type;

            find_symbol = _null_find_symbol;
        si


        // FIXME: these should do more specific stuff, and some
        // are dummy values loaded only for their symbol/type information

        load_integer_literal(value: String) -> Graph.Value.BASE =>
            new Graph.Value.Literal.INTEGER(value, _int_type);

        load_namespace(symbol: Symbol.NAMESPACE) -> Graph.Value.BASE =>
            new Graph.Value.Load.SYMBOL(null, symbol);

        load_class(symbol: Symbol.CLASS) -> Graph.Value.BASE =>
            new Graph.Value.Load.SYMBOL(null, symbol);

        load_trait(symbol: Symbol.TRAIT) -> Graph.Value.BASE =>
            new Graph.Value.Load.SYMBOL(null, symbol);

        load_enum(symbol: Symbol.ENUM) -> Graph.Value.BASE =>
            new Graph.Value.Load.SYMBOL(null, symbol);

        load_enum_member(symbol: Symbol.ENUM_MEMBER) -> Graph.Value.BASE =>
            new Graph.Value.Load.SYMBOL(null, symbol);

        load_function_group(from: Graph.Value.BASE, symbol: Symbol.BASE) -> Graph.Value.BASE =>
            new Graph.Value.Load.SYMBOL(from, symbol);

        load_global_function(symbol: Symbol.BASE) -> Graph.Value.BASE =>
            new Graph.Value.Load.SYMBOL(null, symbol);

        load_instance_function(from: Graph.Value.BASE, symbol: Symbol.BASE) -> Graph.Value.BASE is
            if from == null then
                from = new Graph.Value.Load.SELF(_symbol_table.current_instance_context);
            fi

            return new Graph.Value.Load.SYMBOL(from, symbol);
        si
        
        load_local_variable(symbol: Symbol.BASE) -> Graph.Value.BASE =>
            new Graph.Value.Load.SYMBOL(new Graph.Value.Load.LOCAL(), symbol);

        store_local_variable(symbol: Symbol.BASE, value: Graph.Value.BASE) -> Graph.Value.BASE =>
            new Graph.Value.Store.SYMBOL(new Graph.Value.Load.LOCAL(), symbol, value);

        load_global_variable(symbol: Symbol.BASE) -> Graph.Value.BASE =>
            new Graph.Value.Load.SYMBOL(null, symbol);

        store_global_variable(symbol: Symbol.BASE, value: Graph.Value.BASE) -> Graph.Value.BASE =>
            new Graph.Value.Store.SYMBOL(null, symbol, value);
            
        load_instance_variable(from: Graph.Value.BASE, symbol: Symbol.BASE) -> Graph.Value.BASE is
            if from == null then
                from = new Graph.Value.Load.SELF(_symbol_table.current_instance_context);
            fi

            return new Graph.Value.Load.SYMBOL(from, symbol);
        si

        store_instance_variable(from: Graph.Value.BASE, symbol: Symbol.BASE, value: Graph.Value.BASE) -> Graph.Value.BASE is
            if from == null then
                from = new Graph.Value.Load.SELF(_symbol_table.current_instance_context);
            fi
        
            return new Graph.Value.Store.SYMBOL(from, symbol, value);
        si

        load_instance_property(from: Graph.Value.BASE, symbol: Symbol.Property) -> Graph.Value.BASE is
            if from == null then
                from = new Graph.Value.Load.SELF(_symbol_table.current_instance_context);
            fi

            let function_group = cast Symbol.FUNCTION_GROUP(find_symbol("__read_" + symbol.name));
            var function: Symbol.Function;

            for f in function_group.functions do
                if f.arguments.Length == 0 then
                    function = f;
                fi
            od

            find_symbol = _null_find_symbol;

            return function.call(from, new Graph.Value.BASE[](0), _function_caller);
        si

        store_instance_property(from: Graph.Value.BASE, symbol: Symbol.BASE, value: Graph.Value.BASE) -> Graph.Value.BASE is
            if from == null then
                from = new Graph.Value.Load.SELF(_symbol_table.current_instance_context);
            fi
        
            let function_group = cast Symbol.FUNCTION_GROUP(find_symbol("__assign_" + symbol.name));
            var function: Symbol.Function;

            for f in function_group.functions do
                if f.arguments.Length == 1 then
                    function = f;
                fi
            od

            find_symbol = _null_find_symbol;

            return function.call(from, [value], _function_caller);
        si

        load_global_property(symbol: Symbol.BASE) -> Graph.Value.BASE =>
            new Graph.Value.Load.SYMBOL(null, symbol);

        store_global_property(symbol: Symbol.BASE, value: Graph.Value.BASE) -> Graph.Value.BASE =>
            new Graph.Value.Store.SYMBOL(null, symbol, value);        
    si
si