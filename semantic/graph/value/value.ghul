namespace Semantic.Graph.Value is
    use System;

    use Generic;

    class BASE: Object, Semantic.Type.Typed is
        has_type: bool => false;

        type: Type.BASE is
            throw new NotImplementedException(Object.dump(self) + " has no type");
        si

        init() is si
    si

    class NEW: BASE is
        has_type: bool => type != null;
        type: Type.BASE;
        constructor: Symbol.Function;
        arguments: Iterable[BASE];

        init(
            type: Type.BASE,
            constructor: Symbol.Function,
            arguments: Iterable[BASE]
        ) is
            self.type = type;
            self.constructor = constructor;
            self.arguments = arguments;
        si

        toString() -> String =>
            "new:" + type + "(\"" + constructor.name + "\"," + arguments + ")";
    si

    class CAST: BASE is
        has_type: bool => type != null;
        type: Type.BASE;
        value: BASE;

        init(
            type: Type.BASE,
            value: BASE
        ) is
            self.type = type;
            self.value = value;
        si

        toString() -> String =>
            "cast:" + type + "(" + value + ")";
    si    

    class DUMMY: BASE, Type.Typed is
        has_type: bool => type != null;
        type: Type.BASE;

        init(type: Type.BASE) is
            super.init();

            self.type = type;
        si
    si

    class TUPLE: BASE is
        values: Iterable[BASE];

        init(values: Iterable[BASE]) is
            super.init();

            self.values = values;
        si

        toString() -> String => "(" + values + ")";
    si

    namespace Need is
        class STORE: BASE is
            has_type: bool => value != null && value.has_type;
            type: Type.BASE => value.type;
            value: BASE;

            init(value: BASE) is
                super.init();

                self.value = value;
            si

            toString() -> String =>
                "need-store:" + type;                 
        si
    si

    namespace Expect is
        class STORABLE: BASE is
            has_type: bool => type != null;
            type: Type.BASE;

            init(type: Type.BASE) is
                super.init();

                self.type = type;
            si

            toString() -> String =>
                "expect-storable:" + type;            
        si
    si

    namespace Load is
        class SELF: BASE, Type.Typed is
            self_: Symbol.BASE;
            self_as_typed: Type.Typed => cast Type.Typed(self_);
            type: Type.BASE => self_as_typed.type;

            init(self_: Symbol.BASE) is
                super.init();

                self.self_ = self_;
            si

            toString() -> String => "self:" + type;
        si

        class LOCAL: BASE is
            init() is
                super.init();
            si

            toString() -> String => "local";
        si
        
        class SYMBOL: BASE, Type.Typed is
            symbol: Symbol.BASE;
            symbol_as_typed: Type.Typed => cast Type.Typed(symbol);
            type: Type.BASE => symbol_as_typed.type;
            from: BASE;

            init(from: BASE, symbol: Symbol.BASE) is
                super.init();

                self.from = from;
                self.symbol = symbol;
            si

            toString() -> String => "load:" + type + "(" + from + ",\"" + symbol.name + "\")";
        si
    si

    namespace Store is
        class SYMBOL: BASE, Type.Typed is
            symbol: Symbol.BASE;
            symbol_as_typed: Type.Typed => cast Type.Typed(symbol);
            type: Type.BASE => symbol_as_typed.type;
            from: BASE;
            value: Graph.Value.BASE;

            init(from: BASE, symbol: Symbol.BASE, value: Graph.Value.BASE) is
                super.init();

                self.from = from;
                self.symbol = symbol;
                self.value = value;
            si

            toString() -> String =>
                "store:" + type + "(" + from + ",\"" + symbol.name + "\"," + value + ")";
        si        
    si

    namespace Call is
        class GLOBAL: BASE, Type.Typed is
            function: Symbol.Function;
            arguments: Iterable[BASE];
            type: Type.BASE => function.type;

            init(function: Symbol.Function, arguments: Iterable[BASE]) is
                super.init();

                self.function = function;
                self.arguments = arguments;
            si

            toString() -> String =>
                "global-call:" + type + "(\"" + function.name + "\"," + arguments + ")";
        si

        class INSTANCE: BASE, Type.Typed is
            from: Value.BASE;
            function: Symbol.Function;
            arguments: Iterable[BASE];            
            type: Type.BASE => function.type;

            init(from: Value.BASE, function: Symbol.Function, arguments: Iterable[BASE]) is
                super.init();

                self.from = from;
                self.function = function;
                self.arguments = arguments;
            si

            toString() -> String =>
                "instance-call:" + type + "(" + from + ",\"" + function.name + "\"," +  arguments + ")";            
        si

        class ANON: BASE, Type.Typed is
            self_: Value.BASE;

            self_as_typed: Type.Typed => cast Type.Typed(self_);
            type: Type.BASE => self_as_typed.type;

            init(self_: Value.BASE) is
                super.init();

                self.self_ = self_;
            si
        si
    si

    namespace Literal is
        class INTEGER: BASE, Type.Typed is
            string: String;
            type: Type.BASE;

            init(string: String, type: Type.BASE) is
                super.init();

                self.string = string;
                self.type = type;
            si

            toString() -> String =>
                "literal:" + type + "(" + string + ")";
        si
    si
si
