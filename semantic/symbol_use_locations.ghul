namespace Semantic is
    use System;
    use Generic;

    use Source;

    trait SymbolUseListener is
        add_symbol_use(location: LOCATION, symbol: Symbol.BASE);
    si

    class SYMBOL_USE_LOCATIONS: Object, SymbolUseListener is
        _symbol_use_map: LOCATION_MAP[Symbol.BASE];
        _symbol_reference_map: Dict[Symbol.BASE,List[LOCATION]];

        init() is
            clear();
        si

        dump_counts() is
            _symbol_use_map.dump_counts();
            IO.Std.err.println("symbol reference map: " + _symbol_reference_map.Length);
        si        

        clear() is
            _symbol_use_map = new LOCATION_MAP[Symbol.BASE]();
            _symbol_reference_map = new Map[Symbol.BASE,List[LOCATION]](65521);
        si

        add_symbol_use(location: LOCATION, symbol: Symbol.BASE) is
            if symbol? && symbol.location.file_name !~ "internal" && !symbol.name.startsWith("__") then
                _symbol_use_map.put(location, symbol);
                get_references_list(symbol).add(location);
            fi
        si

        find_definition_from_use(file_name: String, line: int, column: int) -> Symbol.BASE is
            return _symbol_use_map.find(file_name, line, column);
        si

        find_references_to_symbol(symbol: Symbol.BASE) -> Iterable[LOCATION] is
            return get_references_list(symbol);    
        si

        get_references_list(symbol: Symbol.BASE) -> List[LOCATION] is
            var result = _symbol_reference_map[symbol];

            if result == null then
                result = new Vector[LOCATION]();

                _symbol_reference_map[symbol] = result;
            fi

            return result;            
        si
    si

    class LOCATION_MAP[T] is
        file_name_to_file: Dict[String, Dict[int, List[Pair[LOCATION,T]]]];

        init() is
            file_name_to_file = new Map[String, Dict[int, List[Pair[LOCATION,T]]]]();
        si

        dump_counts() is
            IO.Std.err.println("file name to file map: " + file_name_to_file.Length);
        si

        put(location: LOCATION, value: T) is
            var file = file_name_to_file[location.file_name];

            if file == null then
                file = new Map[int, List[Pair[LOCATION,T]]]();
                file_name_to_file[location.file_name] = file;
            fi

            let line = location.start_line;

            var list = file[line];

            if list == null then
                list = new Vector[Pair[LOCATION,T]]();
                file[line] = list;
            fi

            list.add(new Pair[LOCATION,T](location,value));
        si

        find(file_name: String, line: int, column: int) -> T is
            let file = file_name_to_file[file_name];

            if file == null then
                return null;
            fi

            let list = file[line];

            if list == null then
                return null;
            fi

            let line_column = LOCATION.pair(line, column);

            for p in list do
                if p.key.contains(line_column) then
                    return p.value;
                fi
            od
        si        
    si    
si
