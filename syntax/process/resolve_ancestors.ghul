namespace Syntax.Process is
    use System;
    use Generic;
    
    use Logging;

    class RESOLVE_ANCESTORS: ScopedVisitor is
        _logger: LOGGER;
        _symbol_table: Semantic.SYMBOL_TABLE;

        _void_type: Semantic.Type.NAMED;

        init(
            logger: LOGGER,
            symbol_table: Semantic.SYMBOL_TABLE,
            namespaces: Semantic.NAMESPACES
        )
        is
            super.init(logger, symbol_table, namespaces);

            _symbol_table = symbol_table;
            
            _logger = logger;

            let global_scope = symbol_table.global_scope;

            let ghul_symbol = global_scope.find_direct("Ghul");

            var ghul_namespace = cast Semantic.Scope(ghul_symbol);

            _void_type =
                 new Semantic.Type.NAMED(
                    "void",
                    cast Semantic.Symbol.Scoped(ghul_namespace.find_direct("void"))
                );            
        si

        apply(root: Tree.NODE) is
            root.walk(self);
        si

        // FIXME: think I'm doing this twice. This one is probably the correct one:
        visit(function: Tree.Definition.FUNCTION) is
            let symbol = _symbol_table.current_function;            

            let types = new Vector[Semantic.Type.BASE]();

            for argument in function.arguments do
                let type = argument.type_expression.type;

                types.add(type);
            od

            // FIXME: this should be a structured function type
            // encompassing arguments and return type:
            if !isa Tree.TypeExpression.INFER(function.type_expression) then
                symbol.type = function.type_expression.type;
            else
                symbol.type = _void_type;
            fi

            symbol.arguments = types;

            super.visit(function);
        si                

        visit(class_: Tree.Definition.CLASS) is
            let class_symbol = cast Semantic.Symbol.CLASS(_symbol_table.current_instance_context);

            // exit class scope before looking up ancestors:
            super.visit(class_);

            // FIXME: implicit ancestor Ghul.Object
            if class_.ancestors == null then
                return;
            fi

            for a in class_.ancestors do
                var ancestor_type = a.type;

                if isa Semantic.Type.NAMED(ancestor_type) then               
                    var ancestor_symbol = cast Semantic.Type.NAMED(ancestor_type).symbol;
            
                    class_symbol.add_inherited_scope(cast Semantic.Scope(ancestor_symbol));
                else
                    _logger.error(a.location, "cannot inherit from this");
                fi
            od
        si

        visit(trait_: Tree.Definition.TRAIT) is
            let trait_symbol = cast Semantic.Symbol.TRAIT(_symbol_table.current_instance_context);

            // exit class scope before looking up ancestors:
            super.visit(trait_);

            if trait_.ancestors == null then
                return;
            fi            

            for a in trait_.ancestors do
                var ancestor_type = a.type;

                if isa Semantic.Type.NAMED(ancestor_type) then
                    var ancestor_symbol = cast Semantic.Type.NAMED(ancestor_type).symbol;
            
                    trait_symbol.add_inherited_scope(cast Semantic.Scope(ancestor_symbol));
                else
                    _logger.error(a.location, "cannot inherit from this");
                fi
            od
        si
    si
si
