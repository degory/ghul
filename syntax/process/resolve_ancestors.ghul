namespace Syntax.Process is
    use System;
    use Generic;
    
    use Logging;

    class RESOLVE_ANCESTORS: ScopedVisitor is
        _logger: Logger;
        _symbol_table: Semantic.SYMBOL_TABLE;
        _innate_symbol_lookup: Semantic.INNATE_SYMBOL_LOOKUP;

        init(
            logger: Logger,
            symbol_table: Semantic.SYMBOL_TABLE,
            namespaces: Semantic.NAMESPACES,
            innate_symbol_lookup: Semantic.INNATE_SYMBOL_LOOKUP
        )
        is
            super.init(logger, symbol_table, namespaces);

            _logger = logger;
            _symbol_table = symbol_table;
            _innate_symbol_lookup = innate_symbol_lookup;
        si

        apply(root: Tree.NODE) is
            root.walk(self);
        si

        // FIXME: think I'm doing this twice. This one is probably the correct one:
        visit(function: Tree.Definition.FUNCTION) is
            let symbol = _symbol_table.current_function;            

            let types = new Vector[Semantic.Type.BASE]();

            for argument in function.arguments do
                let type = argument.type_expression.type;

                types.add(type);
            od

            // FIXME: this should be a structured function type
            // encompassing arguments and return type:
            if !isa Tree.TypeExpression.INFER(function.type_expression) then
                symbol.type = function.type_expression.type;
            else
                symbol.type = _innate_symbol_lookup.get_void_type();
            fi

            symbol.arguments = types;

            super.visit(function);
        si

        visit(property: Tree.Definition.PROPERTY) is
            let symbol = cast Semantic.Type.SettableTyped(find(property.name));

            if !isa Tree.TypeExpression.INFER(property.type_expression) then
                symbol.type = property.type_expression.type;
            fi

            super.visit(property);
        si

        visit(class_: Tree.Definition.CLASS) is
            let class_symbol = cast Semantic.Symbol.CLASS(_symbol_table.current_instance_context);

            // exit class scope before looking up ancestors:
            super.visit(class_);

            // FIXME: implicit ancestor Ghul.Object
            if class_.ancestors == null then
                return;
            fi

            for a in class_.ancestors do
                var ancestor_type = a.type;

                if ancestor_type? then
                    if isa Semantic.Type.NAMED(ancestor_type) then               
                        class_symbol.add_ancestor(cast Semantic.Type.NAMED(ancestor_type));
                    else
                        _logger.error(a.location, "cannot inherit from this");
                    fi
                else
                    IO.Std.err.println("refusing to add ancestor with null type: " + a);
                fi
            od
        si

        visit(trait_: Tree.Definition.TRAIT) is
            let trait_symbol = cast Semantic.Symbol.TRAIT(_symbol_table.current_instance_context);

            // exit class scope before looking up ancestors:
            super.visit(trait_);

            if trait_.ancestors == null then
                return;
            fi            

            for a in trait_.ancestors do
                var ancestor_type = a.type;

                if ancestor_type? then
                    if isa Semantic.Type.NAMED(ancestor_type) then
                        trait_symbol.add_ancestor(cast Semantic.Type.NAMED(ancestor_type));
                    else
                        _logger.error(a.location, "cannot inherit from this");
                    fi
                else
                    IO.Std.err.println("refusing to add ancestor with null type: " + a);
                fi
            od
        si
    si
si
