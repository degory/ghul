namespace Syntax.Process.Printer is
    use System;

    use Tree;

    class GHUL: BASE  is
        init() is
            super.init(false);
        si

        visit(variable: Variable.NODE) is
            variable.name.accept(self);
            if !isa TypeExpression.INFER(variable.type_expression) then
                print(": ");
                variable.type_expression.accept(self);
            fi
            if variable.initializer != null then
                print(" = ");
                variable.initializer.accept(self);
            fi
        si

        visit(import_: Definition.IMPORT) is
            print("import ");
            var seen_any = false;
            for i in import_.imports do
                if seen_any then
                    print(", ");
                fi
                i.accept(self);
                seen_any = true;
            od
            println(";");
        si

        visit(namespace_: Definition.NAMESPACE) is
            print("namespace ");
            namespace_.name.accept(self);
            println(" is");
            indent();
            namespace_.body.accept(self);
            outdent();
            println("si");
        si

        visit(use_: Definition.USE) is
            print("use ");
            var seen_any = false;
            for u in use_.uses do
                if seen_any then
                    print(", ");
                fi
                u.accept(self);
                seen_any = true;
            od
            println(";");
        si

        visit(class_: Definition.CLASS) is
            print("class ");
            class_.name.accept(self);
            if class_.arguments != null then
                print("[");
                class_.arguments.accept(self);
                print("]");
            fi
            if class_.ancestors != null then
                print(": ");
                class_.ancestors.accept(self);
            fi
            class_.modifiers.accept(self);
            println(" is");
            indent();
            class_.body.accept(self);
            outdent();
            println("si");
        si

        visit(trait_: Definition.TRAIT) is
            print("trait ");
            trait_.name.accept(self);
            if trait_.arguments != null then
                print("[");
                trait_.arguments.accept(self);
                print("]");
            fi
            if trait_.ancestors != null then
                print(": ");
                trait_.ancestors.accept(self);
            fi
            trait_.modifiers.accept(self);
            println(" is");
            indent();
            trait_.body.accept(self);
            outdent();
            println("si");
        si

        after_body(node: Body.NODE) is
            if node==null || !node.is_block then
                print(";");
            fi
            println();
        si

        visit(function: Definition.FUNCTION) is
            function.name.accept(self);
            print("(");
            function.arguments.accept(self);
            print(")");
            if !isa TypeExpression.INFER(function.type_expression) then
                print(" -> ");
                function.type_expression.accept(self);
                function.modifiers.accept(self);
            elif !function.modifiers.is_empty then
                function.modifiers.accept(self);
            fi
            if function.body != null then
                print(" ");
                function.body.accept(self);
            fi
            after_body(function.body);
        si

        print_member_type_and_modifiers(type_expression: TypeExpression.NODE, modifiers: Modifier.LIST) is
            if !isa TypeExpression.INFER(type_expression) then
                print(": ");
                type_expression.accept(self);
                print(" ");
                modifiers.accept(self);
            elif !modifiers.is_empty then
                modifiers.accept(self);
            fi
        si

        indent_property(has_getter: bool, has_setter: bool) -> bool is
            if has_getter && has_setter!=null then
                println();
                indent();
                return true;
            elif has_getter || has_setter!=null then
                print(" ");
            fi
            return false;
        si

        visit(property: Definition.PROPERTY) is
            property.name.accept(self);
            print_member_type_and_modifiers(property.type_expression, property.modifiers);
            var out_again = indent_property(property.read_body != null, property.assign_body != null);
            if property.read_body != null then
                property.read_body.accept(self);
                if property.assign_body != null then
                    println(",");
                else
                    after_body(property.read_body);
                fi
            else
                print(" ");
            fi
            if property.assign_body != null then
                print("= ");
                property.assign_argument.accept(self);
                property.assign_body.accept(self);
                after_body(property.assign_body);
            fi
            if out_again then
                outdent();
            fi
        si

        visit(indexer: Definition.INDEXER) is
            if indexer.name != null then
                indexer.name.accept(self);
            fi
            print("[");
            indexer.index_argument.accept(self);
            print("]");
            print_member_type_and_modifiers(indexer.type_expression, indexer.modifiers);
            var out_again = indent_property(indexer.read_body != null, indexer.assign_body != null);
            if indexer.read_body != null then
                indexer.read_body.accept(self);
                if indexer.assign_body != null then
                    println(",");
                else
                    after_body(indexer.read_body);
                fi
            else
                print(' ');
            fi
            if indexer.assign_body != null then
                print("= ");
                indexer.assign_argument.accept(self);
                print(' ');
                indexer.assign_body.accept(self);
                after_body(indexer.assign_body);
            fi
            if out_again then
                outdent();
            fi
        si

        visit(generic: TypeExpression.GENERIC) is
            generic.name.accept(self);
            print('[');
            generic.arguments.accept(self);
            print(']');
        si

        visit(function: TypeExpression.FUNCTION) is
            print("(");
            function.arguments.accept(self);
            print(")");
            if !isa TypeExpression.INFER(function.result) then
                print(" -> ");
                function.result.accept(self);
            fi
        si

        visit(tuple: TypeExpression.TUPLE) is
            print("(");
            tuple.elements.accept(self);
            print(")");
        si

        visit(element: TypeExpression.NAMED_TUPLE_ELEMENT) is
            element.name.accept(self);
            print(": ");
            element.type_expression.accept(self);
        si

        visit(null_: Expression.NULL) is
            print("null");
        si

        visit(self_: Expression.SELF) is
            print("self");
        si

        visit(variable: Expression.VARIABLE) is
            variable.name.accept(self);

            if !isa TypeExpression.INFER(variable.type_expression) then
                print(": ");
                variable.type_expression.accept(self);
            fi
            
            if variable.initializer != null then
                print(" = ");
                variable.initializer.accept(self);
            fi
        si

        visit(function: Syntax.Tree.Expression.FUNCTION) is
            function.arguments.accept(self);
            if !isa TypeExpression.INFER(function.type_expression) then
                print(" -> ");
                function.type_expression.accept(self);
            fi
            function.body.accept(self);
        si

        visit(sequence: Expression.SEQUENCE) is
            print('[');
            sequence.elements.accept(self);
            print(']');
            if !isa Tree.TypeExpression.INFER(sequence.type_expression) then
                print(": ");
                sequence.type_expression.accept(self);
            fi
        si

        visit(has_value: Expression.HAS_VALUE) is
            has_value.left.accept(self);
            print("?");
        si

        visit(l: Statement.LET) is
            print("let ");
            l.variables.accept(self);
            println(";");
        si

        visit(for_: Statement.FOR) is
            println("for ");
            for_.variable.accept(self);
            print(" in ");
            for_.expression.accept(self);
            println(" do");
            indent();
            for_.body.accept(self);
            outdent();
            println("od");
        si

        visit(expression: Body.EXPRESSION) is
            print("=> ");
            expression.expression.accept(self);
        si

        visit(block: Body.BLOCK) is
            println("is");
            indent();
            block.statements.accept(self);
            outdent();
            print("si");
        si

    si

si
