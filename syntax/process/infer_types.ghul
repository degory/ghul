namespace Syntax.Process is
    use System;
    use Generic;
    
    use Logging;

    class INFER_TYPES: ScopedVisitor is
        _logger: LOGGER;
        _symbol_table: Semantic.SYMBOL_TABLE;
        _int_type: Semantic.Type.NAMED;
        _symbol_loader: Semantic.SYMBOL_LOADER;
        _function_caller: Semantic.FUNCTION_CALLER;
        _overload_resolver: Semantic.OVERLOAD_RESOLVER;
        _generic_specializer: Semantic.GENERIC_SPECIALIZER;

        init(
            logger: LOGGER,
            symbol_table: Semantic.SYMBOL_TABLE,
            namespaces: Semantic.NAMESPACES
        )
        is
            super.init(logger, symbol_table, namespaces);

            _symbol_table = symbol_table;
            
            _logger = logger;

            let global_scope = symbol_table.global_scope;

            let ghul_symbol = global_scope.find_direct("Ghul");

            var ghul_namespace = cast Semantic.Scope(ghul_symbol);

            _int_type =
                 new Semantic.Type.NAMED(
                    "int",
                    cast Semantic.Symbol.Scoped(ghul_namespace.find_direct("int"))
                );


            _function_caller = new Semantic.FUNCTION_CALLER(_symbol_table);

            _symbol_loader = new Semantic.SYMBOL_LOADER(
                _logger,
                _symbol_table,
                _function_caller,
                _int_type
            );

            _overload_resolver = new Semantic.OVERLOAD_RESOLVER(_logger);
            _generic_specializer = new Semantic.GENERIC_SPECIALIZER();
        si

        apply(root: Tree.NODE) is
            root.walk(self);
        si

        pre(assignment: Tree.Statement.ASSIGNMENT) -> bool is
            assignment.right.walk(self);

            if assignment.right.value != null then
                assignment.left.value = new Semantic.Graph.Value.Need.STORE(assignment.right.value);

                assignment.left.walk(self);
            fi

            return true;
        si

        visit(tuple: Tree.Expression.TUPLE) is
            let values = new Vector[Semantic.Graph.Value.BASE]();

            for v in tuple.elements do
                values.add(v.value);
            od

            tuple.value = new Semantic.Graph.Value.TUPLE(values);
        si

        visit(self_: Tree.Expression.SELF) is
            self_.value =
                new Semantic.Graph.Value.Load.SELF(
                    _symbol_table.current_instance_context
                );

            _logger.info(self_.location, "" + self_.value);                
        si

        visit(cast_: Tree.Expression.CAST) is
            var type = cast_.type_expression.type;

            if type == null then
                _logger.error(cast_.type_expression.location, "cast has no type");
                return;
            fi

            type = type.specialize(_generic_specializer);
            
            cast_.value =
                new Semantic.Graph.Value.CAST(
                    type,
                    cast_.right.value
                );

            _logger.info(cast_.location, "" + cast_.value);                
        si

        visit(new_: Tree.Expression.NEW) is
            var type = new_.type_expression.type;

            if type == null then
                _logger.error(new_.type_expression.location, "new has no type");
                return;
            fi

            type = type.specialize(_generic_specializer);

            if !isa Semantic.Type.NAMED(type) then
                _logger.error(new_.type_expression.location, "cannot instantiate this: " + type);
                return;
            fi

            let named_type = cast Semantic.Type.NAMED(type);

            let symbol = named_type.scope.find_member("init");

            let function_group = cast Semantic.Symbol.FUNCTION_GROUP(symbol);

            let arguments = new Vector[Semantic.Graph.Value.BASE]();
            let argument_types = new Vector[Semantic.Type.BASE]();

            for a in new_.arguments.elements do
                if a.value == null then
                    _logger.error(a.location, "argument has no value");
                    return;
                fi

                arguments.add(a.value);
                argument_types.add(a.value.type);
            od

            let function = _overload_resolver.resolve(new_.location, function_group, argument_types);

            if function == null then
                return;
            fi

            new_.value =
                new Semantic.Graph.Value.NEW(
                    type,
                    function,
                    arguments
                );

            _logger.info(new_.location, "" + new_.value);
        si

        visit(unary: Tree.Expression.UNARY) is
            if unary.right.value != null then
                let arguments = [unary.right.value.type];

                let function_group = find(unary.operation);

                if function_group == null || !isa Semantic.Symbol.FUNCTION_GROUP(function_group) then
                    return;
                fi

                let function = _overload_resolver.resolve(unary.location, cast Semantic.Symbol.FUNCTION_GROUP(function_group), arguments);

                if function == null then                
                    return;
                fi

                let value = function.call(null, [unary.right.value], _function_caller);

                unary.value = value;

                _logger.info(unary.location, "" + unary.value);                                    
            fi
        si        

        visit(binary: Tree.Expression.BINARY) is
            if binary.left.value != null && binary.right.value != null then
                let arguments = [binary.left.value.type, binary.right.value.type];

                let function_group = find(binary.operation);

                if function_group == null || !isa Semantic.Symbol.FUNCTION_GROUP(function_group) then
                    return;
                fi

                let function = _overload_resolver.resolve(binary.location, cast Semantic.Symbol.FUNCTION_GROUP(function_group), arguments);

                if function == null then
                    return;
                fi

                let value = function.call(null, [binary.left.value, binary.right.value], _function_caller);

                binary.value = value;

                _logger.info(binary.location, "" + binary.value);                                    
            fi
        si

        visit(index: Tree.Expression.INDEX) is
            let need_store = index.value? && isa Semantic.Graph.Value.Need.STORE(index.value);

            if index.left.value? then
                let type = index.left.value.type;

                if type == null then
                    _logger.error(index.left.location, "index left has no type");
                    return;
                fi

                if !isa Semantic.Type.NAMED(type) then
                    _logger.error(index.left.location, "type has no members: " + type);
                    return;
                fi

                let named_type = cast Semantic.Type.NAMED(type);

                var function_name: String;
                var arguments: Iterable[Semantic.Graph.Value.BASE];
                var argument_types: List[Semantic.Type.BASE];

                if need_store then
                    function_name = "__assign";

                    let need_store_value = cast Semantic.Graph.Value.Need.STORE(index.value).value;

                    arguments = [index.index.value, need_store_value];
                    argument_types = [index.index.value.type, need_store_value.type];
                else
                    function_name = "__read";
                    arguments = [index.index.value];
                    argument_types = [index.index.value.type];
                fi

                let symbol = named_type.scope.find_member(function_name);

                if symbol == null then
                    _logger.error(index.location, "no indexer found");
                    return;
                fi

                if !isa Semantic.Symbol.FUNCTION_GROUP(symbol) then
                    _logger.error(index.index.location, "indexer is not a function group: " + symbol);
                fi

                let function = _overload_resolver.resolve(index.location, cast Semantic.Symbol.FUNCTION_GROUP(symbol), argument_types);

                if function == null then
                    _logger.error(index.index.location, "no indexer overload found");
                    return;
                fi

                index.value = function.call(index.left.value, arguments, _function_caller);

                _logger.info(index.location, "" + index.value);                    
            fi
        si

        visit(member: Tree.Expression.MEMBER) is
            let need_store = member.value? && isa Semantic.Graph.Value.Need.STORE(member.value);

            if member.left.value? then
                let type = member.left.value.type;

                if type == null then
                    _logger.error(member.left.location, "member left has no type");
                    return;
                fi

                if !isa Semantic.Type.NAMED(type) then
                    _logger.error(member.left.location, "type has no members: " + type);
                    return;
                fi

                let named_type = cast Semantic.Type.NAMED(type);

                let symbol = named_type.scope.find_member(member.identifier.name);

                if symbol == null then
                    _logger.error(member.identifier.location, "member " + member.identifier.name + " not found");
                    return;
                fi

                _symbol_loader.find_symbol = 
                    (name: String) -> Semantic.Symbol.BASE use named_type
                        => named_type.scope.find_member(name);

                var value: Semantic.Graph.Value.BASE;

                if need_store then
                    let store_value = cast Semantic.Graph.Value.Need.STORE(member.value).value;

                    value = symbol.store(member.left.value, store_value, _symbol_loader);
                else
                    value = symbol.load(member.left.value, _symbol_loader);
                fi

                member.value = value;

                _logger.info(member.location, "" + member.value);
            fi
        si
        
        visit(variable: Tree.Variable.NODE) is
            var symbol = find(variable.name);

            if symbol != null && isa Semantic.Type.SettableTyped(symbol) then
                var typed_symbol = cast Semantic.Type.SettableTyped(symbol);

                if typed_symbol.type == null then
                    if variable.type_expression != null && !isa Tree.TypeExpression.INFER(variable.type_expression) then
                        
                        typed_symbol.type = variable.type_expression.type.specialize(_generic_specializer);
                    elif variable.initializer != null && variable.initializer.value != null then
                        typed_symbol.type = variable.initializer.value.type;
                    else
                        _logger.error(variable.location, "unable to set type for variable " + variable.name);
                    fi
                fi
            else
                _logger.error(variable.name.location, "couldn't find typed symbol for variable " + variable.name);
            fi        
        si

        visit(integer: Tree.Expression.Literal.INTEGER) is
            if integer.value == null then
                integer.value = new Semantic.Graph.Value.Literal.INTEGER(
                    integer.string,
                    _int_type
                );
            fi
        si

        visit(call: Tree.Expression.CALL) is
            if call.value == null then
                if call.function.value == null then
                    _logger.error(call.function.location, "cannot call this");
                    return;
                fi

                if isa Semantic.Graph.Value.Load.SYMBOL(call.function.value) then
                    let load = cast Semantic.Graph.Value.Load.SYMBOL(call.function.value);

                    let function_group = cast Semantic.Symbol.FUNCTION_GROUP(load.symbol);

                    let arguments = new Vector[Semantic.Graph.Value.BASE]();
                    let argument_types = new Vector[Semantic.Type.BASE]();

                    for a in call.arguments do
                        if a.value == null then
                            _logger.error(a.location, "argument has no value");
                            return;
                        fi

                        arguments.add(a.value);
                        argument_types.add(a.value.type);
                    od

                    let function = _overload_resolver.resolve(call.location, function_group, argument_types);

                    if function == null then
                        return;
                    fi

                    call.value = function.call(load.from, arguments, _function_caller);

                    _logger.info(call.location, "" + call.value);                    
                fi 
            fi
        si

        visit(identifier: Tree.Expression.IDENTIFIER) is
            let need_store = identifier.value != null && isa Semantic.Graph.Value.Need.STORE(identifier.value);
        
            // if identifier.value == null then
                var symbol = find(identifier.identifier);

                if symbol != null then
                    _symbol_loader.find_symbol = (name: String) -> Semantic.Symbol.BASE => find(name);

                    if need_store then
                        var store_value = cast Semantic.Graph.Value.Need.STORE(identifier.value).value;
                        identifier.value = symbol.store(null, store_value, _symbol_loader);
                    else
                        identifier.value = symbol.load(null, _symbol_loader);
                    fi

                    _logger.info(identifier.location, "" + identifier.value);                    
                fi                
            // fi
        si
    si
si
