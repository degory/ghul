namespace Syntax.Process is
    use System;
    use Generic;
    
    use Logging;

    class INFER_TYPES: ScopedVisitor is
        _logger: LOGGER;
        _symbol_table: Semantic.SYMBOL_TABLE;
        _int_type: Semantic.Type.NAMED;
        _symbol_loader: Semantic.SYMBOL_LOADER;
        _function_caller: Semantic.FUNCTION_CALLER;
        _overload_resolver: Semantic.OVERLOAD_RESOLVER;

        init(
            logger: LOGGER,
            symbol_table: Semantic.SYMBOL_TABLE,
            namespaces: Semantic.NAMESPACES
        )
        is
            super.init(logger, symbol_table, namespaces);

            _symbol_table = symbol_table;
            
            _logger = logger;

            let global_scope = symbol_table.global_scope;

            let ghul_symbol = global_scope.find_direct("Ghul");

            var ghul_namespace = cast Semantic.Scope(ghul_symbol);

            _int_type =
                 new Semantic.Type.NAMED(
                    "int",
                    cast Semantic.Symbol.Scoped(ghul_namespace.find_direct("int"))
                );

            _symbol_loader = new Semantic.SYMBOL_LOADER(
                _logger,
                _symbol_table,
                _int_type
            );

            _function_caller = new Semantic.FUNCTION_CALLER(_symbol_table);

            _overload_resolver = new Semantic.OVERLOAD_RESOLVER(_logger);
        si

        apply(root: Tree.NODE) is
            root.walk(self);
        si

        pre(property: Tree.Definition.PROPERTY) -> bool is
            IO.Std.err.println("look for property: " + property.name + "...");

            var symbol = find(property.name);

            IO.Std.err.println("type_expression of " + symbol + " is " + property.type_expression);

            return true;
        si

        pre(property: Tree.Definition.INDEXER) -> bool is
            return true;
        si

        visit(unary: Tree.Expression.UNARY) is
            if unary.right.value then
                let arguments = [unary.right.value.type];

                let function_group = find(unary.operation);

                if function_group == null || !isa Semantic.Symbol.FUNCTION_GROUP(function_group) then
                    return;
                fi

                let function = _overload_resolver.resolve(cast Semantic.Symbol.FUNCTION_GROUP(function_group), arguments);

                if function == null then
                    return;
                fi

                let value = function.call(null, _function_caller);

                unary.value = value;

                _logger.info(binary.location, "" + unary.operation + " " + unary.right.value.type + " -> " + unary.value.type);                                    
            fi
        si        

        visit(binary: Tree.Expression.BINARY) is
            if binary.left.value != null && binary.right.value != null then
                let arguments = [binary.left.value.type, binary.right.value.type];

                let function_group = find(binary.operation);

                if function_group == null || !isa Semantic.Symbol.FUNCTION_GROUP(function_group) then
                    return;
                fi

                let function = _overload_resolver.resolve(cast Semantic.Symbol.FUNCTION_GROUP(function_group), arguments);

                if function == null then
                    return;
                fi

                let value = function.call(null, _function_caller);

                binary.value = value;

                _logger.info(binary.location, "" + binary.left.value.type + " " + binary.operation + " " + binary.right.value.type + " -> " + binary.value.type);                                    
            fi
        si

        visit(member: Tree.Expression.MEMBER) is
            if member.left.value != null then
                let type = member.left.value.type;

                if type == null then
                    _logger.error(member.left.location, "member left has no type");
                    return;
                fi

                if !isa Semantic.Type.NAMED(type) then
                    _logger.error(member.left.location, "type has no members: " + type);
                    return;
                fi

                let named_type = cast Semantic.Type.NAMED(type);

                let symbol = named_type.scope.find_member(member.identifier.name);

                if symbol == null then
                    _logger.error(member.identifier.location, "member not found");
                    return;
                fi

                var value = new Semantic.Graph.Value.Load.SYMBOL(member.left.value, symbol);

                member.value = value;
            fi
        si
        
        visit(variable: Tree.Variable.NODE) is
            var symbol = find(variable.name);

            if symbol != null && isa Semantic.Type.SettableTyped(symbol) then
                var typed_symbol = cast Semantic.Type.SettableTyped(symbol);

                if typed_symbol.type == null then
                    if variable.type_expression != null && !isa Tree.TypeExpression.INFER(variable.type_expression) then
                        
                        typed_symbol.type = variable.type_expression.type;
                    elif variable.initializer != null && variable.initializer.value != null then
                        typed_symbol.type = variable.initializer.value.type;
                    else
                        _logger.error(variable.location, "unable to set type for variable");
                    fi
                fi
            else
                _logger.error(variable.name.location, "couldn't find typed symbol for variable");
            fi        
        si

        visit(integer: Tree.Expression.Literal.INTEGER) is
            if integer.value == null then
                integer.value = new Semantic.Graph.Value.Literal.INTEGER(
                    integer.string,
                    _int_type
                );
            fi
        si

        visit(call: Tree.Expression.CALL) is
            if call.value == null then
                if call.function.value == null then
                    _logger.error(call.function.location, "cannot call this");
                    return;
                fi

                if isa Semantic.Graph.Value.Load.SYMBOL(call.function.value) then
                    var load = cast Semantic.Graph.Value.Load.SYMBOL(call.function.value);

                    var function_group = cast Semantic.Symbol.FUNCTION_GROUP(load.symbol);

                    var arguments = new Vector[Semantic.Type.BASE]();

                    for a in call.arguments do
                        if a.value == null then
                            _logger.error(a.location, "argument has no value");
                            return;
                        fi

                        arguments.add(a.value.type);
                    od

                    var function = _overload_resolver.resolve(function_group, arguments);

                    if function == null then
                        _logger.error(call.location, "no overload found");
                        return;
                    fi

                    call.value = function.call(load.from, _function_caller);

                    _logger.info(call.location, "" + call.value + " (" + arguments + ") -> " + call.value.type);                    
                fi 
            fi
        si

        visit(identifier: Tree.Expression.IDENTIFIER) is
            if identifier.value == null then
                var symbol = find(identifier.identifier);

                if symbol != null then
                    identifier.value = new Semantic.Graph.Value.Load.SYMBOL(
                        new Semantic.Graph.Value.Load.LOCAL(),
                        symbol
                    );
                fi                
            fi
        si
    si
si
