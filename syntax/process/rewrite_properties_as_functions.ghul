namespace Syntax.Process is
    use System;
    use Generic;

    use Source;
    use Tree;

    class REWRITE_PROPERTIES_AS_FUNCTIONS: Visitor  is
        _map: MAP;

        _stack: Vector[Definition.LIST];

        enclosing_definition: Definition.LIST => _stack.Top;

        init() is
            super.init();

            _map = new MAP();
            _stack = new Vector[Definition.LIST]();
        si

        apply(root: NODE) is
            root.walk(self);
        si

        pre(property: Definition.PROPERTY) -> bool is
            var name =
                new Identifier.NODE(
                    property.name.location,
                    "$" + property.name.name
                );

            if property.read_body != null then
                enclosing_definition.add(
                    new Definition.FUNCTION(
                        property.location,
                        name,
                        new Variable.LIST(LOCATION.dummy, new Variable.NODE[](0)),
                        property.type.copy(),
                        property.modifiers.copy(),
                        property.read_body
                    )
                );
            fi

            if property.assign_body != null then
                enclosing_definition.add(
                    new Definition.FUNCTION(
                        property.location,
                        name,
                        new Variable.LIST(LOCATION.dummy, [
                            new Variable.NODE(
                                property.assign_argument.location,
                                property.assign_argument.copy(),
                                property.type.copy(),
                                null
                            )
                        ]),
                        property.type.copy(),
                        property.modifiers.copy(),
                        property.assign_body
                    )
                );
            fi

            enclosing_definition.remove(property);
                       
            return false;
        si

        visit(property: Definition.PROPERTY) is
        si

        pre(indexer: Definition.INDEXER) -> bool is
            var name: Identifier.NODE; 

            if indexer.name != null then
                name =
                    new Identifier.NODE(
                        indexer.name.location,
                        "[]" + indexer.name.name
                    );
            else
                name =
                    new Identifier.NODE(
                        indexer.name.location,
                        "[]"
                    );
            fi

            if indexer.read_body != null then
                enclosing_definition.add(
                    new Definition.FUNCTION(
                        indexer.location,
                        name,
                        new Variable.LIST(LOCATION.dummy, [indexer.index_argument.copy()]),
                        indexer.type.copy(),
                        indexer.modifiers.copy(),
                        indexer.read_body
                    )
                );
            fi

            if indexer.assign_body != null then
                enclosing_definition.add(
                    new Definition.FUNCTION(
                        indexer.location,
                        name,
                        new Variable.LIST(LOCATION.dummy, [
                            indexer.index_argument.copy(),
                            new Variable.NODE(
                                indexer.assign_argument.location,
                                indexer.assign_argument.copy(),
                                indexer.type.copy(),
                                null
                            )
                        ]),
                        indexer.type.copy(),
                        indexer.modifiers.copy(),
                        indexer.assign_body
                    )
                );
            fi

            enclosing_definition.remove(indexer);
                       
            return false;
        si
        

        pre(class_: Definition.CLASS) -> bool is
            _stack.add(class_.body);

            return false;
        si

        visit(class_: Definition.CLASS) is
            _stack.pop();
        si

        pre(interface_: Definition.INTERFACE) -> bool is
            _stack.add(interface_.body);

            return false;
        si

        visit(interface_: Definition.INTERFACE) is
            _stack.pop();
        si

        pre(namespace_: Definition.NAMESPACE) -> bool is
            _stack.add(namespace_.body);

            return false;
        si

        visit(namespace_: Definition.NAMESPACE) is
            _stack.pop();
        si        
    si
si
