namespace Syntax.Tree.Statement is
    use Source;

    use Generic;
    
    class TRY: NODE  is
        expression: Expression.NODE;
        body: LIST;
        catches: List[CATCH];
        finally_: LIST;

        children: Iterable[Tree.NODE] is
            var result = new Vector[Tree.NODE](catches.Length + 4);

            result.add(expression);
            result.add(body);

            for c in catches do
                result.add(c);
            od

            if finally_? then
                result.add(finally_);
            fi

            return result;
        si

        init(
            location: LOCATION,
            body: LIST, catches: List[CATCH],
            finally_: LIST
        )
        is
            super.init(location);

            self.body = body;
            self.catches = catches;
            self.finally_ = finally_;
        si

        accept(visitor: Visitor) is
            visitor.visit(self);
        si

        walk(visitor: Visitor) is
            if !visitor.pre(self) then
                body.walk(visitor);

                for c in catches do
                    c.walk(visitor);
                od

                if finally_? then
                    finally_.walk(visitor);
                fi
            fi
            accept(visitor);
        si

    si

    class CATCH: NODE  is
        variable: Variable.NODE;
        body: LIST;

        init(location: LOCATION, variable: Variable.NODE, body: LIST) is
            super.init(location);

            self.variable = variable;
            self.body = body;
        si

        accept(visitor: Visitor) is
            visitor.visit(self);
        si

        walk(visitor: Visitor) is
            if !visitor.pre(self) then
                variable.walk(visitor);
                body.walk(visitor);
            fi
            
            accept(visitor);
        si
    si
si
