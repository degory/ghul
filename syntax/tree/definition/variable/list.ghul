namespace Syntax.Tree.Variable is
    use Generic;

    use Source;

    class LIST: Tree.NODE, Iterable[NODE] is
        variables: List[NODE];

        children: Iterable[Tree.NODE] is
            // do not have type variance so can't just
            // cast: 
            var result = new Vector[Tree.NODE]();

            for v in variables do
                result.add(v);
            od

            return result;
        si

        Iterator: Iterator[NODE] => variables.Iterator;

        init(location: LOCATION, variables: Iterable[NODE]) is
            super.init(location);

            self.variables = new Vector[NODE](variables);
        si

        accept(visitor: Visitor) is
            visitor.visit(self);
        si

        walk(visitor: Visitor) is
            if !visitor.pre(self) then
                for v in variables do
                    v.walk(visitor);
                od
            fi
            
            accept(visitor);
        si

        rewrite(m: Dict[Tree.NODE,Tree.NODE]) is
            var nv = new Vector[NODE]();

            for v in variables do
                nv.add(cast NODE(m[v]));
            od

            variables = nv;
        si
    si
si
