namespace Syntax.Tree.Variable is
    use Generic;

    use Source;

    class NODE: Tree.Definition.NODE is
        name: Identifier.NODE;
        type: Type.NODE;
        initializer: Expression.NODE;
        
        init(location: LOCATION, name: Identifier.NODE, type: Type.NODE, initializer: Expression.NODE) is
            super.init(location);
            self.name = name;
            self.type = type;
            self.initializer = initializer;
        si

        copy() -> NODE is
            assert(initializer == null, "cannot copy a variable node with non null initializer: " + location + ": " + self);

            return new NODE(
                location,
                name.copy(),
                type.copy(),
                null
            );
        si

        accept(visitor: Visitor) is
            visitor.visit(self);
        si

        walk(visitor: Visitor) is
            if !visitor.pre(self) then
                name.walk(visitor);
                type.walk(visitor);
                if initializer != null then
                    initializer.walk(visitor);
                fi
            fi
            accept(visitor);
        si

        rewrite(m: Dict[Tree.NODE,Tree.NODE]) is
            name = cast Identifier.NODE(m[name]);
            type = cast Type.NODE(m[type]);
            initializer = cast Expression.NODE(m[initializer]);
        si
    si
si
