namespace Syntax.Tree.Definition is
    use Source;
    
    class INDEXER: MODIFIABLE  is
        name: Identifier.NODE;
        index_argument: Variable.NODE;
        type: Type.NODE;
        getter_body: Body.NODE;
        setter_argument: Identifier.NODE;
        setter_body: Body.NODE;

        init(
            location: LOCATION,
            name: Identifier.NODE,
            index_argument: Variable.NODE,
            type: Type.NODE,
            modifiers: Modifier.LIST,
            getter_body: Body.NODE,
            setter_argument: Identifier.NODE,
            setter_body: Body.NODE)
        is
            super.init(location, modifiers);

            self.name = name;
            self.index_argument = index_argument;
            self.type = type;
            self.getter_body = getter_body;
            self.setter_argument = setter_argument;
            self.setter_body = setter_body;
        si

        accept(visitor: Visitor) is
            visitor.visit(self);
        si

        walk(visitor: Visitor) is
            if !visitor.pre(self) then
                modifiers.walk(visitor);

                if name != null then
                    name.walk(visitor);
                fi

                index_argument.walk(visitor);
                type.walk(visitor);

                if getter_body != null then
                    getter_body.walk(visitor);
                fi

                if setter_argument != null then
                    setter_argument.walk(visitor);
                fi

                if setter_body != null then
                    setter_body.walk(visitor);
                fi
            fi
            accept(visitor);
        si
    si
si
