namespace Syntax.Tree.Definition is
    use Source;
    
    class INDEXER: MODIFIABLE  is
        name: Identifier.NODE;
        index_argument: Variable.NODE;
        type: TypeExpression.NODE;
        read_body: Body.NODE;
        assign_argument: Identifier.NODE;
        assign_body: Body.NODE;

        init(
            location: LOCATION,
            name: Identifier.NODE,
            index_argument: Variable.NODE,
            type: TypeExpression.NODE,
            modifiers: Modifier.LIST,
            read_body: Body.NODE,
            assign_argument: Identifier.NODE,
            assign_body: Body.NODE)
        is
            super.init(location, modifiers);

            self.name = name;
            self.index_argument = index_argument;
            self.type = type;
            self.read_body = read_body;
            self.assign_argument = assign_argument;
            self.assign_body = assign_body;
        si

        accept(visitor: Visitor) is
            visitor.visit(self);
        si

        walk(visitor: Visitor) is
            if !visitor.pre(self) then
                modifiers.walk(visitor);

                if name != null then
                    name.walk(visitor);
                fi

                index_argument.walk(visitor);
                type.walk(visitor);

                if read_body != null then
                    read_body.walk(visitor);
                fi

                if assign_argument != null then
                    assign_argument.walk(visitor);
                fi

                if assign_body != null then
                    assign_body.walk(visitor);
                fi
            fi
            accept(visitor);
        si
    si
si
