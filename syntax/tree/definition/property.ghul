namespace Syntax.Tree.Definition is
    use Source;
    
    use Generic;

    class PROPERTY: MODIFIABLE is
        type_expression: TypeExpression.NODE;
        name: Identifier.NODE;
        read_body: Body.NODE;
        assign_argument: Identifier.NODE;
        assign_body: Body.NODE;

        children: Iterable[Tree.NODE] is
            var result = new Vector[Tree.NODE](6);

            result.add(modifiers);
            result.add(type_expression);

            if name != null then
                result.add(name);
            fi

            if read_body != null then
                result.add(read_body);
            fi

            if assign_argument != null then
                result.add(assign_argument);
            fi
            
            if assign_body != null then
                result.add(assign_body);
            fi
        
            return result;
        si

        init(
            location: LOCATION,
            type_expression: TypeExpression.NODE,
            name: Identifier.NODE,
            modifiers: Modifier.LIST,
            read_body: Body.NODE,
            assign_argument: Identifier.NODE,
            assign_body: Body.NODE
        ) is
            super.init(location, modifiers);

            self.type_expression = type_expression;
            self.name = name;
            self.read_body = read_body;
            self.assign_argument = assign_argument;
            self.assign_body = assign_body;
        si

        accept(visitor: Visitor) is
            visitor.visit(self);
        si

        walk(visitor: Visitor) is
            if !visitor.pre(self) then
                modifiers.walk(visitor);
                type_expression.walk(visitor);

                if name != null then
                    name.walk(visitor);
                fi

                if read_body != null then
                    read_body.walk(visitor);
                fi

                if assign_argument != null then
                    assign_argument.walk(visitor);
                fi
                
                if assign_body != null then
                    assign_body.walk(visitor);
                fi
            fi
            accept(visitor);
        si
    si
si
