namespace Syntax.Tree.Definition is
    use Source;

    class FUNCTION: MODIFIABLE  is
        name: Identifier.NODE;
        arguments: Variable.LIST;
        type: TypeExpression.NODE;
        body: Body.NODE;
        
        init(
            location: LOCATION,
            name: Identifier.NODE, 
            arguments: Variable.LIST,
            type: TypeExpression.NODE,
            modifiers: Modifier.LIST,
            body: Body.NODE
        ) is
            super.init(location, modifiers);

            self.name = name;
            self.arguments = arguments;
            self.type = type;
            self.body = body;
        si

        accept(visitor: Visitor) is
            visitor.visit(self);
        si

        walk(visitor: Visitor) is
            if !visitor.pre(self) then
                name.walk(visitor);
                arguments.walk(visitor);
                type.walk(visitor);
                
                if body != null then
                    body.walk(visitor);
                fi
            fi

            accept(visitor);
        si
    si
si
