namespace Syntax.Tree.Type is
    use Generic;

    use Source;

    class FUNCTION_GROUP: NODE, Iterable[FUNCTION] is
        functions: List[FUNCTION];
        
        Iterator: Iterator[FUNCTION] is
            return functions.Iterator;
        si

        init(location: LOCATION) is
            super.init(location);
            
            self.functions = new Vector[FUNCTION]();
        si

        add(function: FUNCTION) is
            functions.add(function);
        si

        accept(visitor: Visitor) is
            visitor.visit(self);
        si

        walk(visitor: Visitor) is
            if !visitor.pre(self) then
                for f in functions do
                    f.walk(visitor);
                od
            fi
            accept(visitor);
        si

        rewrite(m: Dict[Tree.NODE,Tree.NODE]) is
            var nf = new Vector[FUNCTION]();

            for f in functions do
                nf.add(cast FUNCTION(m[f]));
            od

            functions = nf;
        si
    si
si
