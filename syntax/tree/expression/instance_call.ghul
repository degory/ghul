namespace Syntax.Tree.Expression is
    use System;
    use Generic;

    use Source;

    class INSTANCE_CALL: NODE  is
        self_: NODE;
        name: String;
        arguments: LIST;

        children: Iterable[Tree.NODE] =>
            [self, arguments]: Tree.NODE;        
        
        init(
            location: LOCATION,
            self_: NODE,
            name: String,
            arguments: LIST
        )
        is
            super.init(location);

            self.self_ = self_;
            self.name = name;
            self.arguments = arguments;
        si

        accept(visitor: Visitor) is
            visitor.visit(self);
        si

        walk(visitor: Visitor) is
            if !visitor.pre(self) then
                self_.walk(visitor);
                arguments.walk(visitor);
            fi
            accept(visitor);
        si

        rewrite(m: Dict[Tree.NODE,Tree.NODE]) is
            self_ = cast NODE(m[self_]);
            arguments = cast LIST(m[arguments]);
        si
    si
si
