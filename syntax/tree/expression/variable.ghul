namespace Syntax.Tree.Expression is
    use Generic;

    use Source;
    
    class VARIABLE: NODE is
        identifier: Identifier.NODE;
        initializer: NODE;
        type_expression: TypeExpression.NODE;

        children: Iterable[Tree.NODE] is
            var result = new Vector[Tree.NODE]();

            result.add(identifier);
            result.add(type_expression);

            if initializer != null then
                result.add(initializer);
            fi

            return result;
        si

        init(
            location: LOCATION,
            identifier: Identifier.NODE,
            type_expression: TypeExpression.NODE,
            initializer: NODE
        )
        is
            super.init(location);
            
            self.identifier = identifier;
            self.type_expression = type_expression;
            self.initializer = initializer;
        si

        accept(visitor: Visitor) is
            visitor.visit(self);
        si

        walk(visitor: Visitor) is
            if !visitor.pre(self) then
                identifier.walk(visitor);
                type_expression.walk(visitor);

                if initializer != null then
                    initializer.walk(visitor);
                fi
            fi

            accept(visitor);
        si

        rewrite(m: Dict[Tree.NODE,Tree.NODE]) is
            identifier = cast Identifier.NODE(m[identifier]);
            type_expression = cast TypeExpression.NODE(m[type_expression]);
            initializer = cast NODE(m[initializer]);
        si
    si
si
