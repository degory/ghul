namespace Syntax.Tree.Identifier is
    use System;
    use Generic;

    use Source;

    class NODE: Tree.NODE, Iterable[String] is
        name: String;

        qualifier: NODE => null;
        
        qualifier_names: List[String] => new String[](0);

        names: List[String] => [name]: String;

        is_incomplete: bool => name =~ "__incomplete";

        Iterator: Generic.Iterator[String] => ([name]: String).Iterator;

        init(location: LOCATION, name: String) is
            super.init(location);

            if name == null then
                poison();
                name = "__poisoned";
            fi

            self.name = name;
        si

        copy() -> NODE =>
            new NODE(location, name);

        accept(visitor: Visitor) is
            visitor.visit(self);
        si
    si

    class NONE: NODE is
        names: List[String] => new String[](0);

        init(location: LOCATION) is
            super.init(location, null);
        si        
    si

    class QUALIFIED: NODE is
        _qualifier: NODE;

        completion_target: LOCATION;        

        qualifier: NODE is
            return _qualifier;
        si

        children: Iterable[Tree.NODE] is
            var result = new Vector[Tree.NODE](1);

            if _qualifier? then
                result.add(_qualifier);
            fi

            return result;
        si

        qualifier_names: List[String] is
            var result = new Vector[String]();
            var p = qualifier;

            while p? do
                result.add(p.name);
                p = p.qualifier;
            od

            return result;
        si

        names: List[String] is
            var result = new Vector[String]();

            result.add(name);

            var p = qualifier;

            while p? do
                result.add(p.name);
                p = p.qualifier;
            od

            return result;
        si

        Iterator: Generic.Iterator[String] is
            var result = new Vector[String]();

            result.add(name);

            var p = qualifier;

            while p? do
                result.add(p.name);
                p = p.qualifier;
            od
            
            return result.Iterator;
        si        

        init(location: LOCATION, qualifier: NODE, name: String, completion_target: LOCATION) is
            super.init(location, name);

            self._qualifier = qualifier;
            self.completion_target = completion_target;

            poison(qualifier.is_poisoned);
        si

        copy() -> QUALIFIED is
            var np: NODE;

            if qualifier? then
                np = qualifier.copy();
            fi

            return new QUALIFIED(
                location,
                np,
                name,
                completion_target
            );
        si

        accept(visitor: Visitor) is
            visitor.visit(self);
        si

        walk(visitor: Visitor) is
            if !visitor.pre(self) then
                if qualifier? then
                    qualifier.walk(visitor);
                fi
            fi

            accept(visitor);
        si
    si
si
