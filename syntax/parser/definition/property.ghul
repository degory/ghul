namespace Syntax.Parser.Definition is
    use System;
    use Source;

    class PROPERTY: BASE[Tree.Definition.PROPERTY] is
        identifier_parser: Parser[Tree.Identifier.NODE];
        type_parser: Parser[Tree.TypeExpression.NODE];
        modifier_list_parser: Parser[Tree.Modifier.LIST];
        definition_list_parser: Parser[Tree.Definition.LIST];
        body_parser: Parser[Tree.Body.NODE];

        init(
            identifier_parser: Parser[Tree.Identifier.NODE],
            type_parser: Parser[Tree.TypeExpression.NODE],
            modifier_list_parser: Parser[Tree.Modifier.LIST],
            definition_list_parser: Parser[Tree.Definition.LIST],
            body_parser: Parser[Tree.Body.NODE]
        ) is
            super.init();

            self.identifier_parser = identifier_parser;
            self.type_parser = type_parser;
            self.modifier_list_parser = modifier_list_parser;
            self.definition_list_parser = definition_list_parser;
            self.body_parser = body_parser;
        si

        parse(context: CONTEXT) -> Tree.Definition.PROPERTY is
            var fail = false;

            var start = context.location;
            var name = identifier_parser.parse(context);
            var type_expression: Tree.TypeExpression.NODE;

            if context.current.token == Lexical.TOKEN.COLON then
                context.next_token();
                type_expression = type_parser.parse(context);

                if type_expression == null || isa Tree.TypeExpression.UNDEFINED(type_expression) then
                    IO.Std.err.println("bad type expression: " + type_expression);
                    fail = true;
                else
                    IO.Std.err.println("good type expression: " + type_expression);
                fi                
            else
                type_expression = new Tree.TypeExpression.INFER(context.location);
            fi

            var modifiers = modifier_list_parser.parse(context);
            var read_body: Tree.Body.NODE;
            var assign_body: Tree.Body.NODE;
            var setter_argument_name: Tree.Identifier.NODE;
            var expect_semicolon = true;

            do
                if context.current.token == Lexical.TOKEN.ASSIGN then
                    if setter_argument_name? then
                        context.error(context.location, "replacing assign");
                    fi

                    context.next_token();
                    setter_argument_name = identifier_parser.parse(context);
                    expect_semicolon = context.current.token == Lexical.TOKEN.ARROW_FAT;
                    assign_body = body_parser.parse(context);

                    if context.current.token == Lexical.TOKEN.COMMA then
                        context.next_token();
                    else
                        break;
                    fi
                elif [Lexical.TOKEN.IS, Lexical.TOKEN.ARROW_FAT].contains(context.current.token) then
                    expect_semicolon = context.current.token == Lexical.TOKEN.ARROW_FAT;

                    if read_body? then
                        context.error(context.location, "replacing read");
                    fi

                    read_body = body_parser.parse(context);

                    if context.current.token == Lexical.TOKEN.COMMA then
                        context.next_token();
                    else
                        break;
                    fi
                elif context.current_token == Lexical.TOKEN.SEMICOLON then
                    break;
                else
                    if !fail then
                        context.error(context.location, "unexpected input in property");
                    fi

                    fail = true;

                    break;
                fi

                expect_semicolon = true;
            od

            if context.in_trait && read_body == null then
                read_body = new Tree.Body.NULL(context.location);
            fi            

            var result =
                new Tree.Definition.PROPERTY(
                    start::context.location, 
                    type_expression, 
                    name, 
                    modifiers, 
                    read_body, 
                    setter_argument_name, 
                    assign_body
                );

            if expect_semicolon then
                if !fail || context.current.token == Lexical.TOKEN.SEMICOLON then
                    context.next_token(Lexical.TOKEN.SEMICOLON);
                fi
            fi

            if !fail then
                IO.Std.err.println("no fail");
                return result;
            else
                IO.Std.err.println("yes fail");
                return null;
            fi
        si
    si
si
