namespace Syntax is
    namespace Parser is
        namespace Definition is
            use System;
            use Generic;
            use Source;
            class LIST : BASE[Tree.Definition.LIST]  is
                definition_parser: Parser[Tree.Definition.NODE];
                description: String is
                    return "definition list";
                si

                init(definition_parser: Parser[Tree.Definition.NODE]) -> void is
                    super.init();
                    self.definition_parser = definition_parser;
                si

                parse(context: CONTEXT) -> Tree.Definition.LIST is
                    var start = context.location;
                    var end = context.location;
                    var definitions = new Vector[Tree.Definition.NODE]();
                    while context.current.token != Lexical.TOKEN.SI do
                        var definition = definition_parser.parse(context);
                        if definition != null then
                            end = definition.location;
                            definitions.add(definition);
                        fi
                    od
                    return new Tree.Definition.LIST(start .. end, definitions);
                si

            si

        si
    si
si
