namespace Syntax.Parser.Identifier is
    use System;

    use Source;

    class QUALIFIED: BASE[Tree.Identifier.NODE] is
        description: String => "qualified identifier";

        init() is
            super.init();
        si

        parse(context: CONTEXT) -> Tree.Identifier.NODE is
            var start = context.location;
            
            if context.expect_token(Lexical.TOKEN.IDENTIFIER, syntax_error_message) then
                var name = context.current.string;
                var result = new Tree.Identifier.NODE(context.location, name);

                context.next_token();

                while context.current.token == Lexical.TOKEN.DOT do
                    let completion_target_start = context.location;
                
                    context.next_token();
                    if context.expect_token(Lexical.TOKEN.IDENTIFIER, syntax_error_message) then
                        result = new Tree.Identifier.QUALIFIED(start::context.location, result, context.current.string, completion_target_start::context.location);
                        context.next_token();
                    else
                        return null;
                    fi
                od

                return result;
            fi
        si
    si
si
