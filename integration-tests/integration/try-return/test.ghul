namespace Test is
    @IF.not.v3()
    use Std = System.Console;
    @IF.not.v3()
    use object = System.Object;
    @IF.not.v3()
    use string = System.String;

    @IF.v3()
    use Std = IO.Std;

        use System.Exception;

    class Main is
        entry() static is
            

            let v1 = finally_should_run_after_return_value(123);
            Std.out.write_line("result 1 is: " + v1);

            let v2 = finally_should_run_after_return_default_value(123);
            Std.out.write_line("result 2 is: " + v2);

            let v3 = return_from_finally_should_override_return_from_try_body(123);
            Std.out.write_line("result 3 is: " + v3);

            finally_should_run_after_return_void(123);

            let v5 = return_value_should_pass_through_nested_trys();
            Std.out.write_line("result 5 is: " + v5);

            let v6 = return_from_catch(333);
            Std.out.write_line("result 6 is: " + v6);

            let v7 = return_from_finally_should_override_return_from_catch(333);
            Std.out.write_line("result 7 is: " + v7);
        si

        finally_should_run_after_return_value(value: int) -> int static is
            try
                Std.out.write_line("will return: " + value * 2);
                return value * 2;
            finally
                Std.out.write_line("finally: " + value);
            yrt

            throw new System.Exception("should not be reachable"); // not reachable
        si
        
        finally_should_run_after_return_default_value(value: int) -> int static is
            try
                Std.out.write_line("will return: " + value * 2);
                return;
            finally
                Std.out.write_line("finally: " + value);
            yrt

            throw new System.Exception("should not be reachable"); // not reachable
        si

        return_from_finally_should_override_return_from_try_body(value: int) -> int static is
            try
                Std.out.write_line("will return: " + value * 2);
                return value * 2;
            finally
                Std.out.write_line("finally: " + value);
                return value * 3;
            yrt

            throw new System.Exception("should not be reachable"); // not reachable            
        si        

        finally_should_run_after_return_void(value: int) static is
            try
                Std.out.write_line("will return: " + value * 2);
                return;
            finally
                Std.out.write_line("finally: " + value);
            yrt

            throw new System.Exception("should not be reachable"); // not reachable
        si

        return_value_should_pass_through_nested_trys() -> int static is
            try
                try
                    return 123;
                catch ex: Exception
                finally
                    Std.out.write_line("finally 1");
                yrt
                
            catch ex: Exception

            finally
                Std.out.write_line("finally 2");
            yrt
            
            throw new System.Exception("should not be reachable"); // not reachable
        si
        
        return_from_catch(value: int) -> int static is
            try
                throw new Exception("oops");
            catch ex: Exception
                return value * 2;
            yrt            

            throw new System.Exception("should not be reachable"); // not reachable
        si

        return_from_finally_should_override_return_from_catch(value: int) -> int static is
            try
                throw new Exception("oops");
            catch ex: Exception
                return value * 2;
            finally
                return value * 3;
            yrt
            
            throw new System.Exception("should not be reachable"); // not reachable
        si
    si
si