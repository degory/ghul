namespace Test is








    use Std = IO.Std;

        
    class Main is
        entry() static is
            
            
            let rats = [RAT(), RAT(), RAT()];

            let dog = DOG("Spot");
            let cat = CAT("Mr Tibbles");
            let chimera = CHIMERA("Nigel");

            for r in rats do
                vocalise(r);
                digest(r);
            od
            
            vocalise(dog);
            vocalise(dog);
            digest(dog);

            vocalise(cat);
            vocalise(cat);
            digest(cat);

            vocalise(chimera);
            vocalise(chimera);
            rend(chimera);
            digest(chimera);
        si

        vocalise(v: Vocaliser) static is
            Std.out.write("" + v.name + " says ");
            v.vocalise();
        si

        rend(r: Render) static is
            Std.out.write("" + r.name + " roars ");
            r.rend();
        si

        digest(d: Digester) static is
            Std.out.write("" + d.name + " goes ");
            d.digest();
        si        
    si

    trait Named is
        name: string;
    si    

    trait Digester: Named is
        digest();
    si
    
    trait Vocaliser: Named is
        vocalise();
    si

    trait Render: Named is
        rend();
    si

    class WithName: Named is
        name: string;
        
        init(name: string) is
            self.name = name;
        si
    si

    class RAT: Vocaliser, Digester is
        name: string => "Shrug";

        init() is si

        vocalise() is
            Std.out.write_line("meep!");
        si

        digest() is
            Std.out.write_line("frrrp");
        si        
    si
        
    class DOG: WithName, Vocaliser, Digester is
        init(name: string) is
            super.init(name);
        si

        vocalise() is
            Std.out.write_line("bark!");
        si

        digest() is
            Std.out.write_line("burp!");
        si
        
    si

    class CAT: WithName, Digester, Named, Vocaliser is
        init(name: string) is
            super.init(name);
        si

        vocalise() is
            Std.out.write_line("meow!");
        si

        digest() is
            Std.out.write_line("purrrrrrp!");
        si
    si

    class CHIMERA: WithName, Render, Vocaliser, Digester is
        name: string;

        init(name: string) is
            self.name = name;
        si

        rend() is
            Std.out.write_line("grrrrargh!");
        si
        
        vocalise() is
            Std.out.write_line("baeow! meark!");                                
        si
        
        digest() is
            Std.out.write_line("burarkmeowp!");
        si        
    si
si