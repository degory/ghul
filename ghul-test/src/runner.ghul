namespace Tester is
    use System.String;
    use System.Exception;
    use System.Console;
    use System.Text.StringBuilder;

    use Collections.Iterable;
    use Collections.List;
    use Collections.MutableList;
    use Collections.Map;
    use Collections.MutableMap;
    use Collections.LIST;
    use Collections.MAP;

    use System.IO2.File;
    use System.IO2.Directory;

    use System.Diagnostics.Process;
    use System.Diagnostics.ProcessStartInfo;

    use Shim.COUNT;
    use Shim.JOIN;
    use Shim.MAPPER;

    class RUNNER is
        _collate_environment: MutableMap[String,String] static;

        _host_cli: String;
        _target_cli: String;

        _failure: FAILURE;
        _use_installed_compiler: bool;

        _test: TEST;

        init(
            host_cli: String,
            target_cli: String,
            use_installed_compiler: bool,
            test: TEST
        ) is
            _host_cli = host_cli;
            _target_cli = target_cli;
            _use_installed_compiler = use_installed_compiler;
            _test = test;

            if !_collate_environment? then
                _collate_environment = new MAP[String,String]();
                _collate_environment.add("LC_COLLATE", "C");                    
            fi
        si

        get_result() -> Result is
            if _failure? then
                return _failure;
            else
                return new PASS(_test);
            fi
        si

        get_run() -> () -> Result
            is               
                return () -> Result is
                    let result = run_test();
                    
                    return result;
                si;
            si
        
        run_test() -> Result is
            try
                if new COUNT[String](Directory.get_files(_test.path, "disabled*")).result > 0 then
                    return new SKIP(_test);
                fi

                _run_test();

                clean_up();

                mark_if_failed();

                return get_result();
            catch ex: Exception
                return new FAILURE(_test, ex.message, ex.stack_trace);
            yrt
        si

        _run_test() is
            if new Shim.COUNT[String](_test.source_files).result == 0 then
                add_fail("no source files found");
                return;
            fi

            let build_succeeded = run_compiler();

            if build_succeeded then
                if _test.is_build_fail_expected then
                    add_fail("unexpected build success: " + _test.path);

                    return;
                fi
            else
                if !_test.is_build_fail_expected then
                    add_fail("unexpected build failure", "compiler.out");

                    return;
                fi
            fi

            run_sed();

            run_grep("error:", "sed.out", "err.grep");
                 
            run_grep("warn:", "sed.out", "warn.grep");

            run_sort("err.grep", "err.sort");

            run_sort("warn.grep", "warn.sort");

            run_diff("errors", "err.expected", "err.sort", "err.diff", true);

            run_diff("warnings", "warn.expected", "warn.sort", "warn.diff", true);

            run_diff_il_out();

            if !build_succeeded then
                return;
            fi

            if !run_binary() then
                return;
            fi

            run_diff("test output", "run.expected", "run.out", "run.diff", true);
        si

        clean_up() is
            if !_failure? then
                delete_files([
                    "binary.exe",
                    "binary.exe.mdb",
                    "binary.runtimeconfig.json",                    
                    "compiler.out",
                    "sed.out",
                    "warn.grep",
                    "warn.diff",
                    "warn.diff",
                    "err.grep",
                    "err.sort",
                    "err.diff",
                    "out.il",
                    "il.out",
                    "il.diff",
                    "run.out",
                    "run.err",
                    "run.diff"
                ]);
            fi            
        si
        
        run_binary() -> bool is
            let binary_path = get_path("binary.exe");

            if !File.exists(binary_path) then
                add_fail("no binary found: " + binary_path);
                return false;
            fi

            let exit_status =
                run(
                    _target_cli,
                    ["binary.exe"],
                    5000,
                    "run.out",
                    "run.err"
                );
            
            if 
                exit_status != 0
            then
                add_fail("binary exited with non-zero status (" + exit_status + ")", "run.err");
                return false;
            fi

            return true;
        si
        
        run_diff_il_out() is
            let want_delete_il_out = false;
            let il_out_path = get_path("il.out"); 

            if !File.exists(il_out_path) then
                File.create(il_out_path).close();
            fi
            
            run_diff("IL output", "il.expected", "il.out", "il.diff", true);

            if want_delete_il_out then
                File.delete(il_out_path);
            fi            
        si
       
        run_compiler() -> bool is
            let arguments = new LIST[String]();

            arguments.add(get_compiler_binary_path());

            arguments.add_range(["--library-prefix", get_library_prefix_path()]);

            if _test.flags? && _test.flags.length > 0 then
                arguments.add_range(
                    new MAPPER[String,String](
                        _test.flags.split([' ']),
                        (flag: String) -> String => flag.trim()
                    )
                );                
            fi
            
            arguments.add_range(_test.source_files);

            arguments.add_range(["-o", "binary.exe"]);                
            
            let result = run(
                _host_cli,
                arguments,
                5000,
                null,
                "compiler.out"
            );

            if !File.exists(get_path("compiler.out")) then
                throw new Exception("compiler output file not found");
            fi

            if result != 0 then
                return false;
            fi

            if !read_all_text("compiler.out").contains("*** succeeded ***") then
                throw new Exception("compiler success exit status but success message not found in output");
            fi

            return File.exists(get_path("binary.exe"));
        si

        run_sed() -> bool is
            run(
                "sed",
                ["-f", get_library_prefix_path() + "/test.sed", "compiler.out"],
                1000,
                "sed.out",
                null,
                _collate_environment
            );
        si

        run_grep(pattern: String, in_file: String, out_file: String) -> bool is
            return
                run(
                    "fgrep",
                    [pattern, in_file],
                    5000,
                    out_file,
                    null,
                    _collate_environment
                ) != 0;
        si

        run_sort(in_file: String, out_file: String) -> bool is
            return
                run(
                    "sort",
                    [in_file, "--output=" + out_file],
                    5000,
                    null,
                    null,
                    _collate_environment
                ) != 0;            
        si
        
        run_diff(
            description: String, 
            expected_file_name: String, 
            actual_file_name: String, 
            out_file_name: String, 
            ignore_space_changes: bool
        ) -> bool
        is
            let expected_path = get_path(expected_file_name);
            let actual_path = get_path(actual_file_name);
            let out_path = get_path(out_file_name);

            if
                !File.exists(expected_path)
            then
                expected_file_name = "/dev/null";
            fi

            if
                !File.exists(actual_path)
            then
                return false;
            fi

            let args: String[];

            if ignore_space_changes then
                args = ["-b", "--strip-trailing-cr", expected_file_name, actual_file_name];
            else
                args = [expected_file_name, actual_file_name];
            fi

            let result = run(
                "diff",
                args,
                5000,
                out_file_name,
                null
            );

            if result != 0 then
                add_fail(description, out_file_name);
            fi
                
            return result == 0;
        si

        run(
            path: String,
            arguments: Iterable[String],
            timeout: int,
            out_file: String,
            err_file: String
        ) -> int => 
            run(path, arguments, timeout, out_file, err_file, null);

        run(
            path: String,
            arguments: Iterable[String],
            timeout: int,
            out_file: String,
            err_file: String,
            environment: Iterable[Collections.Pair[String,String]]
        ) -> int
        is
            let process = start(path, arguments);
            
            return wait(process, timeout, out_file, err_file);
        si

        start(path: String, arguments: Iterable[String]) -> Process =>
            start(path, arguments, null);

        start(path: String, arguments: Iterable[String], environment: Iterable[Collections.Pair[String,String]]) -> Process is
            let start_info = new System.Diagnostics.ProcessStartInfo();

            start_info.file_name = path;

            start_info.use_shell_execute = false;

            if environment? then
                for element in environment do
                    start_info.environment.add(element.key, element.value);
                od                        
            fi

            // FIXME: StartInfo.arguments_list is broken:
            // for arg in arguments do
            //     start_info.argument_list.add(arg);                
            // od

            let arguments_string = new StringBuilder();

            if arguments? then
                start_info.arguments =
                new JOIN[String](
                    new MAPPER[String,String](
                        arguments,
                        (arg: String) -> String => arg.replace("\\", "\\\\").replace("\"", "\"\"")
                    ),
                    " "
                ).result;                
            fi
            
            start_info.working_directory = _test.path;

            start_info.redirect_standard_output = true;
            start_info.redirect_standard_error = true;
            start_info.use_shell_execute = false;
            
            let result = Process.start(start_info);

            if result == null then
                throw new Exception("failed to start process: " + path + " " + new JOIN[String](arguments, " "));
            fi
            
            return result;
        si

        wait(process: Process, timeout: int, out_file_name: String, err_file_name: String) -> int is
            if out_file_name then
                let stdout = process.standard_output.read_to_end();
                write_all_text(out_file_name, stdout);
            fi
            
            if err_file_name then
                let stderr = process.standard_error.read_to_end();
                write_all_text(err_file_name, stderr);
            fi
            
            process.wait_for_exit(5000);

            return process.exit_code;
        si

        add_fail(message: String) is
            add_fail(message, null);
        si
        
        add_fail(message: String, file_name: String) is
            let details: DETAILS;

            if file_name? then
                details = new DETAILS(message, read_all_text_quiet(file_name));
            else
                details = new DETAILS(message);                
            fi

            if !_failure? then
                _failure = new FAILURE(_test, details);
            else
                _failure.add(details);
            fi
        si

        mark_if_failed() is
            if _failure? then
                File.create(get_path("failed")).close();
            else
                delete_file("failed");
            fi
        si

        delete_files(file_names: Iterable[String]) is
            for file_name in file_names do
                delete_file(file_name);
            od
        si
        
        delete_file(file_name: String) is
            let file_path = get_path(file_name);

            if File.exists(file_path) then
                File.delete(file_path);
            fi
        si
        
        get_compiler_binary_path() -> String is
            if _use_installed_compiler then
                return "/usr/bin/ghul.exe";
            fi
            
            return
                new StringBuilder()
                    .insert(0, "../", _test.depth)
                    .append("ghul.exe")  
                    .to_string();
        si

        get_library_prefix_path() -> String is
            if _use_installed_compiler then
                return "/usr/lib/ghul";
            fi

            return 
                new StringBuilder()
                    .insert(0, "../", _test.depth)
                    .append("lib")
                    .to_string();
        si

        get_path(file_name: String) -> String =>
            _test.path + "/" + file_name;

        write_all_text(file_name: String, text: String) is
            File.write_all_text(get_path(file_name), text);
        si
        
        read_all_text(file_name: String) -> String =>
            File.read_all_text(get_path(file_name));

        read_all_text_quiet(file_name: String) -> String is
            let file_path = get_path(file_name);

            if !File.exists(file_path) then
                return "(could not read file: " + file_path + ")";
            fi
            
            return File.read_all_text(get_path(file_name));
        si
    si
si
