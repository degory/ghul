namespace IR is
    use IO.Std;

    use Semantic.Types.Type;
    use Values.Value;

    class TEMP is
        _context: IR.CONTEXT;
        _next_id: int static;
        _name: string is
            if suffix > 0 then
                return "'.{prefix}.{id}.{suffix}'";
            else
                return "'.{prefix}.{id}'";
            fi
        si

        id: int;
        prefix: string;
        suffix: int;
        type: Type;

        init(context: IR.CONTEXT, prefix: string, suffix: int, type: Type) is
            _context = context;
            self.prefix = prefix;
            self.suffix = suffix;

            self.id = _next_id;
            _next_id = _next_id + 1;

            self.type = type;

            declare();
        si

        init(context: IR.CONTEXT, prefix: string, value: Value) is
            init(context, prefix, 0, value);
        si

        init(context: IR.CONTEXT, prefix: string, suffix: int, value: Value) is
            init(context, prefix, suffix, value.type);

            store(value);
        si

        init(context: IR.CONTEXT, prefix: string, type: Type) is
            init(context, prefix, 0, type);
        si

        reset_id() static is
            _next_id = 0;
        si

        declare() is
            let buffer = new System.Text.StringBuilder();

            buffer
                .append(".locals init (");

            type.gen_type(buffer);

            buffer
                .append(' ')
                .append(_name)
                .append(')');

            _context.write_line(buffer);
        si

        load() -> Values.Load.TEMP => new Values.Load.TEMP(_name, type);

        store(value: Value) is
            Value.gen(value, _context);

            _context.write_line("stloc {_name}");
        si        
    si
si