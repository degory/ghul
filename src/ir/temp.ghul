namespace IR is
        use System;

    class TEMP is
        _context: IR.CONTEXT;
        _next_id: int static;

        id: int;
        suffix: int;
        type: Semantic.Type.BASE;

        name: String is
            if suffix > 0 then
                return "'.temp." + id + "." + suffix + "'";
            else
                return "'.temp." + id + "'";                                
            fi            
        si

        init(context: IR.CONTEXT, suffix: int, type: Semantic.Type.BASE) is
            self._context = context;
            self.suffix = suffix;

            self.id = _next_id;
            _next_id = _next_id + 1;

            self.type = type;

            declare();
        si

        init(context: IR.CONTEXT, value: Semantic.Graph.Value.BASE) is
            init(context, 0, value);
        si

        init(context: IR.CONTEXT, suffix: int, value: Semantic.Graph.Value.BASE) is
            init(context, suffix, value.type);

            store(value);
        si

        init(context: IR.CONTEXT, type: Semantic.Type.BASE) is
            init(context, 0, type);
        si

        declare() is            
            _context.write_line(".locals init (" + type.il_type_name + " " + name + ")");
        si

        load() -> Semantic.Graph.Value.Load.TEMP => new Semantic.Graph.Value.Load.TEMP(name, type);

        store(value: Semantic.Graph.Value.BASE) is
            Semantic.Graph.Value.BASE.gen(value, _context);

            _context.write_line("stloc " + name);
        si        
    si
si