namespace IR is
    use IO.Std;

    use Semantic.Types.Type;
    use Values.Value;

    class TEMP is
        _block: IR.Values.BLOCK;
        _next_id: int static;
        _name: string is
            if suffix > 0 then
                return "'.{prefix}.{id}.{suffix}'";
            else
                return "'.{prefix}.{id}'";
            fi
        si

        id: int;
        prefix: string;
        suffix: int;
        type: Type;

        init(block: IR.Values.BLOCK, prefix: string, suffix: int, type: Type) is
            _block = block;
            self.prefix = prefix;
            self.suffix = suffix;

            self.id = _next_id;
            _next_id = _next_id + 1;

            self.type = type;

            declare();
        si

        init(block: IR.Values.BLOCK, prefix: string, value: Value) is
            init(block, prefix, 0, value);
        si

        init(block: IR.Values.BLOCK, prefix: string, suffix: int, value: Value) is
            init(block, prefix, suffix, value.type);

            store(value);
        si

        init(block: IR.Values.BLOCK, prefix: string, type: Type) is
            init(block, prefix, 0, type);
        si

        reset_id() static is
            _next_id = 0;
        si

        declare() is
            let buffer = System.Text.StringBuilder();

            buffer
                .append(".locals init (");

            type.gen_type(buffer);

            buffer
                .append(' ')
                .append(_name)
                .append(')');

            _block.add(RAW(type, buffer.to_string()));
        si

        load() -> Values.Load.TEMP => Values.Load.TEMP(_name, type);

        store(value: Value) is
            _block.add(value);

            _block.add(RAW(type, "stloc {_name}"));
        si        
    si
si