namespace IR is
    use Generic;
    use System;

    use Semantic.Graph.Value.Call;

    class AssemblyOutput is
        want_comments: bool;
        
        init(want_comments: bool) is
            self.want_comments = want_comments;
        si

        write(value: Object) is
            throw new NotImplementedException("assembly output must implement write_line");            
        si
        
        write_line(value: Object) is
            write(value);
            write("\n");
        si
        
        write_line(indent: int, value: Object) is
            if want_comments then
                write_indent(indent);
                write_line(value);
            else
                write_line(value);
            fi
        si

        write_line(indent: int, value: Object, comment: Object) is
            if want_comments then
                write_indent(indent);
                write_line("" + value + "        // " + comment);
            else
                write_line(value);
            fi
        si

        write_indent(indent: int) is
            for i in 0..indent do
                write("  ");
            od            
        si

        close() is
            
        si
    si

    class FILE_ASSEMBLER_OUTPUT: AssemblyOutput is
        path: String;
        writer: IO2.TextWriter;

        init(
            path: String,
            writer: IO2.TextWriter,
            want_comments: bool
        ) is
            super.init(want_comments);

            self.path = path;
            self.writer = writer;
        si

        write(value: Object) is
            writer.write(value);
        si

        close() is
            writer.close();
        si
    si

    class STUB_ASSEMBLER_OUTPUT: AssemblyOutput is
        init() is
            super.init(false);
        si

        write(value: Object) is
            // do nothing
        si
    si

    class BUFFER_ASSEMBLER_OUTPUT: AssemblyOutput is
        buffer: System.Text.StringBuilder;

        init(want_comments: bool) is
            super.init(want_comments);

            buffer = new System.Text.StringBuilder();
        si

        write(value: Object) is
            buffer.append(value);
        si
    si
    
    class CONTEXT is
        _logger: Logging.Logger;
        _indent: int;

        _seen: Collections.SET[String];
        _outputs: Collections.STACK[AssemblyOutput];

        throw_on_fixme: bool public;

        init(logger: Logging.Logger) is
            _logger = logger;
            _seen = new Collections.SET[String]();
            _outputs = new Collections.STACK[AssemblyOutput]();
            _outputs.push(new STUB_ASSEMBLER_OUTPUT());
        si

        current_output: AssemblyOutput => _outputs.peek();

        fixme(value: Object) is
            if throw_on_fixme then
                throw new System.NotImplementedException("FIXME: " + value);
            else
                write_line("// FIXME: " + value);
            fi            
        si
    
        write_line(value: Object) is
            @IF.dotnet()
            System.Console.write_line(value);

            current_output.write_line(_indent, value);
        si

        write_line(value: Object, comment: Object) is
            @IF.dotnet()
            System.Console.write_line(value);

            current_output.write_line(_indent, value, comment);
        si

        indent() is
            _indent = _indent + 1;
        si

        outdent() is
            _indent = _indent - 1;
        si

        enter_stub() is
            _outputs.push(new STUB_ASSEMBLER_OUTPUT());
        si

        leave_stub() is
            assert
                _outputs.count > 0 &&
                isa STUB_ASSEMBLER_OUTPUT(current_output)
            else
                "IL output context stack corrupt";

            _outputs.pop();
        si

        enter_file(path: String, want_comments: bool) is
            let writer: IO2.TextWriter;

            if _seen.contains(path) then
                writer = Shim.FILE.open_append(path);
            else
                writer = Shim.FILE.open_create(path);
                _seen.add(path);
            fi
            
            _outputs.push(new FILE_ASSEMBLER_OUTPUT(path, writer, want_comments));
        si

        leave_file(path: String) is
            assert
                _outputs.count > 0 &&
                isa FILE_ASSEMBLER_OUTPUT(current_output) &&
                cast FILE_ASSEMBLER_OUTPUT(current_output).path =~ path
            else
                "IL output context stack corrupt";
            
            current_output.close();
            _outputs.pop();
        si
        
        enter_buffer(want_comments: bool) is
            _outputs.push(new BUFFER_ASSEMBLER_OUTPUT(want_comments));
        si

        leave_buffer() -> String is
            assert
                _outputs.count > 0 &&
                isa BUFFER_ASSEMBLER_OUTPUT(current_output)
            else
                "IL output context stack corrupt";

            let result = cast BUFFER_ASSEMBLER_OUTPUT(current_output).buffer;

            _outputs.pop();

            return result.to_string();
        si        
    si
si
