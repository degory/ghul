namespace Semantic.Symbols is
    use IO.Std;

    use System.Text.StringBuilder;
    
    use IoC;
    use Logging;
    use Source;

    use IR.Values.Value;

    class Closure: Function, Types.Typed is
        _is_loading_captures: bool;

        captures: Collections.SET[Symbol];
        is_self_captured: bool;
        is_delegate: bool;
        is_anon_func_or_delegate: bool => is_delegate \/ !frame?;

        could_be_delegate: bool => is_self_captured /\ captures.count <= 1;

        frame: FRAME;

        description: string => "" + type + " // closure";
        short_description: string => description;

        symbol_kind: SYMBOL_KIND => SYMBOL_KIND.FUNCTION;
        completion_kind: COMPLETION_KIND => COMPLETION_KIND.FUNCTION;

        qualified_name: string => "[closure]" + name; // FIXME

        il_name: string => name; // FIXME
        owner_il_name: string => "[closure]"; // FIXME

        il_body: string public;

        is_closure: bool => true;
        
        init(location: LOCATION, owner: Scope, name: string, enclosing_scope: Scope) is
            super.init(location, owner, name, enclosing_scope);            
        si

        convert_to_delegate() is
            assert could_be_delegate;

            is_self_captured = false;
            captures = null;
            frame = null;
            is_delegate = true;
        si
        
        find_or_add_capture(variable: Symbol) -> Field is            
            if !frame? then
                captures = new Collections.SET[Symbol]();

                frame = new Semantic.Symbols.FRAME(cast Classy(owner), self);
            fi

            if variable? then
                if !captures.contains(variable) then
                    captures.add(variable);

                    return frame.declare_captured(variable.name, variable.type, IoC.CONTAINER.instance.symbol_definition_locations);
                else

                    return frame.get_captured(variable.name);
                fi
            else
                if !is_self_captured then
                    is_self_captured = true;

                    captures.add(null);

                    return frame.declare_captured("$self", owner.type, IoC.CONTAINER.instance.symbol_definition_locations);
                else                    
                    return frame.get_captured("$self");
                fi                
            fi
        si

        find_or_add_capture_self() -> Field => find_or_add_capture(null);

        set_void_return_type() is
            return_type = new Types.ANY();
        si

        load(location: LOCATION, loader: SYMBOL_LOADER) -> Value is
            throw new System.NotImplementedException("abstract");
        si

        load_closure(location: LOCATION, loader: SYMBOL_LOADER) -> Value is
            if is_delegate then
                return loader.load_instance_anonymous_function(self, type);
            fi

            let actual_arguments = new Collections.LIST[Value]();

            for c in captures do
                if c == null then
                    let outer_self = loader.load_outer_self(location);

                    actual_arguments.add(outer_self);
                else
                    let outer_captured_value =
                        c.load_outer(
                            location, 
                            null,
                            loader
                        );

                    actual_arguments.add(outer_captured_value);
                fi                
            od

            owner = frame;

            let frame_instance = frame.get_create_instance(actual_arguments);

            let result = loader.load_closure(self, frame_instance, type);

            return result;
        si

        load_outer_self(location: Source.LOCATION, loader: SYMBOL_LOADER) -> Value is
            if is_delegate then
                let context = loader._symbol_table.current_instance_context;

                return new IR.Values.Load.SELF(context, context.type);                
            fi
            
            let is_captured = false;

            let stack = IoC.CONTAINER.instance.symbol_table._stack;

            let index = stack.count - 1;
            let seen_self = false;

            while index >= 0 do
                let scope = stack[index];

                if scope.is_namespace then
                    break;
                fi

                if seen_self /\ scope.is_capture_context then
                    if scope.is_closure then
                        let c = cast Closure(scope);

                        // FIXME: may not be needed - load_self() should capture self:
                        c.find_or_add_capture_self();

                        return c.load_self(location, loader);
                    fi
                fi

                if scope == self then
                    seen_self = true;
                fi

                index = index - 1;
            od

            if !is_captured then
                // load self normally: 
                let context = loader._symbol_table.current_instance_context;

                return new IR.Values.Load.SELF(context, context.type);
            fi
        si

        load_self(location: Source.LOCATION, loader: SYMBOL_LOADER) -> Value is
            if is_delegate then
                let context = loader._symbol_table.current_instance_context;

                return new IR.Values.Load.SELF(context, context.type);                
            fi

            let field = find_or_add_capture_self();

            return loader.load_instance_variable(location, new IR.Values.Load.SELF(frame, frame.type), field);
        si

        load_outer_captured_value(location: LOCATION, symbol: Variable, loader: SYMBOL_LOADER) -> Value is
            let is_captured = false;

            let stack = IoC.CONTAINER.instance.symbol_table._stack;

            let index = stack.count - 1;
            let seen_self = false;

            while index >= 0 do
                let scope = stack[index];

                if seen_self /\ scope.is_capture_context then
                    if symbol.owner == scope then
                        // FIXME: use the symbol loader
                        if isa LOCAL_ARGUMENT(symbol) then
                            return new IR.Values.Load.LOCAL_ARGUMENT(symbol);
                        else
                            return new IR.Values.Load.LOCAL_VARIABLE(symbol);
                        fi                        
                    elif scope.is_closure then
                        let c = cast Closure(scope);

                        // FIXME: probably redundant
                        c.find_or_add_capture(symbol);

                        c.load_captured_value(location, symbol, loader);

                        let result = c.load_captured_value(location, symbol, loader);

                        return result;
                    fi     
                fi

                if scope == self then
                    seen_self = true;
                fi

                index = index - 1;
            od
        si
        
        load_captured_value(location: LOCATION, symbol: Variable, loader: SYMBOL_LOADER) -> Value is
            symbol.is_captured = true;

            let field = find_or_add_capture(symbol);

            return loader.load_instance_variable(LOCATION.internal, new IR.Values.Load.SELF(frame, frame.type), field);
        si

        gen_frame(context: IR.CONTEXT, symbol_loader: SYMBOL_LOADER) is
            if frame? then
                frame.gen_all(context, symbol_loader);                
            fi
        si        

        to_string() -> string => "[closure " + name + "]";
    si

    class INSTANCE_CLOSURE: Closure is
        is_instance: bool => true;

        init(location: LOCATION, owner: Scope, name: string, enclosing_scope: Scope) is
            super.init(location, owner, name, enclosing_scope);
        si

        declare_closure(location: LOCATION, name: string, owner: Scope, enclosing: Scope, symbol_definition_listener: SymbolDefinitionListener) -> Symbol is
            var result = new Symbols.INSTANCE_CLOSURE(location, owner, name, enclosing);

            declare(location, result, null);

            return result;
        si

        load(location: LOCATION, loader: SYMBOL_LOADER) -> Value is
            if frame? then
                return super.load_closure(location, loader);
            fi
            
            return loader.load_instance_anonymous_function(self, type);
        si

        gen_calling_convention(buffer: StringBuilder) is
            buffer.append("instance ");
        si

        gen_flags(buffer: StringBuilder) is
            buffer
                .append("hidebysig specialname ");
        si

        to_string() -> string => "[instance closure " + name + "]";
    si

    class STATIC_CLOSURE: Closure is
        init(location: LOCATION, owner: Scope, name: string, enclosing_scope: Scope) is
            super.init(location, owner, name, enclosing_scope);
        si

        declare_closure(location: LOCATION, name: string, owner: Scope, enclosing: Scope, symbol_definition_listener: SymbolDefinitionListener) -> Symbol is
            var result = new Symbols.STATIC_CLOSURE(location, owner, name, enclosing);

            declare(location, result, null);

            return result;
        si

        load(location: LOCATION, loader: SYMBOL_LOADER) -> Value is
            if frame? then
                return super.load_closure(location, loader);
            fi
            
            return loader.load_static_anonymous_function(self, type);
        si

        gen_calling_convention(buffer: StringBuilder) is
            // if we captured values then we're an instance method on our frame, even if the closure is defined in a static method:
            if frame? then
                buffer.append("instance ");
            fi
        si

        gen_flags(buffer: StringBuilder) is
            if frame? then
                buffer
                    .append("hidebysig specialname ");
            else
                buffer
                    .append("hidebysig specialname static ");
            fi
        si

        to_string() -> string => "[static closure " + name + "]";
    si
si
