namespace Semantic.Symbols is
    @IF.not.v3() use object = System.Object;
    @IF.not.v3() use string = System.String;
    @IF.not.v3() use Std = System.Console;

    use System.Text.StringBuilder;

    use Collections.Iterable;
    use Collections.LIST;
    
    use IoC;
    use Logging;
    use Source;

    use IR.Values.Value;

    use Types.Type;

    class Method: Function is
        init(location: LOCATION, owner: Scope, name: string, enclosing_scope: Scope) is
            super.init(location, owner, name, enclosing_scope);
        si
    si
    
    class INSTANCE_METHOD: Method is
        description: string => qualified_name + "(" + argument_descriptions + ") -> " + return_type + " // method";
        symbol_kind: SYMBOL_KIND => SYMBOL_KIND.METHOD;
        completion_kind: COMPLETION_KIND => COMPLETION_KIND.METHOD;

        is_instance: bool => true;

        init(location: LOCATION, owner: Scope, name: string, enclosing_scope: Scope) is
            super.init(location, owner, name, enclosing_scope);
        si

        declare_closure(location: LOCATION, name: string, owner: Scope, enclosing: Scope, symbol_definition_listener: SymbolDefinitionListener) -> Scope is
            var result = new Symbols.INSTANCE_CLOSURE(location, owner, name, enclosing);

            declare(location, result, null);

            return result;
        si

        load(location: LOCATION, from: Value, loader: SYMBOL_LOADER) -> Value is
            return loader.load_instance_method(from, self);
        si

        call(location: Source.LOCATION, from: Value, arguments: Collections.LIST[Value], type: Type, caller: FUNCTION_CALLER) -> Value is
            if !from? then
                let context = IoC.CONTAINER.instance.symbol_table.current_function;

                if !context? then
                    IoC.CONTAINER.instance.logger.error(location, "cannot access instance member from non-function context");                    
                elif !context.is_instance then
                    IoC.CONTAINER.instance.logger.error(location, "cannot access instance member from static context");
                fi
            fi
            
            return caller.call_instance_method(from, self, arguments, self.arguments, type);
        si

        try_override(into: Classy, overridee: Function, logger: Logging.Logger) is
            @IF.debug()  Std.error.write_line("instance try override: " + into + " " + self + " <- " + overridee);

            overridee.try_instance_override_me(into, self, logger);
        si

        try_instance_override_me(into: Classy, overrider: Function, logger: Logger) is
            @IF.debug()  Std.error.write_line("instance try override me: " + into + " " + overrider + " <- " + self);

            let return_type_matches = overrider.ensure_return_type_matches(into, self, true, logger);

            let il_name_matches = overrider.ensure_il_name_matches(into, self, "override", logger);

            overrider.add_overridee(self);

            @IF.debug()
            if !into.is_stub && return_type_matches && il_name_matches then
                logger.info(overrider.location, "overrides " + self);                
            fi            
        si

        try_struct_override_me(into: Classy, overrider: Function, logger: Logger) is
            let return_type_matches = overrider.ensure_return_type_matches(into, self, true, logger);

            let il_name_matches = overrider.ensure_il_name_matches(into, self, "override", logger);

            overrider.add_overridee(self);

            @IF.debug() 
            if !into.is_stub && return_type_matches && il_name_matches then
                logger.info(overrider.location, "overrides " + self);                
            fi            
        si

        try_abstract_override_me(into: Classy, overrider: Function, logger: Logger) is
            logger.error(overrider.location, "cannot override abstract method " + self);
        si

        gen_calling_convention(buffer: StringBuilder) is            
            buffer.append("instance ");
        si

        gen_flags(buffer: StringBuilder) is
            if name =~ "init" then
                buffer
                    .append("hidebysig specialname ");
            elif is_internal then
                buffer
                    .append("virtual hidebysig specialname ");
            else
                buffer
                    .append("virtual hidebysig ");
            fi
        si
    si

    class STRUCT_METHOD: INSTANCE_METHOD is
        description: string => qualified_name + "(" + argument_descriptions + ") -> " + return_type + " // method";
        symbol_kind: SYMBOL_KIND => SYMBOL_KIND.METHOD;
        completion_kind: COMPLETION_KIND => COMPLETION_KIND.METHOD;

        override: Function public;

        init(location: LOCATION, owner: Scope, name: string, enclosing_scope: Scope) is
            super.init(location, owner, name, enclosing_scope);
        si

        load(location: LOCATION, from: Value, loader: SYMBOL_LOADER) -> Value is
            return loader.load_struct_method(from, self);
        si

        declare_closure(location: LOCATION, name: string, owner: Scope, enclosing: Scope, symbol_definition_listener: SymbolDefinitionListener) -> Scope is
            var result = new Symbols.INSTANCE_CLOSURE(location, owner, name, enclosing);

            declare(location, result, null);

            return result;
        si

        call(location: Source.LOCATION, from: Value, arguments: Collections.LIST[Value], type: Type, caller: FUNCTION_CALLER) -> Value is
            return caller.call_struct_method(from, self, arguments, self.arguments, type);
        si

        try_override(into: Classy, overridee: Function, logger: Logging.Logger) is
            @IF.debug() Std.error.write_line("try override C: " + self + " " + overridee);
            overridee.try_struct_override_me(into, self, logger);
        si

        try_struct_override_me(into: Classy, overrider: Function, logger: Logger) is
            logger.error(overrider.location, "cannot override struct method " + self);
        si

        try_instance_override_me(into: Classy, overrider: Function, logger: Logger) is
            logger.error(overrider.location, "cannot override struct method " + self);
        si

        try_abstract_override_me(into: Classy, overrider: Function, logger: Logger) is
            logger.error(overrider.location, "cannot override struct method " + self);
        si

        gen_calling_convention(buffer: StringBuilder) is            
            buffer.append("instance ");
        si

        gen_flags(buffer: StringBuilder) is
            if name !~ "init" then
                buffer
                    .append("final virtual hidebysig ");
            elif is_internal then
                buffer
                    .append("hidebysig specialname ");
            else
                buffer
                    .append("hidebysig specialname ");
            fi
        si
    si

    class ABSTRACT_METHOD: INSTANCE_METHOD is
        description: string => qualified_name + "(" + argument_descriptions + ") -> " + return_type + " // abstract method";

        is_abstract: bool => true;

        init(location: LOCATION, owner: Scope, name: string, enclosing_scope: Scope) is
            super.init(location, owner, name, enclosing_scope);

            assert owner? else "abstract method created without owner";
        si

        call(location: Source.LOCATION, from: Value, arguments: Collections.LIST[Value], type: Type, caller: FUNCTION_CALLER) -> Value is
            return caller.call_abstract_method(from, self, arguments, self.arguments, type);
        si

        try_pull_down_into(
            into: Classy,
            other_overridee_symbols: Collections.Iterable[Symbol],
            logger: Logging.Logger
        ) is
            if !into.is_trait && !into.is_stub then
                logger.error(into.location, "must implement " + self);
            fi
            
            into.add_member(self);
        si

        try_override(into: Classy, overridee: Function, logger: Logging.Logger) is
            @IF.debug()  Std.error.write_line("try override D: " + self + " " + overridee);
            overridee.try_abstract_override_me(into, self, logger);
        si

        try_instance_override_me(into: Classy, overrider: Function, logger: Logger) is
            let return_type_matches = overrider.ensure_return_type_matches(into, self, false, logger);

            let il_name_matches = overrider.ensure_il_name_matches(into, self, "implement", logger);

            overrider.add_overridee(self);
            
            @IF.debug() 
            if !into.is_stub && return_type_matches && il_name_matches then
                logger.info(overrider.location, "implements " + self);                
            fi            
        si

        try_struct_override_me(into: Classy, overrider: Function, logger: Logger) is
            let return_type_matches = overrider.ensure_return_type_matches(into, self, false, logger);

            let il_name_matches = overrider.ensure_il_name_matches(into, self, "implement", logger);

            overrider.add_overridee(self);

            @IF.debug() 
            if !into.is_stub && return_type_matches && il_name_matches then
                logger.info(overrider.location, "implements " + self);
            fi
        si

        try_abstract_override_me(into: Classy, overrider: Function, logger: Logger) is
            @IF.not.release()
            logger.info(overrider.location, "hides " + self + " (case D)");
        si

        gen_calling_convention(buffer: StringBuilder) is            
            buffer.append("instance ");
        si

        gen_flags(buffer: StringBuilder) is
            if is_internal then
                buffer
                    .append("virtual hidebysig specialname abstract ");
            else
                buffer
                    .append("virtual hidebysig abstract ");
            fi
        si
    si

    class STATIC_METHOD: Method is
        description: string => qualified_name + "(" + argument_descriptions + ") -> " + return_type + " // class method";
        symbol_kind: SYMBOL_KIND => SYMBOL_KIND.METHOD;
        completion_kind: COMPLETION_KIND => COMPLETION_KIND.METHOD;

        override: Function public;

        init(location: LOCATION, owner: Scope, name: string, enclosing_scope: Scope) is
            super.init(location, owner, name, enclosing_scope);
        si

        declare_closure(location: LOCATION, name: string, owner: Scope, enclosing: Scope, symbol_definition_listener: SymbolDefinitionListener) -> Scope is
            var result = new Symbols.STATIC_CLOSURE(location, owner, name, enclosing);

            declare(location, result, null);

            return result;
        si

        load(location: LOCATION, from: Value, loader: SYMBOL_LOADER) -> Value is
            return loader.load_static_method(self);
        si

        call(location: Source.LOCATION, from: Value, arguments: Collections.LIST[Value], type: Type, caller: FUNCTION_CALLER) -> Value is
            return caller.call_static_method(self, arguments, self.arguments, type);
        si

        try_override(into: Classy, overridee: Function, logger: Logging.Logger) is
            // This is arguably not an issue as the hidden instance remains available via super.method()
            if !is_internal && overridee.is_instance then
                logger.warn(self.location, "static method hides " + overridee);                
            fi
        si

        // Don't believe it makes sense to report an instance method hiding a static method
        // as the static method remains available via CLASS.method()

        try_instance_override_me(into: Classy, overrider: Function, logger: Logger) is
            // if !is_internal then
            //     logger.info(overrider.location, "hides static method " + self);
            // fi
        si

        try_struct_override_me(into: Classy, overrider: Function, logger: Logger) is
            // if !is_internal then
            //     logger.info(overrider.location, "hides static method " + self);
            // fi
        si

        try_abstract_override_me(into: Classy, overrider: Function, logger: Logger) is
            // if !is_internal then
            //     logger.info(overrider.location, "hides static method " + self);
            // fi
        si

        gen_flags(buffer: StringBuilder) is
            if is_internal then
                buffer
                    .append("hidebysig specialname static ");
            else
                buffer
                    .append("hidebysig static ");
            fi
        si
    si
si
