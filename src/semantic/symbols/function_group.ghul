namespace Semantic.Symbols is
    use System.NotImplementedException;
    
    use IoC;
    use Logging;
    use Source;

    use IR.Values.Value;

    use Types.Type;

    class FUNCTION_GROUP: Symbol, Types.Typed is
        // _overrides_resolved: bool;

        description: string => qualified_name + "(...) // function group";
        short_description: string => name + "(...)";
        symbol_kind: SYMBOL_KIND => SYMBOL_KIND.FUNCTION;
        completion_kind: COMPLETION_KIND => COMPLETION_KIND.FUNCTION;

        type: Type;

        set_type(value: Type) is type = value; si

        functions: Collections.MutableList[Function];

        init(location: LOCATION, owner: Scope, name: string) is
            super.init(location, owner, name);

            type = new Types.FUNCTION_GROUP(name, self);
            functions = new Collections.LIST[Function]();
        si

        specialize_function_group(type_map: Collections.MAP[string,Type], owner: GENERIC) -> FUNCTION_GROUP is
            let result = cast FUNCTION_GROUP(memberwise_clone());

            result.specialized_from = self;

            result.type = new Types.FUNCTION_GROUP(
                name,
                result
            );

            result.functions = new Collections.LIST[Function](functions);

            for i in 0..functions.count do
                result.functions[i] = functions[i].specialize_function(type_map, owner);
            od

            return result;
        si

        specialize(type_map: Collections.MAP[string,Type], owner: GENERIC) -> Symbol =>
            specialize_function_group(type_map, owner);

        add(function: Function) is
            assert function.name =~ self.name;
                        
            functions.add(function);
        si

        load(location: LOCATION, from: Value, loader: SYMBOL_LOADER) -> Value is
            return loader.load_function_group(from, self);
        si

        call(location: Source.LOCATION, from: Value, arguments: Collections.LIST[Value], type: Type, caller: FUNCTION_CALLER) -> IR.Values.Value is
            throw new NotImplementedException("cannot call unresolved function overload group: " + self);
        si

        collapse_group_if_single_member() -> Symbols.Symbol is
            if functions.count == 1 then
                return functions[0];
            else
                return self;
            fi
        si

        collapse_group_or_null() -> Symbols.Function is
            if functions.count == 1 then
                return functions[0];
            else
                return null;
            fi
        si

        to_string() -> string =>
            description + " [" + functions| + "]";
    si
si