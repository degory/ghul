namespace Semantic.Symbols is
    use System.Text.StringBuilder;
    
    use IoC;
    use Logging;
    use Source;

    use Types.Type;

    // FIXME: should inherit from TYPE
    class FUNCTION_GENERIC_ARGUMENT: Scoped, Types.SettableTyped is
        type: Type;
        set_type(value: Type) is type = value; si
        
        is_value_type: bool => true;
        is_type: bool => true;
        is_type_variable: bool => true;

        _ancestor_type: Type;

        index: int;

        ancestors: Collections.List[Type] is
            if _ancestor_type == null then
                return new Collections.LIST[Type](0);
            fi

            return [_ancestor_type];
        si

        set_ancestor_type(ancestor: Type) is
            _ancestor_type = ancestor;
        si        
        
        symbol_kind: SYMBOL_KIND => SYMBOL_KIND.TYPE_PARAMETER;
        completion_kind: COMPLETION_KIND => COMPLETION_KIND.TYPE_PARAMETER;

        description: string => name + ": " + type + " // type variable";
        short_description: string => description;

        init(location: LOCATION, owner: Scope, name: string, index: int) is
            init(location, owner, name, new Types.FUNCTION_GENERIC_ARGUMENT(self));

            self.index = index; 
        si

        init(location: LOCATION, owner: Scope, name: string, type: Type) is
            super.init(location, owner, name);

            self.type = type;
        si

        get_ancestor(i: int) -> Type is
            return ancestors[i];
        si

        find_member(name: string) -> Symbol is
            if _ancestor_type? then
                return _ancestor_type.find_member(name);
            fi
        si

        find_member_matches(prefix: string, matches: Collections.MAP[string, Symbol]) is
            if _ancestor_type? then
                _ancestor_type.scope.find_member_matches(prefix, matches);
            fi
        si

        specialize(type_map: Collections.Map[string,Type], owner: GENERIC) -> Symbol is
            if type_map.contains_key(name) then
                return new TYPE(
                    location,
                    self,
                    name,
                    type_map[name]
                );                
            fi

            return self;
        si

        gen_type(buffer: StringBuilder) is
            buffer
                .append("!!")
                .append(index)
                .append(' ');
        si        

        gen_reference(buffer: StringBuilder) is
            gen_type(buffer);
        si

        gen_class_name(buffer: StringBuilder) is
            gen_type(buffer);
        si
    si
si
