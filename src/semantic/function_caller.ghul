namespace Semantic is
    use IO.Std;

    use Logging;

    use Types.Type;

    use IR.Values;

    class FUNCTION_CALLER is
        _symbol_table: SYMBOL_TABLE;
        _value_boxer: IR.VALUE_BOXER;

        // public so container can initialize it to break a
        // circular dependency:
        loader: SYMBOL_LOADER public;

        init(
            symbol_table: SYMBOL_TABLE,
            value_boxer: IR.VALUE_BOXER,
            blocks: IR.BlockContext
        )
        is
            super.init();

            _value_boxer = value_boxer;
            _symbol_table = symbol_table;
        si

        call_innate_function(
            function: Symbols.INNATE_FUNCTION, 
            arguments: Collections.List[Value],
            argument_types: Collections.List[Type],
            type: Type
        ) -> Value =>
            new Call.INNATE(
                function,
                null,
                arguments,
                type
            );

        call_innate_function(
            function: Symbols.INNATE_METHOD, 
            from: Value, 
            arguments: Collections.List[Value],
            argument_types: Collections.List[Type],
            type: Type
        ) -> Value =>
            new Call.INNATE(
                function,
                from,
                arguments,
                type
            );
            
        call_global_function(
            function: Symbols.Function, 
            arguments: Collections.List[Value], 
            argument_types: Collections.List[Type],
            type: Type
        ) -> Value =>
            new Call.GLOBAL(
                function,
                _value_boxer.box_arguments(arguments, function.arguments),
                type
            );        

        call_instance_method(
            location: Source.LOCATION,
            from: Value, 
            function: Symbols.Function, 
            arguments: Collections.List[Value], 
            argument_types: Collections.List[Type],
            type: Type
        ) -> Value is
            if from == null then
                from = loader.load_self(location);
            fi

            if from.type.is_type_variable then
                from = new CONSTRAINED(from);
            fi

            return 
                new Call.INSTANCE(
                    _value_boxer.box_if_value(from),
                    function,
                    _value_boxer.box_arguments(arguments, function.arguments),
                    type
                );
        si

        call_struct_method(
            location: Source.LOCATION,
            from: Value, 
            function: Symbols.Function, 
            arguments: Collections.List[Value], 
            argument_types: Collections.List[Type],
            type: Type
        ) -> Value is
            if from == null then
                from = loader.load_self(location);
            else
                from = new ADDRESS(from);
            fi

            return 
                new Call.STRUCT(
                    from,
                    function,
                    _value_boxer.box_arguments(arguments, function.arguments),
                    type
                );
        si

        call_abstract_method(
            location: Source.LOCATION,
            from: Value, 
            function: Symbols.Function, 
            arguments: Collections.List[Value], 
            argument_types: Collections.List[Type],
            type: Type
        ) -> Value is
            if from == null then
                from = loader.load_self(location);
            elif from.is_value_type then
                // Don't believe this is possible in practice except
                // for calls to IDisposable.Dispose() generated by the
                // compiler for `let use` statements.
                from = new CONSTRAINED(from);
            fi

            return 
                new Call.INSTANCE(
                    from,
                    function,
                    _value_boxer.box_arguments(arguments, function.arguments),
                    type
                );
        si

        call_static_method(
            function: Symbols.Function,
            arguments: Collections.List[Value],
            argument_types: Collections.List[Type],
            type: Type
        ) -> Value =>
            new Call.STATIC(
                function,
                _value_boxer.box_arguments(arguments, function.arguments),
                type
            );
    si
si