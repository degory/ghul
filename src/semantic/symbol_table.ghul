namespace Semantic is
    use IO.Std;

    use Logging;
    use Source;

    use Syntax.Trees;

    // the symbol table helps keeps track of what scope the compiler is currently working in. it does not
    // manage searching for symbols within or across scopes: that's handled by the scopes themselves

    class SYMBOL_TABLE is
        _logger: Logger;
        _scopes: Collections.MAP[Node,Scope];
        _stack: Collections.LIST[Scope];

        stack: Collections.List[Scope] => _stack;

        current_scope: Scope =>
            if _stack.count > 0 then
                _stack[_stack.count-1];
            else
                null;
            fi;

        global_scope: Scope => _stack[0];

        current_namespace_context: NamespaceContext is
            for scope in Collections.LIST_REVERSE_ITERATOR[Scope](_stack) do
                if isa NamespaceContext(scope) then
                    return cast NamespaceContext(scope);
                fi
            od
            
            assert false else "no current namespace";
        si

        current_declaration_context: DeclarationContext is
            for scope in Collections.LIST_REVERSE_ITERATOR[Scope](_stack) do
                if isa DeclarationContext(scope) then
                    return cast DeclarationContext(scope);
                fi
            od
        si

        current_instance_context: Symbols.Classy is
            for scope in Collections.LIST_REVERSE_ITERATOR[Scope](_stack) do
                if scope.is_instance_context then
                    return cast Symbols.Classy(scope);
                fi
            od
        si

        current_union_context: Symbols.UNION is
            for scope in Collections.LIST_REVERSE_ITERATOR[Scope](_stack) do
                if isa Symbols.UNION(scope) then
                    return cast Symbols.UNION(scope);
                fi
            od
        si
        
        current_function: Symbols.Function is
            for scope in Collections.LIST_REVERSE_ITERATOR[Scope](_stack) do
                if isa Symbols.Function(scope) then
                    return cast Symbols.Function(scope);
                fi
            od
        si

        current_closure_context: ClosureContext is
            for scope in Collections.LIST_REVERSE_ITERATOR[Scope](_stack) do
                if isa ClosureContext(scope) then
                    return cast ClosureContext(scope);
                fi
            od
            
            assert false else "no current closure context";
        si

        current_capture_context: Symbols.Symbol is
            for scope in Collections.LIST_REVERSE_ITERATOR[Scope](_stack) do
                if scope.is_capture_context then
                    return cast Symbols.Symbol(scope);
                fi
            od
        si

        current_closure: Symbols.Closure is
            for scope in Collections.LIST_REVERSE_ITERATOR[Scope](_stack) do
                let result = cast Symbols.Closure(scope);

                if result? then
                    return result;
                fi
            od
        si

        current_function_group: Symbols.FUNCTION_GROUP is
            for scope in Collections.LIST_REVERSE_ITERATOR[Scope](_stack) do
                if isa Symbols.FUNCTION_GROUP(scope) then
                    return cast Symbols.FUNCTION_GROUP(scope);
                fi
            od
        si

        current_property: Symbols.Property is
            for scope in Collections.LIST_REVERSE_ITERATOR[Scope](_stack) do
                if isa Symbols.Property(scope) then
                    return cast Symbols.Property(scope);
                fi
            od
        si

        all_classes: Collections.Iterable[Symbols.Classy] =>
            _scopes.values | .filter(s => s.is_classy) .map(s => cast Symbols.Classy(s));

        all_namespaces: Collections.Iterable[Symbols.NAMESPACE] =>
            _scopes.values | .filter(s => s.is_namespace) .map(s => cast Symbols.NAMESPACE(s));
                    
        init(logger: Logger) is
            _logger = logger;

            clear();
        si

        dump_counts() is
            Std.error.write_line("scopes map: {_scopes.count}");
        si        

        clear() is
            _stack = Collections.LIST[Scope](50);
            _scopes = Collections.MAP[Node,Scope](65521);

            enter_scope(Symbols.NAMESPACE(LOCATION.internal, "", null, "", true));
        si

        scope_for(node: Node) -> Scope is
            if _scopes.contains_key(node) then
                return _scopes[node];
            fi
        si

        associate_node_with_scope(node: Node, scope: Scope) is
            _scopes[node] = scope;
        si

        mark_scope_stack() -> int is
            return _stack.count;
        si

        release_scope_stack(mark: int) is
            assert mark <= _stack.count;

            while _stack.count > mark do
                _stack.remove_at(_stack.count - 1);
            od
        si

        enter_scope(node: Syntax.Trees.Node) is
            if !_scopes.contains_key(node) then
                _logger.poison(node.location, "no scope found for {node.get_type()}");

                return;
            fi

            let scope = _scopes[node];

            enter_scope(scope);
        si

        enter_scope(scope: Scope) is
            _stack.add(scope);
        si

        leave_scope(node: Syntax.Trees.Node) is
            if !_scopes.contains_key(node) then
                _logger.poison(node.location, "no scope found for node");
                return;
            fi

            leave_scope(_scopes[node]);
        si

        leave_scope(scope: Scope) is
            assert current_scope == scope else "scope stack corrupt: stack top: {current_scope} leaving scope: {scope}";
            
            _stack.remove_at(_stack.count - 1);
        si

        leave_scope() is
            let scope = _stack[_stack.count - 1];

            _stack.remove_at(_stack.count - 1);
        si

        to_string() -> string is
            let result = System.Text.StringBuilder();

            result.append("symbol table:\n");

            for scope in Collections.LIST_REVERSE_ITERATOR[Semantic.Scope](_stack) do
                result
                    .append(scope)
                    .append("\n");
            od

            result.append("\n");

            return result.to_string();
        si
    si
si
