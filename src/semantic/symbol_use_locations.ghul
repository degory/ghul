namespace Semantic is
    use System;
    use Generic;
    use Collections;

    use Source;

    trait SymbolUseListener is
        add_symbol_use(location: LOCATION, symbol: Symbol.BASE);
    si

    class SYMBOL_USE_LOCATIONS: Object, SymbolUseListener is
        _symbol_use_map: LOCATION_MAP[Symbol.BASE];
        _symbol_reference_map: Collections.MAP[Symbol.BASE,Collections.SET[LOCATION]];

        init() is
            clear();
        si

        dump_counts() is
            _symbol_use_map.dump_counts();
            System.Console.error.write_line("symbol reference map: " + _symbol_reference_map.count);
        si        

        clear() is
            _symbol_use_map = new LOCATION_MAP[Symbol.BASE]();
            _symbol_reference_map = new Collections.MAP[Symbol.BASE,Collections.SET[LOCATION]](65521);
        si

        add_symbol_use(location: LOCATION, symbol: Symbol.BASE) is
            if 
                symbol? && 
                symbol.location.file_name !~ "internal" &&
                !Shim.STR.starts_with(symbol.name, "__")
            then
                while symbol.specialized_from? do
                    symbol = symbol.specialized_from;
                od

                _symbol_use_map.put(location, symbol);

                let set_ = get_references_set(symbol);

                if !set_.contains(location) then
                    // FIXME: why is this getting called multiple times for the same symbol and location?
                    set_.add(location);

                fi
            fi
        si

        find_definition_from_use(file_name: String, line: int, column: int) -> Symbol.BASE is
            return _symbol_use_map.find(file_name, line, column);
        si

        find_references_to_symbol(symbol: Symbol.BASE) -> Collections.Iterable[LOCATION] is
            return get_references_set(symbol);    
        si

        get_references_set(symbol: Symbol.BASE) -> Collections.SET[LOCATION] is
            if _symbol_reference_map.contains_key(symbol) then
                return _symbol_reference_map[symbol];                
            fi

            let result = new Collections.SET[LOCATION]();

            _symbol_reference_map[symbol] = result;

            return result;            
        si
    si

    class LOCATION_MAP[T] is
        file_name_to_file: Collections.MAP[String, Collections.MAP[int, Collections.LIST[Pair[LOCATION,T]]]];

        init() is
            file_name_to_file = new Collections.MAP[String, Collections.MAP[int, Collections.LIST[Pair[LOCATION,T]]]]();
        si

        dump_counts() is
            System.Console.error.write_line("file name to file map: " + file_name_to_file.count);
        si

        put(location: LOCATION, value: T) is
            let file = _get_file(location.file_name);

            if file == null then
                file = new Collections.MAP[int, Collections.LIST[Pair[LOCATION,T]]]();
                file_name_to_file[location.file_name] = file;
            fi

            let line = location.start_line;

            let list: Collections.LIST[Pair[LOCATION,T]];

            if file.contains_key(line) then
                list = file[line];
            else
                list = new Collections.LIST[Pair[LOCATION,T]]();
                file[line] = list;
            fi

            list.add(new Pair[LOCATION,T](location,value));
        si

        find(file_name: String, line: int, column: int) -> T is
            let file = _get_file(file_name);

            if !file.contains_key(line) then
                return null;
            fi

            let list = file[line];

            if list == null then
                return null;
            fi

            let line_column = LOCATION.pair(line, column);

            for p in list do
                if p.key.contains(line_column) then
                    return p.value;
                fi
            od
        si
        
        _get_file(file_name: String) -> Collections.MAP[int, Collections.LIST[Pair[LOCATION,T]]] is
            if file_name_to_file.contains_key(file_name) then
                return file_name_to_file[file_name];
            fi
        si        
    si    
si
