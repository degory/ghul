namespace Semantic is
    use System.Exception;

    use Logging;
    use Source;

    use Types.Type;

    class OVERLOAD_RESOLVE_RESULT is
        function: Symbols.Function;
        score: Types.MATCH;

        init(function: Symbols.Function, score: Types.MATCH) is
            self.function = function;
            self.score = score;
        si
    si

    class OVERLOAD_MATCHES_RESULT is
        results: Collections.List[Symbols.Function];
        best_result_index: int;
        current_parameter_index: int public;

        init(
            results: Collections.List[Symbols.Function],
            best_result_index: int,
            current_parameter_index: int
        )
        is
            self.results = results;
            self.best_result_index = best_result_index;
            self.current_parameter_index = current_parameter_index;
        si
    si

    class OVERLOAD_RESOLVER is
        _logger: Logger;

        init(logger: Logger) is
            super.init();

            _logger = logger;
        si

        resolve(
            location: LOCATION,
            group: Symbols.FUNCTION_GROUP,
            arguments: Collections.List[Type],
            want_infer: bool,
            want_instance: bool
        ) -> OVERLOAD_RESOLVE_RESULT
        is
            let mark = _logger.mark();

            try
                return _resolve(location, group, arguments, want_infer, want_instance);
            catch e: Exception
                _logger.release(mark);

                _logger.exception(location, e, "exception resolving overload: {group} arguments {arguments}");
                return null;

            finally
                _logger.release(mark);
            yrt
        si

        find_matches(
            group: Symbols.FUNCTION_GROUP,
            arguments: Collections.List[Type]
        ) -> OVERLOAD_MATCHES_RESULT
        is
            let mark = _logger.mark();

            try
                return _find_matches(group, arguments);
            catch e: Exception
                _logger.release(mark);

                _logger.exception(null, e, "exception resolving overload: {group} arguments {arguments}");
                return null;

            finally
                _logger.release(mark);
            yrt
        si

        _resolve(
            location: LOCATION,
            group: Symbols.FUNCTION_GROUP,
            arguments: Collections.List[Type],
            want_infer: bool,
            want_instance: bool
        ) -> OVERLOAD_RESOLVE_RESULT
        is
            if group == null \/ group.functions == null \/ arguments == null then
                return null;
            fi

            // TODO pass this in as this is not a reliable check - user could
            // be calling "init" directly
            let is_constructor_call = group.name =~ "init";

            let use debug_dispose = debug_enter();

            debug("resolve overloads {group.name} {arguments |}");

            // FIXME: this could just as well be applied to any parameters of generic type, not just anon functions
            let needs_second_call = want_infer /\ arguments | .any(a => a? /\ a.is_function_with_any_implicit_argument_types);

            let is_ambiguous = false;

            let best_score = cast int (Types.MATCH.DIFFERENT);
            let result: Symbols.Function;

            let ambiguous_matches: Collections.LIST[Symbols.Function];

            let functions_to_search = group.functions | .filter(f => want_instance \/ !f.is_instance);

            for f in functions_to_search do
                debug("try function {f}");

                let actual = f;

                if f.arguments == null then
                    return OVERLOAD_RESOLVE_RESULT(f, Types.MATCH.DIFFERENT);
                elif f.arguments.count == 0 /\ arguments.count == 0 then
                    return OVERLOAD_RESOLVE_RESULT(f, Types.MATCH.SAME);
                elif f.arguments.count == arguments.count then
                    let any_wild = false;
                    let score = cast int(Types.MATCH.SAME);

                    let owner_symbol = cast Symbols.Classy(f.owner);

                    for i in 0..f.arguments.count do
                        let match: Types.MATCH;

                        let f_arg = f.arguments[i];
                        let arg = arguments[i];

                        debug("argument #{i} try {f_arg} from {arg}...");

                        if f_arg? /\ arg? then
                            match = f_arg.compare(arg);

                            if match == Types.MATCH.DIFFERENT then
                                debug("at least one different argument");

                                score = cast int(Types.MATCH.DIFFERENT);
                                break;
                            fi

                            if match == Types.MATCH.WILD then
                                // FIXME we really need to distinguish between generic arguments that
                                // are bound vs unbound. We could do this by copying the type and
                                // marking it not-wild, but retaining the symbol so type equality checks
                                // still work correctly
                                // also I'm not convinced our split type comparison mechanism, where we
                                // return WILD for a range of possible matches and then confirm if they
                                // really match later, is actually safe. We maybe should at least filter
                                // out more obviously wrong WILD results earlier in the process, plus
                                // is_wild types should only be wild in one comparison direction 

                                // it could be this check is sufficient though in practice:
                                if !f.is_generic /\ f.is_instance /\ !is_constructor_call then
                                    debug("ignore wild match argument {if f_arg.is_wild then "is wild" else "is not wild" fi}");
                                    match = Types.MATCH.DIFFERENT;
                                elif !f_arg.is_wild then
                                    debug("ignore not really wild match argument {if f_arg.is_wild then "is wild" else "is not wild" fi}");
                                    match = Types.MATCH.DIFFERENT;
                                else
                                    any_wild = true;
                                    debug("at least one wild match argument {if f_arg.is_wild then "is wild" else "is not wild" fi}");
                                    match = Types.MATCH.SAME;
                                fi
                            fi
                        else
                            match = Types.MATCH.ASSIGNABLE;
                        fi

                        score = cast int(score) + cast int(match);
                    od

                    let function_generic_argument_bindings = 
                        if any_wild /\ f.is_generic /\ score <= best_score /\ score != cast int(Types.MATCH.DIFFERENT) then
                            debug("generic function: try bind generic arguments");
                            f.try_bind_generic_arguments(location, arguments);
                        else
                            null;
                        fi;

                    if function_generic_argument_bindings? then
                        debug("have generic argument bindings");
                    fi

                    let owner_generic_argument_bindings =
                        if any_wild /\ owner_symbol? /\ owner_symbol.is_generic /\
                            (!want_instance \/ f.name =~ "init") /\
                            score <= best_score /\ score != cast int(Types.MATCH.DIFFERENT)
                        then
                            debug("generic owner: try bind generic arguments");
                            f.try_bind_owner_generic_arguments(location, arguments);
                        else
                            null
                        fi;

                    if owner_generic_argument_bindings? then
                        debug("have owner generic argument bindings");                        
                    fi

                    if function_generic_argument_bindings? then
                        if function_generic_argument_bindings.is_bound then
                            debug("function arguments bound, will specialize");
                            actual = f.specialize_function(function_generic_argument_bindings.map, null);
                        elif want_infer then
                            debug("function arguments not bound but want infer, will specialize and return partial match");

                            actual = f.specialize_function(function_generic_argument_bindings.map, null);
                            score = cast int(Types.MATCH.PARTIAL);
                        else
                            debug("function arguments not bound and don't want infer score different");
                            score = cast int(Types.MATCH.DIFFERENT);
                        fi
                    elif owner_generic_argument_bindings? then
                        if owner_generic_argument_bindings.is_bound then
                            debug("owner arguments bound, will try to specialize");
                            let specialized_owner = Symbols.GENERIC.try_create_from(location, owner_symbol, owner_generic_argument_bindings.map);

                            if specialized_owner? then
                                actual = specialized_owner.find_specialized_function(f);
                                debug("owner specialize succeeded: actual function now {actual} score unchanged");
                            else
                                debug("owner specialize failed: score different");

                                score = cast int(Types.MATCH.DIFFERENT);
                            fi
                        elif want_infer then
                            debug("owner arguments not bound but want infer, will specialize");
                            let specialized_owner = Symbols.GENERIC.try_create_from(location, owner_symbol, owner_generic_argument_bindings.map);

                            if specialized_owner? then
                                actual = specialized_owner.find_specialized_function(f);
                                score = cast int(Types.MATCH.PARTIAL);
                                debug("owner specialize succeeded: actual function now {actual} score partial");
                            else
                                debug("owner specialize failed: score different");

                                score = cast int(Types.MATCH.DIFFERENT);
                            fi
                        else
                            debug("owner arguments not bound and don't want infer score different");
                            score = cast int(Types.MATCH.DIFFERENT);
                        fi
                    elif any_wild then
                        debug("wild arguments but no generic bindings: not a match");
                        score = cast int(Types.MATCH.DIFFERENT);
                    fi

                    // TODO what if both are bound? Could happen with a generic static method in a generic class 

                    if score == best_score /\ score != cast int(Types.MATCH.DIFFERENT) then
                        if !ambiguous_matches? then
                            ambiguous_matches = Collections.LIST[Symbols.Function]();
                        fi

                        if ambiguous_matches.count == 0 then
                            ambiguous_matches.add(result);
                        fi

                        ambiguous_matches.add(actual);

                        debug("ambiguous matches now {ambiguous_matches |}");

                        is_ambiguous = true;
                    elif score < best_score then
                        if ambiguous_matches? then
                            ambiguous_matches.clear();
                        fi

                        debug("best result now {actual}");

                        is_ambiguous = false;
                        best_score = score;
                        result = actual;
                    fi
                fi
            od

            if is_ambiguous then
                debug("results are ambiguous {ambiguous_matches |}");

                let non_object_matches = 
                    ambiguous_matches |
                        .filter(f => !f.arguments | .any(a => a.is_object));

                let count = non_object_matches | .count();

                if count == 1 then
                    result = non_object_matches | .only();
                    is_ambiguous = false;
                elif count > 1 then
                    ambiguous_matches = Collections.LIST[Symbols.Function](non_object_matches);
                fi
            fi

            if is_ambiguous then
                debug("results are still ambiguous {ambiguous_matches |}");

                let non_generic_matches = 
                    ambiguous_matches | 
                        .filter(f => !f.is_generic);

                let count = non_generic_matches | .count();

                if count == 1 then
                    result = non_generic_matches | .only();
                    is_ambiguous = false;
                elif count > 1 then
                    ambiguous_matches = Collections.LIST[Symbols.Function](non_generic_matches);
                fi
            fi

            if result? /\ !is_ambiguous then
                debug("results not ambiguous {result}");

                if needs_second_call then
                    debug("but needs second call");
                    best_score = cast int(Types.MATCH.PARTIAL);
                fi
                
                return OVERLOAD_RESOLVE_RESULT(result, cast Types.MATCH(best_score));
            fi

            if 
                arguments | .any(a => a.is_error \/ (!want_infer /\ a.is_any))
            then
                debug("no useable result but suppress error");

                return null;
            fi


            let tried = Collections.LIST[Symbols.Function](20);

            for f in functions_to_search do
                if f.arguments.count == arguments.count then
                    tried.add(f);
                fi
            od

            debug("report error tried {tried |}");

            let maybe_static = "";

            if !want_instance then
                maybe_static = "static ";
            fi

            if is_ambiguous then
                _logger.error(
                    location, 
                    "call is ambiguous {group.name}({arguments|}), tried {get_sorted_function_list_as_string(ambiguous_matches)}"
                );
            elif tried.count > 0 then
                _logger.error(
                    location, 
                    "no {maybe_static}overload found for {group.name}({arguments|}), tried {get_sorted_function_list_as_string(tried)}"
                );
            else
                _logger.error(location, "no {maybe_static}overload found for {group.name}({arguments|})");
            fi

            return null;
        si

        get_sorted_function_list_as_string(functions: Collections.Iterable[Symbols.Function]) -> string static =>
            functions |
                .map(f => f.to_string())
                .sort()
                .to_string();        

        _find_matches(
            group: Symbols.FUNCTION_GROUP,
            arguments: Collections.List[Type]
        ) -> OVERLOAD_MATCHES_RESULT
        is
            if group == null \/ group.functions == null \/ arguments == null then
                return null;
            fi

            if group.functions.count == 0 then
                return null;
            fi

            if group.functions.count == 1 \/ arguments.count == 0 then
                return OVERLOAD_MATCHES_RESULT(group.functions, 0, -1);
            fi

            let results = Collections.LIST[Symbols.Function]();

            let best_score = cast int(Types.MATCH.DIFFERENT) * arguments.count;
            let best_index = -1;

            for f in group.functions do
                if f.arguments.count >= arguments.count then
                    let score = cast int(Types.MATCH.SAME);

                    for i in 0..arguments.count do
                        let match: Types.MATCH;

                        if f.arguments[i]? /\ arguments[i]? then
                            match = f.arguments[i].compare(arguments[i]);

                            if match == Types.MATCH.DIFFERENT then
                                score = cast int(Types.MATCH.DIFFERENT);
                            fi
                        else
                            match = Types.MATCH.ASSIGNABLE;
                        fi

                        score = score + cast int(match);
                    od

                    results.add(f);

                    if score < best_score then
                        best_score = score;
                        best_index = results.count - 1;
                    fi
                fi
            od

            return
                OVERLOAD_MATCHES_RESULT(
                    results,
                    best_index,
                    -1
                );
        si
    si
si