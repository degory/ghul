namespace Semantic is
    use System;
    use Generic;

    use Logging;
    use Source;

    class OVERLOAD_RESOLVE_RESULT is
        function: Symbol.Function;
        score: Type.MATCH;

        init(function: Symbol.Function, score: Type.MATCH) is
            self.function = function;
            self.score = score;
        si
    si

    class OVERLOAD_MATCHES_RESULT is
        results: Collections.LIST[Symbol.Function];
        best_result_index: int;
        current_parameter_index: int public;

        init(
            results: Collections.LIST[Symbol.Function],
            best_result_index: int,
            current_parameter_index: int
        )
        is
            self.results = results;
            self.best_result_index = best_result_index;
            self.current_parameter_index = current_parameter_index;
        si
    si

    class OVERLOAD_RESOLVER is
        _logger: Logger;

        init(logger: Logger) is
            super.init();

            _logger = logger;
        si

        resolve(
            location: LOCATION,
            group: Symbol.FUNCTION_GROUP,
            arguments: Collections.LIST[Type.BASE],
            want_error: bool
        ) -> OVERLOAD_RESOLVE_RESULT
        is
            try
                return _resolve(location, group, arguments, want_error);
            catch e: Exception
                System.Console.error.write_line("something went wrong resolving overload: " + group + " arguments " + arguments);
                System.Console.error.write_line("caught: " + e);                
                return null;
            yrt
        si

        find_matches(
            group: Symbol.FUNCTION_GROUP,
            arguments: Collections.LIST[Type.BASE]
        ) -> OVERLOAD_MATCHES_RESULT
        is
            try
                return _find_matches(group, arguments);
            catch e: Exception
                System.Console.error.write_line("something went wrong resolving overload: " + group + " arguments " + arguments);
                System.Console.error.write_line("caught: " + e);
                return null;
            yrt            
        si

        _resolve(
            location: LOCATION,
            group: Symbol.FUNCTION_GROUP,
            arguments: Collections.LIST[Type.BASE],
            want_error: bool            
        ) -> OVERLOAD_RESOLVE_RESULT
        is
            if group == null || group.functions == null || arguments == null then
                return null;
            fi

            let best_score = Type.MATCH.DIFFERENT;
            var result: Symbol.Function;

            for f in group.functions do
                if f.arguments == null then
                    return new OVERLOAD_RESOLVE_RESULT(f, Type.MATCH.DIFFERENT);
                elif f.arguments.count == 0 && arguments.count == 0 then
                    return new OVERLOAD_RESOLVE_RESULT(f, Type.MATCH.SAME);
                elif f.arguments.count == arguments.count then
                    var score = Type.MATCH.SAME;

                    for i in 0..f.arguments.count do
                        var match: Type.MATCH;

                        if f.arguments[i]? && arguments[i]? then
                            match = f.arguments[i].compare(arguments[i]);

                            if match == Type.MATCH.DIFFERENT then
                                score = Type.MATCH.DIFFERENT;
                                break;
                            fi
                        else
                            match = Type.MATCH.ASSIGNABLE;
                        fi

                        score = cast Type.MATCH(cast int(score) + cast int(match));
                    od

                    if score < best_score then
                        best_score = score;
                        result = f;
                    fi
                fi
            od

            if result? then
                return new OVERLOAD_RESOLVE_RESULT(result, best_score);
            fi

            if want_error then
                let tried = new Collections.LIST[Symbol.Function](20);

                for f in group.functions do
                    if f.arguments.count == arguments.count then
                        tried.add(f);
                    fi
                od

                if tried.count > 0 then
                    _logger.error(
                        location, 
                        "no overload found for " + group.name + "(" + new Shim.JOIN_SORTED[Type.BASE](arguments) + "), tried " + new Shim.JOIN_SORTED[Symbol.Function](tried)
                    );
                else
                    _logger.error(location, "no overload found for " + group.name + "(" + new Shim.JOIN_SORTED[Type.BASE](arguments) + ")");
                fi
            fi

            return null;
        si        

        _find_matches(
            group: Symbol.FUNCTION_GROUP,
            arguments: Collections.LIST[Type.BASE]
        ) -> OVERLOAD_MATCHES_RESULT
        is
            if group == null || group.functions == null || arguments == null then
                return null;
            fi

            let results = new Collections.LIST[Symbol.Function]();

            let best_score = Type.MATCH.DIFFERENT;
            var best_index = -1;

            for f in group.functions do
                if f.arguments.count >= arguments.count then
                    var score = Type.MATCH.SAME;

                    for i in 0..arguments.count do
                        var match: Type.MATCH;

                        if f.arguments[i]? && arguments[i]? then
                            match = f.arguments[i].compare(arguments[i]);

                            if match == Type.MATCH.DIFFERENT then
                                score = Type.MATCH.DIFFERENT;
                                break;
                            fi
                        else
                            match = Type.MATCH.ASSIGNABLE;
                        fi

                        score = cast Type.MATCH(cast int(score) + cast int(match));
                    od

                    results.add(f);

                    if score < best_score then
                        best_score = score;
                        best_index = results.count - 1;
                    fi
                fi
            od

            return
                new OVERLOAD_MATCHES_RESULT(
                    results,
                    best_index,
                    -1
                );
        si                
    si
si