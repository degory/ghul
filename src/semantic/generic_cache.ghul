namespace Semantic is
    use System;
    
    class GENERIC_KEY is
        // FIXME: if symbol.owner is correct, then we don't need this
        specialized_for: Symbol.BASE;
        symbol: Symbol.BASE;
        arguments: Collections.LIST[Type.BASE];

        init(specialized_for: Symbol.BASE, symbol: Symbol.BASE, arguments: Collections.LIST[Type.BASE]) is
            self.specialized_for = specialized_for;
            self.symbol = symbol;
            self.arguments = arguments;
        si

        get_hash_code() -> int is
            var result = symbol.get_hash_code();

            result = result + specialized_for.get_hash_code();

            for a in arguments do
                result = result + a.get_hash_code();
            od

            return result;
        si

        @IL.name("Equals")
        =~(other: Object) -> bool is
            if !isa GENERIC_KEY(other) then
                return false;
            fi

            return self =~ cast GENERIC_KEY(other);            
        si

        @IL.name("Equals")
        =~(other: GENERIC_KEY) -> bool is
            if symbol != other.symbol then
                return false;
            fi

            for i in 0..arguments.count do
                if arguments[i] !~ other.arguments[i] then
                    return false;
                fi
            od

            return true;
        si

        to_string() -> String =>
            specialized_for.qualified_name + "[" + new Shim.JOIN[Type.BASE](arguments) + "] / " + symbol.qualified_name;                
    si

    class GENERIC_CACHE is
        specializations: Collections.MAP[GENERIC_KEY,Symbol.BASE];

        // members: Dict[Symbol.BASE,List[Symbol.BASE]];

        init() is
            super.init();

            clear();
        si

        [key: GENERIC_KEY]: Symbol.BASE public
            is 
                let result: Symbol.BASE;

                if specializations.try_get_value(key, result ref) then
                    return result;
                fi
            si,
            = value is
                specializations[key] = value;
            si

        dump_counts() is
            System.Console.error.write_line("specializations: " + specializations.count);
        si        

        clear() is
            specializations = new Collections.MAP[GENERIC_KEY,Symbol.BASE]();
        si
    si
si