namespace Semantic is
    @IF.not.v3() use object = System.Object;
    @IF.not.v3() use string = System.String;
    @IF.not.v3() use Std = System.Console;

    use System;
    
    use Source;

    trait SymbolDefinitionListener is
        add_symbol_definition(location: LOCATION, symbol: Symbols.Symbol);
    si

    class SYMBOL_DEFINITION_LOCATIONS: object, SymbolDefinitionListener is
        _symbol_use_listener: SymbolUseListener;

        _symbol_definition_map: Collections.MAP[string,Collections.LIST[Symbols.Symbol]];
        _workspace_definition_map: Collections.MAP[string,Collections.LIST[Symbols.Symbol]];

        init(symbol_use_listener: SymbolUseListener) is
            _symbol_use_listener = symbol_use_listener;

            clear();
        si

        dump_counts() is
            Std.error.write_line("symbol definition map: " + _symbol_definition_map.count);
            Std.error.write_line("workspace definition map: " + _workspace_definition_map.count);
        si                

        clear() is
            _symbol_definition_map = new Collections.MAP[string,Collections.LIST[Symbols.Symbol]]();
            _workspace_definition_map = new Collections.MAP[string,Collections.LIST[Symbols.Symbol]]();
        si

        add_symbol_definition(location: LOCATION, symbol: Symbols.Symbol) is
            if symbol? && !symbol.location.is_internal && !symbol.name.starts_with("__") then
                get_symbol_list_for_file_name(location.file_name).add(symbol);

                _symbol_use_listener.add_symbol_use(location, symbol);
            fi
        si

        find_definitions_from_file(file_name: string, workspace_search: bool) -> Collections.Iterable[Symbols.Symbol] is
            if workspace_search then
                return get_workspace_list_for_file_name(file_name);
            else
                return get_symbol_list_for_file_name(file_name);
            fi
        si

        get_symbol_list_for_file_name(file_name: string) -> Collections.LIST[Symbols.Symbol] is
            if _symbol_definition_map.contains_key(file_name) then
                return _symbol_definition_map[file_name];
            fi

            let result = new Collections.LIST[Symbols.Symbol]();

            _symbol_definition_map[file_name] = result;

            return result;
        si

        get_workspace_list_for_file_name(file_name: string) -> Collections.LIST[Symbols.Symbol] is
            if _workspace_definition_map.contains_key(file_name) then
                return _workspace_definition_map[file_name];                
            fi
            
            let result = new Collections.LIST[Symbols.Symbol]();

            _workspace_definition_map[file_name] = result;

            for symbol in get_symbol_list_for_file_name(file_name) do
                if !symbol.is_internal then
                    result.add(symbol);
                fi
            od                

            return result;
        si        
    si
si
