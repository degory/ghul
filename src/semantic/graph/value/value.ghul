namespace Semantic.Graph.Value is
    use System;

    use Generic;

    class BASE: Object, Semantic.Type.Typed is
        has_type: bool => false;
        is_value_type: bool => has_type && type.is_value_type;
        is_self: bool => false;
        has_address: bool => false;

        type: Type.BASE is
            throw new NotImplementedException("value has no type");
        si

        gen(context: IR.CONTEXT) is
            throw new NotImplementedException("value gen");
        si

        gen_address(context: IR.CONTEXT) is            
            throw new NotImplementedException("value gen addres");
        si
        
        gen(value: BASE, context: IR.CONTEXT) static is
            if value? then
                value.gen(context);
            else
                context.fixme("null value");
            fi
        si

        init() is si
    si

    class SKIP: BASE is
        init() is
            super.init();
        si
    si

    class NEW: BASE is
        has_type: bool => type?;
        type: Type.BASE;
        constructor: Symbol.Function;
        arguments: Collections.LIST[BASE];

        init(
            type: Type.BASE,
            constructor: Symbol.Function,
            arguments: Collections.LIST[BASE]
        ) is
            super.init();

            _assert type?;
            _assert constructor?;
            _assert arguments?;

            self.type = type;
            self.constructor = constructor;
            self.arguments = arguments;
        si

        gen(context: IR.CONTEXT) is
            for a in arguments do
                gen(a, context);
            od
            
            context.write_line("newobj instance " + constructor.il_type_name, type);
        si

        toString() -> String =>
            "new:[" + type + "](\"" + constructor.name + "\"," + new Shim.JOIN[BASE](arguments) + ")";
    si

    class TYPE_WRAPPER: BASE is
        has_type: bool => type?;
        type: Type.BASE;
        value: BASE;

        init(
            type: Type.BASE,
            value: BASE
        ) is
            super.init();

            _assert type?;
            _assert value?;

            self.type = type;
            self.value = value;
        si

        gen(context: IR.CONTEXT) is
            gen(value, context);
        si

        toString() -> String =>
            "type_wrapper:[" + type + "](" + value + ")";        
    si

    class ADDRESS: BASE is
        _next_id: int static;

        has_type: bool => value.has_type;
        type: Type.BASE => value.type;
        value: BASE;

        name: String;

        next_name: String static is
            let result = "'.address." + _next_id + "'";

            _next_id = _next_id + 1;

            return result;
        si

        init(
            value: BASE
        ) is
            super.init();

            _assert value?;

            self.value = value;

            name = next_name;
        si

        gen(context: IR.CONTEXT) is
            if value.has_address then
                value.gen_address(context);
            else
                context.write_line(".locals init (" + type.il_type_name + " " + name + ")");

                value.gen(context);
                context.write_line("stloc " + name);
                context.write_line("ldloca " + name);
            fi
        si

        toString() -> String =>
            "type_wrapper:[" + type + "](" + value + ")";        
    si

    class BOX: BASE is
        value: BASE;
        has_type: bool => type?;
        type: Type.BASE => value.type;
        is_value_type: bool => false;

        init(
            value: BASE
        ) is
            super.init();

            _assert value?;

            self.value = value;
        si

        gen(context: IR.CONTEXT) is
            gen(value, context);

            // FIXME: this is very specific - could there be other circumstances where the left value is actually an address?
            if value.is_self then
                context.write_line("ldobj " + type.il_type_name);
            fi
            
            context.write_line("box " + type.il_type_name);
        si

        toString() -> String =>
            "box:[" + type + "](" + value + ")";        
    si

    class UNBOX: BASE is
        value: BASE;
        has_type: bool => type?;
        type: Type.BASE => value.type;
        is_value_type: bool => true;

        init(
            value: BASE
        ) is
            super.init();

            _assert value?;

            self.value = value;
        si

        gen(context: IR.CONTEXT) is
            gen(value, context);

            context.write_line("unbox.any " + type.il_type_name);
        si

        toString() -> String =>
            "unbox:[" + type + "](" + value + ")";        
    si

    class CONVERT: BASE is
        has_type: bool => type?;
        type: Type.BASE;
        value: BASE;
        instruction: String;

        init(
            type: Type.BASE,
            value: BASE,
            instruction: String
        ) is
            super.init();

            _assert type?;

            // native generates null values, which cannot easily be prevented and which propogate, and must be cast before use: 
            // _assert value?;

            self.type = type;
            self.value = value;
            self.instruction = instruction;
        si

        gen(context: IR.CONTEXT) is
            gen(value, context);

            context.write_line(instruction, "convert to " + type);
        si

        toString() -> String =>
            "cast:[" + type + "](" + value + ")";
    si

    
    class CAST: BASE is
        has_type: bool => type?;
        type: Type.BASE;
        value: BASE;

        init(
            type: Type.BASE,
            value: BASE
        ) is
            super.init();

            _assert type?;

            // native generates null values, which cannot easily be prevented and which propogate, and must be cast before use: 
            // _assert value?;

            self.type = type;
            self.value = value;
        si

        gen(context: IR.CONTEXT) is
            gen(value, context);

            context.write_line("castclass " + type.il_type_name);
        si

        toString() -> String =>
            "cast:[" + type + "](" + value + ")";
    si

    class ISA: BASE is
        has_type: bool => type?;
        type: Type.BASE;
        isa_type: Type.BASE;
        value: BASE;

        init(
            type: Type.BASE,
            isa_type: Type.BASE,
            value: BASE
        ) is
            super.init();

            _assert type?;
            _assert isa_type?;
            _assert value?;

            self.type = type;
            self.isa_type = type;
            self.value = value;
        si

        gen(context: IR.CONTEXT) is
            gen(value, context);

            context.write_line("isinst " + isa_type.il_type_name);
            context.write_line("ldnull");
            context.write_line("cgt.un");
        si

        toString() -> String =>
            "isa:[" + isa_type + "](" + value + ")";
    si

    class HAS_VALUE: BASE, Type.Typed is
        has_type: bool => type?;
        type: Type.BASE;

        value: BASE;

        init(
            value: BASE,
            type: Type.BASE
        ) is
            super.init();

            _assert type?;
            _assert value?;

            self.value = value;
            self.type = type;
        si

        gen(context: IR.CONTEXT) is
            gen(value, context);

            context.write_line("ldnull");
            context.write_line("cgt.un");
        si
    si

    class NOT: BASE, Type.Typed is
        value: BASE;

        has_Type: bool => type?;
        type: Type.BASE => value.type;
        is_value_type: bool => value.is_value_type;

        init(value: BASE) is
            super.init();

            _assert value?;

            self.value = value;
        si

        gen(context: IR.CONTEXT) is
            BASE.gen(value, context);

            context.write_line("ldc.i4 0");
            context.write_line("ceq");
        si
    si

    class NULL: BASE, Type.Typed is
        has_type: bool => type?;
        type: Type.BASE;

        init(
            type: Type.BASE
        ) is
            super.init();

            _assert type?;

            self.type = type;
        si

        gen(context: IR.CONTEXT) is
            context.write_line("ldnull");
        si
    si

    class DUMMY: BASE, Type.Typed is
        has_type: bool => type?;
        type: Type.BASE;

        init(type: Type.BASE) is
            super.init();

            _assert type?;

            self.type = type;
        si

        gen(context: IR.CONTEXT) is
            context.fixme("dummy " + type);
        si
    si

    class NATIVE: BASE, Type.Typed is
        has_type: bool => type?;
        type: Type.BASE;

        init(type: Type.BASE) is
            super.init();

            _assert type?;

            self.type = type;
        si

        gen(context: IR.CONTEXT) is
            context.write_line("native" + type);
        si
    si

    class TUPLE: BASE, Type.Typed is
        has_type: bool => type?;
        type: Type.BASE;

        values: Collections.Iterable[BASE];

        init(type: Type.BASE, values: Collections.Iterable[BASE]) is
            super.init();

            _assert type?;
            _assert values?;

            self.type = type;
            self.values = values;
        si

        gen(context: IR.CONTEXT) is
            for v in values do
                gen(v, context);

                context.write_line("tuple " + type);
            od
            
        si

        toString() -> String => "tuple:[" + type + "](" + values + ")";
    si

    class SEQUENCE: BASE, Type.Typed is
        has_type: bool => type?;
        type: Type.BASE;
        element_type: Type.BASE;

        values: Collections.LIST[BASE];

        init(
            type: Type.BASE,
            element_type: Type.BASE,
            values: Collections.LIST[BASE]
        ) is
            super.init();

            _assert type?;
            _assert element_type?;
            _assert values?;

            self.type = type;
            self.element_type = element_type;
            self.values = values;
        si

        gen(context: IR.CONTEXT) is
            let boxer = IoC.CONTAINER.instance.value_boxer;

            context.write_line("ldc.i4 " + values.count);
            context.write_line("newarr " + element_type.il_type_name);

            for i in 0..values.count do
                context.write_line("dup");

                let v = boxer.box_if_needed(values[i], element_type);

                context.write_line("ldc.i4 " + i);
                gen(v, context);

                context.write_line("stelem " + element_type.il_type_name);
            od
            
        si

        toString() -> String => "sequence:[" + type + "](" + values + ")";
    si

    namespace Need is
        class STORE: BASE is
            has_type: bool => value? && value.has_type;
            type: Type.BASE => value.type;
            value: BASE;

            init(value: BASE) is
                super.init();

                _assert value?;    

                self.value = value;
            si

            gen(context: IR.CONTEXT) is
                gen(value, context);
                context.write_line("unresolved need store");
            si

            toString() -> String =>
                "need-store:[" + type + "]";
        si
    si

    namespace Expect is
        class STORABLE: BASE is
            has_type: bool => type?;
            type: Type.BASE;

            init(type: Type.BASE) is
                super.init();

                _assert type?;
                    
                self.type = type;
            si

            gen(context: IR.CONTEXT) is
                context.write_line("unresolved expect");
            si

            toString() -> String =>
                "expect-storable:[" + type + "]";
        si
    si

    namespace Load is
        class SELF: BASE, Type.Typed is
            self_: Symbol.BASE;
            has_type: bool => type?;
            type: Type.BASE;

            is_self: bool => true;
            has_address: bool => true;

            init(self_: Symbol.BASE) is
                init(self_, null);
            si
            
            init(self_: Symbol.BASE, type: Type.BASE) is
                super.init();

                _assert self_?;
 
                self.self_ = self_;

                if type? then
                    self.type = type;
                elif isa Semantic.Type.Typed(self_) then
                    self.type = cast Semantic.Type.Typed(self_).type;
                fi
            si

            gen(context: IR.CONTEXT) is
                context.write_line("ldarg.0", "self " + type);
            si

            gen_address(context: IR.CONTEXT) is
                context.write_line("ldarg.0", "self " + type);
            si

            toString() -> String => "self:[" + type + "]";
        si

        class TEMP: BASE, Type.Typed is
            name: String;

            type: Type.BASE;
            has_type: bool => type?;

            has_address: bool => true;

            init(name: String, type: Type.BASE) is
                self.name = name;
                self.type = type;
            si

            gen(context: IR.CONTEXT) is
                context.write_line("ldloc " + name, "temp " + type);
            si

            gen_address(context: IR.CONTEXT) is
                context.write_line("ldloca " + name, "temp " + type);
            si

            toString() -> String =>
                "load:[" + type + "](" + name + "\")";
        si

        class SYMBOL: BASE, Type.Typed is
            symbol: Symbol.BASE;
            symbol_as_typed: Type.Typed => cast Type.Typed(symbol);
            has_type: bool => type?;
            has_address: bool => true;
            type: Type.BASE => symbol_as_typed.type;
            from: BASE;

            init(from: BASE, symbol: Symbol.BASE) is
                super.init();

                _assert symbol?;

                self.from = from;
                self.symbol = symbol;
            si

            gen(context: IR.CONTEXT) is
                gen(from, context);
                context.fixme("load member " + symbol.il_qualified_name);
            si

            gen_address(context: IR.CONTEXT) is
                gen(from, context);
                context.fixme("load member address " + symbol.il_qualified_name);
            si

            toString() -> String =>
                "load:[" + type + "](" + from + ",\"" + symbol.name + "\")";
        si

        class LOCAL_VARIABLE: SYMBOL, Type.Typed is
            init(symbol: Symbol.BASE) is
                super.init(null, symbol);
            si

            gen(context: IR.CONTEXT) is
                context.write_line("ldloc " + symbol.il_name, symbol);
            si

            gen_address(context: IR.CONTEXT) is
                context.write_line("ldloca " + symbol.il_name, symbol);
            si

            toString() -> String =>
                "load:[" + type + "](" + from + ",\"" + symbol.name + "\")";
        si

        class LOCAL_ARGUMENT: SYMBOL, Type.Typed is
            init(symbol: Symbol.BASE) is
                super.init(null, symbol);
            si

            gen(context: IR.CONTEXT) is
                context.write_line("ldarg " + symbol.il_name, symbol);
            si

            gen_address(context: IR.CONTEXT) is
                context.write_line("ldarga " + symbol.il_name, symbol);
            si

            toString() -> String =>
                "load:[" + type + "](" + from + ",\"" + symbol.name + "\")";
        si

        class INSTANCE_FIELD: SYMBOL, Type.Typed is
            init(from: BASE, symbol: Symbol.BASE) is
                super.init(from, symbol);

                _assert from?;
            si

            gen(context: IR.CONTEXT) is
                gen(from, context);
                context.write_line("ldfld " + symbol.il_type_name, symbol);
            si

            gen_address(context: IR.CONTEXT) is
                gen(from, context);
                context.write_line("ldflda " + symbol.il_type_name, symbol);
            si

            toString() -> String =>
                "load:[" + type + "](" + from + ",\"" + symbol.name + "\")";
        si

        class STATIC_FIELD: SYMBOL, Type.Typed is
            init(symbol: Symbol.BASE) is
                super.init(null, symbol);
            si

            gen(context: IR.CONTEXT) is
                context.write_line("ldsfld " + symbol.il_type_name, symbol);
            si

            gen_address(context: IR.CONTEXT) is
                context.write_line("ldsflda " + symbol.il_type_name, symbol);
            si

            toString() -> String =>
                "load:[" + type + "](\"" + symbol.name + "\")";
        si

        class INSTANCE_CLOSURE: SYMBOL, Type.Typed is
            func_type: Semantic.Type.BASE;

            has_address: bool => false;

            init(symbol: Symbol.BASE, func_type: Semantic.Type.BASE) is
                super.init(null, symbol);

                _assert func_type?;
                
                self.func_type = func_type;
            si

            gen(context: IR.CONTEXT) is
                context.write_line("ldarg.0");
                context.write_line("ldftn instance " + symbol.il_type_name, symbol);
                context.write_line("newobj instance void " + func_type.il_type_name + "::'.ctor'(object, native int)");
            si

            toString() -> String =>
                "load:[" + type + "](" + from + ",\"" + symbol.name + "\")";
        si

        class STATIC_CLOSURE: SYMBOL, Type.Typed is
            func_type: Semantic.Type.BASE;

            has_address: bool => false;

            init(symbol: Symbol.BASE, func_type: Semantic.Type.BASE) is
                super.init(null, symbol);

                _assert func_type?;
                
                self.func_type = func_type;
            si

            gen(context: IR.CONTEXT) is
                context.write_line("ldnull");
                context.write_line("ldftn " + symbol.il_type_name, symbol);
                context.write_line("newobj instance void " + func_type.il_type_name + "::'.ctor'(object, native int)");
            si

            toString() -> String =>
                "load:[" + type + "](" + from + ",\"" + symbol.name + "\")";
        si
    si

    namespace Store is
        class SYMBOL: BASE, Type.Typed is
            symbol: Symbol.BASE;
            symbol_as_typed: Type.Typed => cast Type.Typed(symbol);
            type: Type.BASE => symbol_as_typed.type;
            from: BASE;
            value: Graph.Value.BASE;

            init(from: BASE, symbol: Symbol.BASE, value: Graph.Value.BASE) is
                super.init();

                _assert symbol?;
                _assert value?;

                self.from = from;
                self.symbol = symbol;
                self.value = value;
            si

            gen(context: IR.CONTEXT) is
                gen(from, context);
                gen(value, context);

                context.write_line("store member " + symbol);
            si            

            toString() -> String =>
                "store:[" + type + "](" + from + ",\"" + symbol.name + "\"," + value + ")";
        si

        class LOCAL_VARIABLE: SYMBOL is
            init(symbol: Symbol.BASE, value: Graph.Value.BASE) is
                super.init(null, symbol, value);
            si

            gen(context: IR.CONTEXT) is
                gen(value, context);
                
                context.write_line("stloc " + symbol.il_name, symbol);
            si            

            toString() -> String =>
                "store:[" + type + "](" + from + ",\"" + symbol.name + "\"," + value + ")";
        si

        class LOCAL_ARGUMENT: SYMBOL, Type.Typed is
            init(symbol: Symbol.BASE, value: Graph.Value.BASE) is
                super.init(null, symbol, value);
            si

            gen(context: IR.CONTEXT) is
                gen(value, context);

                context.write_line("starg " + symbol.il_name, symbol);
            si

            toString() -> String =>
                "load:[" + type + "](" + from + ",\"" + symbol.name + "\")";
        si

        class INSTANCE_FIELD: SYMBOL is
            init(from: Graph.Value.BASE, symbol: Symbol.BASE, value: Graph.Value.BASE) is
                super.init(from, symbol, value);

                _assert from?;
            si

            gen(context: IR.CONTEXT) is
                gen(from, context);
                gen(value, context);

                context.write_line("stfld " + symbol.il_type_name, symbol);
            si            

            toString() -> String =>
                "store:[" + type + "](" + from + ",\"" + symbol.name + "\"," + value + ")";
        si

        class STATIC_FIELD: SYMBOL is
            init(symbol: Symbol.BASE, value: Graph.Value.BASE) is
                super.init(null, symbol, value);
            si

            gen(context: IR.CONTEXT) is
                gen(value, context);

                context.write_line("stsfld " + symbol.il_type_name, symbol);
            si            

            toString() -> String =>
                "store:[" + type + "](\"" + symbol.name + "\"," + value + ")";
        si
    si

    namespace Call is
        class INNATE: BASE, Type.Typed is
            function: Semantic.Symbol.InnateFunction;
            from: BASE;
            arguments: Collections.LIST[BASE];
            type: Type.BASE;
            has_type: bool => type?;

            actual_operation: String public;
            
            innate_name: String => function.innate_name;
            type_name: String => innate_name.substring(0, Shim.STR.index_of(innate_name, '.'));
            op_name: String => innate_name.substring(Shim.STR.index_of(innate_name, '.') + 1);    

            init(
                function: Semantic.Symbol.InnateFunction,
                from: BASE,
                arguments: Collections.LIST[BASE],
                type: Type.BASE) is
                super.init();

                _assert function?;
                _assert arguments?;

                self.function = function;
                self.arguments = arguments;

                if type? then
                    self.type = type;
                else
                    self.type = function.return_type;
                fi
            si

            gen(context: IR.CONTEXT) is
                IoC.CONTAINER.instance.innate_operation_generator.gen(self, context);
            si

            toString() -> String =>
                "innate-call:[" + type + "](\"" + function.name + "\"," + from + "," + arguments + ")";
        si

        class GLOBAL: BASE, Type.Typed is
            function: Symbol.Function;
            arguments: Collections.LIST[BASE];
            type: Type.BASE;
            has_type: bool => type?;

            init(function: Symbol.Function, arguments: Collections.LIST[BASE], type: Type.BASE) is
                super.init();

                self.function = function;
                self.arguments = arguments;

                if type? then
                    self.type = type;
                else
                    self.type = function.return_type;
                fi

                _assert self.type?;
            si

            gen(context: IR.CONTEXT) is
                for a in arguments do
                    gen(a, context);
                od

                context.write_line("call " + function.il_type_name);                
            si

            toString() -> String =>
                "global-call:[" + type + "](\"" + function.name + "\"," + arguments + ")";
        si

        class INSTANCE: BASE, Type.Typed is
            from: Value.BASE;
            function: Symbol.Function;
            arguments: Collections.LIST[BASE];
            type: Type.BASE;
            has_type: bool => type?;

            init(from: Value.BASE, function: Symbol.Function, arguments: Collections.LIST[BASE], type: Type.BASE) is
                super.init();

                self.from = from;
                self.function = function;
                self.arguments = arguments;

                if type? then
                    self.type = type;
                else
                    self.type = function.return_type;
                fi

                _assert self.type?;                
            si

            gen(context: IR.CONTEXT) is
                gen(from, context);

                for a in arguments do
                    gen(a, context);
                od
   
                context.write_line("callvirt " + function.il_type_name, function);
            si

            toString() -> String =>
                "instance-call:[" + type + "](" + from + ",\"" + function.name + "\"," +  arguments + ")";
        si

        class STRUCT: BASE, Type.Typed is
            from: Value.BASE;
            function: Symbol.Function;
            arguments: Collections.LIST[BASE];
            type: Type.BASE;
            has_type: bool => type?;

            init(from: Value.BASE, function: Symbol.Function, arguments: Collections.LIST[BASE], type: Type.BASE) is
                super.init();

                self.from = from;
                self.function = function;
                self.arguments = arguments;

                if type? then
                    self.type = type;
                else
                    self.type = function.return_type;
                fi

                _assert self.type?;                
            si

            gen(context: IR.CONTEXT) is
                gen(from, context);

                for a in arguments do
                    gen(a, context);
                od
   
                context.write_line("call instance " + function.il_type_name);
            si

            toString() -> String =>
                "struct-call:[" + type + "](" + from + ",\"" + function.name + "\"," +  arguments + ")";
        si

        class STATIC: BASE, Type.Typed is
            function: Symbol.Function;
            arguments: Collections.LIST[BASE];
            type: Type.BASE;
            has_type: bool => type?;

            init(function: Symbol.Function, arguments: Collections.LIST[BASE], type: Type.BASE) is
                super.init();

                self.function = function;
                self.arguments = arguments;

                if type? then
                    self.type = type;
                else
                    self.type = function.return_type;
                fi

                _assert self.type?;
            si

            gen(context: IR.CONTEXT) is
                for a in arguments do
                    gen(a, context);
                od

                context.write_line("call " + function.il_type_name);
            si

            toString() -> String =>
                "static-call:[" + type + "](\"" + function.name + "\"," +  arguments + ")";
        si

        class CLOSURE: BASE, Type.Typed is
            from: BASE;
            type: Type.BASE;
            has_type: bool => type?;

            is_function: bool;
            func_type: Type.BASE;
            arguments: Collections.LIST[BASE];

            init(
                from: BASE,
                type: Type.BASE,
                is_function: bool,
                func_type: Type.BASE,
                arguments: Collections.LIST[BASE]
            ) is
                super.init();

                _assert type?;
                _assert func_type?;
                _assert arguments?;

                self.from = from;
                self.type = type;
                self.is_function = is_function;
                self.func_type = func_type;
                self.arguments = arguments;
            si

            gen(context: IR.CONTEXT) is
                gen(from, context);

                let count = 0;
                for a in arguments do
                    gen(a, context);

                    count = count + 1;
                od

                let call = new System.Text.StringBuilder();

                call
                    .append("callvirt instance ");

                if is_function then
                    call
                        .append("!")
                        .append(count);
                else
                    call
                        .append("void");
                fi
                                    
                call
                    .append(" ")
                    .append(func_type.il_type_name)
                    .append("::Invoke(");

                for i in 0..count do
                    if i > 0 then
                        call.append(",");
                    fi
                    
                    call
                        .append("!")
                        .append(i);                    
                od

                call.append(")");
                
                context.write_line(call.to_string());
            si
            
            toString() -> String =>
                "closure-call:[" + type + "](" + from + "," + arguments + ")";
        si
    si

    namespace Literal is
        class INTEGER: BASE, Type.Typed is
            string: String;
            type: Type.BASE;
            suffix: String;
            has_type: bool => type?;

            init(string: String, type: Type.BASE, suffix: String) is
                super.init();

                _assert string?;
                _assert type?;
                _assert suffix?;

                self.string = string;
                self.type = type;
                self.suffix = suffix;
            si

            gen(context: IR.CONTEXT) is
                context.write_line("ldc." + suffix + " " + string);
            si

            toString() -> String =>
                "literal:[" + type + "](" + string + ")";
        si

        class STRING: BASE, Type.Typed is
            string: String;
            type: Type.BASE;
            has_type: bool => type?;

            init(string: String, type: Type.BASE) is
                super.init();

                _assert string?;
                _assert type?;

                self.string = string;
                self.type = type;
            si

            @IF.legacy()
            gen(context: IR.CONTEXT) is
                let result = new System.StringBuffer(Shim.STR.length(string) * 8);

                for c in string do

                    if c < cast char(0) then
                        result
                            .append("0 ");
                    elif c < cast char(16) then
                        result
                            .append("0")
                            .append(cast int(c), 16)
                            .append(" 00 ");
                    else
                        result
                            .append(cast int(c), 16)
                            .append(" 00 ");
                    fi
                od
                
                context.write_line("ldstr bytearray (" + result + ")");
            si

            @IF.dotnet()
            gen(context: IR.CONTEXT) is
                let result = new System.Text.StringBuilder(Shim.STR.length(string) * 8);

                for c in string do
                    if c < cast char(0) then
                        result
                            .append("0 ");
                    elif c < cast char(16) then
                        result
                            .append("0")
                            .append(cast int(c).to_string("X02"))
                            .append(" 00 ");
                    else
                        result
                            .append(cast int(c).to_string("X02"))
                            .append(" 00 ");
                    fi
                od
                
                context.write_line("ldstr bytearray (" + result + ")");
            si


            toString() -> String =>
                "literal:[" + type + "](\"" + string + "\")";
        si
    si
si
