namespace Semantic is
    @IF.not.v3() use object = System.Object;
    @IF.not.v3() use string = System.String;
    @IF.not.v3() use STD = System.Console;

    use System;
    
    use IoC;
    use Logging;
    use Source;

    class BLOCK_SCOPE: object, Scope, DeclarationContext  is
        _enclosing: Scope;
        _symbols: Semantic.SYMBOL_MAP;

        name: string => "[block]";
        qualified_name: string => _enclosing.qualified_name + ".[block]";

        symbols: Collections.Iterable[Symbol.BASE] => _symbols.values;

        type: Type.BASE => Type.NONE.instance;

        unspecialized_symbol: Symbol.BASE => null;

        is_trait: bool => false;

        init() is
            init(null);
        si

        init(enclosing: Scope) is
            self._enclosing = enclosing;
            _symbols = new Semantic.SYMBOL_MAP();
        si

        qualify(name: string) -> string => _enclosing.qualify(name);

        find_direct(name: string) -> Symbol.BASE =>
            _symbols[name];

        find_direct_matches(prefix: string, matches: Collections.MAP[string,Semantic.Symbol.BASE]) is
            _symbols.find_matches(prefix, matches);
        si            

        find_member(name: string) -> Symbol.BASE is
            throw new NotImplementedException("cannot search for member " + name + " in block scope");
        si

        find_member_matches(prefix: string, matches: Collections.MAP[string,Semantic.Symbol.BASE]) is
            throw new NotImplementedException("cannot search for member matches " + prefix + " in block scope");
        si        

        find_enclosing(name: string) -> Symbol.BASE is
            var result = find_direct(name);

            if result? then
                return result;
            elif _enclosing? then
                return _enclosing.find_enclosing(name);
            else
                return null;
            fi
        si

        find_enclosing_matches(prefix: string, matches: Collections.MAP[string,Semantic.Symbol.BASE]) is
            find_direct_matches(prefix, matches);

            _enclosing.find_enclosing_matches(prefix, matches);
        si

        declare_undefined(location: LOCATION, kind: string, name: string) -> Scope is
            CONTAINER.instance.logger.error(location, "cannot declare " + kind + " here");

            return new Symbol.UNDEFINED(location, self, name);
        si

        declare_class(location: LOCATION, name: string, arguments: Collections.LIST[string], is_stub: bool, enclosing: Scope, symbol_definition_listener: SymbolDefinitionListener) -> Scope =>
            declare_undefined(location, "class", name);

        declare_trait(location: LOCATION, name: string, arguments: Collections.LIST[string], is_stub: bool, enclosing: Scope, symbol_definition_listener: SymbolDefinitionListener) -> Scope =>
            declare_undefined(location, "trait", name);

        declare_struct(location: LOCATION, name: string, arguments: Collections.LIST[string], is_stub: bool, enclosing: Scope, symbol_definition_listener: SymbolDefinitionListener) -> Scope =>
            declare_undefined(location, "struct", name);    

        declare_type(location: LOCATION, name: string, index: int, symbol_definition_listener: SymbolDefinitionListener) -> Scope =>
            declare_undefined(location, "type", name);

        declare_enum(location: LOCATION, name: string, symbol_definition_listener: SymbolDefinitionListener) -> Scope =>
            declare_undefined(location, "enum", name);

        declare_enum_member(location: LOCATION, name: string, value: string, symbol_definition_listener: SymbolDefinitionListener) is
            declare_undefined(location, "enum member", name);
        si

        declare_innate(location: LOCATION, name: string, innate_name: string, enclosing: Scope, symbol_definition_listener: SymbolDefinitionListener) -> Scope =>
            declare_undefined(location, "innate", name);

        declare_function(location: LOCATION, name: string, is_static: bool, is_private: bool, enclosing: Scope, symbol_definition_listener: SymbolDefinitionListener) -> Scope =>
            declare_undefined(location, "function", name);

        declare_namespace(location: LOCATION, name: string, namespace_: Symbol.NAMESPACE, symbol_definition_listener: SymbolDefinitionListener) is
            declare_undefined(location, "namespace", name);
        si

        declare_label(location: LOCATION, name: string, symbol_definition_listener: SymbolDefinitionListener) is
            var label = new Symbol.LABEL(location, self, name);
            declare(location, label, symbol_definition_listener);
        si

        declare_variable(location: LOCATION, name: string, is_static: bool, symbol_definition_listener: SymbolDefinitionListener) -> Scope is
            var variable = new Symbol.LOCAL_VARIABLE(location, self, name);
            declare(location, variable, symbol_definition_listener);
            return variable;
        si

        declare_closure(location: LOCATION, name: string, owner: Scope, enclosing: Scope, symbol_definition_listener: SymbolDefinitionListener) -> Scope is
            return declare_undefined(location, "anonymous function", name);
        si        

        declare_property(location: LOCATION, name: string, is_static: bool, is_private: bool, is_assignable: bool, symbol_definition_listener: SymbolDefinitionListener) -> Scope =>
            declare_undefined(location, "property", name);

        declare(location: LOCATION, symbol: Symbol.BASE, symbol_definition_listener: SymbolDefinitionListener) is
            var name = symbol.name;
            var existing = find_direct(symbol.name);
            
            if existing? then
                CONTAINER.instance.logger.error(location, "redefining symbol " + symbol.name + " originally defined at " + existing.location);
                CONTAINER.instance.logger.error(existing.location, "symbol " + symbol.name + " is redefined at " + location);
            fi

            if symbol_definition_listener? then
                symbol_definition_listener.add_symbol_definition(location, symbol);
            fi
            
            _symbols[name] = symbol;
        si

        to_string() -> string =>
            "" + get_type() + " " + _symbols;
    si
si