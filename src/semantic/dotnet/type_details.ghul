namespace Semantic.DotNet is
    @IF.not.v3() use object = System.Object;
    @IF.not.v3() use string = System.String;
    @IF.not.v3() use Std = System.Console;

    @IF.not.v3() use TYPE = System.Type2;
    
    class TYPE_DETAILS is        
        dotnet_type: TYPE;
        ghul_namespace: string;
        ghul_type_name: string;
        assembly_name: string;
        il_name: string;

        is_complete: bool =>
            ghul_namespace? &&
            ghul_type_name? &&
            assembly_name? &&
            il_name?;

        init(
            dotnet_type: TYPE, 
            ghul_namespace: string,
            ghul_type_name: string,
            il_name: string,
            assembly_name: string
        ) is
            assert dotnet_type? else "dotnet_type is null";
            assert ghul_namespace? else "ghul namespace is null";
            assert ghul_type_name? else "ghul type name is null";

            self.dotnet_type = dotnet_type;

            self.ghul_namespace = ghul_namespace;
            self.ghul_type_name = ghul_type_name;
            self.assembly_name = assembly_name;
            self.il_name = il_name;
        si

        init(
            dotnet_type: TYPE, 
            assembly_name: string
        ) is
            assert dotnet_type? else "dotnet_type is null";

            self.dotnet_type = dotnet_type;

            self.ghul_namespace = dotnet_type.namespace_;
            self.ghul_type_name = dotnet_type.name;
            self.assembly_name = assembly_name;
            self.il_name = dotnet_type.name;
        si

        matches(namespace_name: string, name: string) -> bool => 
            namespace_name =~ ghul_namespace && name =~ ghul_type_name;

        merge_assembly_name(type: TYPE, assembly_name: string) is
            if !self.assembly_name? then
                self.assembly_name = assembly_name;                
            fi
        si

        to_string() -> string => "(.NET type: " + dotnet_type + " ghul ns: " + ghul_namespace + " ghul name: " + ghul_type_name + " il_name: " + il_name + " assembly_name: " + assembly_name + ")";
    si
si