namespace Semantic.DotNet is
    @IF.not.v3() use object = System.Object;
    @IF.not.v3() use string = System.String;
    @IF.not.v3() use Std = System.Console;

    @IF.not.v3() use TYPE = System.Type2;
    @IF.not.v3() use Path = System.IO2.Path;

    use System.Reflection.Assembly;

    use Collections.MAP;
    use Collections.SET;
    use Collections.LIST;
    use Collections.Iterable;

    use Logging.TIMERS;

    class ASSEMBLIES is
        _is_enabled: bool;
        _timers: TIMERS;
        _ghul_symbol_table: Semantic.SYMBOL_TABLE;
        _namespaces: NAMESPACES;
        _type_name_map: TYPE_NAME_MAP;

        _is_started: bool;

        all_assemblies: Iterable[Assembly] => assemblies_by_name.values;
        assemblies_by_name: MAP[string,Assembly];
        assemblies_by_dotnet_namespace: MAP[string,SET[Assembly]];
        assemblies_by_ghul_namespace: MAP[string,SET[Assembly]];

        all_types: SET[TYPE];

        init(
            timers: TIMERS,
            ghul_symbol_table: Semantic.SYMBOL_TABLE,
            namespaces: NAMESPACES,
            type_name_map: TYPE_NAME_MAP
        ) is
            _timers = timers;
            _ghul_symbol_table = ghul_symbol_table;
            _namespaces = namespaces;
            _type_name_map = type_name_map;

            _is_enabled = true;

            assemblies_by_name = new MAP[string,Assembly]();
            assemblies_by_dotnet_namespace = new MAP[string,SET[Assembly]]();
            assemblies_by_ghul_namespace = new MAP[string,SET[Assembly]]();

            all_types = new SET[TYPE]();
        si

        disable() is
            _is_enabled = false;
        si

        start() is
            if !_is_enabled then
                return;
            fi

            let sdk_path = Path.combine("/usr/share/dotnet/shared/Microsoft.NETCore.App", System.Environment.version.to_string());

            let default_imports = ["System.Console", "System.Runtime", "System.IO.FileSystem", "System.Collections", "System.Threading"];

            for a in default_imports do
                import(Path.combine(sdk_path, a + ".dll"));                
            od

            Std.error.write_line(_timers);
        si
        
        reset() is
            if !_is_enabled then
                return;
            fi

            if !_is_started then
                start();
                _type_name_map.start();
                
                _is_started = true;
            fi

            create_namespaces();
        si

        create_namespaces() is
            let current = new LIST[string]();

            let namespaces = new LIST[string]();

            for ns in assemblies_by_ghul_namespace.keys do
                namespaces.add(ns);
            od

            namespaces.sort();

            for a in namespaces do
                let to_create = new LIST[string](a.split(['.']));

                while current.count > to_create.count do
                    _namespaces.leave_namespace(null, null);
                    _ghul_symbol_table.leave_scope();
                    current.remove_at(current.count - 1);
                od

                let index = 0;

                while index < current.count && current[index] =~ to_create[index] do
                    index = index + 1;
                od
                
                while current.count > index do
                    _namespaces.leave_namespace(null, null);
                    _ghul_symbol_table.leave_scope();
                    current.remove_at(current.count - 1);
                od

                for i in index..to_create.count do
                    let ns = _namespaces.declare_and_enter_namespace(Source.LOCATION.internal, to_create[i], null);
                    _ghul_symbol_table.enter_scope(ns);
                od
                
                current = to_create;
            od

            while current.count > 0 do
                _namespaces.leave_namespace(null, null);
                _ghul_symbol_table.leave_scope();
                current.remove_at(current.count - 1);                
            od            
        si        

        import(assembly_names: Iterable[string]) is
            for assembly_name in assembly_names do
                import(assembly_name);
            od            
        si

        import(path: string) is
            _timers.start("import assembly");

            try
                let assembly_name = System.IO2.Path.get_file_name_without_extension(path);

                if assemblies_by_name.contains_key(assembly_name) then
                    return;
                fi

                let assembly = Assembly.load_from(path);

                assemblies_by_name.add(assembly_name, assembly);

                import(assembly_name, assembly);

                @IF.debug() Std.error.write_line("imported " + assembly);
            catch ex: System.Exception
                Std.error.write_line("failed to import: " + path);
            finally
                _timers.finish("import assembly");
            yrt            
        si

        import(name: string, assembly: Assembly) is
            let distinct_namespaces = new SET[string]();

            try
                for type in assembly.get_exported_types() do
                    all_types.add(type);

                    let namespace_name = _type_name_map.get_ghul_namespace(type);

                    @IF.debug() Std.error.write_line("have type: " + type + " map to ghul namespace: " + namespace_name);

                    distinct_namespaces.add(namespace_name);
                od
            catch ex: System.Exception
                Std.error.write_line("couldn't enumerate exported types in: " + assembly);
            yrt

            try
                for type in assembly.get_forwarded_types() do
                    all_types.add(type);

                    let namespace_name = _type_name_map.get_ghul_namespace(type);

                    @IF.debug() Std.error.write_line("have type: " + type + " map to ghul namespace: " + namespace_name);

                    distinct_namespaces.add(namespace_name);
                od
            catch ex: System.Exception
                Std.error.write_line("couldn't enumerate forwarded types in: " + assembly);
            yrt
                    
            for namespace_name in distinct_namespaces do
                let set: SET[Assembly];
                // Std.error.write_line("assembly " + assembly + " contains types in " + namespace_name + " namespace");

                if !assemblies_by_ghul_namespace.try_get_value(namespace_name, set ref) then
                    set = new SET[Assembly]();
                    assemblies_by_ghul_namespace[namespace_name] = set;
                fi
                
                set.add(assembly);
            od
        si

        find_all(namespace_name: string) -> Iterable[TYPE] is
            return all_types;
        si
        
        find(assembly_name: string, namespace_name: string, dotnet_namespace_name: string, type_name: string) -> TYPE is
            @IF.debug() Std.error.write_line("AAA: search assembly: " + assembly_name + " for namespace: " + namespace_name + " type: " + type_name);

            let assembly: Assembly;            
        
            if !assemblies_by_name.try_get_value(assembly_name, assembly ref) then
                Std.error.write_line("assembly " + assembly_name + " not found looking for " + namespace_name + " " + type_name);

                return null;
            fi

            let qualified_name = dotnet_namespace_name + "." + type_name;

            // Std.error.write_line("search " + assembly +  " for " + qualified_name);
            let result = assembly.get_type(qualified_name);
                
            if result? then
                Std.error.write_line("found type: " + result + " assembly name: " + assembly_name + " apparent assembly: " + result.assembly);
                return result;
            fi                
        si
        
        find(namespace_name: string, dotnet_namespace_name: string, name: string) -> TYPE is
            @IF.debug() Std.error.write_line("AAA: namespace: " + namespace_name + " name: " + name);

            let set: SET[Assembly];

            // Std.error.write_line("search for type: " + name + " in namespace: " + namespace_name);
            
            if !assemblies_by_ghul_namespace.try_get_value(namespace_name, set ref) then
                @IF.debug() Std.error.write_line("AAA: namespace " + namespace_name +  " not found in any assembly");

                return null;
            fi

            let qualified_name = dotnet_namespace_name + "." + name;

            for assembly in set do
                @IF.debug() Std.error.write_line("AAA: search " + assembly +  " for " + qualified_name);
                let result = assembly.get_type(qualified_name);
                
                if result? then
                    Std.error.write_line("found type: " + result + " actual assembly: " + assembly + " apparent assembly: " + result.assembly);
                    return result;
                fi                
            od

            @IF.debug() Std.error.write_line("AAA: namespace: " + namespace_name + " name: " + name + " not found");
        si
    si
si