namespace Driver is
    use System;
    use Generic;
 
    use IoC;
    use Logging;
    use Source;

    trait CommandHandler is
        handle(reader: IO2.TextReader, writer: IO2.TextWriter);
    si

    trait WorkItemLookup is
        file_names: Collections.Iterable[String];
        find_work_item(file_name: String) -> WORK_ITEM;
    si

    class ANALYSER is
        _symbol_table: Semantic.SYMBOL_TABLE;
        _compiler: COMPILER;

        _command_map: Collections.MAP[String,CommandHandler];
        _despatcher: COMMAND_DESPATCHER;

        init(
            compiler: COMPILER,
            symbol_table: Semantic.SYMBOL_TABLE,
            symbol_use_locations: Semantic.SYMBOL_USE_LOCATIONS,
            symbol_definition_locations: Semantic.SYMBOL_DEFINITION_LOCATIONS,
            completer: Syntax.Process.COMPLETER,
            signature_help: Syntax.Process.SIGNATURE_HELP,
            reader: IO2.TextReader,
            writer: IO2.TextWriter,
            build_flags: BUILD_FLAGS
        ) is
            System.Console.error.write_line("ghūl: analyser starting up...");

            _compiler = compiler;
            _symbol_table = symbol_table;

            _command_map = new Collections.MAP[String,CommandHandler]();

            let watchdog = new WATCHDOG();

            _despatcher = new COMMAND_DESPATCHER(reader, writer, watchdog);

            build_flags.want_legacy = false;
            build_flags.want_compile_expressions = true;

            let file_edited_handler = new FILE_EDITED_HANDLER(watchdog, compiler, build_flags);
            let analyse_handler = new ANALYSE_HANDLER(watchdog, compiler, symbol_table, file_edited_handler, file_edited_handler);
            let hover_handler = new HOVER_HANDLER(watchdog, symbol_use_locations);
            let find_definition_handler = new FIND_DEFINITION_HANDLER(watchdog, symbol_use_locations);
            let completion_handler = new COMPLETION_HANDLER(watchdog, completer, file_edited_handler);
            let signature_handler = new SIGNATURE_HANDLER(watchdog, signature_help, file_edited_handler);
            let symbols_handler = new SYMBOLS_HANDLER(watchdog, symbol_definition_locations, file_edited_handler);
            let references_handler = new REFERENCES_HANDLER(watchdog, symbol_use_locations);
            let restart_handler = new RESTART_HANDLER();

            _despatcher.add_handler(
                "EDIT", file_edited_handler
            );

            _despatcher.add_handler(
                "ANALYSE", analyse_handler
            );

            _despatcher.add_handler(
                "HOVER", hover_handler
            );

            _despatcher.add_handler(
                "DEFINITION", find_definition_handler
            );

            _despatcher.add_handler(
                "COMPLETE", completion_handler
            );

            _despatcher.add_handler(
                "SIGNATURE", signature_handler
            );

            _despatcher.add_handler(
                "SYMBOLS", symbols_handler
            );

            _despatcher.add_handler(
                "REFERENCES", references_handler
            );

            _despatcher.add_handler(
                "RESTART", restart_handler
            );
        si

        run() is
            do
                if !_despatcher.poll() then
                    System.Console.error.write_line("ghūl: exiting");
                    return;
                fi
            od
        si
    si

    class WATCHDOG is
        operation_limit: int static => 1024;
        _operation_count: int;

        _want_restart: bool;

        _poison_count: int;
        is_poisoned: bool => _poison_count > 10;

        init() is si

        poison() is
            _poison_count = _poison_count + 1;

            System.Console.error.write_line("poison: " + _poison_count);
            System.Console.error.flush();
        si

        antidote() is
            _poison_count = 0;
        si
                
        request_restart() is
            _want_restart = true;
        si

        increment(writer: IO2.TextWriter) is
            _operation_count = _operation_count + 1;

            /*
            if _operation_count > operation_limit || _want_restart then
                _want_restart = false;

                System.Console.error.write_line("watchdog: requesting restart...");
                System.Console.error.flush();

                writer.write_line("RESTART");
                writer.write("" + cast char(12));
                writer.flush();
            fi
            */
        si
    si

    class COMMAND_DESPATCHER is
        _watchdog: WATCHDOG;
        _reader: IO2.TextReader;
        _writer: IO2.TextWriter;
        _command_map: Collections.MAP[String,CommandHandler];

        _listening: bool;

        init(reader: IO2.TextReader, writer: IO2.TextWriter, watchdog: WATCHDOG) is
            _reader = reader;
            _writer = writer;
            _watchdog = watchdog;

            _command_map = new Collections.MAP[String,CommandHandler]();
        si

        add_handler(command_name: String, command_handler: CommandHandler) is
            assert _command_map[command_name] == null else "replacing command handler for " + command_name;

            _command_map[command_name] = command_handler;
        si

        poll() -> bool is
            // IO.Std.err.FlushOnPrintln = true;

            if !_listening then
                _writer.write_line("LISTEN");
                _writer.write("" + cast char(12));
                _writer.flush();

                _listening = true;
            fi

            let command = read_command();

            if command? then
                let handler = _command_map[command];

                if handler? then
                    if _watchdog.is_poisoned && command !~ "EDIT" && command !~ "ANALYSE" then
                        System.Console.error.write_line("poisoned: ignoring non-edit command: " + command);
                    fi

                    handler.handle(_reader, _writer);
                    
                    _watchdog.increment(_writer);

                    return true;
                else
                    System.Console.error.write_line("ghūl: no handler found for command: '" + command + "' in " + _command_map);

                    _watchdog.request_restart();

                    return true;
                fi
            else
                System.Console.error.write_line("ghūl: reader is at end");
            fi

            System.Console.error.flush();

            return false;
        si

        read_command() -> System.String is
            let desynchronized = false;

            do
                let command = _reader.read_line();

                if !command? then
                    break;
                fi
                
                while Shim.STR.starts_with(command,'\014') do
                    command = command.substring(1);
                od

                if 
                    Shim.STR.length(command) >= 3 && 
                    Shim.STR.starts_with(command,'#') && 
                    Shim.STR.ends_with(command,'#')
                then
                    if desynchronized then
                        System.Console.error.write_line("ghūl: well-formed command received: resynchronized");
                    fi

                    return command.substring(1, Shim.STR.length(command)-1);
                fi

                if !desynchronized then
                    System.Console.error.write_line("ghūl: garbled command: attempting to resynchronize");

                    desynchronized = true;
                fi
            od

            return null;
        si

        @IF.legacy()
        restart(writer: IO2.TextWriter) is
            writer.flush();

            let executable_path = System.Arguments.ProgramArguments[0];

            let p = new Util.Process();

            p.spawn(
                executable_path,
                Arguments.ProgramArguments,
                Arguments.ProgramEnvironment
            );
        si

        @IF.dotnet()
        restart(writer: IO2.TextWriter) is
            throw new System.NotImplementedException("restart");
        si
    si

    class HOVER_HANDLER: Object, CommandHandler is
        _watchdog: WATCHDOG;
        _symbol_use_locations: Semantic.SYMBOL_USE_LOCATIONS;

        init(
            watchdog: WATCHDOG,
            symbol_use_locations: Semantic.SYMBOL_USE_LOCATIONS
        )
        is
            self._watchdog = watchdog;
            self._symbol_use_locations = symbol_use_locations;
        si

        handle(reader: IO2.TextReader, writer: IO2.TextWriter) is
            IoC.CONTAINER.instance.want_tab_delimited_logger(writer);

            let path = reader.read_line();
            let line = Shim.STR.parse_int(reader.read_line());
            let column = Shim.STR.parse_int(reader.read_line()) + 1;

            let written_header = false;
            
            try
                writer.write_line("HOVER");
                written_header = true;
                
                if !_watchdog.is_poisoned then
                    let symbol = _symbol_use_locations.find_definition_from_use(path, line, column);

                    if symbol? then
                        writer.write_line(symbol.description);
                    fi
                fi
            catch e: Exception
                System.Console.error.write_line(e);
                _watchdog.poison();

                if !written_header then
                    writer.write_line("HOVER");                    
                fi
            yrt

            System.Console.error.flush();

            writer.write("" + cast char(12));
            writer.flush();
        si
    si

    class FIND_DEFINITION_HANDLER: Object, CommandHandler is
        _watchdog: WATCHDOG;
        _symbol_use_locations: Semantic.SYMBOL_USE_LOCATIONS;

        init(
            watchdog: WATCHDOG,
            symbol_use_locations: Semantic.SYMBOL_USE_LOCATIONS
        )
        is
            self._watchdog = watchdog;
            self._symbol_use_locations = symbol_use_locations;
        si

        handle(reader: IO2.TextReader, writer: IO2.TextWriter) is
            IoC.CONTAINER.instance.want_tab_delimited_logger(writer);

            let path = reader.read_line();
            let line = Shim.STR.parse_int(reader.read_line());
            let column = Shim.STR.parse_int(reader.read_line());

            let written_header = false;

            try
                writer.write_line("DEFINITION");
                written_header = true;
                
                if !_watchdog.is_poisoned then
                    let symbol = _symbol_use_locations.find_definition_from_use(path, line, column);                    
                    
                    if symbol? then
                        writer.write_line("" + symbol.location.file_name);
                        writer.write_line("" + symbol.location.start_line);
                        writer.write_line("" + symbol.location.start_column);
                        writer.write_line("" + symbol.location.end_line);
                        writer.write_line("" + symbol.location.end_column);
                    fi
                fi
            catch e: Exception
                System.Console.error.write_line(e);
                _watchdog.poison();

                if !written_header then
                    writer.write_line("DEFINITION");                    
                fi                
            yrt

            System.Console.error.flush();

            writer.write("" + cast char(12));
            writer.flush();
        si
    si

    class FILE_EDITED_HANDLER: Object, CommandHandler, WorkItemLookup, Collections.Iterable[WORK_ITEM] is
        _watchdog: WATCHDOG;
        _compiler: COMPILER;
        _work_items: Collections.MutableMap[String,WORK_ITEM];
        _build_flags: BUILD_FLAGS;

        file_names: Collections.Iterable[String] => _work_items.keys;

        // @IL.name.read("GetEnumerator")
        iterator: Collections.Iterator[WORK_ITEM]
            => _work_items.values.iterator;

        init(
            watchdog: WATCHDOG,
            compiler: COMPILER,
            build_flags: BUILD_FLAGS)
        is
            _watchdog = watchdog;
            _compiler = compiler;
            _work_items = new Collections.MAP[String,WORK_ITEM]();
            _build_flags = build_flags;
        si

        find_work_item(path: String) -> WORK_ITEM =>
            _work_items[path];

        handle(reader: IO2.TextReader, writer: IO2.TextWriter) is
            IoC.CONTAINER.instance.want_tab_delimited_logger(writer);

            let node_id = Syntax.Tree.NODE.debug_next_id;

            let path = reader.read_line();

            writer.write_line("DIAG PARSE");

            try
                _work_items[path] =
                    _compiler.parse(path, reader, _build_flags);

                _watchdog.antidote();
            catch e: Exception
                System.Console.error.write_line(e);

                _watchdog.request_restart();
            yrt

            System.Console.error.flush();

            writer.write("" + cast char(12));
            writer.flush();
        si
    si

    class ANALYSE_HANDLER: Object, CommandHandler is
        _watchdog: WATCHDOG;
        _compiler: COMPILER;
        _symbol_table: Semantic.SYMBOL_TABLE;
        _work_item_source: Collections.Iterable[WORK_ITEM];
        _work_item_lookup: WorkItemLookup;

        init(
            watchdog: WATCHDOG,
            compiler: COMPILER,
            symbol_table: Semantic.SYMBOL_TABLE,
            work_item_source: Collections.Iterable[WORK_ITEM],
            work_item_lookup: WorkItemLookup
        ) is
            _watchdog = watchdog;
            _compiler = compiler;
            _symbol_table = symbol_table;
            _work_item_source = work_item_source;
            _work_item_lookup = work_item_lookup;
        si

        handle(reader: IO2.TextReader, writer: IO2.TextWriter) is
            writer.write_line("DIAG ANALYSIS");
            let node_id = Syntax.Tree.NODE.debug_next_id;

            let uris = Shim.STR.split(reader.read_line(),'\t');

            try
                if !_watchdog.is_poisoned then
                    if uris.count >= 1 && uris[0] =~ "all" then
                        for i in _work_item_source do
                            i.build_flags.want_compile_expressions = true;
                        od
                    else
                        let count = 0;

                        for i in _work_item_source do
                            i.build_flags.want_declare_symbols = true;
                            i.build_flags.want_compile_expressions = false;
                        od

                        for uri in uris do
                            let i = _work_item_lookup.find_work_item(uri);

                            if i? then
                                count = count + 1;
                                i.build_flags.want_compile_expressions = true;
                            fi
                        od
                    fi
                        
                    _compiler.clear_symbols();

                    _compiler.queue(_work_item_source);

                    _compiler.build();

                    _watchdog.antidote();
                fi

                writer.write("" + cast char(12));
                writer.write_line("ANALYSED");

            catch e: Exception
                System.Console.error.write_line(e);

                _watchdog.poison();

                writer.write("" + cast char(12));
                writer.write_line("ANALYSED");
            finally
                _compiler.clear_queue();

                writer.write("" + cast char(12));
                writer.flush();
            yrt
        si

        @IF.legacy()
        format_thousands(value: long) -> String is
            // FIXME: needs to be divided by 1000:
            return "" + value;
        si

        @IF.dotnet()
        format_thousands(value: long) -> String is
            // FIXME: needs to be divided by 1000:
            return value.to_string();
        si
    si

    class COMPLETION_HANDLER: Object, CommandHandler is
        _watchdog: WATCHDOG;
        _completer: Syntax.Process.COMPLETER;
        _work_item_lookup: WorkItemLookup;

        init(
            watchdog: WATCHDOG,
            completer: Syntax.Process.COMPLETER,
            work_item_lookup: WorkItemLookup
        )
        is
            _watchdog = watchdog;
            _completer = completer;
            _work_item_lookup = work_item_lookup;
        si

        handle(reader: IO2.TextReader, writer: IO2.TextWriter) is
            var written_header = false;

            try
                let path = reader.read_line();
                let target_line = Shim.STR.parse_int(reader.read_line());
                let target_column = Shim.STR.parse_int(reader.read_line());

                writer.write_line("COMPLETION");
                written_header = true;

                if !_watchdog.is_poisoned then

                    let results = find_completions(path, target_line, target_column);

                    if results? then
                        for symbol in results do
                            @IF.legacy()
                            writer.write_line("%\t%\t%" % [symbol.name, symbol.completion_kind, symbol.description]: Object);

                            @IF.dotnet()
                            writer.write_line("{0}\t{1}\t{2}", [symbol.name, symbol.completion_kind, symbol.description]: Object);
                        od
                    fi
                fi
            catch e: Exception
                System.Console.error.write_line(e);

                _watchdog.poison();

                if !written_header then
                    writer.write_line("COMPLETION");
                fi
            yrt

            System.Console.error.flush();

            writer.write("" + cast char(12));
            writer.flush();
        si

        find_completions(path: String, target_line: int, target_column: int) -> Collections.Iterable[Semantic.Symbol.BASE] is
            let i = _work_item_lookup.find_work_item(path);

            return _completer.find_completions(i.definition, target_line, target_column);
        si
    si

    class SIGNATURE_HANDLER: Object, CommandHandler is
        _watchdog: WATCHDOG;
        _signature_help: Syntax.Process.SIGNATURE_HELP;
        _work_item_lookup: WorkItemLookup;

        init(
            watchdog: WATCHDOG,
            signature_help: Syntax.Process.SIGNATURE_HELP,
            work_item_lookup: WorkItemLookup
        )
        is
            _watchdog = watchdog;
            _signature_help = signature_help;
            _work_item_lookup = work_item_lookup;
        si

        handle(reader: IO2.TextReader, writer: IO2.TextWriter) is
            var written_header = false;

            try
                let path = reader.read_line();
                let target_line = Shim.STR.parse_int(reader.read_line());
                let target_column = Shim.STR.parse_int(reader.read_line());

                if !_watchdog.is_poisoned then
                    writer.write_line("SIGNATURE");
                    written_header = true;

                    let results = find_signatures(path, target_line, target_column);                    

                    if results? then
                        writer.write_line("" + results.best_result_index);
                        writer.write_line("" + results.current_parameter_index);
                        for symbol in results.results do
                            let line = get_function_doc_for(symbol);
                            writer.write_line(line);
                        od
                    fi
                fi
            catch e: Exception
                System.Console.error.write_line(e);

                _watchdog.poison();

                if !written_header then
                    writer.write_line("SIGNATURE");
                fi
            yrt

            System.Console.error.flush();

            writer.write("" + cast char(12));
            writer.flush();
        si

        get_function_doc_for(function: Semantic.Symbol.Function) -> String is
            let result = new System.Text.StringBuilder();

            result
                .append(function.short_description);

            for i in 0..function.arguments.count do
                result
                    .append('\t')
                    .append(function.get_short_argument_description(i));
            od

            return result.to_string();
        si

        find_signatures(path: String, target_line: int, target_column: int) -> Semantic.OVERLOAD_MATCHES_RESULT is
            let i = _work_item_lookup.find_work_item(path);

            return _signature_help.find_signatures(i.definition, target_line, target_column);
        si
    si

    class SYMBOLS_HANDLER: Object, CommandHandler is
        _watchdog: WATCHDOG;
        _symbol_definition_locations: Semantic.SYMBOL_DEFINITION_LOCATIONS;
        _work_item_lookup: WorkItemLookup;

        init(
            watchdog: WATCHDOG,
            symbol_definition_locations: Semantic.SYMBOL_DEFINITION_LOCATIONS,
            work_item_lookup: WorkItemLookup
        )
        is
            _watchdog = watchdog;
            _symbol_definition_locations = symbol_definition_locations;
            _work_item_lookup = work_item_lookup;
        si

        handle(reader: IO2.TextReader, writer: IO2.TextWriter) is
            var written_header = false;

            try
                let path = reader.read_line();

                writer.write_line("SYMBOLS");
                written_header = true;

                if !_watchdog.is_poisoned then
                    if path? && Shim.STR.length(path) > 0 then
                        write_symbol_list_for_file(writer, path, _symbol_definition_locations.find_definitions_from_file(path, false));
                    else
                        for i in _work_item_lookup.file_names do
                            if i? && Shim.STR.length(i) > 0 then
                                write_symbol_list_for_file(writer, i, _symbol_definition_locations.find_definitions_from_file(i, true));
                            fi
                        od
                    fi
                fi
            catch e: Exception
                System.Console.error.write_line(e);

                _watchdog.poison();

                if !written_header then
                    writer.write_line("SYMBOLS");
                fi
            yrt

            System.Console.error.flush();

            writer.write("" + cast char(12));
            writer.flush();
        si

        write_symbol_list_for_file(writer: IO2.TextWriter, path: String, symbols: Collections.Iterable[Semantic.Symbol.BASE]) is
            writer.write_line(path);

            if symbols? then
                for symbol in symbols do
                    writer.write_line(
                        get_symbol_info_for(symbol)
                    );
                od
            fi
        si

        get_symbol_info_for(symbol: Semantic.Symbol.BASE) -> String is
            let result = new System.Text.StringBuilder();

            let qualified_name = symbol.qualified_name;

            let qualifier = qualified_name.substring(0, Shim.STR.length(qualified_name) - Shim.STR.length(symbol.name) - 1);

            result
                .append(symbol.name)
                .append('\t')
                .append(cast int(symbol.symbol_kind))
                .append('\t')
                .append(symbol.location.start_line)
                .append('\t')
                .append(symbol.location.start_column)
                .append('\t')
                .append(symbol.location.end_line)
                .append('\t')
                .append(symbol.location.end_column)
                .append('\t')
                .append(qualifier);

            return result.to_string();
        si
    si

    class REFERENCES_HANDLER: Object, CommandHandler is
        _watchdog: WATCHDOG;
        _symbol_use_locations: Semantic.SYMBOL_USE_LOCATIONS;

        init(
            watchdog: WATCHDOG,
            symbol_use_locations: Semantic.SYMBOL_USE_LOCATIONS
        )
        is
            _watchdog = watchdog;
            _symbol_use_locations = symbol_use_locations;
        si

        handle(reader: IO2.TextReader, writer: IO2.TextWriter) is
            IoC.CONTAINER.instance.want_tab_delimited_logger(writer);

            var written_header = false;

            try
                let path = reader.read_line();
                let line = Shim.STR.parse_int(reader.read_line());
                let column = Shim.STR.parse_int(reader.read_line());

                writer.write_line("REFERENCES");
                written_header = true;

                if !_watchdog.is_poisoned then
                    let symbol = _symbol_use_locations.find_definition_from_use(path, line, column);

                    if symbol? then
                        let locations = _symbol_use_locations.find_references_to_symbol(symbol);

                        write_location_list(writer, locations);
                    fi
                fi

            catch e: Exception
                System.Console.error.write_line(e);

                if !written_header then
                    writer.write_line("REFERENCES");
                fi
            yrt

            System.Console.error.flush();

            writer.write("" + cast char(12));
            writer.flush();
        si

        write_location_list(writer: IO2.TextWriter, locations: Collections.Iterable[LOCATION]) is
            for location in locations do
                writer.write_line(
                    format_location(location)
                );
            od
        si

        format_location(location: LOCATION) -> String is
            let result = new System.Text.StringBuilder();

            result
                .append(location.file_name)
                .append('\t')
                .append(location.start_line)
                .append('\t')
                .append(location.start_column)
                .append('\t')
                .append(location.end_line)
                .append('\t')
                .append(location.end_column);

            return result.to_string();
        si
    si

    class RESTART_HANDLER: Object, CommandHandler is
        init() is si

        handle(reader: IO2.TextReader, writer: IO2.TextWriter) is
            IoC.CONTAINER.instance.want_tab_delimited_logger(writer);

            @IF.legacy()
            try
                writer.flush();

                let executable_path = System.Arguments.ProgramArguments[0];

                let p = new Util.Process();

                p.spawn(
                    executable_path,
                    Arguments.ProgramArguments,
                    Arguments.ProgramEnvironment
                );
            catch e: Exception
                System.Console.error.write_line(e);

                writer.write("" + cast char(12));
                writer.write_line("EXCEPT");
                writer.write_line(e);
            yrt

            @IF.dotnet()
            throw new System.NotImplementedException("restart - spawn");

            writer.write("" + cast char(12));
            writer.flush();
        si
    si
si