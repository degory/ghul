namespace Driver is
    use System;

    class OUTPUT_FILE_NAME_GENERATOR is
        _current: String;

        result: String is
            if _current? then
                return _current;
            else
                return "binary";
            fi
        si

        init() is
        si

        force(path: String) is
            _current = path;
        si

        seen_file(path: String) is
            if _current? then
                return;
            fi

            var extensions = [".l", ".ghul"];

            for e in extensions do
                if Shim.STR.ends_with(path,e) then
                    path = path.substring(0, Shim.STR.length(path) - Shim.STR.length(e));
                    let last_slash_index = Shim.STR.last_index_of(path, '/');

                    if last_slash_index >= 0 then
                        _current = path.substring(last_slash_index + 1);
                    else
                        _current = path;
                    fi

                    return;
                fi
            od      
        si
    si
si