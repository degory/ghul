namespace Driver is
    use System.NotImplementedException;

    use Logging.TIMERS;
    use Logging.TIMER;
    
    class Pass is
        _timers: TIMERS;
        description: string;

        init(
            timers: TIMERS,
            description: string
        )
        is
            _timers = timers;
            self.description = description;
        si

        start() is
            _timers.start(description);
        si

        apply(source_file: SOURCE_FILE) is
            throw new NotImplementedException();
        si

        finish() is
            _timers.finish(description);
        si    

        get_hash_code() -> int => description.get_hash_code();

        to_string() -> string => _timers[description].to_string();
    si

    class PASS: Pass is
        _start: () -> void;
        _apply: SOURCE_FILE -> void;
        _finish: () -> void;

        init(
            timers: TIMERS,
            description: string,
            start: () -> void,
            apply: SOURCE_FILE -> void,
            finish: () -> void
        )
        is
            super.init(timers, description);
            _start = start;
            _apply = apply;
            _finish = finish;
        si

        start() is
            super.start();

            if _start? then
                _start();
            fi            
        si

        apply(source_file: SOURCE_FILE) is
            _apply(source_file);
        si

        finish() is
            if _finish? then
                _finish();
            fi

            super.finish();
        si    
    si
si