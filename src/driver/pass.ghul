namespace Driver is
    @IF.not.v3() use object = System.Object;
    @IF.not.v3() use string = System.String;
    @IF.not.v3() use STD = System.Console;

    use System;

    use Logging.TIMERS;
    use Logging.TIMER;
    
    class Pass is
        _timers: TIMERS;
        _description: string;

        init(
            timers: TIMERS,
            description: string
        )
        is
            self._timers = timers;
            self._description = description;
        si

        start() is
            _timers.start(_description);
        si

        apply(source_file: SOURCE_FILE) is
            throw new NotImplementedException();
        si

        finish() is
            _timers.finish(_description);
        si    

        get_hash_code() -> int => _description.get_hash_code();

        to_string() -> string => _timers[_description].to_string();
    si

    class PASS: Pass is
        _start: () -> void;
        _apply: SOURCE_FILE -> void;
        _finish: () -> void;

        init(
            timers: TIMERS,
            description: string,
            start: () -> void,
            apply: SOURCE_FILE -> void,
            finish: () -> void
        )
        is
            super.init(timers, description);
            _start = start;
            _apply = apply;
            _finish = finish;
        si

        start() is
            super.start();

            if _start? then
                _start();
            fi            
        si

        apply(source_file: SOURCE_FILE) is
            _apply(source_file);
        si

        finish() is
            if _finish? then
                _finish();
            fi

            super.finish();
        si    
    si
si