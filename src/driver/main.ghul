namespace Driver is
    use System;
    use System.IO2.File;

    use Collections.Iterable;
    use Collections.LIST;
    
    use IoC;
    use Logging;

    class Main is
        paths: Driver.PATH_CONFIG;
        project_name: String;
        container: IoC.CONTAINER;
        compiler: COMPILER;
        flags: BUILD_FLAGS;
        output_file_name_generator: OUTPUT_FILE_NAME_GENERATOR;
        library_locations: LIST[String];
        ghul_source_files: LIST[String];

        standard_ghul_library_locations: Iterable[String] => new LIST[String](["dotnet/ghul", "dotnet/stubs"]);

        entry(arguments: String[]) static is
            @IL.entrypoint()

            let full_arguments = new LIST[String](arguments.count + 1);

            full_arguments.add("ghul.exe");

            full_arguments.add_range(arguments);

            let instance = new Main(new LIST[String](full_arguments));
        si
        
        init(arguments: LIST[String]) is
            let result = 1;

            try
                System.Console.input_encoding = new System.Text.UTF8Encoding(false);
                System.Console.output_encoding = new System.Text.UTF8Encoding(false);

                if arguments.count <= 1 then
                    System.Console.output.write_line("ghūl " + Source.BUILD.number);

                    System.Console.output.flush();

                    System.Environment.exit(0);
                fi

                container = IoC.CONTAINER.instance;

                paths = container.path_config;
                flags = new BUILD_FLAGS();
                compiler = new COMPILER();
                output_file_name_generator = new OUTPUT_FILE_NAME_GENERATOR();

                parse_flags(arguments);

                if flags.want_analyse then
                    analyse();

                    System.Environment.exit(0);
                fi

                start_build();

                compiler.post_parse();

                compiler.build();

                result = finish_build();
            catch e: Exception
                System.Console.error.write_line(e);
                System.Console.error.flush();

                result = 1;
            yrt

            if result == 0 then
                System.Console.error.write_line("*** succeeded ***");
            else
                System.Console.error.write_line("!!! failed !!!");
            fi
            
            // FIXME: the runtime should be doing this - wrapping the L stderr + stdout IO.Writers should not prevent them getting flushed:
            System.Console.error.flush();
            System.Console.output.flush();

            System.Environment.exit(result);
        si

        parse_flags(args: Iterable[String]) is
            ghul_source_files = new LIST[String]();
            library_locations = new LIST[String]();

            let args_iterator = args.iterator;

            args_iterator.move_next();

            flags.want_compile_expressions = true;
            flags.want_assembler = true;
            flags.want_executable = true;

            let want_type_check = false;
            let do_not_want_type_check = false;

            let get_next_argument = (a: Collections.Iterator[String]) -> String is
                a.move_next();
                return a.current.trim();
            si;

            let conditional_defines = new Collections.LIST[String]();

            for s in args_iterator do
                if s =~ "-A" || s =~ "--analyse" then
                    flags.want_analyse = true;
                    flags.want_assembler = false;
                    flags.want_executable = false;
                elif s =~ "-G" || s =~ "--type-check" then
                    flags.want_assembler = false;
                    flags.want_executable = false;
                elif s =~ "-g" || s =~ "--no-type-check" then
                    flags.want_compile_expressions = false;
                elif s =~ "-E" || s =~ "--ignore-errors" then
                    flags.ignore_errors = true;
                elif s =~ "-S" || s =~ "--assembler" then
                    flags.want_executable = false;
                elif s =~ "--debug" then
                    conditional_defines.add("debug");
                elif s =~ "--define" then
                    conditional_defines.add(get_next_argument(args_iterator));
                elif s =~ "-N" || s =~ "--dotnet" then
                    // do nothing - .NET is the only supported option
                elif s =~ "-o" || s =~ "--output" then
                    output_file_name_generator.force(get_next_argument(args_iterator));
                elif s =~ "-p" || s =~ "--library-prefix" then
                    paths.library_prefix = get_next_argument(args_iterator);
                elif s =~ "-l" || s =~ "--library" then
                    library_locations.add(get_next_argument(args_iterator));
                elif s.starts_with('-') then
                    System.Console.error.write_line("warning: ignoring unknown option: " + s);
                elif SOURCE_FILE_CATEGORIZER.is_ghul(s) then
                    output_file_name_generator.seen_file(s);
                    ghul_source_files.add(s);
                fi
            od

            container.conditional_compilation.set_is_enabled(conditional_defines);
        si

        start_build() is
            @IF.not.release()
            container.timers.start("parse-all");

            @IF.not.release()
            container.timers.start("parse-lib");
            queue_library_locations();
            @IF.not.release()
            container.timers.finish("parse-lib");

            @IF.not.release()
            container.timers.start("parse-src");
            queue_source_files();
            @IF.not.release()
            container.timers.finish("parse-src");

            @IF.not.release()
            container.timers.finish("parse-all");

            @IF.not.release()
            Console.error.write_line(container.timers["parse-src"]);
            @IF.not.release()
            Console.error.write_line(container.timers["parse-lib"]);
            @IF.not.release()
            Console.error.write_line(container.timers["parse-all"]);

            if flags.want_assembler then
                container.value_boxer.want_boxing = true;
                container.ir_context.enter_file("out.il", true);

                container.boilerplate_generator.gen("header");
            fi
        si

        finish_build() -> int is
            if container.logger.any_errors && !flags.ignore_errors then
                return 1;
            elif container.logger.is_poisoned then
                Console.error.write_line("internal error");
                return 2;
            fi

            if flags.want_assembler then
                return finish_build_dotnet();
            else
                return 0;
            fi
        si

        finish_build_dotnet() -> int is
            let ir_context = container.ir_context;
            let output_file = output_file_name_generator.result;

            if output_file.last_index_of('.') < 0 then
                output_file = output_file + ".exe";
            fi            

            ir_context.leave_file("out.il");

            if !flags.want_executable then
                return 0;
            fi

            let ilasm_path = "/usr/lib/ghul/ilasm/ilasm";

            if !File.exists(ilasm_path) then
                Console.error.write_line(".NET Core ILAsm not found: falling back to /usr/bin/ilasm");
                ilasm_path = "/usr/bin/ilasm";
            fi            
            
            let ilasm_args = 
                "-quiet out.il -output=" + output_file;

            @IF.not.release()
            container.timers.start("il-asm");

            let ilasm = System.Diagnostics.Process.start(ilasm_path, ilasm_args);

            @IF.not.release()
            container.timers.finish("il-asm");

            @IF.not.release()
            Console.error.write_line(container.timers["il-asm"]);

            ilasm.wait_for_exit();

            let result = ilasm.exit_code;

            if result != 0 then
                Console.error.write_line("error: ilasm failed");
    
                return result;
            fi

            let runtime_config_file: String;

            if output_file.last_index_of('.') == output_file.length - 4 then
                runtime_config_file = output_file.substring(0, output_file.last_index_of('.')) + ".runtimeconfig.json";
            else
                runtime_config_file = output_file + ".runtimeconfig.json";
            fi
            
            let runtime_config = System.IO2.File.create_text(runtime_config_file);
            runtime_config.write("{\"runtimeOptions\":{\"tfm\":\"netcoreapp5.0\",\"framework\":{\"name\":\"Microsoft.NETCore.App\",\"version\":\"5.0.0\"}}}");
            runtime_config.close();

            let chmod = System.Diagnostics.Process.start("/bin/chmod", "+x " + output_file);

            chmod.wait_for_exit();

            result = chmod.exit_code;

            if result != 0 then
                System.Console.error.write_line("compiled successfully but failed to set executable bit on resulting binary: " + output_file);
            fi

            return result;
        si
        
        queue_library_locations() is
            let cuttoff = 0;
            
            let locations_to_queue: Iterable[String];

            if library_locations.count > 0 then
                let explicit_locations = new LIST[String](library_locations.count);
                
                for location in library_locations do
                    explicit_locations.add(get_library_location(location));
                od

                locations_to_queue = explicit_locations;
            else
                locations_to_queue = get_library_locations(standard_ghul_library_locations);
            fi
            
            for directory in locations_to_queue do
                queue_library_location(directory);
            od
        si

        queue_library_location(directory: String) is
            if !directory.ends_with('/') then
                directory = directory + '/';
            fi

            let files: Iterable[String] = System.IO2.Directory.get_files(directory);
            
            for file in files do
                if file.ends_with(".ghul") then
                    queue_source_file(file);
                fi
            od
        si

        queue_source_files() is
            for file in ghul_source_files do
                if file.ends_with(".ghul") then
                    queue_source_file(file);                    
                fi
            od            
        si
        
        queue_source_file(path: String) is
            let reader = System.IO2.File.open_text(path);

            compiler.parse_and_queue(path, reader, flags.copy());
        si

        get_library_locations(directories: Iterable[String]) -> Iterable[String] is
            let result = new LIST[String]();
            
            for directory in directories do
                result.add(get_library_location(directory));
            od

            return result;            
        si

        get_library_location(directory: System.String) -> System.String is
            if !directory.starts_with('/') then
                directory = paths.library_prefix + directory;
            fi

            if !directory.ends_with('/') then
                directory = directory + '/';
            fi

            return directory;
        si        

        analyse() is
            let analyser = new ANALYSER(
                compiler,
                container.timers,
                container.symbol_table,
                container.symbol_use_locations,
                container.symbol_definition_locations,
                container.completer,
                container.signature_help,
                System.Console.input,
                System.Console.output,
                flags
            );

            System.Console.error.write_line("ghūl compiler " + Source.BUILD.number + ": serving analysis requests");

            analyser.run();
        si
    si
si

