namespace Syntax.Tree.Expression is
    use Generic;

    use Source;

    class NEW: NODE is
        type_expression: TypeExpression.NODE;    
        arguments: LIST;

        children: Collections.Iterable[Tree.NODE] =>
            [type_expression, arguments]: Tree.NODE;                
        
        init(location: LOCATION, type_expression: TypeExpression.NODE, arguments: LIST) is
            super.init(location);

            self.type_expression = type_expression;
            self.arguments = arguments;
        si

        accept(visitor: Visitor) is
            visitor.visit(self);
        si

        walk(visitor: Visitor) is
            if !visitor.pre(self) then
                type_expression.walk(visitor);
                arguments.walk(visitor);
            fi

            accept(visitor);
        si

        rewrite(m: Dict[Tree.NODE,Tree.NODE]) is
            type_expression = cast TypeExpression.NODE(m[type_expression]);
            arguments = cast LIST(m[arguments]);
        si
    si
si
