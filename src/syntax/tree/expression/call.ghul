namespace Syntax.Tree.Expression is
    use System;
    use Generic;

    use Source;

    class CALL: NODE is
        function: NODE;
        arguments: LIST;

        children: Collections.Iterable[Tree.NODE] =>
            [function, arguments]: Tree.NODE;        

        init(location: LOCATION, function: NODE, arguments: LIST) is
            super.init(location);
            
            self.function = function;
            self.arguments = arguments;
        si

        accept(visitor: Visitor) is
            visitor.visit(self);
        si

        walk(visitor: Visitor) is
            if !visitor.pre(self) then
                arguments.walk(visitor);
                function.walk(visitor);
            fi

            accept(visitor);
        si

        rewrite(m: Dict[Tree.NODE,Tree.NODE]) is
            function = cast NODE(m[function]);
            arguments = cast LIST(m[arguments]);
        si
    si
si
