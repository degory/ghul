namespace Syntax.Tree.Definition is
    use Source;

    use Generic;
    
    class INDEXER: MODIFIABLE  is
        name: Identifier.NODE;
        index_argument: Variable.NODE;
        type_expression: TypeExpression.NODE;
        read_body: Body.NODE;
        assign_argument: Identifier.NODE;
        assign_body: Body.NODE;

        children: Collections.Iterable[Tree.NODE] is
            var result = new Collections.LIST[Tree.NODE](6);

            if name? then
                result.add(name);
            fi

            result.add(index_argument);
            result.add(type_expression);

            if read_body? then
                result.add(read_body);
            fi

            if assign_argument? then
                result.add(assign_argument);
            fi

            if assign_body? then
                result.add(assign_body);
            fi

            return result;
        si

        init(
            location: LOCATION,
            name: Identifier.NODE,
            index_argument: Variable.NODE,
            type_expression: TypeExpression.NODE,
            modifiers: Modifier.LIST,
            read_body: Body.NODE,
            assign_argument: Identifier.NODE,
            assign_body: Body.NODE)
        is
            super.init(location, modifiers);

            self.name = name;
            self.index_argument = index_argument;
            self.type_expression = type_expression;
            self.read_body = read_body;
            self.assign_argument = assign_argument;
            self.assign_body = assign_body;
        si

        accept(visitor: Visitor) is
            visitor.visit(self);
        si

        walk(visitor: Visitor) is
            if !visitor.pre(self) then
                modifiers.walk(visitor);

                if name? then
                    name.walk(visitor);
                fi

                index_argument.walk(visitor);
                type_expression.walk(visitor);

                if read_body? then
                    read_body.walk(visitor);
                fi

                if assign_argument? then
                    assign_argument.walk(visitor);
                fi

                if assign_body? then
                    assign_body.walk(visitor);
                fi
            fi

            accept(visitor);
        si

        rewrite(m: Dict[Tree.NODE,Tree.NODE]) is
            modifiers = cast Modifier.LIST(m[modifiers]);
            name = cast Identifier.NODE(m[name]);
            index_argument = cast Variable.NODE(m[index_argument]);
            type_expression = cast TypeExpression.NODE(m[type_expression]);

            read_body = cast Body.NODE(m[read_body]);
            assign_argument = cast Identifier.NODE(m[assign_argument]);
            assign_body = cast Body.NODE(m[assign_body]);
        si        
    si
si
