namespace Syntax.Trees.Definitions is
    @IF.not.v3() use Std = System.Console;

    use Source;
    
    class FUNCTION: MODIFIABLE  is
        name: Identifiers.Identifier;
        arguments: Variables.LIST;
        type_expression: TypeExpressions.TypeExpression;
        body: Bodies.Body;

        for_property: PROPERTY public;
        
        init(
            location: LOCATION,
            name: Identifiers.Identifier, 
            arguments: Variables.LIST,
            type_expression: TypeExpressions.TypeExpression,
            modifiers: Modifiers.LIST,
            body: Bodies.Body
        ) is
            super.init(location, modifiers);

            self.name = name;
            self.arguments = arguments;
            self.type_expression = type_expression;
            self.body = body;
        si

        accept(visitor: Visitor) is
            visitor.visit(self);
        si

        walk(visitor: Visitor) is
            if !visitor.pre(self) then
                if name? then
                    name.walk(visitor);
                else 
                    Std.error.write_line("function name is null");
                fi

                if arguments? then
                    arguments.walk(visitor);
                else 
                    Std.error.write_line("function arguments is null");
                fi

                if type_expression? then
                    type_expression.walk(visitor);
                fi
                
                if body? then
                    body.walk(visitor);
                fi
            fi

            accept(visitor);
        si
    si
si
