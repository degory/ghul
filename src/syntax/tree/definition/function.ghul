namespace Syntax.Tree.Definition is
    use Source;

    
    class FUNCTION: MODIFIABLE  is
        name: Identifier.NODE;
        arguments: Variable.LIST;
        type_expression: TypeExpression.NODE;
        body: Body.NODE;

        for_property: PROPERTY public;
        
        init(
            location: LOCATION,
            name: Identifier.NODE, 
            arguments: Variable.LIST,
            type_expression: TypeExpression.NODE,
            modifiers: Modifier.LIST,
            body: Body.NODE
        ) is
            super.init(location, modifiers);

            self.name = name;
            self.arguments = arguments;
            self.type_expression = type_expression;
            self.body = body;
        si

        accept(visitor: Visitor) is
            visitor.visit(self);
        si

        walk(visitor: Visitor) is
            if !visitor.pre(self) then
                if name? then
                    name.walk(visitor);
                else 
                    System.Console.error.write_line("function name is null");
                fi

                if arguments? then
                    arguments.walk(visitor);
                else 
                    System.Console.error.write_line("function arguments is null");
                fi

                if type_expression? then
                    type_expression.walk(visitor);
                fi
                
                if body? then
                    body.walk(visitor);
                fi
            fi

            accept(visitor);
        si
    si
si
