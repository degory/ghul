namespace Syntax.Tree.Definition is
    use System;
    
    use Source;

    class LIST: NODE, Collections.Iterable[NODE]  is
        definitions: Collections.MutableList[NODE];

        @IL.name.read("GetEnumerator")
        iterator: Collections.Iterator[NODE] => definitions.iterator;

        uses: Collections.LIST[USE] is
            var result = new Collections.LIST[USE](definitions.count);

            for d in definitions do
                let d_without_pragmas = d.without_pragmas;

                if isa USE(d_without_pragmas) then
                    result.add(cast USE(d_without_pragmas));
                fi
            od
            
            return result;
        si
        
        init(location: LOCATION, definitions: Collections.Iterable[NODE]) is
            super.init(location);

            self.definitions = new Collections.LIST[NODE](definitions);
        si

        add(definition: NODE) is
            definitions.add(definition);
        si

        push(definition: NODE) is
            let nd = new Collections.LIST[NODE](definitions.count + 1);

            nd.add(definition);
            nd.add_range(definitions);

            definitions = nd;
        si

        remove(definition: NODE) is
            definitions.remove(definition);
        si

        accept(visitor: Visitor) is
            visitor.visit(self);
        si

        walk(visitor: Visitor) is
            if !visitor.pre(self) then
                let i = 0;

                while i < definitions.count do
                    definitions[i].walk(visitor);
                    
                    i = i + 1;
                od
            fi
            
            accept(visitor);
        si
    si
si
