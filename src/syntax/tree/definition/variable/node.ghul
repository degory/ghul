// FIXME: correct this namespace:
namespace Syntax.Tree.Variable is
    use Generic;

    use Source;

    class NODE: Tree.Definition.NODE is
        name: Identifier.NODE;
        type_expression: TypeExpression.NODE;
        initializer: Expression.NODE;
        is_static: bool;

        children: Iterable[Tree.NODE] is
            let result = new Vector[Tree.NODE](3);

            result.add(name);
            result.add(type_expression);

            if initializer? then
                result.add(initializer);
            fi
        
            return result;
        si
        
        init(
            location: LOCATION,
            name: Identifier.NODE,
            type_expression: TypeExpression.NODE,
            is_static: bool,
            initializer: Expression.NODE
        ) is
            super.init(location);

            self.name = name;
            self.type_expression = type_expression;
            self.is_static = is_static;
            self.initializer = initializer;
        si

        copy() -> NODE is
            assert(initializer == null, "cannot copy a variable node with non null initializer: " + location + ": " + self);

            return new NODE(
                location,
                name.copy(),
                type_expression.copy(),
                is_static,
                null
            );
        si

        accept(visitor: Visitor) is
            visitor.visit(self);
        si

        walk(visitor: Visitor) is
            if !visitor.pre(self) then
                name.walk(visitor);
                type_expression.walk(visitor);
                if initializer? then
                    initializer.walk(visitor);
                fi
            fi
            accept(visitor);
        si

        rewrite(m: Dict[Tree.NODE,Tree.NODE]) is
            name = cast Identifier.NODE(m[name]);
            type_expression = cast TypeExpression.NODE(m[type_expression]);
            initializer = cast Expression.NODE(m[initializer]);
        si
    si
si
