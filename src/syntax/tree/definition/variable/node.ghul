// FIXME: correct this namespace:
namespace Syntax.Trees.Variables is
    
    use Source;

    class Variable: Trees.Definitions.Definition is
        name: Identifiers.Identifier;
        type_expression: TypeExpressions.TypeExpression;
        initializer: Expressions.Expression;
        is_static: bool;
        
        init(
            location: LOCATION,
            name: Identifiers.Identifier,
            type_expression: TypeExpressions.TypeExpression,
            is_static: bool,
            initializer: Expressions.Expression
        ) is
            super.init(location);

            self.name = name;
            self.type_expression = type_expression;
            self.is_static = is_static;
            self.initializer = initializer;
        si

        copy() -> Variable is
            assert initializer == null else "cannot copy a variable node with non null initializer";

            return new Variable(
                location,
                name.copy(),
                type_expression.copy(),
                is_static,
                null
            );
        si

        accept(visitor: Visitor) is
            visitor.visit(self);
        si

        walk(visitor: Visitor) is
            if !visitor.pre(self) then
                name.walk(visitor);
                type_expression.walk(visitor);
                if initializer? then
                    initializer.walk(visitor);
                fi
            fi
            accept(visitor);
        si
    si
si
