namespace Syntax.Tree.Statement is
    use System;
    use Generic;
    
    use Source;

    class LIST: Tree.NODE, Collections.Iterable[NODE] is
        statements: Collections.Iterable[NODE];

        children: Collections.Iterable[Tree.NODE] is
            var result = new Collections.LIST[Tree.NODE]();

            for s in statements do
                result.add(s);
            od

            return result;
        si

        init(location: LOCATION, statements: Collections.Iterable[NODE]) is
            super.init(location);

            self.statements = new Collections.LIST[NODE](statements);
        si

        Iterator: Generic.Iterator[NODE] is
            return statements.Iterator;
        si

        accept(visitor: Visitor) is
            visitor.visit(self);
        si

        walk(visitor: Visitor) is
            if !visitor.pre(self) then
                for s in statements do
                    s.walk(visitor);
                od
            fi

            accept(visitor);
        si
    si
si
