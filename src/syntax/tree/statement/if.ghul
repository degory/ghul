namespace Syntax.Tree.Statement is
    use Generic;

    use Source;
    
    class IF: NODE is
        branches: Iterable[IF_BRANCH];

        children: Iterable[Tree.NODE] is
            var result = new Vector[Tree.NODE]();

            for b in branches do
                result.add(b);
            od

            return result;
        si
        
        init(location: LOCATION, branches: Iterable[IF_BRANCH]) is
            super.init(location);

            self.branches = new Vector[IF_BRANCH](branches);
        si

        accept(visitor: Visitor) is
            visitor.visit(self);
        si

        walk(visitor: Visitor) is
            if !visitor.pre(self) then
                for branch in branches do
                    branch.walk(visitor);
                od
            fi

            accept(visitor);
        si
    si

    class IF_BRANCH: Tree.NODE is
        condition: Expression.NODE;
        body: LIST;

        children: Iterable[Tree.NODE] is
            var result = new Vector[Tree.NODE](2);

            if condition? then
                result.add(condition);
            fi

            result.add(body);

            return result;
        si

        init(
            location: LOCATION,
            condition: Expression.NODE,
            body: LIST
        )
        is
            super.init(location);

            self.condition = condition;
            self.body = body;
        si

        accept(visitor: Visitor) is
            visitor.visit(self);
        si

        walk(visitor: Visitor) is
            if !visitor.pre(self) then
                if condition? then
                    condition.walk(visitor);
                fi

                body.walk(visitor);
            fi

            accept(visitor);
        si
    si
si
