namespace Syntax.Trees.TypeExpressions is
    
    use Source;

    class LIST: Trees.Node, Collections.Iterable[TypeExpression] is
        elements: Collections.LIST[TypeExpression];
        @IL.name.read("GetEnumerator")
        iterator: Collections.Iterator[TypeExpression] => elements.iterator;

        init(location: LOCATION, elements: Collections.Iterable[TypeExpression]) is
            super.init(location);

            self.elements = new Collections.LIST[TypeExpression](elements);

            for e in elements do
                if 
                    e.is_poisoned
                then
                    poison();

                    return;
                fi
            od                
        si

        copy() -> LIST is
            let ne = new Collections.LIST[TypeExpression]();
            for e in elements do
                ne.add(e);
            od

            return
                new LIST(
                    location,
                    ne
                );
        si

        accept(visitor: Visitor) is
            visitor.visit(self);
        si

        walk(visitor: Visitor) is
            if !visitor.pre(self) then
                for t in elements do
                    t.walk(visitor);
                od
            fi

            accept(visitor);
        si
    si
si
