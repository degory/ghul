namespace Syntax.Tree.TypeExpression is
    
    use Source;

    class LIST: Tree.NODE, Collections.Iterable[NODE] is
        elements: Collections.LIST[NODE];
        @IL.name.read("GetEnumerator")
        iterator: Collections.Iterator[NODE] => elements.iterator;

        init(location: LOCATION, elements: Collections.Iterable[NODE]) is
            super.init(location);

            self.elements = new Collections.LIST[NODE](elements);

            for e in elements do
                if 
                    e.is_poisoned
                then
                    poison();

                    return;
                fi
            od                
        si

        copy() -> LIST is
            let ne = new Collections.LIST[NODE]();
            for e in elements do
                ne.add(e);
            od

            return
                new LIST(
                    location,
                    ne
                );
        si

        accept(visitor: Visitor) is
            visitor.visit(self);
        si

        walk(visitor: Visitor) is
            if !visitor.pre(self) then
                for t in elements do
                    t.walk(visitor);
                od
            fi

            accept(visitor);
        si
    si
si
