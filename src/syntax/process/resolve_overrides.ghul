namespace Syntax.Process is
    use Queue = Collections.Queue`1;

    use System.Threading.Tasks;

    use Logging;
    use Trees;
    use Source;

    class RESOLVE_OVERRIDES: ScopedVisitor is
        _logger: Logger;
        _symbol_table: Semantic.SYMBOL_TABLE;

        _done_object: bool;

        _duplicate_method_checker: Semantic.DUPLICATE_METHOD_CHECKER;
        _work_queue: Queue[() -> void];

        want_threads: bool public;

        init(
            logger: Logger,
            stable_symbols: Semantic.STABLE_SYMBOLS,
            symbol_table: Semantic.SYMBOL_TABLE,
            namespaces: Semantic.NAMESPACES
        )
        is
            super.init(logger, stable_symbols, symbol_table, namespaces);

            _logger = logger;
            _symbol_table = symbol_table;

            _duplicate_method_checker = new Semantic.DUPLICATE_METHOD_CHECKER(logger);
            _work_queue = new Queue[() -> void]();
        si

        apply(root: Trees.Node) is
            // FIXME: this isn't needed 

            if !_done_object then

                let lookup = IoC.CONTAINER.instance.innate_symbol_lookup;

                lookup.get_object_type().symbol.pull_down_super_symbols();
                lookup.get_value_type().symbol.pull_down_super_symbols();

                _done_object = true;
            fi            

            root.walk(self);
        si

        drain() is
            if want_threads then
                let tasks =
                    _work_queue |
                        .map(wi => Task.run(wi));

                Task.wait_all(tasks.collect_array());
            else
                for wi in _work_queue do
                    wi();
                od                
            fi            
        si
        

        check_duplicate_global_functions() is
            for ns in _namespaces do
                _work_queue.enqueue(() =>
                    _duplicate_method_checker.check(ns, "duplicate function")
                );
            od
        si

        pre(`class: Trees.Definitions.CLASS) -> bool => true;

        visit(`class: Definitions.CLASS) is
            let symbol = symbol_for(`class);

            if symbol? /\ isa Semantic.Symbols.Classy(symbol) then
                let classy = cast Semantic.Symbols.Classy(symbol);

                _work_queue.enqueue(() is
                    classy.read_lock();
                    _duplicate_method_checker.check(symbol, "duplicate method");
                    classy.exit_lock();

                    classy.pull_down_super_symbols();
                si);
            fi
        si

        pre(`trait: Trees.Definitions.TRAIT) -> bool => true;

        visit(`trait: Definitions.TRAIT) is
            let symbol = symbol_for(`trait);

            if symbol? /\ isa Semantic.Symbols.Classy(symbol) then
                let classy = cast Semantic.Symbols.Classy(symbol);

                _work_queue.enqueue(() is
                    let job = classy.next_job;
                    let b = new System.Text.StringBuilder();

                    classy.read_lock();

                    _duplicate_method_checker.check(symbol, "duplicate method");

                    classy.exit_lock();

                    classy.pull_down_super_symbols(job);
                si);
            fi
        si

        pre(`struct: Trees.Definitions.STRUCT) -> bool => true;

        visit(`struct: Definitions.STRUCT) is
            let symbol = symbol_for(`struct);

            if symbol? /\ isa Semantic.Symbols.Classy(symbol) then
                _work_queue.enqueue(() =>
                    cast Semantic.Symbols.Classy(symbol).pull_down_super_symbols()
                );
            fi
        si

        pre(`enum: Trees.Definitions.ENUM) -> bool => true;

        visit(`enum: Definitions.ENUM) is
            let symbol = symbol_for(`enum);

            if symbol? /\ isa Semantic.Symbols.Classy(symbol) then
                _work_queue.enqueue(() =>
                    cast Semantic.Symbols.Classy(symbol).pull_down_super_symbols()
                );
            fi
        si
    si
si
