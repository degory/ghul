namespace Syntax.Process is
    use IO.Std;
    
    use Source;
    use Trees;

    use Logging;

    class ADD_ACCESSORS_FOR_PROPERTIES: Visitor is
        _stub_depth: int;
        _stack: Collections.STACK[Definitions.LIST];

        enclosing_definition: Definitions.LIST => _stack.peek();

        init() is
            super.init();

            _stack = new Collections.STACK[Definitions.LIST]();
        si

        apply(root: Node) is
            root.walk(self);
        si

        // FIXME: #500 Pragma handling code is duplicated across multiple visitors
        pre(pragma: Definitions.PRAGMA) -> bool is
            if !pragma.pragma? then
                Std.error.write_line("pragma is null");
                return false;
            fi

            let p = pragma.pragma;

            let name = p.name.to_string();

            if name =~ "IL.stub" then
                _stub_depth = _stub_depth + 1;
            fi
        si

        visit(pragma: Definitions.PRAGMA) is
            if !pragma.pragma? then
                Std.error.write_line("pragma is null");
                return;
            fi

            let p = pragma.pragma;

            let name = p.name.to_string();

            if name =~ "IL.stub" then
                _stub_depth = _stub_depth - 1;
            fi            
        si

        pre(property: Definitions.PROPERTY) -> bool is
            let use debug_dispose = debug_enter();

            debug("processing property {property.name.name} ...");

            if property.modifiers.is_field then
                if property.read_body? \/ property.assign_argument? then
                    IoC.CONTAINER.instance.logger
                        .error(property.read_body.location, "fields cannot have accessors");
                fi

                if property.name.name.starts_with('_') then
                    IoC.CONTAINER.instance.logger
                        .hint(property.modifiers.location, "unnecessary field modifier");
                fi

                return true;
            fi

            if
                !property.is_poisoned /\ (
                    !property.name.name.starts_with('_') \/
                    property.read_body? \/
                    property.assign_body?
                )
            then
                let is_assignable = !property.read_body? \/ property.assign_argument?;

                debug("will add accessors for property {property.name.name} ...");

                add_accessor_functions_for_property(property, is_assignable);
            fi

            return true;
        si

        add_accessor_functions_for_property(property: Definitions.PROPERTY, is_assignable: bool) is
            let read_name =
                new Identifiers.Identifier(
                    property.name.location,
                    "$get_{property.name.name}"
                );

            property.is_auto_property =
                _stub_depth == 0 /\
                !property.read_body? /\
                !property.assign_body? /\
                !property.name.name.starts_with('_') /\
                !property.modifiers.is_private;

            let backing_variable_name = "${property.name.name}";

            let assign_argument_name = "$$value";

            if property.assign_argument != null then
                assign_argument_name = property.assign_argument.name;
            fi

            if property.is_auto_property then
                debug("property {property.name.name} is auto property");

                enclosing_definition.add(
                    new Variables.VARIABLE(
                        property.name.location,
                        new Identifiers.Identifier(
                            LOCATION.internal,
                            backing_variable_name
                        ),
                        property.type_expression.copy(),
                        property.modifiers.is_static,
                        true,
                        null
                    )
                );

                property.read_body =
                    new Trees.Bodies.BLOCK(property.location,
                        new Trees.Statements.LIST(property.location,
                            new Collections.LIST[Statements.Statement]([
                                new Trees.Statements.RETURN(property.location,
                                    new Trees.Expressions.IDENTIFIER(property.location,
                                        new Trees.Identifiers.Identifier(property.location, backing_variable_name)
                                    )
                                )
                            ]: Statements.Statement)
                        )
                    );

                property.assign_body =
                    new Trees.Bodies.BLOCK(property.location,
                        new Trees.Statements.LIST(property.location,
                            new Collections.LIST[Statements.Statement]([
                                new Trees.Statements.ASSIGNMENT(property.location,
                                    new Trees.Expressions.SIMPLE_LEFT_EXPRESSION(property.location,
                                        new Trees.Expressions.IDENTIFIER(property.location,
                                            new Trees.Identifiers.Identifier(property.location, backing_variable_name)
                                        )
                                    ),
                                    new Trees.Expressions.IDENTIFIER(property.location,
                                        new Trees.Identifiers.Identifier(property.location, assign_argument_name)
                                    )                                        
                                )
                            ]: Statements.Statement)
                        )
                    );
            else
                debug("property {property.name.name} is not auto property");

                if property.read_body? then
                    debug("property {property.name.name} has read body\n{property.read_body}");
                fi

                if property.assign_body? then
                    debug("property {property.name.name} has assign body\n{property.assign_body}");
                fi
            fi

            let read_function = new Definitions.FUNCTION(
                property.location,
                read_name,
                null,
                new Variables.LIST(property.name.location, new Collections.LIST[Variables.VARIABLE](0)),
                property.type_expression.copy(),
                property.modifiers.copy(),
                property.read_body
            );

            read_function.for_property = property;

            enclosing_definition.add(
                read_function
            );

            property.read_function = read_function;

            if is_assignable \/ property.is_auto_property then
                let assign_name =
                    new Identifiers.Identifier(
                        property.name.location,
                        "$set_{property.name.name}"
                    );                

                if !property.assign_argument? then
                    property.assign_argument = new Identifiers.Identifier(property.location, "$$value");
                fi

                let assign_function = new Definitions.FUNCTION(
                    property.name.location,
                    assign_name,
                    null,
                    new Variables.LIST(LOCATION.internal, new Collections.LIST[Variables.VARIABLE]([
                        new Variables.VARIABLE(
                            property.assign_argument.location,
                            property.assign_argument,
                            property.type_expression.copy(),
                            false,
                            true,
                            null
                        )
                    ]:Variables.VARIABLE)),
                    new TypeExpressions.NAMED(
                        LOCATION.internal,
                        new Identifiers.Identifier(
                            LOCATION.internal,
                            "void"
                        )
                    ),
                    property.modifiers.copy(),
                    property.assign_body
                );

                assign_function.for_property = property;

                enclosing_definition.add(
                    assign_function
                );

                property.assign_function = assign_function;
            fi
        si

        visit(property: Definitions.PROPERTY) is
        si

        pre(indexer: Definitions.INDEXER) -> bool is
            let name: string;
            let location: LOCATION;

            if indexer.name? then
                name = indexer.name.name;
                location = indexer.name.location;
            else
                name = "Item";
                location = indexer.location;
            fi

            if indexer.read_body? then
                enclosing_definition.add(
                    new Definitions.FUNCTION(
                        indexer.location,
                        new Identifiers.Identifier(
                            location,
                            "get_{name}"
                        ),
                        null,
                        new Variables.LIST(
                            LOCATION.internal,
                            new Collections.LIST[Variables.VARIABLE]([indexer.index_argument.copy()]:Variables.VARIABLE)),
                        indexer.type_expression.copy(),
                        indexer.modifiers.copy(),
                        indexer.read_body
                    )
                );
            fi

            if indexer.assign_body? then
                enclosing_definition.add(
                    new Definitions.FUNCTION(
                        indexer.location,
                        new Identifiers.Identifier(
                            location,
                            "set_{name}"
                        ),
                        null,
                        new Variables.LIST(LOCATION.internal, new Collections.LIST[Variables.VARIABLE]([
                            indexer.index_argument.copy(),
                            new Variables.VARIABLE(
                                indexer.assign_argument.location,
                                indexer.assign_argument.copy(),                                
                                indexer.type_expression.copy(),
                                false,
                                true,
                                null
                            )
                        ]:Variables.VARIABLE)),
                        new TypeExpressions.NAMED(
                            LOCATION.internal,
                            new Identifiers.Identifier(
                                LOCATION.internal,
                                "void"
                            )
                        ),
                        indexer.modifiers.copy(),
                        indexer.assign_body
                    )
                );
            fi
                       
            return true;
        si

        pre(`class: Definitions.CLASS) -> bool is
            _stack.push(`class.body);

            return false;
        si

        visit(`class: Definitions.CLASS) is
            _stack.pop();
        si

        pre(`trait: Definitions.TRAIT) -> bool is
            _stack.push(`trait.body);

            return false;
        si

        visit(`trait: Definitions.TRAIT) is
            _stack.pop();
        si

        pre(`struct: Definitions.STRUCT) -> bool is
            _stack.push(`struct.body);

            return false;
        si

        visit(`struct: Definitions.STRUCT) is
            _stack.pop();
        si

        pre(`variant: Trees.Definitions.VARIANT) -> bool is
            debug_enter();
            super.pre(`variant);

            let has_value_property = get_has_value_property(`variant.location, `variant.fields.count > 0);
            let value_property = get_value_property(`variant.location, `variant.fields | .collect_list());

            debug("has_value property for variant {`variant.name} is \n{has_value_property}");
            debug("value property for variant {`variant.name} is \n{value_property}");

            `variant.body.add(has_value_property);
            `variant.body.add(value_property);

            debug("added properties for variant {`variant.name}");
            debug("body now\n{`variant.body}");
        si

        visit(`variant: Trees.Definitions.VARIANT) is

            debug("adding init method for variant {`variant.name}...");

            let init_function = get_init_method_for_variant(`variant);

            debug("init function is \n{init_function}");

            `variant.body.add(init_function);

            debug_exit();
        si

        pre(`namespace: Definitions.NAMESPACE) -> bool is
            _stack.push(`namespace.body);

            return false;
        si

        visit(`namespace: Definitions.NAMESPACE) is
            _stack.pop();
        si
    si

    get_value_property(location: LOCATION, fields: Collections.List[Variables.VARIABLE]) -> Definitions.PROPERTY is
        let return_type: TypeExpressions.TypeExpression =
            if fields.count == 0 then
                new TypeExpressions.NAMED(
                    location,
                    new Identifiers.Identifier(
                        location,
                        "void"
                    )
                )
            elif fields.count == 1 then
                fields[0].type_expression.copy();
            else
                new TypeExpressions.TUPLE(
                    location,
                    new TypeExpressions.LIST(
                        location,
                        fields | .map((v: Variables.VARIABLE) => v.type_expression.copy())
                    )
                )
            fi;

        let body = 
            if fields.count == 0 then
                get_throw_body(location, "unit variant has no value");
            elif fields.count == 1 then
                get_value_body_single_field(location, fields[0]);
            else
                get_value_body_multiple_fields(location, fields);
            fi;

        let value_property = new Definitions.PROPERTY(
            location,
            return_type,
            new Identifiers.Identifier(
                location,
                "value"
            ),
            new Modifiers.LIST(
                location,
                null,
                null
            ),
            body,
            null,
            null
        );

        return value_property;
    si

    get_has_value_property(location: LOCATION, result: bool) -> Definitions.PROPERTY is
        let has_value_property = new Definitions.PROPERTY(
            location,
            new TypeExpressions.NAMED(
                location,
                new Identifiers.Identifier(
                    location,
                    "bool"
                )
            ),
            new Identifiers.Identifier(
                location,
                "has_value"
            ),
            new Modifiers.LIST(
                location,
                null,
                null
            ),
            new Trees.Bodies.EXPRESSION(
                location,
                new Trees.Expressions.Literals.BOOLEAN(
                    location,
                    if result then "true" else "false" fi
                )
            ),
            null,
            null
        );

        return has_value_property;
    si

    get_init_method_for_variant(`variant: Trees.Definitions.VARIANT) -> Definitions.FUNCTION is
        let initializers = 
            `variant.fields | 
                .map((v: Variables.VARIABLE) -> Trees.Statements.Statement is
                    let member_access = 
                        new Trees.Expressions.MEMBER(
                            LOCATION.internal,
                            new Trees.Expressions.SELF(`variant.location),
                            new Trees.Identifiers.Identifier(
                                LOCATION.internal,
                                v.name.name
                            ),
                            LOCATION.internal
                        );

                    let left = 
                        new Trees.Expressions.SIMPLE_LEFT_EXPRESSION(
                            `variant.location,
                            member_access
                        );

                    return new Trees.Statements.ASSIGNMENT(
                        `variant.location,
                        left,

                        new Trees.Expressions.IDENTIFIER(
                            `variant.location,
                            new Trees.Identifiers.Identifier(
                                `variant.location,
                                v.name.name
                            )
                        )
                    )                   
                si)
                .collect_list();

        let body_statement_list = new Trees.Statements.LIST(
            `variant.location,
            initializers
        );

        let init_body = new Trees.Bodies.BLOCK(
            LOCATION.internal,
            body_statement_list
        );

        let init_function = new Definitions.FUNCTION(
            LOCATION.internal,
            new Identifiers.Identifier(
                `variant.location,
                "init"
            ),
            null,
            new Variables.LIST(
                LOCATION.internal,
                new Collections.LIST[Variables.VARIABLE]()
            ),
            new TypeExpressions.NAMED(
                LOCATION.internal,
                new Identifiers.Identifier(
                    `variant.location,
                    "void"
                )
            ),
            new Modifiers.LIST(
                LOCATION.internal,
                null,
                null
            ),
            init_body
        );

        return init_function;
    si

    get_throw_body(location: LOCATION, message: string) -> Bodies.Body =>
        new Trees.Bodies.BLOCK(
            location,
            new Trees.Statements.LIST(
                location,
                new Collections.LIST[Statements.Statement]([
                    new Trees.Statements.THROW(
                        location,
                        new Trees.Expressions.NEW(
                            location,
                            new Trees.TypeExpressions.NAMED(
                                location,
                                new Trees.Identifiers.QUALIFIED(
                                    location,
                                    new Trees.Identifiers.Identifier(
                                        location,
                                        "System"
                                    ),
                                    "Exception",
                                    location,
                                    location
                                )
                            ),
                            new Trees.Expressions.LIST(
                                location,
                                new Collections.LIST[Expressions.Expression]([
                                    new Trees.Expressions.Literals.STRING(location, message)
                                ]: Expressions.Expression)
                            )
                        )
                    )
                ]: Statements.Statement)
            )
        );

    get_value_body_single_field(location: LOCATION, `field: Variables.VARIABLE) -> Bodies.Body =>
        new Trees.Bodies.EXPRESSION(
            location,
            new Trees.Expressions.IDENTIFIER(
                location,
                new Trees.Identifiers.Identifier(
                    location,
                    `field.name.name
                )
            )
        );

    get_value_body_multiple_fields(location: LOCATION, fields: Collections.List[Variables.VARIABLE]) -> Bodies.Body =>
        new Trees.Bodies.EXPRESSION(
            location,
            new Trees.Expressions.TUPLE(
                location,
                new Trees.Expressions.LIST(
                    location,
                    fields | .map((v: Variables.VARIABLE) -> Expressions.Expression => 
                        new Trees.Expressions.IDENTIFIER(
                            location,
                            new Identifiers.Identifier(
                                location,
                                v.name.name
                            )
                        )
                    )
                ),
                false
            )
        );
si
