namespace Syntax.Process is
    use System;
    
    use Logging;
    use Source;
    use Tree;

    class CONDITIONAL_COMPILATION: Visitor is
        // FIXME: Remove all instances of @IF.dotnet() before removing support from here

        _want_debug: bool;

        _flags: Collections.MAP[String,bool];

        is_analysis: bool;

        init() is
            super.init();

            _flags = new Collections.MAP[String,bool]();
        si

        set_is_enabled(name: String, value: bool) is
            _flags[name] = value;
            _flags["not." + name] = !value;
        si

        get_is_enabled(name: String) -> bool is
            if _flags.contains_key(name) then
                return _flags[name];
            fi            
        si

        get_is_enabled(pragma_: Pragma.NODE) -> bool is
            if !pragma_? || !pragma_.name? then
                return true;
            fi

            let name = pragma_.name.to_string();

            if !name.starts_with("IF.") then
                return true;
            fi

            return get_is_enabled(name.substring(3));
        si        
    
        set_analysis_want_debug(want_debug: bool) is
            is_analysis = true;

            set_is_enabled("debug", want_debug);

            if want_debug then
                System.Console.error.write_line("analysis will include @IF.debug() and exclude @IF.not.debug()");
            fi
        si
        
        apply(
            node: NODE,
            want_debug: bool
        ) is
            set_is_enabled("debug", want_debug);

            set_is_enabled("legacy", false);
            set_is_enabled("dotnet", true);

            node.walk(self);
        si

        // FIXME: #500 Pragma handling code is duplicated across multiple visitors
        visit(pragma_: Definition.PRAGMA) is
            assert pragma_.pragma_? else "pragma is null";

            if !get_is_enabled(pragma_.pragma_) then
                pragma_.definition =    
                    new Definition.LIST(
                        pragma_.definition.location, 
                        new Collections.LIST[Definition.NODE](0)
                    );
            fi            
        si

        visit(pragma_: Statement.PRAGMA) is
            assert pragma_.pragma_? else "pragma is null";

            if !get_is_enabled(pragma_.pragma_) then
                pragma_.statement = null;
            fi
        si        
    si
si