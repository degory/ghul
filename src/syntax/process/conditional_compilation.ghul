namespace Syntax.Process is
    use System;
    use Generic;

    use Logging;
    use Source;
    use Tree;

    class CONDITIONAL_COMPILATION: Visitor is
        _want_legacy: bool;
        _want_dotnet: bool;

        is_analysis: bool public;

        init() is
            super.init();
        si

        apply(
            node: NODE,
            want_legacy: bool,
            want_dotnet: bool
        ) is
            _want_legacy = want_legacy || is_analysis;
            _want_dotnet = want_dotnet;

            node.walk(self);
        si

        visit(pragma_: Definition.PRAGMA) is
            assert(pragma_.pragma_?, "pragma is null");

            let p = pragma_.pragma_;

            let name = p.name.toString();

            let is_legacy = name =~ "IF.legacy";
            let is_dotnet = name =~ "IF.dotnet";

            if 
                (is_legacy && !_want_legacy) ||
                (is_dotnet && !_want_dotnet)
            then
                pragma_.definition =    
                    new Definition.LIST(
                        pragma_.definition.location, 
                        new Definition.NODE[](0)
                    );
            fi            
        si

        visit(pragma_: Statement.PRAGMA) is
            assert(pragma_.pragma_?, "pragma is null");

            let p = pragma_.pragma_;

            let name = p.name.toString();

            let is_legacy = name =~ "IF.legacy";
            let is_dotnet = name =~ "IF.dotnet";

            if 
                (is_legacy && !_want_legacy)
            then
                pragma_.statement =
                    new Statement.EXPRESSION(
                        pragma_.statement.location,
                        new Expression.NULL(LOCATION.dummy)
                    );
            elif
                (is_dotnet && !_want_dotnet) 
            then
                pragma_.statement = null;
            fi
        si        
    si
si