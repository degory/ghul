namespace Syntax.Process.Printer is
    use System;
    use Logging;

    use Source;
    use Tree;

    class LEGACY: BASE  is
        _logger: Logger;

        _in_trait: bool;

        _stub_depth: int;

        init(logger: Logger) is
            super.init(true);

            _logger = logger;
        si

        error(location: LOCATION, message: String) is
            _logger.error(location, message);
        si

        apply(node: NODE) is
            node.accept(self);
        si

        print(value: String) is
            if _stub_depth == 0 then
                super.print(value);
            fi
        si
        
        print(location: LOCATION, type_expression: TypeExpression.NODE) is
            if !isa TypeExpression.INFER(type_expression) then
                type_expression.accept(self);
            else
                print("void");
            fi
        si

        print_name(name: String) is
            var c = name[0];

            if (c>='a'&&c<='z') || (c>='A'&&c<='Z') || c=='_' then
                print(name);
            else
                print("operator");
                print(name);
            fi
        si

        enter_stub() is
            _stub_depth = _stub_depth + 1;
        si

        leave_stub() is
            _stub_depth = _stub_depth - 1;
        si

        visit(variable: Variable.NODE) is
            if variable.name.name.startsWith("__") then
                return;
            fi
            
            if !isa TypeExpression.INFER(variable.type_expression) then
                variable.type_expression.accept(self);
                print(' ');
            else
                print("var ");
            fi
            variable.name.accept(self);
            if variable.initializer? then
                print(" = ");
                variable.initializer.accept(self);
            fi
        si

        visit(import_: Definition.IMPORT) is
            for i in import_.imports do
                print("import ");
                i.accept(self);
                println(";");
            od
        si

        visit(namespace_: Definition.NAMESPACE) is
            if _stub_depth > 0 then
                super.print("namespace __Dummy__ is si\n");
                return;
            fi
            
            var seen_any = false;
            for ns in namespace_.name do
                if seen_any then
                    print(' ');
                fi
                print("namespace ");
                print(ns);
                print(" is");
                seen_any = true;
            od
            println();
            indent();
            namespace_.body.accept(self);
            outdent();
            seen_any = false;
            for ns in namespace_.name do
                if seen_any then
                    print(' ');
                fi
                print("si");
                seen_any = true;
            od
            println();
        si

        visit(use_: Definition.USE) is
            for u in use_.uses do
                print("use ");
                u.accept(self);
                println(";");
            od
        si

        visit(class_: Definition.CLASS) is
            print("class ");

            class_.name.accept(self);

            if class_.arguments? then
                print('<');
                let first = true;
                for a in class_.arguments do
                    if !first then
                        print(",");
                    fi

                    if isa TypeExpression.NAMED_TUPLE_ELEMENT(a) then
                        let named = cast TypeExpression.NAMED_TUPLE_ELEMENT(a);

                        named.name.accept(self);
                    else
                        a.accept(self);
                    fi

                    first = false;
                od

                print('>');
            fi

            if class_.ancestors? then
                var count = 0;

                for ancestor in class_.ancestors do
                    if count == 0 then
                        print(" isa ");
                    elif count == 1 then
                        print(" do ");
                    else
                        print(", ");
                    fi
                    ancestor.accept(self);
                    count = count + 1;
                od
            fi
            println(" is");
            indent();

            class_.body.accept(self);
            
            outdent();
            println("si");
        si

        visit(trait_: Definition.TRAIT) is
            print("interface ");
            trait_.name.accept(self);

            if trait_.arguments? then
                print('<');
                trait_.arguments.accept(self);
                print('>');
            fi

            if trait_.ancestors? then
                print(" isa ");
                trait_.ancestors.accept(self);
            fi

            println(" is");
            indent();
            _in_trait = true;
            trait_.body.accept(self);
            _in_trait = false;
            outdent();
            println("si");
        si

        visit(struct_: Definition.STRUCT) is
            print("class ");

            struct_.name.accept(self);

            if struct_.arguments? then
                print('<');
                struct_.arguments.accept(self);
                print('>');
            fi

            println(" is");
            indent();

            struct_.body.accept(self);
            
            outdent();
            println("si");
        si        

        print_newline_after_body(body: Body.NODE) is
            if !body.is_block then
                println();
            fi
        si

        visit(function: Definition.FUNCTION) is
            if function.name.name.startsWith("__") then
                return;
            fi

            if function.modifiers? then
                function.modifiers.accept(self);
                print(' ');
            fi

            print(function.location, function.type_expression);
            print(' ');
            function.name.accept(self);
            print('(');
            function.arguments.accept(self);
            print(')');

            if function.body? then
                print(' ');
                function.body.accept(self);
                println();
            else
                println(";");
            fi
        si

        print_getter_modifiers(modifiers: Modifier.LIST, name: Identifier.NODE) is
            if modifiers.is_explicit_access then
                modifiers.access_modifier.accept(self);
                print(' ');
            elif 
                name!=null && name.name.startsWith('_')
            then
                print("private ");
            fi

            if modifiers.storage_class? then
                modifiers.storage_class.accept(self);
                print(' ');
            fi
        si

        print_setter_modifiers(modifiers: Modifier.LIST) is
            if modifiers.is_explicit_access then
                modifiers.access_modifier.accept(self);
                print(' ');
            elif !_in_trait then
                print("private ");
            fi

            if modifiers.storage_class? then
                modifiers.storage_class.accept(self);
                print(' ');
            fi
        si

        print_backing_field_modifiers(modifiers: Modifier.LIST) is
            print("private ");
            if modifiers.storage_class? then
                modifiers.storage_class.accept(self);
                print(' ');
            fi
        si

        visit(property: Definition.PROPERTY) is
            if property.is_auto_property then
                print_backing_field_modifiers(property.modifiers);
                print(property.location, property.type_expression);
                print(" __backing_");
                property.name.accept(self);
                println(";");

                print_getter_modifiers(property.modifiers, property.name);
                print("get ");
                print(property.location, property.type_expression);
                print(" ");

                property.name.accept(self);
                print(" is return __backing_");
                property.name.accept(self);
                println("; si");
                print_setter_modifiers(property.modifiers);
                print("set ");
                print(property.location, property.type_expression);
                print(" ");
                property.name.accept(self);
                print(" = __value is __backing_");
                property.name.accept(self);
                println(" = __value; si");
            elif property.read_body? || property.assign_body? then
 
                if property.read_body? then
                    print_getter_modifiers(property.modifiers, property.name);
                    print("get ");
                    print(property.location, property.type_expression);
                    print(' ');
                    property.name.accept(self);
                    print(' ');
                    property.read_body.accept(self);
                    println();
                fi

                if property.assign_body? then
                    print_setter_modifiers(property.modifiers);
                    print("set ");
                    print(property.location, property.type_expression);
                    print(' ');
                    property.name.accept(self);
                    print(' ');
                    print(" = ");
                    property.assign_argument.accept(self);
                    print(' ');
                    property.assign_body.accept(self);
                    println();
                fi
            else
                property.modifiers.accept(self);
                print(property.location, property.type_expression);
                print(" ");
                property.name.accept(self);
                println(";");
            fi
        si

        visit(indexer: Definition.INDEXER) is
            if indexer.read_body!=null || indexer.assign_body!=null then
                if indexer.read_body? then
                    print_getter_modifiers(indexer.modifiers, indexer.name);
                    print("get ");
                    print(indexer.location, indexer.type_expression);
                    if indexer.name? then
                        print(' ');
                        indexer.name.accept(self);
                    fi
                    print("[");
                    indexer.index_argument.accept(self);
                    print("]");
                    indexer.read_body.accept(self);
                    println();
                fi

                if indexer.assign_body? then
                    print_setter_modifiers(indexer.modifiers);
                    print("set ");
                    print(indexer.location, indexer.type_expression);
                    if indexer.name? then
                        print(' ');
                        indexer.name.accept(self);
                    fi
                    print("[");
                    indexer.index_argument.accept(self);
                    print("] =");
                    indexer.assign_argument.accept(self);
                    print(" ");
                    indexer.assign_body.accept(self);
                    println();
                fi
            else
                error(indexer.location, "indexer must have at at least read or assign body");
            fi
        si

        visit(pragma_: Definition.PRAGMA) is
            process_pragma(pragma_.pragma_, true);

            pragma_.definition.accept(self);

            process_pragma(pragma_.pragma_, false);
        si

        process_pragma(pragma_: Pragma.NODE, is_enter: bool) is
            if !pragma_? then
                IO.Std.err.println("pragma is null");
                return;
            fi

            let name = pragma_.name.toString();

            if name =~ "IL.stub" then
                if is_enter then
                    enter_stub();
                else
                    leave_stub();
                fi
            fi            
        si

        visit(type_expression: TypeExpression.BUILT_IN) is
            print(type_expression.name);
        si

        visit(array: TypeExpression.ARRAY_) is
            array.element.accept(self);
            print("[]");
        si

        visit(pointer: TypeExpression.POINTER) is
            pointer.element.accept(self);
            print(" ptr");
        si

        visit(reference: TypeExpression.REFERENCE) is
            reference.element.accept(self);
            print(" ref");
        si

        visit(named: TypeExpression.NAMED) is
            named.name.accept(self);
        si

        visit(generic: TypeExpression.GENERIC) is
            generic.name.accept(self);
            print('<');
            generic.arguments.accept(self);
            print('>');
        si

        visit(function: TypeExpression.FUNCTION) is
            print(function.location, function.result);
            print(" proc(");
            function.arguments.accept(self);
            print(")");
        si

        visit(tuple: TypeExpression.TUPLE) is
            print("(");
            tuple.elements.accept(self);
            print(")");
        si

        visit(types: TypeExpression.LIST) is
            var seen_any = false;
            for t in types do
                if seen_any then
                    print(',');
                fi
                t.accept(self);
                seen_any = true;
            od
        si

        visit(null_: Expression.NULL) is
            print("null");
        si

        visit(self_: Expression.SELF) is
            print("this");
        si

        visit(new_: Expression.NEW) is
            location(new_);
            print("new ");

            if isa Tree.TypeExpression.ARRAY_(new_.type_expression) then
                cast Tree.TypeExpression.ARRAY_(new_.type_expression).element.accept(self);
                print('[');
                new_.arguments.accept(self);
                print(']');
            else
                new_.type_expression.accept(self);
                print('(');
                new_.arguments.accept(self);
                print(')');
            fi
        si

        visit(unary: Expression.UNARY) is
            location(unary);
            print(unary.operation.name);
            print(' ');
            unary.right.accept(self);
        si

        visit(binary: Expression.BINARY) is
            location(binary);
            binary.left.accept(self);
            print(' ');
            print(binary.actual_operation);
            print(' ');
            binary.right.accept(self);
        si

        visit(variable: Expression.VARIABLE) is
            if !isa TypeExpression.INFER(variable.type_expression) then
                variable.type_expression.accept(self);
                print(' ');
            else
                _logger.error(variable.location, "legacy compiler cannot infer type_expression here");
            fi

            variable.name.accept(self);

            if variable.initializer? then
                print(" = ");
                variable.initializer.accept(self);
            fi
        si

        visit(function: Syntax.Tree.Expression.FUNCTION) is
            print(function.location, function.type_expression);
            print(" proc");
            function.arguments.accept(self);

            if function.uses? then
                print(" use ");
                var seen_any = false;
                for u in function.uses do
                    if seen_any then
                        print(",");
                    fi
                    u.accept(self);
                od
            fi

            print(" ");
            function.body.accept(self);
        si

        visit(sequence: Expression.SEQUENCE) is
            if !isa Tree.TypeExpression.INFER(sequence.type_expression) then
                sequence.type_expression.accept(self);
            fi
            
            print('{');
            sequence.elements.accept(self);
            print('}');
        si

        visit(has_value: Expression.HAS_VALUE) is
            print("((");
            has_value.left.accept(self);
            print(")!=null)");
        si

        visit(l: Statement.LET) is
            l.variables.accept(self);
            println(";");
        si

        visit(match: Statement.CASE_MATCH) is
            if match.expressions? then
                print("is ");
                match.expressions.accept(self);
                println(":");
            else
                println("default:");
            fi
            
            indent();
            match.statements.accept(self);
            outdent();
        si

        visit(for_: Statement.FOR) is
            print("foreach ");
            for_.variable.accept(self);
            print("; ");
            for_.expression.accept(self);
            println(" do");
            indent();
            for_.body.accept(self);
            outdent();
            println("od");
        si

        visit(expression: Body.EXPRESSION) is
            print(" is return ");
            expression.expression.accept(self);
            print("; si");
        si

        visit(block: Body.BLOCK) is
            println("is");
            indent();
            block.statements.accept(self);
            outdent();
            print("si");
        si
    si
si
