namespace Syntax.Process.Printer is
    use System;
    use Tree;
    use Source;

    class BASE : StrictVisitor  is
        _indent: int;
        _depth: int;
        _run_on: bool;
        _indent_needed: bool;
        _want_locations: bool;
        _current_line: int;
        _result: StringBuffer;
        
        init(want_locations: bool) is
            super.init();

            _want_locations = want_locations;
            _depth = 0;
            _indent = 2;
            _result = new StringBuffer();
            _current_line = 1;
        si

        result: String is
            return _result;
        si

        println(string: String) is
            print(string);
            println();
        si

        print(string: String) is
            print_indent();
            _result.append(string);
        si

        print(c: char) is
            print_indent();
            _result.append(c);
        si

        println() is
            _current_line = _current_line + 1;
            _indent_needed = true;
            _result.append('\n');
        si

        indent() is
            _depth = _depth + 1;
        si

        outdent() is
            _depth = _depth - 1;
        si

        print_indent() is
            if _indent_needed then
                var i = 0;
                while i < _indent*_depth do
                    _result.append(' ');
                    i = i + 1;
                od
                _indent_needed = false;
            fi
        si

        location(node: NODE) is
            location(node.location);
        si

        location(location: LOCATION) is
            var new_line = location.start_line;
            if new_line != _current_line then
                _current_line = new_line;
                if _want_locations then
                    print("#" + _current_line + " ");
                fi
            fi
        si

        print(node: NODE) is
            node.accept(self);
        si

        print_name(name: String) is
            print(name);
        si

        visit(identifier: Identifier.NODE) is
            location(identifier);
            print_name(identifier.name);
        si

        visit(identifier: Identifier.QUALIFIED) is
            location(identifier);
            identifier.qualifier.accept(self);
            print('.');
            print_name(identifier.name);
        si

        visit(modifier: Modifier.NODE) is
            location(modifier);
            print(modifier.name);
        si

        visit(modifiers: Modifier.LIST) is
            location(modifiers);
            if modifiers.access_modifier? then
                modifiers.access_modifier.accept(self);
                print(' ');
            fi
            if modifiers.storage_class? then
                modifiers.storage_class.accept(self);
                print(' ');
            fi
        si

        visit(variables: Variable.LIST) is
            location(variables);
            var first = true;
            for v in variables do
                if !first then
                    print(", ");
                fi
                v.accept(self);
                first = false;
            od
        si

        visit(definitions: Definition.LIST) is
            location(definitions);
            for d in definitions do
                d.accept(self);

                // yuck...
                if isa Variable.NODE(d) then
                    if !cast Variable.NODE(d).name.name.startsWith("__") then
                        println(";");
                    fi
                fi
            od
        si

        visit(enum_: Definition.ENUM) is
            location(enum_);
            print("enum ");
            enum_.name.accept(self);
            println(" is");
            indent();
            var seen_any = false;
            for member in enum_.members do
                if seen_any then
                    println(",");
                fi
                member.accept(self);
                seen_any = true;
            od
            println();
            outdent();
            println("si");
        si

        visit(member: Definition.ENUM_MEMBER) is
            location(member);
            member.name.accept(self);
            if member.initializer? then
                print(" = ");
                member.initializer.accept(self);
            fi
        si

        visit(functions: Definition.FUNCTION_GROUP) is
            println("function group ");
            indent();
            for f in functions.functions do
                f.accept(self);
            od
            outdent();
        si
        
        visit(pragma_: Definition.PRAGMA) is
            pragma_.definition.accept(self);
        si

        visit(type_expression: TypeExpression.INFER) is
            print("infer");
        si

        visit(type_expression: TypeExpression.BUILT_IN) is
            print(type_expression.name);
        si

        visit(array: TypeExpression.ARRAY_) is
            array.element.accept(self);
            print("[]");
        si

        visit(pointer: TypeExpression.POINTER) is
            pointer.element.accept(self);
            print(" ptr");
        si

        visit(reference: TypeExpression.REFERENCE) is
            reference.element.accept(self);
            print(" ref");
        si

        visit(functions: TypeExpression.FUNCTION_GROUP) is
            print("function group ");
            for f in functions.functions do
                f.accept(self);
                print(' ');
            od
        si

        visit(named: TypeExpression.NAMED) is
            location(named);
            named.name.accept(self);
        si

        visit(tuple: TypeExpression.TUPLE) is
            location(tuple);
            print("(");
            tuple.elements.accept(self);
            print(")");
        si

        visit(element: TypeExpression.NAMED_TUPLE_ELEMENT) is
            location(element);
            element.name.accept(self);
            print(": ");
            element.type_expression.accept(self);
        si

        visit(types: TypeExpression.LIST) is
            location(types);
            var seen_any = false;
            for t in types.Iterator do
                if seen_any then
                    print(',');
                fi
                t.accept(self);
                seen_any = true;
            od
        si

        visit(none_: Expression.Literal.NONE) is
            location(none_);
            print("none");
        si

        visit(identifier: Expression.IDENTIFIER) is
            location(identifier);
            identifier.identifier.accept(self);
        si

        visit(super_: Expression.SUPER) is
            location(super_);
            print("super");
        si

        visit(native_: Expression.NATIVE) is
            location(native_);
            print("native");
        si        

        visit(new_: Expression.NEW) is
            location(new_);
            print("new ");
            new_.type_expression.accept(self);
            print('(');
            new_.arguments.accept(self);
            print(')');
        si

        visit(cast_: Expression.CAST) is
            location(cast_);
            print("cast ");
            cast_.type_expression.accept(self);
            print('(');
            cast_.right.accept(self);
            print(')');
        si

        visit(isa_: Expression.ISA) is
            location(isa_);
            print("isa ");
            isa_.type_expression.accept(self);
            print('(');
            isa_.right.accept(self);
            print(')');
        si

        visit(tuple: Expression.TUPLE) is
            location(tuple);
            print('(');
            tuple.elements.accept(self);
            print(')');
        si

        visit(call: Expression.CALL) is
            location(call);
            call.function.accept(self);
            print('(');
            call.arguments.accept(self);
            print(')');
        si

        visit(member: Expression.MEMBER) is
            location(member);
            member.left.accept(self);
            print('.');
            member.identifier.accept(self);
        si

        visit(index: Expression.INDEX) is
            location(index);
            index.left.accept(self);
            print('[');
            index.index.accept(self);
            print(']');
        si

        visit(unary: Expression.UNARY) is
            location(unary);
            unary.operation.accept(self);
            print(' ');
            unary.right.accept(self);
        si

        visit(binary: Expression.BINARY) is
            location(binary);
            binary.left.accept(self);
            print(' ');
            binary.operation.accept(self);
            print(' ');
            binary.right.accept(self);
        si

        visit(expressions: Expression.LIST) is
            location(expressions);
            var seen_any = false;
            for e in expressions do
                if seen_any then
                    print(',');
                fi
                e.accept(self);
                seen_any = true;
            od
        si

        visit(literal: Expression.Literal.NODE) is
            location(literal);
            print(literal.string);
        si

        print_escape_char(c: char) is
            var ci = cast int(c);
            if ci < 32 then
                print("\\");
                var b = new StringBuffer(5);
                b.append(ci & 0xFF, 8);
                print(b);
            elif ci == 34 then
                print("\\");
                print(cast char(34));
            elif ci == 39 then
                print("'");
            elif ci == 92 then
                print("\\\\");
            else
                print(c);
            fi
        si

        visit(string: Expression.Literal.STRING) is
            location(string);
            print(cast char(34));
            for c in string.string do
                print_escape_char(c);
            od
            print(cast char(34));
        si

        visit(integer: Expression.Literal.INTEGER) is
            location(integer);
            print(integer.string);
        si        

        visit(character: Expression.Literal.CHARACTER) is
            location(character);
            print("'");
            print_escape_char(character.string[0]);
            print("'");
        si

        visit(list: Statement.LIST) is
            location(list);
            for s in list.Iterator do
                s.accept(self);
            od
        si

        visit(assign: Statement.ASSIGNMENT) is
            location(assign);
            assign.left.accept(self);
            print(" = ");
            assign.right.accept(self);
            println(";");
        si

        visit(expression: Statement.EXPRESSION) is
            location(expression);
            expression.expression.accept(self);
            println(";");
        si

        visit(v: Statement.VAR) is
            location(v);
            v.variables.accept(self);
            println(";");
        si

        visit(r: Statement.RETURN) is
            location(r);
            print("return");
            if r.expression? then
                print(' ');
                r.expression.accept(self);
            fi
            println(";");
        si

        visit(t: Statement.THROW) is
            location(t);
            print("throw");
            if t.expression? then
                print(' ');
                t.expression.accept(self);
            fi
            println(";");
        si

        visit(i: Statement.IF) is
            location(i);
            var is_first = true;
            var seen_else = false;            
            assert(i?);
            assert(i.branches?);
            for b in i.branches do
                location(b);

                if seen_else then
                    IoC.CONTAINER.instance.logger.error(b.location, "broken if statement");
                fi

                assert(!seen_else);
                if b.condition? then
                    if is_first then
                        print("if ");
                    else
                        print("elif ");
                    fi
                    b.condition.accept(self);
                    println(" then");
                else
                    seen_else = true;
                    println("else");
                fi
                indent();
                b.body.accept(self);
                outdent();
                is_first = false;
            od
            println("fi");
        si

        visit(case_: Statement.CASE) is
            location(case_);
            print("case ");
            case_.expression.accept(self);
            println();
            for m in case_.matches do
                m.accept(self);
            od
            println("esac");
        si

        visit(match: Statement.CASE_MATCH) is
            location(match);
            if match.expressions? then
                print("when ");
                match.expressions.accept(self);
                println(":");
            else
                println("default");
            fi
            indent();
            match.statements.accept(self);
            outdent();
        si

        visit(try_: Statement.TRY) is
            location(try_);
            println("try");
            indent();
            try_.body.accept(self);
            outdent();
            for c in try_.catches do
                c.accept(self);
            od
            if try_.finally_? then
                println("finally");
                indent();
                try_.finally_.accept(self);
                outdent();
            fi
            println("yrt");
        si

        visit(catch_: Statement.CATCH) is
            location(catch_);
            print("catch ");
            catch_.variable.accept(self);
            println();
            indent();
            catch_.body.accept(self);
            outdent();
        si

        visit(do_: Statement.DO) is
            location(do_);
            if do_.condition? then
                print("while ");
                do_.condition.accept(self);
                print(" ");
            fi
            println("do");
            indent();
            do_.body.accept(self);
            outdent();
            println("od");
        si

        visit(labelled: Statement.LABELLED) is
            location(labelled);
            labelled.label.accept(self);
            print(": ");
            labelled.statement.accept(self);
        si

        visit(break_: Statement.BREAK) is
            location(break_);
            print("break");
            if break_.label? then
                print(' ');
                break_.label.accept(self);
            fi
            println(";");
        si

        visit(continue_: Statement.CONTINUE) is
            location(continue_);
            print("continue");
            if continue_.label? then
                print(' ');
                continue_.label.accept(self);
            fi
            println(";");
        si

        visit(pragma_: Statement.PRAGMA) is
            if pragma_.statement? then
                pragma_.statement.accept(self);                
            fi
        si

        visit(block: Body.NULL) is
            println(";");
        si       
    si
si
