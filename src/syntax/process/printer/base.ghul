namespace Syntax.Process.Printer is
    use IO.Std;

    use Trees;
    use Source;

    class Base : StrictVisitor  is
        _indent: int;
        _depth: int;
        _run_on: bool;
        _indent_needed: bool;
        _want_locations: bool;
        _current_line: int;
        _result: System.Text.StringBuilder;
        
        init(want_locations: bool) is
            super.init();

            _want_locations = want_locations;
            _depth = 0;
            _indent = 2;
            _result = new System.Text.StringBuilder();
            _current_line = 1;
        si

        result: string is
            return _result.to_string();
        si

        write_line(value: string) is
            write(value);
            write_line();
        si

        write(value: string) is
            write_indent();
            _result.append(value);
        si

        write(c: char) is
            write_indent();
            _result.append(c);
        si

        write_line() is
            _current_line = _current_line + 1;
            _indent_needed = true;
            _result.append('\n');
        si

        indent() is
            _depth = _depth + 1;
        si

        outdent() is
            _depth = _depth - 1;
        si

        write_indent() is
            if _indent_needed then
                let i = 0;
                while i < _indent*_depth do
                    _result.append(' ');
                    i = i + 1;
                od
                _indent_needed = false;
            fi
        si

        location(node: Node) is
            location(node.location);
        si

        location(location: LOCATION) is
            let new_line = location.start_line;
            if new_line != _current_line then
                _current_line = new_line;
                if _want_locations then
                    write("#" + _current_line + " ");
                fi
            fi
        si

        write(node: Node) is
            node.accept(self);
        si

        write_name(name: string) is
            write(name);
        si

        visit(identifier: Identifiers.Identifier) is
            location(identifier);
            write_name(identifier.name);
        si

        visit(identifier: Identifiers.QUALIFIED) is
            location(identifier);
            identifier.qualifier.accept(self);
            write('.');
            write_name(identifier.name);
        si

        visit(modifier: Modifiers.Modifier) is
            location(modifier);
            write(modifier.name);
        si

        visit(modifiers: Modifiers.LIST) is
            location(modifiers);
            if modifiers.access_modifier? then
                modifiers.access_modifier.accept(self);
                write(' ');
            fi
            if modifiers.storage_class? then
                modifiers.storage_class.accept(self);
                write(' ');
            fi
        si

        visit(variables: Variables.LIST) is
            location(variables);
            let first = true;
            for v in variables do
                if !first then
                    write(", ");
                fi
                v.accept(self);
                first = false;
            od
        si

        visit(definitions: Definitions.LIST) is
            location(definitions);
            for d in definitions do
                d.accept(self);

                // yuck...
                if isa Variables.Variable(d) then
                    if !cast Variables.Variable(d).name.name.starts_with("$") then
                        write_line(";");
                    fi
                fi
            od
        si

        visit(`enum: Definitions.ENUM) is
            location(`enum);
            write("enum ");
            `enum.name.accept(self);
            write_line(" is");
            indent();
            let seen_any = false;
            for member in `enum.members do
                if seen_any then
                    write_line(",");
                fi
                member.accept(self);
                seen_any = true;
            od
            write_line();
            outdent();
            write_line("si");
        si

        visit(member: Definitions.ENUM_MEMBER) is
            location(member);
            member.name.accept(self);
            if member.initializer? then
                write(" = ");
                member.initializer.accept(self);
            fi
        si

        visit(functions: Definitions.FUNCTION_GROUP) is
            write_line("function group ");
            indent();
            for f in functions.functions do
                f.accept(self);
            od
            outdent();
        si
        
        visit(pragma: Pragmas.PRAGMA) is
            write("@");

            pragma.name.accept(self);
            write("(");

            pragma.arguments.accept(self);
            write_line(")");
        si

        visit(pragma: Definitions.PRAGMA) is
            pragma.definition.accept(self);
        si

        visit(type_expression: TypeExpressions.UNDEFINED) is
            write("???");
        si

        visit(type_expression: TypeExpressions.INFER) is
            write("infer");
        si

        visit(array: TypeExpressions.ARRAY_) is
            array.element.accept(self);
            write("[]");
        si

        visit(pointer: TypeExpressions.POINTER) is
            pointer.element.accept(self);
            write(" ptr");
        si

        visit(reference: TypeExpressions.REFERENCE) is
            reference.element.accept(self);
            write(" ref");
        si

        visit(functions: TypeExpressions.FUNCTION_GROUP) is
            write("function group ");
            for f in functions.functions do
                f.accept(self);
                write(' ');
            od
        si

        visit(named: TypeExpressions.NAMED) is
            location(named);
            named.name.accept(self);
        si

        visit(tuple: TypeExpressions.TUPLE) is
            location(tuple);
            write("(");
            tuple.elements.accept(self);
            write(")");
        si

        visit(element: TypeExpressions.NAMED_TUPLE_ELEMENT) is
            location(element);
            element.name.accept(self);
            write(": ");
            element.type_expression.accept(self);
        si

        visit(types: TypeExpressions.LIST) is
            location(types);
            let seen_any = false;
            for t in types do
                if seen_any then
                    write(',');
                fi
                t.accept(self);
                seen_any = true;
            od
        si

        visit(`none: Expressions.Literals.NONE) is
            location(`none);
            write("none");
        si

        visit(identifier: Expressions.IDENTIFIER) is
            location(identifier);
            identifier.identifier.accept(self);
        si

        visit(`super: Expressions.SUPER) is
            location(`super);
            write("super");
        si

        visit(`new: Expressions.NEW) is
            location(`new);
            write("new ");
            `new.type_expression.accept(self);
            write('(');
            `new.arguments.accept(self);
            write(')');
        si

        visit(`cast: Expressions.CAST) is
            location(`cast);
            write("cast ");
            `cast.type_expression.accept(self);
            write('(');
            `cast.right.accept(self);
            write(')');
        si

        visit(`isa: Expressions.ISA) is
            location(`isa);
            write("isa ");
            `isa.type_expression.accept(self);
            write('(');
            `isa.right.accept(self);
            write(')');
        si

        visit(`typeof: Expressions.TYPEOF) is
            location(`typeof);
            write("typeof ");
            `typeof.type_expression.accept(self);
        si

        visit(tuple: Expressions.TUPLE) is
            location(tuple);
            write('(');
            tuple.elements.accept(self);
            write(')');
        si

        visit(call: Expressions.CALL) is
            location(call);
            call.function.accept(self);
            write('(');
            call.arguments.accept(self);
            write(')');
        si

        visit(member: Expressions.MEMBER) is
            location(member);
            member.left.accept(self);
            write('.');
            member.identifier.accept(self);
        si

        visit(explicit_specialization: Trees.Expressions.EXPLICIT_SPECIALIZATION) is
            location(explicit_specialization);
            explicit_specialization.left.accept(self);
            write("`[");
            explicit_specialization.types.accept(self);
            write("]");
        si

        visit(index: Expressions.INDEX) is
            location(index);
            index.left.accept(self);
            write('[');
            index.index.accept(self);
            write(']');
        si

        visit(unary: Expressions.UNARY) is
            location(unary);
            unary.operation.accept(self);
            write(' ');
            unary.right.accept(self);
        si

        visit(binary: Expressions.BINARY) is
            location(binary);
            binary.left.accept(self);
            write(' ');

            if binary.actual_operation? then
                write(binary.actual_operation);
            else
                binary.operation.accept(self);
            fi
            
            write(' ');
            binary.right.accept(self);
        si

        visit(expressions: Expressions.LIST) is
            location(expressions);
            let seen_any = false;
            for e in expressions do
                if seen_any then
                    write(',');
                fi
                e.accept(self);
                seen_any = true;
            od
        si

        visit(literal: Expressions.Literals.Literal) is
            location(literal);
            write(literal.value_string);
        si

        write_escape_char(c: char) is
            let ci = cast int(c);
            if ci < 32 then
                write("\\" + string.format("X", ci));
            elif ci == 34 then
                write("\\");
                write(cast char(34));
            elif ci == 39 then
                write("'");
            elif ci == 92 then
                write("\\\\");
            else
                write(c);
            fi
        si

        visit(`string: Expressions.Literals.STRING) is
            location(`string);
            write(cast char(34));
            for c in `string.value_string do
                write_escape_char(c);
            od
            write(cast char(34));
        si

        visit(interpolation: Expressions.STRING_INTERPOLATION) is
            location(interpolation);

            let in_expression = false;

            // TODO proper quoting
            write("{{");
            for e in interpolation.values do
                if in_expression then
                    write("{{");
                fi

                e.accept(self);

                if in_expression then
                    write("}}");
                fi

                in_expression = !in_expression;
            od
            write("}}");
        si

        visit(integer: Expressions.Literals.INTEGER) is
            location(integer);
            write(integer.value_string);
        si        

        visit(float: Expressions.Literals.FLOAT) is
            location(float);
            write(float.value_string);
        si

        visit(character: Expressions.Literals.CHARACTER) is
            location(character);
            write("'");
            write_escape_char(character.value_string.get_chars(0));
            write("'");
        si

        visit(boolean: Expressions.Literals.BOOLEAN) is
            location(boolean);
            write(boolean.value_string);
        si

        visit(list: Statements.LIST) is
            location(list);
            for s in list do
                s.accept(self);
            od
        si

        visit(assign: Statements.ASSIGNMENT) is
            location(assign);
            assign.left.accept(self);
            write(" = ");
            assign.right.accept(self);
            write_line(";");
        si

        visit(expression: Statements.EXPRESSION) is
            location(expression);
            expression.expression.accept(self);
            write_line(";");
        si

        visit(v: Statements.VAR) is
            location(v);
            v.variables.accept(self);
            write_line(";");
        si

        visit(r: Statements.RETURN) is
            location(r);
            write("return");
            if r.expression? then
                write(' ');
                r.expression.accept(self);
            fi
            write_line(";");
        si

        visit(t: Statements.THROW) is
            location(t);
            write("throw");
            if t.expression? then
                write(' ');
                t.expression.accept(self);
            fi
            write_line(";");
        si

        visit(t: Statements.ASSERT) is
            location(t);
            write("assert");
            if t.expression? then
                write(' ');
                t.expression.accept(self);
            fi

            if t.message? then
                write(" else ");
                t.message.accept(self);
            fi

            write_line(";");
        si

        visit(i: Statements.IF) is
            location(i);
            let is_first = true;
            let seen_else = false;

            assert i?;
            assert i.branches?;

            for b in i.branches do
                location(b);

                if seen_else then
                    IoC.CONTAINER.instance.logger.error(b.location, "broken if statement");
                fi

                assert !seen_else;
                
                if b.condition? then
                    if is_first then
                        write("if ");
                    else
                        write("elif ");
                    fi
                    b.condition.accept(self);
                    write_line(" then");
                else
                    seen_else = true;
                    write_line("else");
                fi
                indent();
                b.body.accept(self);
                outdent();
                is_first = false;
            od
            write_line("fi");
        si

        visit(`case: Statements.CASE) is
            location(`case);
            write("case ");
            `case.expression.accept(self);
            write_line();
            for m in `case.matches do
                m.accept(self);
            od
            write_line("esac");
        si

        visit(match: Statements.CASE_MATCH) is
            location(match);
            if match.expressions? then
                write("when ");
                match.expressions.accept(self);
                write_line(":");
            else
                write_line("default");
            fi
            indent();
            match.statements.accept(self);
            outdent();
        si

        visit(`try: Statements.TRY) is
            location(`try);
            write_line("try");
            indent();
            `try.body.accept(self);
            outdent();
            for c in `try.catches do
                c.accept(self);
            od
            if `try.`finally? then
                write_line("finally");
                indent();
                `try.`finally.accept(self);
                outdent();
            fi
            write_line("yrt");
        si

        visit(`catch: Statements.CATCH) is
            location(`catch);
            write("catch ");
            `catch.variable.accept(self);
            write_line();
            indent();
            `catch.body.accept(self);
            outdent();
        si

        visit(`do: Statements.DO) is
            location(`do);
            if `do.condition? then
                write("while ");
                `do.condition.accept(self);
                write(" ");
            fi
            write_line("do");
            indent();
            `do.body.accept(self);
            outdent();
            write_line("od");
        si

        visit(labelled: Statements.LABELLED) is
            location(labelled);
            labelled.label.accept(self);
            write(": ");
            labelled.statement.accept(self);
        si

        visit(`break: Statements.BREAK) is
            location(`break);
            write("break");
            if `break.label? then
                write(' ');
                `break.label.accept(self);
            fi
            write_line(";");
        si

        visit(`continue: Statements.CONTINUE) is
            location(`continue);
            write("continue");
            if `continue.label? then
                write(' ');
                `continue.label.accept(self);
            fi
            write_line(";");
        si

        visit(pragma: Statements.PRAGMA) is
            if pragma.statement? then
                pragma.statement.accept(self);                
            fi
        si

        visit(block: Bodies.NULL) is
            write_line(";");
        si       
    si
si
