namespace Syntax.Process is
    use IO.Std;

    use Logging;

    use Semantic.Types.Type;

    // TODO make sure we only do this if we're actually generating IL -
    // it shouldn't be needed for the language extension
    class RECORD_TYPE_ARGUMENT_USES: ScopedVisitor is
        _logger: Logger;
        _symbol_table: Semantic.SYMBOL_TABLE;
        _innate_symbol_lookup: Semantic.Lookups.InnateSymbolLookup;

        init(
            logger: Logger,
            stable_symbols: Semantic.STABLE_SYMBOLS,
            symbol_table: Semantic.SYMBOL_TABLE,
            namespaces: Semantic.NAMESPACES,
            innate_symbol_lookup: Semantic.Lookups.InnateSymbolLookup
        )
        is
            super.init(logger, stable_symbols, symbol_table, namespaces);

            _symbol_table = symbol_table;
            _logger = logger;
            _innate_symbol_lookup = innate_symbol_lookup;
        si

        apply(root: Trees.Node) is
            root.walk(self);
        si

        pre(ambiguous_expression: Trees.Expressions.AMBIGUOUS_EXPRESSION) -> bool is
            if ambiguous_expression.result == Trees.Expressions.AMBIGUOUS_EXPRESSION_RESULT.INDEX then
                return false;
            fi

            ambiguous_expression.type_arguments.walk(self);

            return true;
        si

        visit(named: Trees.TypeExpressions.NAMED) is  
            if !named.type? then
                return;
            fi
            
            if !named.type.is_type_variable then
                return;
            fi

            let closure = current_closure;

            if !closure? then
                return;
            fi

            closure.add_type_argument_reference(named.type.symbol);
        si
    si
si
