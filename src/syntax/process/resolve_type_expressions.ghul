namespace Syntax.Process is
    use STD = System.Console;

    use System;
    
    use Logging;

    class RESOLVE_TYPE_EXPRESSIONS: ScopedVisitor is
        _logger: Logger;
        _symbol_table: Semantic.SYMBOL_TABLE;
        _symbol_use_locations: Semantic.SYMBOL_USE_LOCATIONS;
        _ghul_symbol_lookup: Semantic.GHUL_SYMBOL_LOOKUP;
        _system_symbol_lookup: Semantic.SYSTEM_SYMBOL_LOOKUP;

        _innate_type_map: Collections.MAP[Semantic.Symbol.BASE, Semantic.Type.NAMED];

        init(
            logger: Logger,
            symbol_table: Semantic.SYMBOL_TABLE,
            namespaces: Semantic.NAMESPACES,
            symbol_use_locations: Semantic.SYMBOL_USE_LOCATIONS,
            ghul_symbol_lookup: Semantic.GHUL_SYMBOL_LOOKUP,
            system_symbol_lookup: Semantic.SYSTEM_SYMBOL_LOOKUP)
        is
            super.init(logger, symbol_table, namespaces);

            _logger = logger;
            _symbol_table = symbol_table;
            _symbol_use_locations = symbol_use_locations;
            _ghul_symbol_lookup = ghul_symbol_lookup;
            _system_symbol_lookup = system_symbol_lookup;
        si

        _initialize_type_map() is
            if _innate_type_map? then
                return;
            fi

            _innate_type_map = new Collections.MAP[Semantic.Symbol.BASE, Semantic.Type.NAMED]();

            add_innate_type(_ghul_symbol_lookup.get_char_type());    
            add_innate_type(_ghul_symbol_lookup.get_byte_type());
            add_innate_type(_ghul_symbol_lookup.get_ubyte_type());    
            add_innate_type(_ghul_symbol_lookup.get_short_type());
            add_innate_type(_ghul_symbol_lookup.get_ushort_type());
            add_innate_type(_ghul_symbol_lookup.get_int_type());
            add_innate_type(_ghul_symbol_lookup.get_uint_type());
            add_innate_type(_ghul_symbol_lookup.get_long_type());
            add_innate_type(_ghul_symbol_lookup.get_ulong_type());
            add_innate_type(_ghul_symbol_lookup.get_word_type());
        si

        apply(root: Tree.NODE) is
            _initialize_type_map();

            root.walk(self);
        si

        add_innate_type(type: Semantic.Type.NAMED) is
            _innate_type_map[type.symbol] = type;
        si

        map_type(symbol: Semantic.Symbol.BASE) -> Semantic.Type.NAMED is
            if _innate_type_map.contains_key(symbol) then
                return _innate_type_map[symbol];                
            fi
        si

        visit(named: Tree.TypeExpression.NAMED) is
            named.type = null;

            let symbol = find(named.name);

            if symbol? then
                if isa Semantic.Type.Typed(symbol) then
                    _symbol_use_locations.add_symbol_use(named.name.location, symbol);

                    let type = map_type(symbol);

                    if type? then
                        named.type = type;
                    else                        
                        let typed_symbol = cast Semantic.Type.Typed(symbol);

                        named.type = typed_symbol.type;
                    fi
                else
                    _logger.error(named.name.location, "not a type: " + named + " " + named.get_type());
                fi
            fi
        si

        visit(element: Tree.TypeExpression.NAMED_TUPLE_ELEMENT) is
            element.type_expression.accept(self);            
        si

        visit(generic: Tree.TypeExpression.GENERIC) is
            generic.type = null;

            let symbol = find(generic.name);

            if symbol? then
                _symbol_use_locations.add_symbol_use(generic.name.location, symbol);

                if !isa Semantic.Symbol.Classy(symbol) then
                    _logger.error(generic.location, "cannot supply type arguments here");
                    return;
                fi

                let arguments = new Collections.LIST[Semantic.Type.BASE]();

                for a in generic.arguments do
                    if a.type? then
                        arguments.add(a.type);
                    else
                        arguments.add(new Semantic.Type.ANY());
                    fi
                od

                generic.type = new Semantic.Type.GENERIC(
                    generic.location,
                    cast Semantic.Symbol.Classy(symbol),
                    arguments);
            fi
        si

        visit(structured: Tree.TypeExpression.STRUCTURED) is
            STD.error.write_line("visit TypeExpression.STRUCTURED called");
        si

        get_type_or_any(element: Semantic.Type.BASE) -> Semantic.Type.BASE is
            if element? then
                return element;
            else
                return new Semantic.Type.ANY();
            fi
        si
        
        visit(array: Tree.TypeExpression.ARRAY_) is
            array.type =
                _ghul_symbol_lookup.get_array_type(
                    get_type_or_any(array.element.type));                        
        si

        visit(pointer: Tree.TypeExpression.POINTER) is
            pointer.type =
                _ghul_symbol_lookup.get_pointer_type(
                    get_type_or_any(pointer.element.type));                        
        si

        visit(reference: Tree.TypeExpression.REFERENCE) is
            reference.type =
                _ghul_symbol_lookup.get_reference_type(
                    get_type_or_any(reference.element.type));                        
        si

        visit(function: Tree.TypeExpression.FUNCTION) is
            let types = new Collections.LIST[Semantic.Type.BASE]();

            for a in function.arguments do
                types.add(get_type_or_any(a.type));
            od
            
            types.add(get_type_or_any(function.result.type));

            function.type =
                _ghul_symbol_lookup.get_function_type(types);
        si

        visit(tuple: Tree.TypeExpression.TUPLE) is
            let types = new Collections.LIST[Semantic.Type.BASE]();

            for a in tuple.elements do
                types.add(get_type_or_any(a.type));
            od

            tuple.type =
                _ghul_symbol_lookup.get_tuple_type(types);
        si
    si
si
