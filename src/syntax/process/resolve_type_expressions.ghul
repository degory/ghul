namespace Syntax.Process is
    @IF.not.v3() use Std = System.Console;

    use System;
    
    use Logging;

    use Semantic.Types.Type;

    class RESOLVE_TYPE_EXPRESSIONS: ScopedVisitor is
        _logger: Logger;
        _symbol_table: Semantic.SYMBOL_TABLE;
        _symbol_use_locations: Semantic.SYMBOL_USE_LOCATIONS;
        _innate_symbol_lookup: Semantic.Lookups.InnateSymbolLookup;

        _innate_type_map: Collections.MAP[Semantic.Symbols.Symbol, Semantic.Types.NAMED];

        init(
            logger: Logger,
            symbol_table: Semantic.SYMBOL_TABLE,
            namespaces: Semantic.NAMESPACES,
            symbol_use_locations: Semantic.SYMBOL_USE_LOCATIONS,
            innate_symbol_lookup: Semantic.Lookups.InnateSymbolLookup    
        )
        is
            super.init(logger, symbol_table, namespaces);

            _logger = logger;
            _symbol_table = symbol_table;
            _symbol_use_locations = symbol_use_locations;
            _innate_symbol_lookup = innate_symbol_lookup;
        si

        _initialize_type_map() is
            if _innate_type_map? then
                return;
            fi

            _innate_type_map = new Collections.MAP[Semantic.Symbols.Symbol, Semantic.Types.NAMED]();

            let symbol_lookup = _innate_symbol_lookup;

            add_innate_type(_innate_symbol_lookup.get_char_type());    
            add_innate_type(_innate_symbol_lookup.get_byte_type());
            add_innate_type(_innate_symbol_lookup.get_ubyte_type());    
            add_innate_type(_innate_symbol_lookup.get_short_type());
            add_innate_type(_innate_symbol_lookup.get_ushort_type());
            add_innate_type(_innate_symbol_lookup.get_int_type());
            add_innate_type(_innate_symbol_lookup.get_uint_type());
            add_innate_type(_innate_symbol_lookup.get_long_type());
            add_innate_type(_innate_symbol_lookup.get_ulong_type());
            add_innate_type(_innate_symbol_lookup.get_word_type());
        si

        apply(root: Trees.Node) is
            _initialize_type_map();

            root.walk(self);
        si

        add_innate_type(type: Semantic.Types.NAMED) is
            _innate_type_map[type.symbol] = type;
        si

        map_type(symbol: Semantic.Symbols.Symbol) -> Semantic.Types.NAMED is
            if _innate_type_map.contains_key(symbol) then
                return _innate_type_map[symbol];                
            fi
        si

        visit(named: Trees.TypeExpressions.NAMED) is
            named.type = null;

            let symbol = find(named.name);

            if symbol? then
                if isa Semantic.Types.Typed(symbol) then
                    _symbol_use_locations.add_symbol_use(named.name.location, symbol);

                    let type = map_type(symbol);

                    if type? then
                        named.type = type;
                    else                        
                        let typed_symbol = cast Semantic.Types.Typed(symbol);

                        named.type = typed_symbol.type;
                    fi
                else
                    _logger.error(named.name.location, "not a type: " + named + " " + named.get_type());
                fi
            fi
        si

        visit(element: Trees.TypeExpressions.NAMED_TUPLE_ELEMENT) is
            element.type_expression.accept(self);            
        si

        visit(generic: Trees.TypeExpressions.GENERIC) is
            generic.type = null;

            let symbol = find(generic.name);

            if symbol? then
                _symbol_use_locations.add_symbol_use(generic.name.location, symbol);

                if !isa Semantic.Symbols.Classy(symbol) then
                    _logger.error(generic.location, "cannot supply type arguments here");
                    return;
                fi

                let arguments = new Collections.LIST[Type]();

                for a in generic.arguments do
                    if a.type? then
                        arguments.add(a.type);
                    else
                        arguments.add(new Semantic.Types.ANY());
                    fi
                od

                generic.type = new Semantic.Types.GENERIC(
                    generic.location,
                    cast Semantic.Symbols.Classy(symbol),
                    arguments);
            fi
        si

        visit(structured: Trees.TypeExpressions.Structured) is
            Std.error.write_line("visit TypeExpressions.Structured called");
        si

        get_type_or_any(element: Type) -> Type is
            if element? then
                return element;
            else
                return new Semantic.Types.ANY();
            fi
        si
        
        visit(array: Trees.TypeExpressions.ARRAY_) is
            array.type =
                _innate_symbol_lookup.get_array_type(
                    get_type_or_any(array.element.type));                        
        si

        visit(pointer: Trees.TypeExpressions.POINTER) is
            pointer.type =
                _innate_symbol_lookup.get_pointer_type(
                    get_type_or_any(pointer.element.type));                        
        si

        visit(reference: Trees.TypeExpressions.REFERENCE) is
            reference.type =
                _innate_symbol_lookup.get_reference_type(
                    get_type_or_any(reference.element.type));                        
        si

        visit(function: Trees.TypeExpressions.FUNCTION) is
            let types = new Collections.LIST[Type]();

            for a in function.arguments do
                types.add(get_type_or_any(a.type));
            od
            
            types.add(get_type_or_any(function.result.type));

            function.type =
                _innate_symbol_lookup.get_function_type(types);
        si

        visit(tuple: Trees.TypeExpressions.TUPLE) is
            let types = new Collections.LIST[Type]();

            for a in tuple.elements do
                types.add(get_type_or_any(a.type));
            od

            tuple.type =
                _innate_symbol_lookup.get_tuple_type(types);
        si
    si
si
