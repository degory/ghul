namespace Syntax.Process is
    use System.Exception;

    use IO.Std;
        
    use Logging;
    use Source;

    use IR.Values;
    use IR.VALUE_CONVERTER;
    use IR.VALUE_BOXER;

    use Semantic.Types.Type;

    use Syntax.Trees.Definitions.PRAGMA;

    class COMPILE_EXPRESSIONS: ScopedVisitor is
        _logger: Logger;
        _symbol_table: Semantic.SYMBOL_TABLE;
        _int_type: Semantic.Types.NAMED;
        _symbol_loader: Semantic.SYMBOL_LOADER;
        _innate_symbol_lookup: Semantic.Lookups.InnateSymbolLookup;
        _function_caller: Semantic.FUNCTION_CALLER;
        _overload_resolver: Semantic.OVERLOAD_RESOLVER;
        _symbol_use_locations: Semantic.SYMBOL_USE_LOCATIONS;
        _ir_context: IR.CONTEXT;
        _value_converter: VALUE_CONVERTER;
        _value_boxer: VALUE_BOXER;

        _partial_argument_function_type: Type;

        _will_generate_code: bool;

        _stub_depth: int;
        _keep_depth: int;

        current_statement_list: Trees.Statements.LIST;

        init(
            logger: Logger,
            stable_symbols: Semantic.STABLE_SYMBOLS,
            symbol_table: Semantic.SYMBOL_TABLE,
            namespaces: Semantic.NAMESPACES,
            symbol_loader: Semantic.SYMBOL_LOADER,
            innate_symbol_lookup: Semantic.Lookups.InnateSymbolLookup,
            function_caller: Semantic.FUNCTION_CALLER,
            overload_resolver: Semantic.OVERLOAD_RESOLVER,
            symbol_use_locations: Semantic.SYMBOL_USE_LOCATIONS,
            context: IR.CONTEXT,
            value_converter: VALUE_CONVERTER,
            value_boxer: VALUE_BOXER
        )
        is
            super.init(logger, stable_symbols, symbol_table, namespaces);

            _logger = logger;
            _symbol_table = symbol_table;
            _symbol_loader = symbol_loader;
            _innate_symbol_lookup = innate_symbol_lookup;
            _function_caller = function_caller;
            _overload_resolver = overload_resolver;
            _symbol_use_locations = symbol_use_locations;
            _ir_context = context;
            _value_converter = value_converter;
            _value_boxer = value_boxer;
        si

        apply(root: Trees.Node) is
            assert _keep_depth == 0;
            assert _stub_depth == 0;

            _will_generate_code = IoC.CONTAINER.instance.build_flags.want_assembler;
            
            root.walk(self);

            assert _keep_depth == 0;
            assert _stub_depth == 0;
        si

        should_skip(node: Trees.Node) -> bool => _stub_depth > 0 \/ is_stable(node);

        get_zero_argument_function(type: Type, name: string) -> Semantic.Symbols.Function is
            let symbol = type.find_member(name);

            if symbol? /\ isa Semantic.Symbols.FUNCTION_GROUP(symbol) then
                let function_group = cast Semantic.Symbols.FUNCTION_GROUP(symbol);

                for f in function_group.functions do
                    if f.arguments.count == 0 then
                        return f;
                    fi
                od
            fi
        si

        set_iterator_for(`for: Trees.Statements.FOR, type: Type, recursing: bool) -> bool is
            if !type? then
                return false;
            fi

            `for.expression.value.check_is_consumable(_logger, `for.expression.location);

            let move_next = get_zero_argument_function(type, "move_next");

            let next_element = get_zero_argument_function(type, "nextElement");
            
            if move_next? then
                let read_current = get_zero_argument_function(type, "$get_current");

                if !read_current? then
                    read_current = get_zero_argument_function(type, "$get_Current");                    
                fi
                
                if !read_current? then
                    _logger.error(`for.expression.location, "incomplete iterator type (has move_next method but no current property)");
                    return false;
                fi

                `for.move_next = move_next;
                `for.read_current = read_current;

                return true;
            elif next_element? then
                // FIXME
                `for.read_current = next_element;

                return true;
            elif !recursing then
                let read_iterator = get_zero_argument_function(type, "$get_iterator");

                if read_iterator? then
                    `for.read_iterator = read_iterator;

                    return set_iterator_for(`for, read_iterator.return_type, true);
                fi
            fi
            
            _logger.error(`for.expression.location, "not iterable");

            return false;
        si

        // FIXME: #500 Pragma handling code is duplicated across multiple visitors
        pre(pragma: Trees.Definitions.PRAGMA) -> bool is
            if !pragma.pragma? then
                return false;
            fi

            let p = pragma.pragma;

            let name = p.name.to_string();

            if name =~ "IL.stub" then
                _stub_depth = _stub_depth + 1;
            elif name =~ "KEEP" then
                _keep_depth = _keep_depth + 1;
            fi
        si

        visit(pragma: Trees.Definitions.PRAGMA) is
            if !pragma.pragma? then
                return;
            fi

            let p = pragma.pragma;

            let name = p.name.to_string();

            if name =~ "IL.stub" then
                _stub_depth = _stub_depth - 1;
            elif name =~ "KEEP" then
                _keep_depth = _keep_depth - 1;
            fi
        si

        pre(`namespace: Trees.Definitions.NAMESPACE) -> bool is
            super.pre(`namespace);
            return _stub_depth > 0;
        si

        pre(`class: Trees.Definitions.CLASS) -> bool is
            super.pre(`class);
            return should_skip(`class);
        si

        pre(`trait: Trees.Definitions.TRAIT) -> bool is
            super.pre(`trait);
            return should_skip(`trait);            
        si        

        pre(`struct: Trees.Definitions.STRUCT) -> bool is
            super.pre(`struct);
            return should_skip(`struct);            
        si

        pre(`let: Trees.Statements.LET) -> bool is
            super.pre(`let);
            return false;
        si

        visit(`let: Trees.Statements.LET) is
            super.visit(`let);

            if `let.want_dispose then
                let idisposable = _innate_symbol_lookup.get_idisposable_type();

                for v in `let.variables do
                    for name in v.names do
                        let symbol = find(v.name);

                        if symbol? /\ isa Semantic.Symbols.Variable(symbol) then
                            let variable = cast Semantic.Symbols.Variable(symbol); 
                            variable.is_disposed = true;

                            let type = variable.type;

                            if !idisposable.is_assignable_from(type) then
                                _logger.error(v.name.location, "not disposable");
                            fi

                            current_statement_list.add_variable_to_dispose(variable);
                        fi
                    od
                od
            fi
        si
        
        pre(`for: Trees.Statements.FOR) -> bool is
            super.pre(`for);

            try
                _pre(`for);
            catch ex: Exception
                _logger.exception(`for.location, ex, "exception compiling for");
            yrt
            
            return true;
        si
        
        _pre(`for: Trees.Statements.FOR) -> bool is
            let symbol: Semantic.Symbols.Symbol;

            let type: Type = new Semantic.Types.ERROR();
    
            if `for.expression? /\ !`for.expression.is_poisoned then
                `for.expression.walk(self);

                if 
                    `for.expression.value? /\ set_iterator_for(`for, `for.expression.value.type, false)
                then
                    if `for.variable.type_expression? /\ !isa Trees.TypeExpressions.INFER(`for.variable.type_expression) then
                        if `for.variable.type_expression.type.is_assignable_from(`for.read_current.return_type) then
                            type = `for.variable.type_expression.type;
                        else
                            _logger.error(`for.variable.location, "type mismatch");
                        fi
                    else
                        type = `for.read_current.return_type;
                    fi
                fi
            fi

            if `for.variable? /\ !`for.variable.is_poisoned then
                set_symbol_type(`for.variable.left, type);
            fi

            if `for.body? then
                `for.body.walk(self);
            fi
        si

        pre(left: Trees.Expressions.SIMPLE_LEFT_EXPRESSION) -> bool is
            super.pre(left);

            if !left.value? then
                return false;
            fi

            if !left.value.is_consumable then
                _logger.error(left.location, "cannot use this here");
                return false;
            fi

            left.expression.value = new Need.STORE(left.value);
            return false;
        si

        visit(left: Trees.Expressions.SIMPLE_LEFT_EXPRESSION) is
            super.visit(left);

            if !left.value? \/ !left.value.type? \/ !left.expression.value? \/ !left.expression.value.type? then
                return;
            fi

            if !left.expression.value.is_consumable then
                _logger.error(left.location, "cannot assign to this");
                return;
            fi

            if !left.expression.value.type.is_assignable_from(left.value.type) then
                _logger.error(left.assign_location, "{left.value.type} is not assignable to {left.expression.value.type}");
                return;
            fi

            left.value = left.expression.value;
        si
        
        get_conventionally_named_destructure_member_types(type: Type) -> Collections.List[Type] is
            // look for elements named _0, _1, _2, etc.
            // TODO proper deconstruct(...) support

            let result = new Collections.LIST[Type]();

            let i = 0;

            do
                let member = type.find_member("`{i}");

                if !member? then
                    break;
                fi

                result.add(member.type);

                i = i + 1;
            od

            return result;
        si

        pre(left: Trees.Expressions.DESTRUCTURING_LEFT_EXPRESSION) -> bool is
            super.pre(left);

            let from = left.value;

            if !from? then
                return true;
            fi
            
            let from_type = from.type;

            if !from_type? then
                return true;
            fi

            let from_types: Collections.List[Type];

            if from_type.is_value_tuple then
                from_types = from_type.arguments;
            else
                from_types = get_conventionally_named_destructure_member_types(from_type);
            fi

            if from_types.count == 0 then
                _logger.error(left.location, "cannot destructure {from_type}");
                return true;
            fi

            let elements = left.elements;

            let count = elements.count;

            if elements.count != from_types.count then
                _logger.error(left.location, "expected {elements.count} destructuring elements but found {from_types.count}");

                if elements.count > from_types.count then
                    count = from_types.count;
                fi
            fi

            let block = _ir_context.enter_block();
            let get_from = from.get_temp_copier(_ir_context, "destructure");

            for i in 0..count do
                let element = elements[i];

                let member = from_type.find_member("`{i}");

                element.value = member.load(LOCATION.internal, get_from(), _symbol_loader);

                element.walk(self);

                if element.value? then
                    element.value.gen(_ir_context);
                fi
            od

            left.value = block.close();

            return true;
        si                
        
        pre(assignment: Trees.Statements.ASSIGNMENT) -> bool is
            assignment.right.walk(self);

            if !assignment.right.value? then
                return true;
            fi
            
            if !assignment.right.value.is_consumable then
                _logger.error(assignment.right.location, "cannot use this here");

                assignment.left.value = new IR.Values.DUMMY(assignment.right.value.type, assignment.right.location);
            else
                assignment.left.value = assignment.right.value;
            fi

            assignment.left.assign_location = assignment.location;

            assignment.left.walk(self);

            assignment.value = assignment.left.value;

            return true;
        si

        pre(expression: Trees.Statements.EXPRESSION) -> bool is
            super.pre(expression);

            return false;
        si

        visit(expression: Trees.Statements.EXPRESSION) is
            super.visit(expression);

            Value.check_is_consumable_allow_void(_logger, expression.expression.location, expression.expression.value);

            if !expression? \/ !expression.want_value then
                return;
            fi

            expression.value = expression.expression.value;
        si

        pre(r: Trees.Statements.RETURN) -> bool is
            super.pre(r);

            if !r.expression? then
                return false;
            fi

            let function = current_function;

            if
                !function.return_type? \/ 
                function.return_type.is_any \/ 
                function.return_type =~ _innate_symbol_lookup.get_void_type()
            then
                return false;
            fi

            r.expression.try_set_constraint(function.return_type, "cannot return value of type {{0}} from function of type {{1}}");

            return false;
        si

        visit(r: Trees.Statements.RETURN) is
            super.visit(r);

            let function = current_function;

            if !function? \/ !function.return_type? then
                // FIXME: null function happens for properties, null return type happens for anonymous functions
                return;
            fi

            if r.expression? then
                if
                    r.expression.value? /\
                    r.expression.value.type? /\
                    r.expression.value.check_is_consumable(_logger, r.expression.location)
                then
                    if function.return_type.is_any then
                        function.set_return_type(r.expression.value.type);                
                    elif function.return_type =~ _innate_symbol_lookup.get_void_type() /\ !function.return_type.is_type_variable then
                        _logger
                            .error(
                                r.expression.location,
                                "cannot return value from function of void type"
                            );                        
                    elif !function.return_type.is_assignable_from(r.expression.value.type) then
                        _logger
                            .error(
                                r.expression.location,
                                "cannot return value of type {r.expression.value.type} from function of type {function.return_type}"
                            );
                    fi

                    return;
                fi
            else
                if function.return_type !~ _innate_symbol_lookup.get_void_type() then
                    _logger
                        .warn(
                            r.location,
                            "return without value from non void function returns default value of type {function.return_type}"
                        );
                fi
            fi
        si

        visit(`throw: Trees.Statements.THROW) is
            super.visit(`throw);

            if 
                !Value.check_is_consumable(_logger, `throw.expression.location, `throw.expression.value)
            then
                return;
            fi

            let exception_type = _innate_symbol_lookup.get_exception_type();

            if !exception_type.is_assignable_from(`throw.expression.value.type) then
                _logger.warn(`throw.expression.location, "thrown value is not derived from System.Exception");
            fi

            // FIXME: need to signal to any enclosing expression if statement that this is a throw so
            // if all branches are throws, an error can be reported
        si

        visit(`assert: Trees.Statements.ASSERT) is
            super.visit(`assert);

            if 
                !`assert.expression.value? \/ 
                !`assert.expression.value.type? \/
                !`assert.expression.value.check_is_consumable(_logger, `assert.expression.location)
            then
                return;
            fi

            if
                !_innate_symbol_lookup
                    .get_bool_type()
                    .is_assignable_from(`assert.expression.value.type)
            then
                _logger.error(`assert.expression.location, "assertion expression must be bool");
            fi

            if `assert.message? /\ `assert.message.value? /\ `assert.message.value.type? then
                `assert.message.value.check_is_consumable(_logger, `assert.message.location);

                if !_innate_symbol_lookup
                    .get_exception_type()
                    .is_assignable_from(`assert.expression.value.type)
                then
                    `assert.needs_exception_wrapper = true;
                elif 
                    !_innate_symbol_lookup
                        .get_string_type()
                        .is_assignable_from(`assert.expression.value.type)
                then
                    _logger.error(`assert.message.location, "assertion else must be string or System.Exception");
                fi
            fi
        si

        visit(`try: Trees.Statements.TRY) is
            super.visit(`try);

            if
                (!`try.catches? \/ `try.catches.count == 0) /\
                !`try.`finally?
            then
                _logger.warn(`try.location, "try statement should have at least one catch clause and/or a finally clause");
            fi
        si
        
        visit(`catch: Trees.Statements.CATCH) is
            super.visit(`catch);

            if !`catch.variable? \/ !`catch.variable.type_expression? then
                return;
            fi
            
            let type = `catch.variable.type_expression.type;

            if !type? then
                return;
            fi

            let exception_type = _innate_symbol_lookup.get_exception_type();

            if !exception_type.is_assignable_from(type) then
                _logger.error(`catch.variable.type_expression.location, "cannot catch {type} because it does not derive from System.Exception");
            fi
        si

        visit(`do: Trees.Statements.DO) is
            if
                `do? /\
                `do.condition? /\
                Value.check_is_consumable(_logger, `do.condition.location, `do.condition.value)
            then
                if `do.condition.value.type !~_innate_symbol_lookup.get_bool_type() then
                    _logger.error(`do.condition.location, "while condition must be bool");
                fi
            fi

            super.visit(`do);
        si

        visit(`if: Trees.Statements.IF_BRANCH) is
            if
                `if? /\
                `if.condition? /\
                Value.check_is_consumable(_logger, `if.condition.location, `if.condition.value)
            then
                if `if.condition.value.type !~_innate_symbol_lookup.get_bool_type() then
                    _logger.error(`if.condition.location, "if condition must be bool");
                fi
            fi

            super.visit(`if);
        si

        pre(expression: Trees.Bodies.EXPRESSION) -> bool is
            super.pre(expression);

            if !expression.expression? then
                return false;
            fi

            let function = current_function;

            if 
                !function.return_type? \/ 
                function.return_type.is_any \/ 
                function.return_type =~ _innate_symbol_lookup.get_void_type()
            then
                return false;
            fi

            expression.expression.try_set_constraint(function.return_type, "cannot return value of type {{0}} from function of type {{1}}");

            return false;

        si

        visit(expression: Trees.Bodies.EXPRESSION) is
            let function = current_function;

            if
                !function? \/
                !function.return_type? \/
                !expression.expression? \/
                !expression.expression.value? \/
                !expression.expression.value.type?
            then
                super.visit(expression);

                expression.expression.value = new IR.Values.DUMMY(new Semantic.Types.ERROR(), expression.expression.location);

                if function? /\ (!function.return_type? \/ function.return_type.is_wild) then
                    function.set_return_type(new Semantic.Types.ERROR());            
                fi

                return;
            fi

            let void_type = _innate_symbol_lookup.get_void_type();
            
            if function.return_type.is_inferred then
                if Value.check_is_consumable(_logger, expression.expression.location, expression.expression.value) then
                    function.set_return_type(expression.expression.value.type);
                elif expression.expression.value? /\ expression.expression.value.type? /\ expression.expression.value.type.is_error then
                    function.set_return_type(new Semantic.Types.ERROR());
                fi
            elif function.return_type =~ void_type /\ !function.return_type.is_type_variable then
                if expression.expression.value.type !~ void_type then
                    _logger
                        .error(
                            expression.location,
                            "cannot return value from function of void type"
                        );                        
                fi
            elif !function.return_type.is_assignable_from(expression.expression.value.type) then
                _logger
                    .error(
                        expression.location,
                        "cannot return value of type {expression.expression.value.type} from function of type {function.return_type}"
                    );
            else
                Value.check_is_consumable_allow_void(_logger, expression.expression.location, expression.expression.value);
            fi

            super.visit(expression);
        si
        
        pre(function: Trees.Expressions.FUNCTION) -> bool is
            super.pre(function);

            return true;
        si

        visit(function: Trees.Expressions.FUNCTION) is
            let closure = cast Semantic.Symbols.Closure(scope_for(function));

            let implied_type: Type;
            let implied_argument_types: Collections.List[Type];

            if _partial_argument_function_type? then
                implied_argument_types = _partial_argument_function_type.arguments;
                _partial_argument_function_type = null;
            fi

            _symbol_table.current_closure_context.add_closure(closure);

            let argument_names = new Collections.LIST[string]();
            let argument_types = new Collections.LIST[Type]();

            let any_errors: bool;

            let arguments = function.arguments.expressions;


            for index in 0..arguments.count do
                let a = arguments[index];
                
                // TODO: destructure
                if isa Trees.Expressions.VARIABLE(a) then
                    let argument = cast Trees.Expressions.VARIABLE(a);

                    argument_names.add(argument.name.name);
                    let partial_argument_type: Type;
                    let symbol = cast Semantic.Symbols.Variable(closure.find_direct(argument.name.name));

                    if !isa Trees.TypeExpressions.INFER(argument.type_expression) then
                        argument_types.add(argument.type_expression.type);
                    elif implied_argument_types? then
                        let implied_argument_type = implied_argument_types[index];

                        if implied_argument_type? /\ !implied_argument_type.is_type_variable then
                            argument_types.add(implied_argument_type);

                            symbol.set_type(implied_argument_type);
                        else
                            _logger.error(a.location, "cannot infer type here");

                            symbol.set_type(new Semantic.Types.ERROR());
                            argument_types.add(new Semantic.Types.ERROR());    
                        fi
                    else
                        _logger.error(a.location, "cannot infer type here");
                        symbol.set_type(new Semantic.Types.ERROR());
                        argument_types.add(new Semantic.Types.ERROR());
                    fi
                else
                    _logger.error(a.location, "unexpected kind of argument ({a.get_type()})");
                    any_errors = true;
                fi

                index = index + 1;
            od

            if !any_errors then
                closure.arguments = new Collections.LIST[Type](argument_types);

                let return_type: Type;

                if function.type_expression.type? then
                    return_type = function.type_expression.type;
                else
                    return_type = new Semantic.Types.INFERED_RETURN_TYPE();
                fi

                argument_types.add(return_type);

                closure.argument_names = argument_names;

                // FIXME QQ should be
                closure.set_return_type(return_type);
            fi

            let use debug_dispose = debug_enter();
            let use logger_snapshot = _logger.speculate_then_commit();

            debug("before first walk, function return type is {closure.return_type}");

            function.body.walk(self);

            debug("after first walk, function return type is {closure.return_type}");

            let reported_inference_failure = false;

            if closure.is_recursive then
                debug("function is recursive, discard any diagnostics and walk again...");

                logger_snapshot.backtrack();

                function.body.walk(self);

                debug("after second walk, function return type is now {closure.return_type}");
            else
                debug("function is not recursive, commit diagnostics");

                logger_snapshot.commit();
            fi
            
            if _will_generate_code /\ !any_errors /\ closure.could_be_delegate then
                closure.convert_to_delegate();

                function.body.walk(self);
            fi
            
            if closure.return_type.is_inferred /\ ! closure.arguments | .any(a => a.is_error) then
                closure.set_return_type(_innate_symbol_lookup.get_void_type());
            fi

            let value = closure.load(function.location, _symbol_loader, _ir_context);

            if _will_generate_code /\ function.value? then
                // FIXME: have to update in place, otherwise the parent IR value tree node that
                // references this will continue to point at the stale value

                // might want to implement some kind of updateable wrapper value for this:
                function.value.update_from(value);
            else
                function.value = value;
            fi

            super.visit(function);
        si

        visit(recurse: Trees.Expressions.RECURSE) is
            let function = current_function;

            if !function? \/ !function.is_closure then
                recurse.value = new IR.Values.DUMMY(new Semantic.Types.ERROR(), recurse.location);

                _logger.error(recurse.location, "rec can only be used in a function literal body");
                return;
            fi

            let closure = cast Semantic.Symbols.Closure(function);

            recurse.value = closure.load_recurse(recurse.location, _symbol_loader);

            debug("recurse in function {function} of type {recurse.value.type} ({recurse.value})");

            if !function.is_recursive then
                _logger.error(recurse.location, "rec can only be used in a recursive function");
                return;
            fi
        si

        pre(tuple: Trees.Expressions.TUPLE) -> bool is
            return false;
        si

        visit(tuple: Trees.Expressions.TUPLE) is
            let names = new Collections.LIST[string]();
            let values = new Collections.LIST[Value]();
            let types = new Collections.LIST[Type]();

            if tuple.elements.expressions.count == 1 then
                tuple.value = tuple.elements.expressions[0].value;
                return;
            elif tuple.elements.expressions.count == 0 then
                tuple.value = new IR.Values.DUMMY(new Semantic.Types.ERROR(), tuple.location);

                _logger.error(tuple.location, "empty tuple");
                return;
            fi

            let seen_any_named = false;

            for (index, v) in tuple.elements | .index() do
                if isa Trees.Expressions.TUPLE_ELEMENT(v) then
                    let element = cast Trees.Expressions.TUPLE_ELEMENT(v);

                    seen_any_named = true;
                    names.add(element.name.name);

                    if element.initializer? then
                        if Value.check_is_consumable(_logger, element.initializer.location, element.initializer.value) then
                            if element.type_expression? /\ element.type_expression.type? then
                                if element.type_expression.check_is_not_reference(_logger, "tuple element cannot be a reference") then
                                    if !element.type_expression.type.is_assignable_from(element.initializer.value.type) then
                                        _logger.error(element.location, "{element.initializer.value.type} is not assignable to {element.type_expression.type}");
                                    fi
                                fi

                                let type = element.type_expression.type;

                                types.add(element.type_expression.type);
                                values.add(_value_boxer.box_if_needed(element.initializer.value, type));
                            else
                                types.add(element.initializer.value.type);
                                values.add(element.initializer.value);
                            fi

                            continue;
                        fi
                    else
                        _logger.error(element.location, "tuple element must have a value");
                    fi  

                    values.add(new IR.Values.DUMMY(new Semantic.Types.ERROR(), element.location));
                    types.add(new Semantic.Types.ERROR());

                elif Value.check_is_consumable(_logger, v.location, v.value) then
                    names.add("`{index}");

                    values.add(v.value);
                    types.add(v.value.type);
                else
                    values.add(new IR.Values.DUMMY(new Semantic.Types.ERROR(), v.location));
                    types.add(new Semantic.Types.ERROR());
                fi
            od

            if !seen_any_named then
                names = null;
            fi

            let type = _innate_symbol_lookup.get_tuple_type(types, names);
            tuple.value = new TUPLE(type, values);
        si

        visit(sequence: Trees.Expressions.SEQUENCE) is
            let type: Type;
            let have_explicit_type: bool;

            let elements = new Collections.LIST[Trees.Expressions.Expression](sequence.elements);

            if sequence.type_expression? /\ !isa Trees.TypeExpressions.INFER(sequence.type_expression) then
                if sequence.type_expression.type? then
                    type = sequence.type_expression.type;

                    have_explicit_type = true;
                else
                    _logger.error(sequence.type_expression.location, "bad explicit type expression");
                    return;
                fi
            elif elements.count == 0 then
                _logger.error(sequence.location, "cannot infer type of list literal with no elements");

                return; 
            else
                // FIXME: this is assuming the type of the sequence is the type of its first element
                // should be the most specific type that is assignable from all element types

                let seen_any = false;

                for v in elements do
                    if v.value? /\ v.value.type? then
                        seen_any = true;

                        v.value.check_is_consumable(_logger, v.location);

                        if !type? then
                            type = v.value.type;
                        elif v.value.type.is_assignable_from(type) then
                            type = v.value.type;
                        elif !type.is_assignable_from(v.value.type) then
                            type = _innate_symbol_lookup.get_object_type();
                            break;
                        fi
                    fi
                od

                if type == null then
                    if seen_any then
                        _logger.error(sequence.location, "cannot infer type of list literal with only null elements");
                    fi
                    
                    return;
                fi
            fi

            let values = new Collections.LIST[Value](sequence.elements.expressions.count);
            
            for v in sequence.elements do
                if v.value? /\ v.value.type? then
                    let u = v.value.type;
                    if have_explicit_type /\ !type.is_assignable_from(v.value.type) then
                        _logger.error(v.location, "element not compatible with explicit type");
                    fi

                    values.add(v.value);
                else
                    values.add(new DUMMY(new Semantic.Types.ERROR(), v.location));
                fi
            od
                        
            sequence.value = 
                new SEQUENCE(_innate_symbol_lookup.get_array_type(type), type, values);
        si        

        visit(`self: Trees.Expressions.SELF) is
            let s = current_instance_context;

            let type: Type;

            if s? then
                let f = current_function;
                
                if !f? \/ !f.is_instance then
                    _logger.error(`self.location, "cannot access self from non-instance context");
                fi

                if s.argument_names.count > 0 then
                    let arguments = new Collections.LIST[Type]();

                    for n in s.argument_names do
                        let argument = s.find_member(n);

                        if argument? /\ argument.is_type_variable then
                            arguments.add(argument.type);
                        fi
                    od
                    
                    if arguments.count == s.argument_names.count then
                        type = new Semantic.Types.GENERIC(
                            `self.location,
                            cast Semantic.Symbols.Classy(s),
                            arguments);
                    fi
                fi

                if s.is_value_type then
                    `self.value =
                        new Load.VALUE_SELF(
                            s, type
                        );
                else
                    `self.value =
                        new Load.REFERENCE_SELF(
                            s, type
                        );
                fi

                // FIXME: this breaks VSCode rename symbol: 
                // _symbol_use_locations.add_symbol_use(`self.location, s);
            else
                _logger.error(`self.location, "cannot access self from non-instance context");
            fi
        si

        visit(`super: Trees.Expressions.SUPER) is
            // FIXME:
            let s = _symbol_table.current_instance_context;

            if s? then
                if s.is_trait then
                    _logger.error(`super.location, "trait {s.description} does not have a super class");
                    
                    return;
                fi
                
                `super.value =
                    new Load.SUPER(
                        s, cast Semantic.Symbols.Classy(s).ancestors[0]
                    );

                // FIXME: breaks VSCode rename symbol:
                // _symbol_use_locations.add_symbol_use(`super.location, s);
            fi
        si

        visit(`cast: Trees.Expressions.CAST) is
            `cast.value = null;

            let type = `cast.type_expression.type;

            if !type? then
                `cast.value = new DUMMY(new Semantic.Types.ERROR(), `cast.location);
                _logger.error(`cast.type_expression.location, "cast has no type");
                return;
            fi

            `cast.type_expression.check_is_not_reference(_logger, "cannot cast to a reference type");

            if !`cast.right.value? then
                _logger.poison(`cast.right.location, "cast has no value");

                `cast.value =
                    new DUMMY(
                        type,
                        `cast.right.location
                    );

                return;
            fi

            if type.is_value_type /\ `cast.right.value.type.is_value_type then
                let instruction = _value_converter.get_instruction(type);

                if !instruction? then
                    let enum_type = _innate_symbol_lookup.get_enum_type();

                    if 
                        !enum_type.is_assignable_from(type) /\
                        !enum_type.is_assignable_from(`cast.right.value.type)
                    then
                        _logger.error(`cast.location, "cannot convert {`cast.right.value.type} to {`cast.type_expression.type}");
                    fi
                    
                    `cast.value =
                        new CONVERT(
                            type,
                            `cast.right.value,
                            "conv.i4"
                        );                    
               else
                    `cast.value =
                        new CONVERT(
                            type,
                            `cast.right.value,
                            instruction
                        );                    
                fi
                
                return;
            fi
            
            if type.is_value_type then
                `cast.value =
                    new TYPE_WRAPPER(
                        type,
                        new UNBOX(
                            `cast.right.value
                        )
                    );

                return;
            fi
            
            `cast.value =
                new CAST(
                    type,
                    `cast.right.value
                );
        si

        visit(`isa: Trees.Expressions.ISA) is
            `isa.value = null;

            let isa_type = `isa.type_expression.type;

            if isa_type == null then
                _logger.error(`isa.type_expression.location, "isa has no type");
                return;
            fi

            let bool_type = _innate_symbol_lookup.get_bool_type();

            `isa.value =
                new ISA(
                    bool_type,
                    isa_type,                     
                    `isa.right.value
                );
        si

        visit(`typeof: Trees.Expressions.TYPEOF) is
            `typeof.value = null;

            let typeof_type = `typeof.type_expression.type;

            if !typeof_type? then
                _logger.error(`typeof.type_expression.location, "typeof has no type");
                return;
            fi

            let type_type = _innate_symbol_lookup.get_type_type();

            `typeof.value =
                new TYPEOF(
                    type_type,
                    typeof_type
                );
        si

        visit(`new: Trees.Expressions.NEW) is
            `new.value = null;

            let type = `new.type_expression.type;

            if type == null then     
                _logger.poison(`new.type_expression.location, "new has no type");
                return;
            fi

            if !isa Semantic.Types.NAMED(type) then
                _logger.error(`new.type_expression.location, "cannot instantiate this: {type}");
                return;
            fi

            `new.type_expression.check_is_not_void(_logger, "cannot use void type here");

            let named_type = cast Semantic.Types.NAMED(type);
            let type_symbol = named_type.symbol;

            let symbol = named_type.scope.find_direct("init");

            let function_group = cast Semantic.Symbols.FUNCTION_GROUP(symbol);

            let arguments = new Collections.LIST[Value]();
            let argument_types = new Collections.LIST[Type]();

            for a in `new.arguments do
                if a? /\ a.value? /\ a.value.type? then
                    arguments.add(a.value);
                    argument_types.add(a.value.type);

                    a.value.check_is_consumable(_logger, a.location);
                else
                    let t = new Semantic.Types.ERROR();

                    if a? then
                        arguments.add(new DUMMY(type, a.location));
                    else
                        arguments.add(new DUMMY(type, `new.arguments.location));
                    fi
                    
                    argument_types.add(t);
                fi
            od

            if !function_group? then
                `new.value =
                    new DUMMY(
                        type,
                        `new.location
                    );

                _logger.error(`new.location, "no constructor found init({argument_types|})");                

                return;
            fi

            let overload_result = _overload_resolver.resolve(`new.location, function_group, argument_types, false, true);

            if overload_result == null then
                `new.value =
                    new DUMMY(
                        type,
                        `new.location
                    );
    
                return;
            fi

            let function = overload_result.function;

            let is_directly_owned = false;

            if isa Semantic.Symbols.GENERIC(type_symbol) /\ isa Semantic.Symbols.Symbol(function.owner) then
                is_directly_owned = cast Semantic.Symbols.GENERIC(type_symbol) =~ cast Semantic.Symbols.Symbol(function.owner);
            else
                is_directly_owned = type_symbol == function.owner;
            fi
            
            if !is_directly_owned then
                _logger.error(`new.location, "cannot call superclass constructor {function}");
            fi
            
            _symbol_use_locations.add_symbol_use(`new.type_expression.right_location, function);
            _symbol_use_locations.add_symbol_use(`new.type_expression.right_location, type_symbol.root_unspecialized_symbol);

            `new.value =
                new NEW(
                    type,
                    function,
                    arguments
                );
        si

        visit(unary: Trees.Expressions.UNARY) is
            unary.value = null;

            if unary.right.value? then
                let arguments = new Collections.LIST[Type]([unary.right.value.type]);

                let function_group = find(unary.operation);

                if function_group == null \/ !isa Semantic.Symbols.FUNCTION_GROUP(function_group) then
                    return;
                fi

                let overload_result =
                    _overload_resolver.resolve(
                        unary.location,
                        cast Semantic.Symbols.FUNCTION_GROUP(function_group),
                        arguments,
                        false,
                        unary.right.value.is_consumable); // FIXME: this doesn't seem right

                if overload_result == null then
                    return;
                fi

                let function = overload_result.function;

                if function.is_unsafe_constraints then
                    _logger.warn(unary.location, "call to {function} has unchecked constraints");
                fi

                _symbol_use_locations.add_symbol_use(unary.operation.location, function);

                let value = function.call(unary.location, null, new Collections.LIST[Value]([unary.right.value]), null, _function_caller);

                unary.value = value;
            fi
        si        

        visit(binary: Trees.Expressions.BINARY) is
            try
                _visit(binary);
            catch ex: Exception
                _logger.exception(binary.location, ex, "exception compiling binary operator");
            yrt            
        si

        _visit(binary: Trees.Expressions.BINARY) is
            binary.value = null;

            if !binary.left? \/ !binary.left.value? \/ !binary.right? \/ !binary.right.value? then
                return;
            fi

            binary.left.value.check_is_consumable(_logger, binary.left.location);
            binary.right.value.check_is_consumable(_logger, binary.right.location);
            
            let binary_function_group: Semantic.Symbols.FUNCTION_GROUP;
            let binary_overload_result: Semantic.OVERLOAD_RESOLVE_RESULT;
            let binary_errors: Logging.DIAGNOSTICS_STATE;

            let binary_name = binary.operation.name.to_string();

            let binary_function_symbol = find(binary.operation.name);

            if binary_function_symbol? /\ isa Semantic.Symbols.FUNCTION_GROUP(binary_function_symbol) then
                binary_function_group = cast Semantic.Symbols.FUNCTION_GROUP(binary_function_symbol);

                let use logger_snapshot = _logger.speculate_then_backtrack();

                binary_overload_result =
                    _overload_resolver
                        .resolve(
                            binary.location,
                            binary_function_group,
                            new Collections.LIST[Type]([binary.left.value.type, binary.right.value.type]),
                            false,
                            _symbol_table.current_instance_context?
                        );
                        
                binary_errors = logger_snapshot.backtrack();
            fi

            let member_overload_result: Semantic.OVERLOAD_RESOLVE_RESULT;
            let member_function_group: Semantic.Symbols.FUNCTION_GROUP;
            let member_errors: Logging.DIAGNOSTICS_STATE;

            if binary.left.value.type? then
                let member_function_symbol = 
                    binary.left.value.type.find_member(binary.operation.name);

                if member_function_symbol? /\ isa Semantic.Symbols.FUNCTION_GROUP(member_function_symbol) then
                    member_function_group = cast Semantic.Symbols.FUNCTION_GROUP(member_function_symbol);
    
                    let use logger_snapshot = _logger.speculate_then_backtrack();

                    member_overload_result = 
                        _overload_resolver
                            .resolve(
                                binary.location,
                                cast Semantic.Symbols.FUNCTION_GROUP(member_function_group),
                                new Collections.LIST[Type]([binary.right.value.type]),
                                false,
                                binary.left.value.is_consumable
                            );

                    member_errors = logger_snapshot.backtrack();
                fi
            fi

            let function: Semantic.Symbols.Function;

            if member_overload_result? /\ binary_overload_result? then
                if cast int(member_overload_result.score) <= cast int(binary_overload_result.score) then
                    function = member_overload_result.function;
                else
                    function = binary_overload_result.function;
                fi              
            elif member_overload_result? then
                function = member_overload_result.function;
            elif binary_overload_result? then
                function = binary_overload_result.function;
            fi
        
            if function? then
                _symbol_use_locations.add_symbol_use(binary.operation.location, function);

                let force_type: Type;
                let want_not = false;

                if binary.operation.name =~ "<>" then                        
                    if function.return_type !~ _innate_symbol_lookup.get_int_type() then
                        // FIXME: do this check on the definition:
                        _logger.error(binary.location, "<> order operator must return int");
                    fi

                    // FIXME: for non-innate implementations of <> operator, need to translate the result to bool
                    // based on actual operation

                    force_type = _innate_symbol_lookup.get_bool_type();
                elif binary.operation.name =~ "==" then
                    if 
                        cast int(binary.left.value.type.compare(binary.right.value.type)) > cast int(Semantic.Types.MATCH.ASSIGNABLE) /\
                        cast int(binary.right.value.type.compare(binary.left.value.type)) > cast int(Semantic.Types.MATCH.ASSIGNABLE) 
                    then
                        if binary.actual_operation =~ "==" then
                            _logger.error(binary.location, "== cannot be applied to values of non-assignable types");
                        else
                            _logger.error(binary.location, "!= cannot be applied to values of non-assignable types");
                        fi
                    fi
                elif !function.is_innate /\ binary.actual_operation =~ "!~" then
                    want_not = true;
                fi

                let value: Value;

                if function.is_unsafe_constraints then
                    _logger.warn(binary.location, "call to {function} has unchecked constraints");
                fi
                
                if function.is_instance then
                    value = function.call(binary.location, binary.left.value, new Collections.LIST[Value]([binary.right.value]), force_type,_function_caller);
                else
                    value = function.call(binary.location, null, new Collections.LIST[Value]([binary.left.value, binary.right.value]), force_type,_function_caller);

                    if isa Call.INNATE(value) then
                        cast Call.INNATE(value).actual_operation = binary.actual_operation;
                    fi
                fi
                
                if want_not then
                    value = new NOT(value);
                fi
                
                binary.value = value;
            else
                if binary_errors? /\ binary_errors.count > 0 then
                    _logger.merge(binary_errors);
                fi

                if member_errors? /\ member_errors.count > 0 then
                    _logger.merge(member_errors);
                fi

                if (!binary_errors? \/ binary_errors.count == 0) /\ (!member_errors? \/ member_errors.count == 0) then
                    _logger.error(binary.operation.location, "operator {binary.left.value.type} {binary.operation} {binary.right.value.type} not found");
                fi

                // FIXME QQ should probably do this
                // binary.value = new DUMMY(new Semantic.Types.ERROR(), binary.location);
            fi
        si

        visit(index: Trees.Expressions.INDEX) is
            try
                _visit(index);
            catch e: Exception
                index.value = null;

                _logger.exception(index.location, e, "exception compiling index");
            yrt
        si

        _visit(index: Trees.Expressions.INDEX) is
            let need_store = index.value? /\ index.value.is_need_store;

            if index.left.value? /\ index.index.value? then
                let type = index.left.value.type;

                if type == null then
                    _logger.poison(index.left.location, "index left has no type");

                    index.value = null;
                    return;
                fi

                if !isa Semantic.Types.NAMED(type) then
                    _logger.error(index.left.location, "cannot index {type}");

                    index.value = null;
                    return;
                fi

                let named_type = cast Semantic.Types.NAMED(type);

                let function_name: string;
                let arguments: Collections.LIST[Value];
                let argument_types: Collections.LIST[Type];

                if need_store then
                    function_name = "set_Item";

                    let need_store_value = cast Need.STORE(index.value).value;

                    arguments = new Collections.LIST[Value]([index.index.value, need_store_value]);
                    argument_types = new Collections.LIST[Type]([index.index.value.type, need_store_value.type]);
                else
                    function_name = "get_Item";
                    arguments = new Collections.LIST[Value]([index.index.value]);
                    argument_types = new Collections.LIST[Type]([index.index.value.type]);
                fi

                let symbol = named_type.scope.find_member(function_name);

                if symbol == null then
                    if need_store then
                        if named_type.scope.find_member("get_Item")? then
                            _logger.error(index.location, "indexer is read-only in {type}");
                        else
                            _logger.error(index.location, "no indexer found in {type}");
                        fi
                    else
                        _logger.error(index.location, "no indexer found in {type}");
                    fi
                    
                    if isa Semantic.Symbols.GENERIC(named_type.scope) then
                        let g = cast Semantic.Symbols.GENERIC(named_type.scope);
                    fi
                    
                    index.value = null;
                    return;
                fi

                if !isa Semantic.Symbols.FUNCTION_GROUP(symbol) then
                    _logger.poison(index.index.location, "indexer is not a function group: {symbol}");
                fi

                let overload_result: Semantic.OVERLOAD_RESOLVE_RESULT;

                let use logger_snapshot = _logger.speculate_then_backtrack();

                overload_result =
                    _overload_resolver.resolve(
                        index.location,
                        cast Semantic.Symbols.FUNCTION_GROUP(symbol),
                        argument_types,
                        false,
                        true);

                logger_snapshot.backtrack();

                if overload_result == null then
                    if need_store then
                        _logger.error(index.location, "indexer [{argument_types[0]}] = {argument_types[1]} not found in {type}");
                    else
                        _logger.error(index.location, "indexer [{argument_types[0]}] not found in {type}");
                    fi

                    index.value = null;
                    return;
                fi

                let function = overload_result.function;

                _symbol_use_locations.add_symbol_use(
                    new LOCATION(
                        index.left.location.file_name,
                        index.left.location.end_line,
                        index.left.location.end_column+1,
                        index.index.location.start_line,
                        index.index.location.end_column-1
                    ), function);

                if function.is_unsafe_constraints then
                    _logger.warn(index.location, "call to {function} has unchecked constraints");
                fi
    
                if need_store then
                    index.value = 
                    new TYPE_WRAPPER(
                        function.arguments[1],
                        function.call(index.location, index.left.value, arguments, null, _function_caller));
                else
                    index.value = function.call(index.location, index.left.value, arguments, null, _function_caller);
                fi                
            fi
        si

        visit(member: Trees.Expressions.MEMBER) is
            let need_store = member.value? /\ isa Need.STORE(member.value);
            let need_deref = false;
            
            if member.identifier == null then
                member.value = null;
                return;
            fi

            if member.left? /\ member.left.value? then
                let type = member.left.value.type;

                if type == null then
                    _logger.poison(member.left.location, "member left has no type");

                    member.value = null;
                    return;
                fi

                // TODO we could loop here to handle multiple levels of dereferencing
                if type.is_ref then
                    need_deref = true;
                    type = type.get_element_type();

                    member.left.value = new DEREF(member.left.value, type);
                fi

                if !isa Semantic.Types.NAMED(type) then
                    if !isa Semantic.Types.ERROR(type) then
                        _logger.poison(member.left.location, "type has no members: {type}");
                    fi
                    
                    member.value = null;
                    return;
                fi

                let named_type = cast Semantic.Types.NAMED(type);

                if named_type.scope == null then
                    _logger.poison(member.left.location, "member left type has no type: {type}");

                    member.value = null;
                    return;
                fi

                let symbol = named_type.scope.find_member(member.identifier.name);

                if !symbol? then
                    _logger.error(member.identifier.location, "member {member.identifier.name} not found in {named_type}");

                    member.value = null;
                    return;
                fi

                _symbol_use_locations.add_symbol_use(member.identifier.right_location, symbol);

                if symbol.is_instance /\ !member.left.value.is_consumable then
                    _logger.error(member.identifier.location, "cannot access instance member {member.identifier.name} in {named_type}");                    
                fi

                _symbol_loader.find_symbol = 
                    (name: string) is
                        let result = named_type.scope.find_member(name);

                        return result;
                    si;

                let value: Value;

                if need_store then
                    let store_value = cast Need.STORE(member.value).value;

                    if check_store_access(member.location, member.left.value, symbol, named_type) then
                        value = symbol.store(member.location, member.left.value, store_value, _symbol_loader, false);
                    fi
                else
                    if check_load_access(member.location, member.left.value, symbol, named_type) then
                        value = symbol.load(member.location, member.left.value, _symbol_loader);
                    fi
                fi

                member.value = value;
            fi
        si

        check_load_access(location: LOCATION, from: Value, symbol: Semantic.Symbols.Symbol, type: Type) -> bool is
            if symbol.is_public_readable then
                return true;
            fi

            if from? /\ from.is_self then
                return true;
            fi

            let instance_context = _symbol_table.current_instance_context;

            if instance_context? /\ instance_context.type.is_assignable_from(type) then
                return true;
            fi

            _logger.error(location, "{symbol} is not publicly readable");

            return false;            
        si        

        check_store_access(location: LOCATION, from: Value, symbol: Semantic.Symbols.Symbol, type: Type) -> bool is
            if !symbol.is_private /\ !symbol.is_field then
                return true;
            fi

            if from? /\ from.is_self then
                return true;
            fi

            let instance_context = _symbol_table.current_instance_context;

            if instance_context? /\ instance_context.type.is_assignable_from(type) then
                return true;
            fi

            _logger.error(location, "{symbol} is not publicly assignable");

            return false;
        si

        visit(explicit_specialization: Trees.Expressions.EXPLICIT_SPECIALIZATION) is
            if 
                !explicit_specialization.left? \/
                !explicit_specialization.left.value? \/
                !explicit_specialization.left.value.type? \/
                !explicit_specialization.left.value.type.symbol? \/
                !explicit_specialization.types?
            then
                explicit_specialization.value = null;

                return;
            fi

            if !explicit_specialization.left.value.has_symbol then
                _logger.error(explicit_specialization.location, "can't explicitly specialize this");
                return;
            fi

            let symbol = explicit_specialization.left.value.symbol;

            symbol = 
                symbol.try_specialize(
                    explicit_specialization.location,
                    _logger,
                    explicit_specialization.types.elements | 
                        .map(t => t.type).collect()
                );

            let from: IR.Values.Value;

            if isa IR.Values.Load.SYMBOL(explicit_specialization.left.value) then
                let load = cast IR.Values.Load.SYMBOL(explicit_specialization.left.value);

                from = load.from;
            fi

            if symbol? then
                explicit_specialization.value =
                    new IR.Values.Load.SYMBOL(from, symbol);
            else
                explicit_specialization.value = null;
            fi
        si

        pre(left: Trees.Variables.SIMPLE_VARIABLE_LEFT) -> bool is
            return false;
        si

        visit(left: Trees.Variables.SIMPLE_VARIABLE_LEFT) is
            let symbol = find(left.name);
    
            if symbol? /\ isa Semantic.Types.SettableTyped(symbol) then
                let typed_symbol = cast Semantic.Types.SettableTyped(symbol);

                symbol.define();

                let right_location =
                    if left.right_location? then
                        left.right_location;
                    else
                        left.location;
                    fi;

                if typed_symbol.type == null then
                    if left.explicit_type? then
                        // _logger.poison(left.location, "explicitly typed variable should already have a type");

                        typed_symbol.set_type(left.explicit_type);
                    elif left.right_value? then
                        if Value.check_is_consumable(_logger, right_location, left.right_value) then
                            typed_symbol.set_type(left.right_value.type);
                        else
                            typed_symbol.set_type(new Semantic.Types.ERROR());                            
                        fi
                    fi
                elif
                    left.explicit_type? /\
                    left.right_value? /\
                    Value.check_is_consumable(_logger, right_location, left.right_value)
                then
                    if
                        !left.explicit_type.is_assignable_from(left.right_value.type)
                    then
                        _logger.error(
                            left.variable_location, 
                            "{left.right_value.type} is not assignable to {left.explicit_type}");
                    fi
                fi

                if left.right_value? then
                    left.value = symbol.store(left.location, null, left.right_value, _symbol_loader, true);
                fi
            else
                _logger.error(left.name.location, "couldn't find typed symbol for variable {left.name}");
            fi
        si

        pre(left: Trees.Variables.DESTRUCTURING_VARIABLE_LEFT) -> bool is
            let from = left.right_value;

            if !from? then
                _logger.error(left.location, "cannot destructure without initializer");
                return true;
            fi

            let from_type =
                if left.explicit_type? then
                    left.explicit_type;
                else
                    from.type;
                fi;

            if !from_type? then
                _logger.error(left.location, "oops: null type");
            fi

            let from_types: Collections.List[Type];

            if from_type.is_value_tuple then
                from_types = from_type.arguments;
            else
                from_types = get_conventionally_named_destructure_member_types(from_type);
            fi

            if from_types.count == 0 then
                _logger.error(left.location, "cannot destructure {from_type}");
                return true;
            fi

            let elements = left.elements;

            let count = elements.count;

            if left.elements.count != from_types.count then
                _logger.error(left.location, "expected {elements.count} destructuring elements but found {from_types.count}");

                if elements.count > from_types.count then
                    count = from_types.count;
                fi
            fi

            let use block = _ir_context.enter_block();
            let get_from = from.get_temp_copier(_ir_context, "destructure");

            for i in 0..count do
                let element = elements[i];

                let member = from_type.find_member("`{i}");

                element.right_value = member.load(LOCATION.internal, get_from(), _symbol_loader);
                element.variable_location = left.variable_location;
                element.right_location = left.right_location;

                element.walk(self);

                if element.value? then
                    element.value.gen(_ir_context);
                fi
            od

            left.value = block.close();

            return true;
        si

        visit(destructure_left: Trees.Variables.DESTRUCTURING_VARIABLE_LEFT) is

        si

        pre(variable: Trees.Variables.VARIABLE) -> bool is
            if variable.type_expression? then
                variable.type_expression.walk(self);

                // push explicit type down into the variable left
                if variable.is_explicit_type /\ variable.type_expression.type? then
                    variable.left.explicit_type = variable.type_expression.type;
                fi
            fi

            if variable.initializer? then
                if variable.type_expression? /\ variable.type_expression.type? /\ !isa Trees.TypeExpressions.INFER(variable.type_expression) then
                    // if we have both an explicit type and an initializer, we
                    // can push a type constraint down into the initializer
                    variable.initializer.try_set_constraint(variable.type_expression.type, "{{0}} is not assignable to {{1}}");
                fi

                variable.initializer.walk(self);

                // push the initializer value down into the variable left
                if variable.initializer.value? then
                    variable.left.right_value = variable.initializer.value;
                else
                    variable.left.right_value = new IR.Values.DUMMY(new Semantic.Types.ERROR(), variable.initializer.location);
                fi

                variable.left.variable_location = variable.location;
                variable.left.right_location = variable.initializer.location;
            fi

            if 
                !variable.is_poisoned /\ 
                (!variable.type_expression? \/ isa Trees.TypeExpressions.INFER(variable.type_expression))
                /\ !variable.initializer?
            then
                _logger.error(variable.location, "variable must have explict type or initializer");
            fi

            variable.left.walk(self);

            return true;
        si

        visit(variable: Trees.Variables.VARIABLE) is
        si

        // TODO used by for loop - needs removing
        set_symbol_type(left: Trees.Variables.VariableLeft, type: Type) is
            if !left? then
                return;
            fi

            if left.is_simple_name then
                let symbol = find(left.name);

                if symbol? /\ isa Semantic.Types.SettableTyped(symbol) then
                    let typed_symbol = cast Semantic.Types.SettableTyped(symbol);

                    symbol.define();

                    typed_symbol.set_type(type);
                else
                    _logger.error(left.location, "couldn't find typed symbol for variable {left.name}");
                fi
            else
                set_symbol_destructure_types(left, type);
            fi
        si

        // TODO used by for loop - needs removing
        set_symbol_destructure_types(left: Trees.Variables.VariableLeft, from_type: Type) is
            if !from_type? then
                return;
            fi

            let from_types: Collections.List[Type];

            if from_type.is_value_tuple then
                from_types = from_type.arguments;
            else
                from_types = get_conventionally_named_destructure_member_types(from_type);
            fi

            if from_types.count == 0 then
                _logger.error(left.location, "cannot destructure {from_type}");
                return;
            fi

            let elements = left.elements;

            let count = elements.count;

            if elements.count != from_types.count then
                _logger.error(left.location, "expected {elements.count} destructuring elements but found {from_types.count}");

                if elements.count > from_types.count then
                    count = from_types.count;
                fi
            fi

            for i in 0..count do
                let element = elements[i];
                let type = from_types[i];

                if !element? then
                    return;
                elif element? /\ element.is_simple_name then
                    let symbol = find(element.name);

                    if symbol? /\ isa Semantic.Types.SettableTyped(symbol) then
                        let typed_symbol = cast Semantic.Types.SettableTyped(symbol);

                        symbol.define();

                        typed_symbol.set_type(type);
                    else
                        _logger.error(element.location, "couldn't find typed symbol for destructuring element {element.name}");
                    fi
                else
                    set_symbol_destructure_types(element, type);
                fi
            od
        si
        
        // TODO used by for loop - needs removing
        get_destructure_types(type: Type) -> Collections.List[Type] is
            let result = new Collections.LIST[Type]();

            if !type? \/ !type.is_value_tuple then
                debug_always("## get_destructure_types: not destructurable {type}");

                return result;                
            fi

            get_destructure_types_into(type, result);

            return result;
        si
        
        // TODO used by for loop - needs removing
        get_destructure_types_into(type: Type, into: Collections.MutableList[Type]) is
            let result = new Collections.LIST[Type]();

            for t in type.arguments do
                if t.is_value_tuple then
                    get_destructure_types_into(t, into);
                else
                    into.add(t);
                fi
            od
        si

        visit(integer: Trees.Expressions.Literals.INTEGER) is
            let type: Type;
            let suffix = "i4";

            let value_string = integer.value_string;
            let conv: string;

            let last_char = value_string.get_chars(value_string.length-1);

            if "bBcCsSiIlLwWuU".contains(last_char) then
                let is_unsigned = false;

                let cutoff = value_string.length;

                if last_char == 'u' \/ last_char == 'U' then
                    is_unsigned = true;

                    cutoff = cutoff - 1;
                elif value_string.length > 2 then
                    let sign_char = value_string.get_chars(value_string.length - 2);

                    if sign_char == 'u' \/ sign_char == 'U' then
                        is_unsigned = true;

                        cutoff = cutoff - 1;
                    elif sign_char == 's' \/ sign_char == 'S' then
                        cutoff = cutoff - 1;
                    fi
                fi

                case last_char
                when 'b', 'B': 
                    if is_unsigned then
                        type = _innate_symbol_lookup.get_ubyte_type();
                    else
                        type = _innate_symbol_lookup.get_byte_type();
                    fi
                    
                    cutoff = cutoff - 1;

                when 'c', 'C':
                    if is_unsigned then
                        _logger.error(integer.location, "numeric character literal cannot be unsigned");
                    else
                        type = _innate_symbol_lookup.get_char_type();
                    fi

                    cutoff = cutoff - 1;

                when 's', 'S':
                    if is_unsigned then
                        type = _innate_symbol_lookup.get_ushort_type();
                    else
                        type = _innate_symbol_lookup.get_short_type();
                    fi

                    cutoff = cutoff - 1;

                when 'i', 'I':
                    if is_unsigned then
                        type = _innate_symbol_lookup.get_uint_type();
                    else
                        type = _innate_symbol_lookup.get_int_type();
                    fi

                    cutoff = cutoff - 1;

                when 'l', 'L':
                    suffix = "i8";

                    if is_unsigned then
                        type = _innate_symbol_lookup.get_ulong_type();
                    else
                        type = _innate_symbol_lookup.get_long_type();
                    fi

                    cutoff = cutoff - 1;

                when 'w', 'W':
                    suffix = "i8";

                    if is_unsigned then
                        type = _innate_symbol_lookup.get_uword_type();
                        conv = "conv.u";
                    else
                        type = _innate_symbol_lookup.get_word_type();
                        conv = "conv.i";
                    fi

                    cutoff = cutoff - 1;

                default
                    if is_unsigned then
                        type = _innate_symbol_lookup.get_uint_type();
                    else
                        type = _innate_symbol_lookup.get_int_type();
                    fi
                esac

                value_string = value_string.substring(0, cutoff);
            else
                type = _innate_symbol_lookup.get_int_type();                
            fi 
            
            integer.value = new Literal.NUMBER(
                value_string,
                type,
                suffix,
                conv
            );
        si

        visit(float: Trees.Expressions.Literals.FLOAT) is
            let type: Type;
            let suffix: string;

            let value_string = float.value_string;

            if
                value_string.ends_with('D') \/
                value_string.ends_with('d')
            then
                type = _innate_symbol_lookup.get_double_type();
                suffix = "r8";
            else
                type = _innate_symbol_lookup.get_single_type();
                suffix = "r4";
            fi
            
            float.value = new Literal.NUMBER(
                value_string.substring(0, value_string.length -1),
                type,
                suffix
            );
        si

        visit(interpolation: Trees.Expressions.STRING_INTERPOLATION) is
            interpolation.value = new IR.Values.BLOCK(_innate_symbol_lookup.get_string_type());
        si

        visit(`string: Trees.Expressions.Literals.STRING) is
            `string.value = new Literal.STRING(
                `string.value_string,
                _innate_symbol_lookup.get_string_type()
            );
        si        

        visit(character: Trees.Expressions.Literals.CHARACTER) is
            let value_string = character.value_string;
            let c: int = 0;

            if value_string.length < 1 \/ value_string.length > 1 then
                _logger.error(character.location, "invalid character literal");
            else
                c = cast int(value_string.get_chars(0));
            fi
            
            character.value = new Literal.NUMBER(
                "{c}",
                _innate_symbol_lookup.get_char_type(),
                "i4"
            );
        si

        visit(boolean: Trees.Expressions.Literals.BOOLEAN) is
            let value_string = boolean.value_string;

            let value: string;

            if value_string =~ "true" then
                value = "1";
            elif value_string =~ "false" then
                value = "0";
            else
                throw new System.Exception("invalid value for boolean literal: {value_string}");
            fi
                        
            boolean.value = new Literal.NUMBER(
                value,
                _innate_symbol_lookup.get_bool_type(),
                "i4"
            );
        si

        pre(call: Trees.Expressions.CALL) -> bool is
            return true;
        si

        visit(call: Trees.Expressions.CALL) is
            let mark = _logger.mark();

            try
                _logger.speculate();

                super.pre(call);

                call.arguments.walk(self);
                call.function.walk(self);

                _visit(call);

                _logger.commit();
            catch e: Exception
                call.value = null;
                _logger.release(mark);
 
                _logger.exception(call.location, e, "exception compiling call");
            yrt
        si

        _visit(call: Trees.Expressions.CALL) is
            if !call.function.value? then
                return;
            fi

            let arguments = new Collections.LIST[Value]();
            let argument_types = new Collections.LIST[Type]();

            let buffer: IO.TextWriter;

            let outer_state: (writer: IO.TextWriter, error_count: int);

            for a in call.arguments do
                if a? /\ a.value? /\ a.value.type? then
                    a.value.check_is_consumable(_logger,a.location);

                    arguments.add(a.value);
                    argument_types.add(a.value.type);                    
                else
                    let type = new Semantic.Types.ERROR();

                    arguments.add(new DUMMY(type, if a? then a.location else call.arguments.location fi));
                                        
                    argument_types.add(type);
                fi
            od

            if isa Load.SYMBOL(call.function.value) then
                let load = cast Load.SYMBOL(call.function.value);

                if load.symbol? /\ isa Semantic.Symbols.FUNCTION_GROUP(load.symbol) then
                    let want_instance: bool;

                    want_instance = 
                        if load.from? then 
                            load.from.is_consumable
                        else 
                            _symbol_table.current_instance_context?
                        fi;
                    
                    let function_group = cast Semantic.Symbols.FUNCTION_GROUP(load.symbol);
                    let overload_result = _overload_resolver.resolve(call.arguments.location, function_group, argument_types, true, want_instance);

                    if overload_result == null then
                        return;
                    fi

                    if overload_result.score == Semantic.Types.MATCH.PARTIAL then
                        _logger.roll_back();
                        _logger.speculate();

                        for (index, a) in call.arguments | .index() do
                            if a? /\ a.value? /\ isa Trees.Expressions.FUNCTION(a) then
                                let f = overload_result.function.arguments[index];

                                try
                                    _partial_argument_function_type = f;

                                    a.walk(self);
                                finally
                                    _partial_argument_function_type = null;
                                yrt
                                
                                argument_types[index] = a.value.type;
                            fi
                        od

                        overload_result = _overload_resolver.resolve(call.arguments.location, function_group, argument_types, false, want_instance);

                        if !overload_result? then
                            return;
                        fi    
                    fi

                    if !overload_result? then
                        return;
                    fi    

                    let function = overload_result.function;

                    if function.is_unsafe_constraints then
                        _logger.warn(call.location, "call to {function} has unchecked constraints");
                    fi    

                    _symbol_use_locations.add_symbol_use(call.function.right_location, function);

                    call.value = function.call(call.function.location, load.from, arguments, null, _function_caller);

                    return;
                fi
            fi

            let function_type = call.function.value.type;

            if !function_type? \/ !isa Semantic.Types.NAMED(function_type) then
                _logger.error(call.function.location, "cannot call value of non-function type {call.function.value.type}");
                return;
            fi

            let function_generic_type = cast Semantic.Types.NAMED(function_type);

            let function_type_arguments = function_generic_type.arguments;

            if function_type.is_action then
                if function_type_arguments.count != arguments.count then
                    _logger.error(
                        call.arguments.location, 
                        "expected {function_type_arguments.count} arguments but {arguments.count} supplied");
                    return;
                fi
            elif function_type.is_function then
                if function_type_arguments.count != arguments.count + 1 then
                    _logger.error(
                        call.arguments.location, 
                        "expected {function_type_arguments.count - 1} arguments but {arguments.count} supplied");
                    return;
                fi
            else
               _logger.error(call.function.location, "cannot call value of non-function type {call.function.value.type}");
                return;                    
            fi

            let ok = true;

            for i in 0..arguments.count do
                if cast int(function_generic_type.arguments[i].compare(argument_types[i])) > cast int(Semantic.Types.MATCH.CONVERTABLE)
                then
                    ok = false;
                    _logger.error(call.arguments.expressions[i].location, "expected argument of type {function_type_arguments[i]} but {argument_types[i]} supplied");
                fi
            od

            if !ok then
                return;
            fi

            let result_type =
                if function_type.is_action then
                    _innate_symbol_lookup.get_void_type();
                else
                    function_generic_type.arguments[function_type_arguments.count - 1];
                fi;

            call.value =
                new Call.CLOSURE(
                    call.function.value,
                    result_type,
                    function_type.is_action,
                    function_generic_type,
                    arguments
                );
        si

        visit(identifier: Trees.Expressions.IDENTIFIER) is
            let mark = _logger.mark();
            
            try
                _visit(identifier);

            catch ex: Exception
                _logger.exception(identifier.location, ex, "something went wrong with identifier");

            finally 
                _logger.release(mark);
            yrt            
        si

        _visit(identifier: Trees.Expressions.IDENTIFIER) is
            let need_store = identifier.value? /\ isa Need.STORE(identifier.value);
        
            let symbol = find(identifier.identifier);

            if symbol? then
                _symbol_use_locations.add_symbol_use(identifier.right_location, symbol);

                _symbol_loader.find_symbol = (name: string) => find(name);

                if need_store then
                    let store_value = cast Need.STORE(identifier.value).value;
                    identifier.value = symbol.store(identifier.location, null, store_value, _symbol_loader, false);
                else
                    identifier.value = symbol.load(identifier.location, null, _symbol_loader);
                fi
            else
                identifier.value = null;
            fi                
        si

        visit(has_value: Trees.Expressions.HAS_VALUE) is
            if !has_value.left? then
                _logger.poison(has_value.location, "has value expression has no left expression");
                return;
            elif !has_value.left.value? then
                _logger.poison(has_value.left.location, "has value expression has no value");
                return;                
            fi

            if 
                has_value.left.value.check_is_consumable(_logger, has_value.left.location) /\
                has_value.left.value.type.is_value_type 
            then
                _logger.error(has_value.left.location, "has value operand must be a reference type");
            fi
            
            has_value.value = new HAS_VALUE(has_value.left.value, _innate_symbol_lookup.get_bool_type());
        si

        visit(reference: Trees.Expressions.REFERENCE) is
            if !reference.left? then
                _logger.poison(reference.location, "reference expression has no left expression");
                return;
            elif !reference.left.value? \/ !reference.left.value.type? then
                _logger.poison(reference.left.location, "reference expression has no value");
                return;                
            elif !reference.left.value.has_address then
                _logger.warn(reference.left.location, "reference to an expression");                
            fi

            reference.left.value.check_is_consumable(_logger, reference.left.location);
            
            reference.value = new ADDRESS(reference.left.value, _innate_symbol_lookup.get_reference_type(reference.left.value.type));
        si
        
        visit(`null: Trees.Expressions.NULL) is
            `null.value = new NULL(new Semantic.Types.NULL());
        si

        pre(statement: Trees.Expressions.STATEMENT) -> bool is
            super.pre(statement);

            statement.statement.want_value = true;

            return false;
        si

        visit(statement: Trees.Expressions.STATEMENT) is
            if !statement.statement.value? then
                _logger.warn(statement.location, "statement expression has no value");                  
            fi

            statement.value = statement.statement.value;
        si

        pre(list: Trees.Statements.LIST) -> bool is
            super.pre(list);
            return true;
        si

        visit(list: Trees.Statements.LIST) is
            let enclosing_statement_list = current_statement_list;
            current_statement_list = list;

            try
                if !list? then
                    return;
                fi

                if list.last? then
                    list.last.want_value = list.want_value;
                fi

                for s in list.statements do
                    s.walk(self);
                od

                if !list.want_value then
                    return;
                fi
                    
                if list.is_empty then
                    _logger.error(list.location, "expected a value");
                    return;
                fi
    
                let last = list.last;
    
                if !last? then
                    return;
                fi
    
                if !last.provides_value then
                    _logger.error(last.location, "expected a value");
                    return;
                fi
    
                if !last.value? then
                    return;
                fi
    
                list.value = new IR.Values.BLOCK(last.value.type);                    
            finally
                current_statement_list = enclosing_statement_list
            yrt
        si

        pre(`if: Trees.Statements.IF) -> bool is
            super.pre(`if);

            if `if.want_value then
                for i in `if.branches do
                    i.body.want_value = true;
                od
            fi

            return false;
        si

        visit(`if: Trees.Statements.IF) is
            if !`if.want_value then
                return;
            fi

            if `if.is_poisoned then
                // if the if is syntactically incomplete don't bother reporting any
                // semantic errors:
                `if.value = new DUMMY_BLOCK(new Semantic.Types.ERROR(), `if.location, "if is poisoned");

                return;
            fi

            if !`if.branches | .any(b => b? /\ !b.condition?) then
                // if the if has no else branch, then don't bother reporting any
                // semantic errors:
                _logger.error(`if.location, "expected else in if expression");
                `if.value = new DUMMY_BLOCK(new Semantic.Types.ERROR(), `if.location, "no else");
                return;
            fi

            let type = `if.constraint;

            let seen_any_values = false;

            for i in `if.branches do
                let branch = i.body;

                if !branch.value? then
                    continue;
                fi

                if !branch.value.type? then
                    continue;
                fi

                if !branch.value.check_is_consumable(_logger, branch.location) then
                    continue;
                fi

                if `if.constraint? then
                    if !`if.constraint.is_assignable_from(branch.value.type) then
                        _logger.error(branch.location, string.format(`if.constraint_error_message, branch.value.type, type));
                    fi
                // FIXME QQ should do this
                elif !type? \/ type.is_any then
                    type = branch.value.type;
                elif branch.value.type.is_assignable_from(type) /\ !branch.value.type.is_any then
                    type = branch.value.type;
                elif !type.is_assignable_from(branch.value.type) then
                    _logger.error(branch.location, "incompatible types in if branches: {type} and {branch.value.type}");

                    `if.value = new DUMMY_BLOCK(new Semantic.Types.ERROR(), `if.location, "incompatible types");
                    return;
                fi

                seen_any_values = true;
            od

            if type? then
                `if.value = new IR.Values.BLOCK(type);
            else
                if seen_any_values then
                    _logger.error(`if.location, "no type infered for if expression");
                fi

                `if.value = new DUMMY_BLOCK(new Semantic.Types.ERROR(), `if.location, "no type infered");
            fi
        si

        // Do not descend into properties (otherwise accessor functions will be walked twice)
        pre(property: Trees.Definitions.PROPERTY) -> bool => true;

        visit(property: Trees.Definitions.PROPERTY) is
        si
    si
si
