namespace Syntax.Process is
    use System;
    use Generic;
    
    use Logging;

    class RESOLVE_ANCESTORS: ScopedVisitor is
        _logger: Logger;
        _symbol_table: Semantic.SYMBOL_TABLE;
        _ghul_symbol_lookup: Semantic.GHUL_SYMBOL_LOOKUP;
        _system_symbol_lookup: Semantic.SYSTEM_SYMBOL_LOOKUP;

        init(
            logger: Logger,
            symbol_table: Semantic.SYMBOL_TABLE,
            namespaces: Semantic.NAMESPACES,
            ghul_symbol_lookup: Semantic.GHUL_SYMBOL_LOOKUP,
            system_symbol_lookup: Semantic.SYSTEM_SYMBOL_LOOKUP
        )
        is
            super.init(logger, symbol_table, namespaces);

            _logger = logger;
            _symbol_table = symbol_table;
            _ghul_symbol_lookup = ghul_symbol_lookup;
            _system_symbol_lookup = system_symbol_lookup;
        si

        apply(root: Tree.NODE) is
            root.walk(self);
        si

        pre(class_: Tree.Definition.CLASS) -> bool is
            super.pre(class_);

            return true;
        si

        visit(class_: Tree.Definition.CLASS) is
            let class_symbol = cast Semantic.Symbol.CLASS(_symbol_table.current_instance_context);

            // exit class scope before looking up ancestors:
            super.visit(class_);            

            var seen_class_ancestor = false;

            // FIXME: implicit ancestor Ghul.Object
            if class_.ancestors? then
                for a in class_.ancestors do
                    var ancestor_type = a.type; 

                    if ancestor_type? then
                        if isa Semantic.Type.NAMED(ancestor_type) then
                            let ancestor_named_type = cast Semantic.Type.NAMED(ancestor_type);

                            if ancestor_named_type.symbol? && !ancestor_named_type.symbol.is_trait then // isa Semantic.Symbol.CLASS(ancestor_named_type.symbol) then
                                seen_class_ancestor = true;
                            fi

                            class_symbol.add_ancestor(ancestor_named_type);
                        else
                            _logger.error(a.location, "cannot inherit from this");
                        fi
                    fi
                od
            fi

            // FIXME: safer test for Ghul.Object:
            if !seen_class_ancestor && class_.name.name !~ "Object" then
                let object_type = _system_symbol_lookup.get_object_type();

                if class_symbol != object_type.symbol then
                    class_symbol.push_ancestor(_system_symbol_lookup.get_object_type());
                fi
            fi
        si

        pre(trait_: Tree.Definition.TRAIT) -> bool is
            super.pre(trait_);

            return true;
        si

        visit(trait_: Tree.Definition.TRAIT) is
            let trait_symbol = cast Semantic.Symbol.TRAIT(_symbol_table.current_instance_context);

            // exit class scope before looking up ancestors:
            super.visit(trait_);

            var seen_valid_ancestor = false;

            if trait_.ancestors? then
                for a in trait_.ancestors do
                    var ancestor_type = a.type;

                    if ancestor_type? then
                        if isa Semantic.Type.NAMED(ancestor_type) then
                            let ancestor_named_type = cast Semantic.Type.NAMED(ancestor_type);

                            if ancestor_named_type.symbol? && ancestor_named_type.symbol.is_trait then
                                trait_symbol.add_ancestor(ancestor_named_type);
                                seen_valid_ancestor = true;
                            else
                                _logger.error(a.location, "trait cannot inherit from class");
                            fi
                        else
                            _logger.error(a.location, "cannot inherit from this");
                        fi
                    else
                        IO.Std.err.println("refusing to add ancestor with null type: " + a);
                    fi
                od
            fi

            if !seen_valid_ancestor then
                let object_type = _system_symbol_lookup.get_object_type();

                if trait_symbol != object_type.symbol then
                    trait_symbol.add_ancestor(object_type);
                fi
            fi
        si

        pre(struct_: Tree.Definition.STRUCT) -> bool is
            super.pre(struct_);

            return true;
        si

        visit(struct_: Tree.Definition.STRUCT) is
            let struct_symbol = cast Semantic.Symbol.STRUCT(_symbol_table.current_instance_context);

            // exit class scope before looking up ancestors:
            super.visit(struct_);

            if struct_.ancestors? then
                for a in struct_.ancestors do
                    var ancestor_type = a.type; 

                    if ancestor_type? then
                        if isa Semantic.Type.NAMED(ancestor_type) then
                            let ancestor_named_type = cast Semantic.Type.NAMED(ancestor_type);

                            if ancestor_named_type.symbol? && ancestor_named_type.symbol.is_trait then
                                struct_symbol.add_ancestor(ancestor_named_type);

                                continue;
                            fi
                        fi

                        _logger.error(a.location, "structs can only inherit from traits");
                    fi
                od
            fi

            struct_symbol.push_ancestor(_system_symbol_lookup.get_value_type());
        si
    si
si
