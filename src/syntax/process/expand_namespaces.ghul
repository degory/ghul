namespace Syntax.Process is
    use System.Uri;
    use System.UriKind;

    use System.IO.Abstractions;
    use Source;
    use Trees;

    class EXPAND_NAMESPACES: Visitor is
        _file_system: IFileSystem;
        _depth: int;

        init(file_system: IFileSystem) is
            super.init();

            _file_system = file_system;
        si

        apply(node: Node) is
            assert isa Definitions.LIST(node) else "expected the root syntax tree node of file to be a definition list";

            let list = cast Definitions.LIST(node);

            let seen_any_namespaces = false;
            let seen_any_other_definitions = false;

            for definition in list do
                if isa Definitions.NAMESPACE(definition) then
                    seen_any_namespaces = true;
                elif !isa Definitions.PRAGMA(definition) then
                    seen_any_other_definitions = true;
                fi
            od
            
            if seen_any_namespaces /\ seen_any_other_definitions then
                let logger = IoC.CONTAINER.instance.logger;

                for definition in list do
                    if 
                        !isa Definitions.NAMESPACE(definition) /\ 
                        !isa Definitions.Classy(definition) /\
                        !isa Definitions.PRAGMA(definition)
                    then
                        logger.error(definition.location, "cannot mix global definitions and namespaces in the same file");                        
                    fi
                od
            elif seen_any_other_definitions then
                let path = list | .first().value.location.file_name;

                let root_namespace_name = get_namespace_name(path);

                let new_definitions = new Collections.LIST[Definitions.Definition](
                    list
                );

                let root_namespace = 
                    new Definitions.NAMESPACE(
                        list.location,
                        new Trees.Identifiers.Identifier(Source.LOCATION.internal, root_namespace_name),
                        new Definitions.LIST(list.location, new_definitions)
                    );

                list.clear();

                list.add(root_namespace);
            fi

            node.walk(self);
        si

        is_root_directory(info: IDirectoryInfo) -> bool is
            let root_path = _file_system.path.get_path_root(info.full_name);

            return root_path =~ info.full_name;
        si

        convert_file_name_to_identifier(name: string) -> string is
            if name.length == 0 then
                return name;
            fi

            name = 
                name
                    .replace('-', '_')
                    .replace('.', '_') |
                    .filter(c => char.is_letter_or_digit(c) \/ c == '_')
                    .to_string("");
            
            if !char.is_letter(name.get_chars(0)) then
                name = "`" + name;
            fi

            return name;
        si

        get_namespace_name(path: string) -> string is
            let local_path: string;
            let uri: Uri;

            if Uri.is_well_formed_uri_string(path, UriKind.ABSOLUTE) then
                uri = new Uri(path);
            fi

            if uri? /\ uri.is_file then
                local_path = uri.local_path;
            else
                local_path = path;
            fi

            let result =             
                convert_file_name_to_identifier(
                    _file_system.path.get_file_name_without_extension(local_path)
                );

            let folder = _file_system.path.get_directory_name(local_path);

            result = get_namespace_name_for_folder(folder) + result;

            return result;
        si
        
        get_namespace_name_for_folder(path: string) -> string is
            let original_path = path;

            if !path? \/ path =~ "" then
                path = _file_system.directory.get_current_directory();
            fi

            let info = _file_system.directory_info.from_directory_name(path);

            let result = get_namespace_name_for_folder(info);

            return result;            
        si        

        get_namespace_name_for_folder(info: IDirectoryInfo) -> string is
            let result = convert_file_name_to_identifier(info.name) + "__";

            if 
                _file_system.file.exists(_file_system.path.combine(info.full_name, "ghul.json")) \/
                _file_system.directory.get_files(info.full_name, "*.ghulproj").count > 0
            then
                return result;
            fi

            if info.parent? /\ !is_root_directory(info) then
                result = get_namespace_name_for_folder(info.parent) + result;
            fi

            return result;
        si

        pre(`namespace: Definitions.NAMESPACE) -> bool is
            _depth = _depth + 1;
        si

        visit(`namespace: Definitions.NAMESPACE) is
            let names = `namespace.name.qualifier_names;

            if _depth == 1 then
                `namespace.body.push(
                    new Definitions.USE(
                        Source.LOCATION.internal,
                        null,
                        new Identifiers.Identifier(
                            LOCATION.internal,
                            "Ghul"
                        )
                    )
                );
            fi

            if names.count > 0 then
                `namespace.name = new Identifiers.Identifier(`namespace.name.location, `namespace.name.name);
                let body = `namespace.body;
                let current = `namespace;
                
                for n in names do
                    current.body = 
                        new Definitions.LIST(
                            current.location, 
                            new Collections.LIST[Definitions.Definition](
                                [new Definitions.NAMESPACE(current.location, current.name, current.body)]: Definitions.Definition
                            )
                        );
                    current.name = new Identifiers.Identifier(`namespace.name.location, n);
                od
            fi

            _depth = _depth - 1;
        si
    si
si
