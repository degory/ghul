namespace Syntax.Process is
    use Source;
    use Trees;

    class EXPAND_NAMESPACES: Visitor is
        _depth: int;

        init() is
            super.init();
        si

        apply(node: Node) is
            node.walk(self);
        si

        pre(`namespace: Definitions.NAMESPACE) -> bool is
            _depth = _depth + 1;
        si

        visit(`namespace: Definitions.NAMESPACE) is
            var names = `namespace.name.qualifier_names;

            if _depth == 1 then
                `namespace.body.push(
                    new Definitions.USE(
                        Source.LOCATION.internal,
                        null,
                        new Identifiers.Identifier(
                            LOCATION.internal,
                            "Ghul"
                        )
                    )
                );
            fi

            if names.count > 0 then
                `namespace.name = new Identifiers.Identifier(`namespace.name.location, `namespace.name.name);
                var body = `namespace.body;
                var current = `namespace;
                
                for n in names do
                    current.body = 
                        new Definitions.LIST(
                            current.location, 
                            new Collections.LIST[Definitions.Definition](
                                [new Definitions.NAMESPACE(current.location, current.name, current.body)]: Definitions.Definition
                            )
                        );
                    current.name = new Identifiers.Identifier(`namespace.name.location, n);
                od
            fi

            _depth = _depth - 1;
        si
    si
si
