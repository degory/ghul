namespace Syntax.Process is
    use Source;
    use Tree;

    class EXPAND_NAMESPACES: Visitor is
        _depth: int;

        init() is
            super.init();
        si

        apply(node: NODE) is
            node.walk(self);
        si

        pre(namespace_: Definition.NAMESPACE) -> bool is
            _depth = _depth + 1;
        si

        visit(namespace_: Definition.NAMESPACE) is
            var names = namespace_.name.qualifier_names;

            if _depth == 1 then
                namespace_.body.push(
                    new Definition.USE(                        
                        LOCATION.dummy,
                        new Collections.LIST[Identifier.NODE]([
                            new Identifier.NODE(
                                LOCATION.dummy,
                                "Ghul"
                            )
                        ])
                    )
                );
            fi

            if names.count > 0 then
                namespace_.name = new Identifier.NODE(namespace_.name.location, namespace_.name.name);
                var body = namespace_.body;
                var current = namespace_;
                
                for n in names do
                    current.body = 
                        new Definition.LIST(
                            current.location, 
                            new Collections.LIST[Definition.NODE](
                                [new Definition.NAMESPACE(current.location, current.name, current.body)]: Definition.NODE
                            )
                        );
                    current.name = new Identifier.NODE(namespace_.name.location, n);
                od
            fi

            _depth = _depth - 1;
        si
    si
si
