namespace Syntax.Parser.Definition is
    use System;
    use Generic;

    use Source;

    class LIST: BASE[Tree.Definition.LIST] is
        definition_parser: Parser[Tree.Definition.NODE];
        description: String => "definition list";

        init(definition_parser: Parser[Tree.Definition.NODE]) is
            super.init();
            self.definition_parser = definition_parser;
        si

        parse(context: CONTEXT) -> Tree.Definition.LIST is
            let start = context.location;
            let end = context.location;
            let definitions = new Collections.LIST[Tree.Definition.NODE]();

            @IF.dotnet()
            System.Console.write_line("start parse definition list...");

            while !context.is_end_of_file && context.current.token != Lexical.TOKEN.SI do

                @IF.dotnet()
                System.Console.write_line("have a non-terminating token: " + context.current.token);
                try                        
                    let definition = definition_parser.parse(context);

                    @IF.dotnet()
                    System.Console.write_line("parsed definition: " + definition);

                    if definition? then
                        end = definition.location;

                        @IF.dotnet()
                        System.Console.write_line("add definition: " + definition);

                        definitions.add(definition);
                    fi
                catch e: Exception
                    System.Console.error.write_line("" + (start..context.current.location) + ": caught exception parsing definition: " + e);

                    while 
                        !context.is_end_of_file &&
                        context.current.token != Lexical.TOKEN.SI &&
                        context.current.token != Lexical.TOKEN.SEMICOLON
                    do
                        context.next_token();
                    od

                    if context.is_end_of_file then
                        break;
                    fi
                yrt

                @IF.dotnet()
                System.Console.write_line("parse definition: loop again");
            od

            @IF.dotnet()
            System.Console.write_line("parse definition: have list: " + new Shim.JOIN[Tree.Definition.NODE](definitions));

            return new Tree.Definition.LIST(start::end, definitions);
        si
    si
si
