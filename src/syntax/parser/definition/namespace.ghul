namespace Syntax.Parser.Definition is
    use System;
    use Source;

    class NAMESPACE: BASE[Tree.Definition.NAMESPACE] is
        identifier_qualified_parser: Parser[Tree.Identifier.NODE];
        definition_list_parser: Parser[Tree.Definition.LIST];

        init(
            identifier_qualified_parser: Parser[Tree.Identifier.NODE],
            definition_list_parser: Parser[Tree.Definition.LIST]
        )
        is
            super.init();
            self.identifier_qualified_parser = identifier_qualified_parser;
            self.definition_list_parser = definition_list_parser;
        si

        parse(context: CONTEXT) -> Tree.Definition.NAMESPACE is
            @IF.dotnet()
            Logging.DEBUG.write_line("parse namespace...");
            @IF.dotnet()
            System.Console.output.flush();

            context.next_token(Lexical.TOKEN.NAMESPACE);

            @IF.dotnet()
            Logging.DEBUG.write_line("parse identifier...");

            var start = context.location;
            var identifier = identifier_qualified_parser.parse(context);

            if 
                (context.current.token == Lexical.TOKEN.IS || !identifier.is_poisoned) &&
                context.next_token(Lexical.TOKEN.IS)
            then
                var body = definition_list_parser.parse(context);
                var result =
                    new Tree.Definition.NAMESPACE(
                        start::context.location,
                        identifier,
                        body
                    );

                if !identifier.is_poisoned || context.current.token == Lexical.TOKEN.SI then
                    context.next_token(Lexical.TOKEN.SI);
                fi

                if identifier.is_poisoned then
                    result.poison();
                fi
                
                return result;
            fi
        si
    si
si
