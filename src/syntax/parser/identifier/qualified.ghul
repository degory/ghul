namespace Syntax.Parser.Identifier is
    @IF.not.v3() use object = System.Object;
    @IF.not.v3() use string = System.String;
    @IF.not.v3() use STD = System.Console;

    use System;

    use Source;

    class QUALIFIED: BASE[Tree.Identifier.NODE] is
        description: string => "qualified identifier";

        init() is
            super.init();
        si

        parse(context: CONTEXT) -> Tree.Identifier.NODE is
            var start = context.location;
            
            if context.expect_token(Lexical.TOKEN.IDENTIFIER, syntax_error_message) then
                var name = context.current.value_string;
                var result = new Tree.Identifier.NODE(context.location, name);

                context.next_token();

                while context.current.token == Lexical.TOKEN.DOT do
                    let completion_target_start = context.location;
                
                    context.next_token();
                    if context.expect_token(Lexical.TOKEN.IDENTIFIER, syntax_error_message) then
                        result = new Tree.Identifier.QUALIFIED(start::context.location, result, context.current.value_string, completion_target_start::context.location);

                        context.next_token();
                    else
                        result = new Tree.Identifier.QUALIFIED(start::context.location, result, null, completion_target_start::context.location);

                        result.poison();

                        return result;
                    fi
                od

                return result;
            fi
        si
    si
si
