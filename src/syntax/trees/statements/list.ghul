namespace Syntax.Trees.Statements is
        
    use Source;

    class LIST: Trees.Statements.Statement, Collections.Iterable[Statement] is
        _statements: Collections.List[Statement];
        statements: Collections.Iterable[Statement] => _statements;
        
        _variables_to_dispose: Collections.LIST[Semantic.Symbols.Variable];

        variables_to_dispose: Collections.List[Semantic.Symbols.Variable] => _variables_to_dispose;

        want_dispose: bool;
        provides_value: bool => true;

        init(location: LOCATION, statements: Collections.List[Statement]) is
            super.init(location);

            _statements = statements;
        si

        iterator: Collections.Iterator[Statement] => _statements.iterator;

        is_empty: bool => _statements.count == 0;

        last: Statement is
            if !_statements? \/ _statements.count == 0 then
                return null;
            fi

            return _statements[_statements.count - 1];
        si

        try_set_constraint(constraint: Semantic.Types.Type, error_message: string) is
            let l = last;

            if l? then
                l.try_set_constraint(constraint, error_message);
            fi
        si

        add_variable_to_dispose(v: Semantic.Symbols.Variable) is
            want_dispose = true;
            
            if !_variables_to_dispose? then
                _variables_to_dispose = Collections.LIST[Semantic.Symbols.Variable]();
            fi

            _variables_to_dispose.add(v);
        si

        accept(visitor: Visitor) is
            visitor.visit(self);
        si

        walk(visitor: Visitor) is
            if !visitor.pre(self) then
                for s in statements do
                    s.walk(visitor);
                od
            fi

            accept(visitor);
        si
    si
si
