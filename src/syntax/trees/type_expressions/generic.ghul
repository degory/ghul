namespace Syntax.Trees.TypeExpressions is
    
    use Source;

    class GENERIC: NAMED is
        arguments: TypeExpressions.LIST;
        argument_count: int => arguments.count;

        is_void: bool =>
            arguments | .any(e => e? /\ e.is_void);

        try_copy_as_value_expression() -> Expressions.Expression is
            if arguments.count != 1 then
                return null;
            fi

            let ee = arguments.elements[0].try_copy_as_value_expression();

            if !ee? then
                return null;
            fi

            return 
                new Expressions.INDEX(
                    location,
                    name.copy_as_expression(),
                    ee
                )
        si

        init(location: LOCATION, name: Identifiers.Identifier, arguments: TypeExpressions.LIST) is
            super.init(location, name);

            assert arguments? else "arguments are null";

            self.arguments = arguments;
        si

        qualify_member(member: Identifiers.Identifier) -> TypeExpression is
            let member_identifier = new Identifiers.QUALIFIED(
                location,
                name,
                member.name,
                name.location::member.location,
                member.location
            );

            return new GENERIC(
                location,
                member_identifier,
                arguments.shallow_copy()
            );
        si

        copy() -> TypeExpression =>
            new GENERIC(
                location,
                name.copy(),
                arguments.shallow_copy()
            );

        accept(visitor: Visitor) is
            visitor.visit(self);
        si

        walk(visitor: Visitor) is
            if !visitor.pre(self) then
                arguments.walk(visitor);
            fi
            accept(visitor);
        si
    si
si
