namespace Syntax.Trees.TypeExpressions is
    
    use Source;

    class GENERIC: NAMED is
        arguments: TypeExpressions.LIST;

        is_void: bool =>
            arguments | .any(e => e? /\ e.is_void);
        
        init(location: LOCATION, name: Identifiers.Identifier, arguments: TypeExpressions.LIST) is
            super.init(location, name);

            self.arguments = arguments;
        si

        qualify_member(member: Identifiers.Identifier) -> TypeExpression is
            let member_identifier = new Identifiers.QUALIFIED(
                location,
                name,
                member.name,
                name.location::member.location,
                member.location
            );

            return new GENERIC(
                location,
                member_identifier,
                arguments.copy()
            );
        si

        copy() -> TypeExpression =>
            new GENERIC(
                location,
                name.copy(),
                arguments.copy()
            );

        accept(visitor: Visitor) is
            visitor.visit(self);
        si

        walk(visitor: Visitor) is
            if !visitor.pre(self) then
                arguments.walk(visitor);
            fi
            accept(visitor);
        si
    si
si
