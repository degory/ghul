namespace Syntax.Trees.Identifiers is
    use IO.Std;
    
    use Source;

    class Identifier: Trees.Node, Collections.Iterable[string] is
        name: string;

        qualifier: Identifier => null;
        
        qualifier_names: Collections.LIST[string] => Collections.LIST[string](0);

        names: Collections.LIST[string] => Collections.LIST[string]([name]); // : string;

        is_qualified: bool => false;

        right_location: LOCATION => location;

        iterator: Collections.Iterator[string] => names.iterator;

        init(location: LOCATION, name: string) is
            super.init(location);

            if name == null then
                poison();
                name = "$poisoned";
            fi

            self.name = name;
        si

        copy() -> Identifier =>
            Identifier(location, name);

        copy_as_expression() -> Expressions.Expression is
            if qualifier? then
                return Expressions.MEMBER(location, qualifier.copy_as_expression(), Identifier(right_location, name), right_location);
            else
                return Expressions.IDENTIFIER(location, self.copy());
            fi
        si

        accept(visitor: Visitor) is
            visitor.visit(self);
        si
    si

    class QUALIFIED: Identifier is
        _qualifier: Identifier;

        completion_target: LOCATION;
        right_location: LOCATION;

        is_qualified: bool => true;

        qualifier: Identifier is
            return _qualifier;
        si

        qualifier_names: Collections.LIST[string] is
            let result = Collections.LIST[string]();
            let p = qualifier;

            while p? do
                result.add(p.name);
                p = p.qualifier;
            od

            return result;
        si

        names: Collections.LIST[string] is
            let result = Collections.LIST[string]();

            result.add(name);

            let p = qualifier;

            while p? do
                result.add(p.name);
                p = p.qualifier;
            od

            return result;
        si

        iterator: Collections.Iterator[string] is
            let result = Collections.LIST[string]();

            result.add(name);

            let p = qualifier;

            while p? do
                result.add(p.name);
                p = p.qualifier;
            od
            
            return result.iterator;
        si        

        init(
            location: LOCATION, 
            qualifier: Identifier, 
            name: string, 
            completion_target: LOCATION,
            right_location: LOCATION
        ) is
            super.init(location, name);

            _qualifier = qualifier;
            self.completion_target = completion_target;
            self.right_location = right_location;

            poison(qualifier.is_poisoned);
        si

        copy() -> Identifier is
            let np: Identifier;

            if qualifier? then
                np = qualifier.copy();
            fi

            return QUALIFIED(
                location,
                np,
                name,
                completion_target,
                right_location
            );
        si

        accept(visitor: Visitor) is
            visitor.visit(self);
        si

        walk(visitor: Visitor) is
            if !visitor.pre(self) then
                if qualifier? then
                    qualifier.walk(visitor);
                fi
            fi

            accept(visitor);
        si
    si
si
