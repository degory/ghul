namespace Syntax.Trees is
    use IO.Std;

    use System;    
    use Source;

    class Node is
        // whilst Node is used as a key in maps, we don't
        // need to override get_hash_code() or equals() as
        // the default implementations are sufficient: each
        // node is unique and its reference is its identity

        location: LOCATION;

        is_poisoned: bool;

        init(location: LOCATION) is
            self.location = location;
        si

        clone() -> Node => cast Node(memberwise_clone());   
        
        accept(visitor: Visitor) is
            visitor.visit(self);
        si

        walk(visitor: Visitor) is
            accept(visitor);
        si

        poison(should_poison: bool) is
            if should_poison then
                is_poisoned = true;
            fi
        si

        poison() is
            is_poisoned = true;
        si        

        to_string() -> string is
            try
                let printer = Process.Printer.GHUL();
                accept(printer);
                return printer.result;
            catch ex: Exception
                return "unprintable {get_type()}: {ex}";
            yrt            
        si
    si
si
