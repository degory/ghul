namespace Syntax.Trees.Expressions is
    use Source;

    class LIST: Expression, Collections.Iterable[Expression] is
        expressions: Collections.LIST[Expression];
        count: int => expressions.count;
        
        init(location: LOCATION, expressions: Collections.Iterable[Expression]) is
            super.init(location);
            
            self.expressions = Collections.LIST[Expression](expressions);
        si

        iterator: Collections.Iterator[Expression] => expressions.iterator;

        try_get_string_literal_at(index: int) -> string is
            if expressions? /\ index < expressions.count then
                return expressions[index].try_get_string_literal();
            fi
        si

        replace_element(index: int, value: Expression) is
            expressions[index] = value;
        si

        rewrite_as_variables() is
            for index in 0..expressions.count do
                let replacement = expressions[index].try_copy_as_variable();

                if replacement? then
                    expressions[index] = replacement;                    
                fi
            od            
        si

        rewrite_as_tuple_elements() is
            for index in 0..expressions.count do
                let replacement = expressions[index].try_copy_as_tuple_element();

                if replacement? then
                    expressions[index] = replacement;                    
                fi
            od            
        si

        accept(visitor: Visitor) is
            visitor.visit(self);
        si

        walk(visitor: Visitor) is
            if !visitor.pre(self) then
                for e in expressions do
                    e.walk(visitor);
                od
            fi
            accept(visitor);
        si
    si
si
