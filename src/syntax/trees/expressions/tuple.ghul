namespace Syntax.Trees.Expressions is
    
    use Source;
    
    class TUPLE: Expression is
        is_function_arguments: bool public;
        is_tuple: bool => true;
        elements: LIST;

        could_be_formal_argument: bool => true;

        init(location: LOCATION, elements: LIST, is_function_arguments: bool) is
            super.init(location);
            
            self.elements = elements;
            self.is_function_arguments = is_function_arguments;
        si

        rewrite_as_assignment_left() -> AssignmentLeft is
            if elements.expressions.count == 1 then
                return new SINGLE_EXPRESSION_LEFT(location, elements.expressions[0]);
            fi

            let result = new Collections.LIST[AssignmentLeft](elements.expressions.count);

            for element in elements.expressions do
                let expression = element.rewrite_as_expression();
                if element.is_tuple then
                    result.add(expression.rewrite_as_assignment_left());
                else
                    result.add(new SINGLE_EXPRESSION_LEFT(element.location, expression));
                fi
            od

            return new DESTRUCTURE_LEFT(location, result);
        si

        replace_element(index: int, value: Expression) is
            elements.replace_element(index, value);
        si

        accept(visitor: Visitor) is
            visitor.visit(self);
        si

        walk(visitor: Visitor) is
            if !visitor.pre(self) then
                elements.walk(visitor);
            fi
            
            accept(visitor);
        si
    si
si
