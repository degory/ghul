namespace Syntax.Trees.Expressions is
    use Source;

    class LET_IN: Expression is
        variables: Variables.LIST;
        expression: Expression;
        want_dispose: bool;

        description: string => "let in expression";

        expects_semicolon: bool => true;
        must_be_consumed: bool => true;

        init(location: LOCATION, want_dispose: bool, variables: Variables.LIST, expression: Expression) is
            super.init(location);

            assert variables? else "let variables list is null";
            assert expression? else "let expression is null";

            self.variables = variables;
            self.expression = expression;
            self.want_dispose = want_dispose;

            if want_dispose then
                variables.mark_want_dispose();
            fi
        si

        try_set_constraint(constraint: Semantic.Types.Type, error_message: string) is
            if expression? then
                expression.try_set_constraint(constraint, error_message);
            fi
        si

        accept(visitor: Visitor) is
            visitor.visit(self);
        si

        walk(visitor: Visitor) is
            if !visitor.pre(self) then
                variables.walk(visitor);
                expression.walk(visitor);
            fi
            
            accept(visitor);
        si
    si
si
