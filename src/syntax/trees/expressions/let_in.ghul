namespace Syntax.Trees.Expressions is
    use Source;

    use Logging;

    class LET_IN: Expression is
        variables: Variables.LIST;
        expression: Expression;
        want_dispose: bool;

        description: string => "let in expression";

        expects_semicolon: bool => true;
        must_be_consumed: bool => true;

        is_tuple_literal: bool =>
            if expression.is_tuple_literal then
                true
            else
                false
            fi;

        init(location: LOCATION, want_dispose: bool, variables: Variables.LIST, expression: Expression) is
            super.init(location);

            assert variables? else "let variables list is null";
            assert expression? else "let expression is null";

            self.variables = variables;
            self.expression = expression;
            self.want_dispose = want_dispose;

            if want_dispose then
                variables.mark_want_dispose();
            fi
        si

        set_constraint(constraint: Semantic.Types.Type, constraint_error_message: string) =>
            expression.set_constraint(constraint, constraint_error_message);

        clear_constraint() =>
            expression.clear_constraint();

        accept(visitor: Visitor) is
            visitor.visit(self);
        si

        walk(visitor: Visitor) is
            if !visitor.pre(self) then
                variables.walk(visitor);
                expression.walk(visitor);
            fi
            
            accept(visitor);
        si
    si
si
