namespace Syntax.Trees.Definitions is
    use Source;

    use Logging;

    class VARIANT: Classy  is
        description_for_walk: string => "variant";

        fields: Variables.LIST;

        init(
            location: LOCATION,
            name: Identifiers.Identifier,
            fields: Variables.LIST,
            modifiers: Modifiers.LIST
        )
        is
            super.init(
                location, 
                name, 
                null,
                null,
                modifiers,
                new Definitions.LIST(LOCATION.internal, System.Array.empty`[Definitions.Definition]())
            );

            self.fields = fields;
        si

        set_arguments(arguments: TypeExpressions.LIST) is
            self.arguments = arguments;
        si

        accept(visitor: Visitor) is
            visitor.visit(self);
        si

        _walk(visitor: Visitor) is
            let use debug_dispose = debug_enter();

            debug("maybe walking variant definition {name}");

            if !visitor.pre(self) then
                debug("walking variant definition {name} name");

                name.walk(visitor);

                if arguments? then
                    debug("walking variant definition {name} arguments");

                    arguments.walk(visitor);
                fi

                debug("walking variant definition {name} modifiers");

                modifiers.walk(visitor);

                debug("walking variant definition {name} fields");

                fields.walk(visitor);

                debug("walking variant definition {name} body");

                body.walk(visitor);
            fi

            debug("accept visitor {visitor.get_type()} for variant definition {name}");            
            accept(visitor);
        si
    si
si
