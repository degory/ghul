namespace Syntax.Trees.Definitions is
    
    use Source;

    class LIST: Definition, Collections.Iterable[Definition]  is
        definitions: Collections.MutableList[Definition];

        iterator: Collections.Iterator[Definition] => definitions.iterator;

        uses: Collections.List[USE] is
            let result = new Collections.LIST[USE](definitions.count);

            for d in definitions do
                let d_without_pragmas = d.without_pragmas;

                if isa USE(d_without_pragmas) then
                    result.add(cast USE(d_without_pragmas));
                fi
            od
            
            return result;
        si
        
        init(location: LOCATION, definitions: Collections.Iterable[Definition]) is
            super.init(location);

            self.definitions = new Collections.LIST[Definition](definitions);
        si

        add(definition: Definition) is
            definitions.add(definition);
        si

        push(definition: Definition) is
            let nd = new Collections.LIST[Definition](definitions.count + 1);

            nd.add(definition);
            nd.add_range(definitions);

            definitions = nd;
        si

        remove(definition: Definition) is
            definitions.remove(definition);
        si

        clear() is
            definitions.clear();
        si

        accept(visitor: Visitor) is
            visitor.visit(self);
        si

        walk(visitor: Visitor) is
            if !visitor.pre(self) then
                let i = 0;

                while i < definitions.count do
                    definitions[i].walk(visitor);
                    
                    i = i + 1;
                od
            fi
            
            accept(visitor);
        si
    si
si
