namespace Syntax.Parsers.Variables is
    
    use Source;

    class LIST: Base[Trees.Variables.LIST] is
        allow_empty: bool;
        variable_parser: Parser[Trees.Variables.Variable];

        init(variable_parser: Parser[Trees.Variables.Variable]) is
            super.init();

            self.variable_parser = variable_parser;
        si

        parse(context: CONTEXT) -> Trees.Variables.LIST is
            let start = context.location;
            let end = context.location;
            let variables = new Collections.LIST[Trees.Variables.Variable]();
            
            if 
                context.current.token == Lexical.TOKEN.IDENTIFIER \/ !allow_empty
            then
                do
                    let variable = variable_parser.parse(context);

                    if variable? then
                        end = variable.location;
                        variables.add(variable);

                        if context.is_end_of_file \/ context.current.token != Lexical.TOKEN.COMMA then
                            break;
                        else
                            context.next_token();
                        fi
                        
                    else
                        end = context.location;
                        break;
                    fi                    
                od
            fi
            
            return new Trees.Variables.LIST(start::end, variables);
        si
    si
si
