namespace Syntax.Parsers.Definitions is
    @IF.not.v3() use Std = System.Console;

    use System;
    use Source;

    class PROPERTY: Base[Trees.Definitions.PROPERTY] is
        identifier_parser: Parser[Trees.Identifiers.Identifier];
        type_parser: Parser[Trees.TypeExpressions.TypeExpression];
        modifier_list_parser: Parser[Trees.Modifiers.LIST];
        definition_list_parser: Parser[Trees.Definitions.LIST];
        body_parser: Parser[Trees.Bodies.Body];

        init(
            identifier_parser: Parser[Trees.Identifiers.Identifier],
            type_parser: Parser[Trees.TypeExpressions.TypeExpression],
            modifier_list_parser: Parser[Trees.Modifiers.LIST],
            definition_list_parser: Parser[Trees.Definitions.LIST],
            body_parser: Parser[Trees.Bodies.Body]
        ) is
            super.init();

            self.identifier_parser = identifier_parser;
            self.type_parser = type_parser;
            self.modifier_list_parser = modifier_list_parser;
            self.definition_list_parser = definition_list_parser;
            self.body_parser = body_parser;
        si

        parse(context: CONTEXT) -> Trees.Definitions.PROPERTY is
            var fail = false;
            var progress = false;

            var start = context.location;
            var name = identifier_parser.parse(context);
            var type_expression: Trees.TypeExpressions.TypeExpression;

            if context.current.token == Lexical.TOKEN.COLON then
                context.next_token();
                progress = true;

                type_expression = type_parser.parse(context);

                if type_expression == null || type_expression.is_poisoned then
                    fail = true;
                fi                
            else
                type_expression = new Trees.TypeExpressions.INFER(context.location);
            fi

            var modifiers = modifier_list_parser.parse(context);
            var read_body: Trees.Bodies.Body;
            var assign_body: Trees.Bodies.Body;
            var setter_argument_name: Trees.Identifiers.Identifier;
            var expect_semicolon = true;

            do                
                if context.current.token == Lexical.TOKEN.ASSIGN then
                    if setter_argument_name? then
                        context.error(context.location, "replacing assign");
                    fi

                    context.next_token();
                    progress = true;

                    setter_argument_name = identifier_parser.parse(context);
                    expect_semicolon = context.current.token == Lexical.TOKEN.ARROW_FAT || context.current.token == Lexical.TOKEN.INNATE;
                    assign_body = body_parser.parse(context);

                    if context.current.token == Lexical.TOKEN.COMMA then
                        context.next_token();
                        progress = true;
                    else
                        if isa Trees.Bodies.NULL(assign_body) then
                            expect_semicolon = true;
                        fi

                        break;
                    fi
                elif 
                    context.current.token == Lexical.TOKEN.IS || 
                    context.current.token == Lexical.TOKEN.ARROW_FAT || 
                    context.current.token == Lexical.TOKEN.INNATE
                then
                    expect_semicolon = context.current.token == Lexical.TOKEN.ARROW_FAT || context.current.token == Lexical.TOKEN.INNATE;

                    if read_body? then
                        context.error(context.location, "replacing read");
                    fi

                    read_body = body_parser.parse(context);

                    if context.current.token == Lexical.TOKEN.COMMA then
                        context.next_token();
                        progress = true;
                    else
                        if isa Trees.Bodies.NULL(read_body) then
                            expect_semicolon = true;
                        else
                            progress = true;
                        fi
                        
                        break;
                    fi
                elif context.current_token == Lexical.TOKEN.SEMICOLON then
                    break;
                elif context.current_token == Lexical.TOKEN.COMMA then
                    if read_body? then
                        context.error(context.location, "replacing read");
                    elif assign_body? then
                        context.error(context.location, "empty read body must precede write body");                        
                    else
                        progress = true;
                        context.next_token();
                        
                        read_body = new Syntax.Trees.Bodies.NULL(context.current.location);
                    fi
                else
                    if !fail then
                        context.error(context.location, "unexpected input in property");
                    fi

                    fail = true;

                    break;
                fi

                expect_semicolon = true;
            od

            if context.in_trait && read_body == null then
                read_body = new Trees.Bodies.NULL(context.location);
            fi

            var result =
                new Trees.Definitions.PROPERTY(
                    start::context.location, 
                    type_expression, 
                    name, 
                    modifiers, 
                    read_body, 
                    setter_argument_name, 
                    assign_body
                );

            if expect_semicolon then
                if !fail || context.current.token == Lexical.TOKEN.SEMICOLON then
                    context.next_token(Lexical.TOKEN.SEMICOLON);
                fi
            fi

            if !fail then
                return result;
            else
                if !progress then
                    Std.error.write_line("parsing property: no progress made so consuming next token: " + context.current_token_name);                    
                    context.next_token();    
                fi
                
                return null;
            fi
        si
    si
si
