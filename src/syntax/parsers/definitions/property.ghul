namespace Syntax.Parsers.Definitions is
    use IO.Std;

    use Source;

    class PROPERTY: Base[Trees.Definitions.PROPERTY] is
        identifier_parser: Parser[Trees.Identifiers.Identifier];
        type_parser: Parser[Trees.TypeExpressions.TypeExpression];
        modifier_list_parser: Parser[Trees.Modifiers.LIST];
        body_parser: Parser[Trees.Bodies.Body];

        init(
            identifier_parser: Parser[Trees.Identifiers.Identifier],
            type_parser: Parser[Trees.TypeExpressions.TypeExpression],
            modifier_list_parser: Parser[Trees.Modifiers.LIST],
            body_parser: Parser[Trees.Bodies.Body]
        ) is
            super.init();

            self.identifier_parser = identifier_parser;
            self.type_parser = type_parser;
            self.modifier_list_parser = modifier_list_parser;
            self.body_parser = body_parser;
        si

        parse(context: CONTEXT) -> Trees.Definitions.PROPERTY is
            let fail = false;
            let progress = false;

            try
                if context.in_classy then
                    context.in_member = true;
                    context.member_indent = context.location.start_column
                else
                    context.in_global_function = true;
                    context.global_indent = context.location.start_column;
                fi
    
                let start = context.location;
                let name = identifier_parser.parse(context);
                let type_expression: Trees.TypeExpressions.TypeExpression;
    
                if context.current.token == Lexical.TOKEN.COLON then
                    context.next_token();
                    progress = true;
    
                    type_expression = type_parser.parse(context);
    
                    if type_expression == null \/ type_expression.is_poisoned then
                        fail = true;
                    fi
                else    
                    type_expression = new Trees.TypeExpressions.INFER(context.location);
                fi
        
                let modifiers = modifier_list_parser.parse(context);
                let read_body: Trees.Bodies.Body;
                let assign_body: Trees.Bodies.Body;
                let setter_argument_name: Trees.Identifiers.Identifier;
                let expect_semicolon = true;
    
                do
                    if context.current.token == Lexical.TOKEN.ASSIGN then
                        if setter_argument_name? then
                            context.error(context.location, "replacing assign");
                        fi
    
                        context.next_token();
                        progress = true;
    
                        setter_argument_name = identifier_parser.parse(context);
                        expect_semicolon = context.current.token == Lexical.TOKEN.ARROW_FAT \/ context.current.token == Lexical.TOKEN.INNATE;

                        try
                            assign_body = body_parser.parse(context);
                        catch ue: UNWIND_TO_MEMBER_EXCEPTION
                            assign_body = new Trees.Bodies.NULL(context.location);
                            fail = true;
                        yrt
        
                        if context.current.token == Lexical.TOKEN.COMMA then
                            context.next_token();
                            progress = true;
                        else
                            if isa Trees.Bodies.NULL(assign_body) then
                                expect_semicolon = true;
                            fi
    
                            break;
                        fi
                    elif 
                        context.current.token == Lexical.TOKEN.IS \/ 
                        context.current.token == Lexical.TOKEN.ARROW_FAT \/ 
                        context.current.token == Lexical.TOKEN.INNATE
                    then
                        expect_semicolon = context.current.token == Lexical.TOKEN.ARROW_FAT \/ context.current.token == Lexical.TOKEN.INNATE;
    
                        if read_body? then
                            context.error(context.location, "replacing read");
                        fi
    
                        try
                            read_body = body_parser.parse(context);
                        catch ue: UNWIND_TO_MEMBER_EXCEPTION
                            read_body = new Trees.Bodies.NULL(context.location);
                            fail = true;
                        yrt
    
                        if context.current.token == Lexical.TOKEN.COMMA then
                            context.next_token();
                            progress = true;
                        else
                            if isa Trees.Bodies.NULL(read_body) then
                                expect_semicolon = true;
                            else
                                progress = true;
                            fi
                            
                            break;
                        fi
                    elif context.current_token == Lexical.TOKEN.SEMICOLON then
                        break;
                    elif context.current_token == Lexical.TOKEN.COMMA then
                        if read_body? then
                            context.error(context.location, "replacing read");
                        elif assign_body? then
                            context.error(context.location, "empty read body must precede write body");                        
                        else
                            progress = true;
                            context.next_token();
                            
                            read_body = new Syntax.Trees.Bodies.NULL(context.current.location);
                        fi
                    else
                        if !fail then
                            context.error(context.location, "syntax error in property: unexpected token {context.current_token_name}");
                        fi
    
                        fail = true;
    
                        break;
                    fi
    
                    expect_semicolon = true;
                od
    
                if context.in_trait /\ read_body == null then
                    read_body = new Trees.Bodies.NULL(context.location);
                fi
    
                let result =
                    new Trees.Definitions.PROPERTY(
                        start::context.location, 
                        type_expression, 
                        name, 
                        modifiers, 
                        read_body, 
                        setter_argument_name, 
                        assign_body
                    );
    
                if expect_semicolon then
                    if !fail \/ context.current.token == Lexical.TOKEN.SEMICOLON then
                        context.next_token(Lexical.TOKEN.SEMICOLON);
                    fi
                fi
    
                if fail /\ !progress then
                    IO.Std.error.write_line("no progress parsing property at {context.location}");
                    IoC.CONTAINER.instance.watchdog.request_restart();
                    throw new System.Exception("no progress parsing property");
                fi
        
                result.poison(fail);
    
                return result;    
            finally                
                context.in_member = false;
            yrt
        si
    si
si
