namespace Syntax.Parsers.Definitions is
    
    use Source;
    use Logging;

    class FUNCTION: Base[Trees.Definitions.FUNCTION] is
        identifier_function_name_parser: Parser[Trees.Identifiers.Identifier];
        type_parser: Parser[Trees.TypeExpressions.TypeExpression];
        type_list_parser: Parser[Trees.TypeExpressions.LIST];
        modifier_list_parser: Parser[Trees.Modifiers.LIST];
        body_parser: Parser[Trees.Bodies.Body];
        variable_list_parser: Parser[Trees.Variables.LIST];

        init(
            identifier_function_name_parser: Parser[Trees.Identifiers.Identifier],
            type_parser: Parser[Trees.TypeExpressions.TypeExpression],
            type_list_parser: Parser[Trees.TypeExpressions.LIST],
            modifier_list_parser: Parser[Trees.Modifiers.LIST],
            body_parser: Parser[Trees.Bodies.Body],
            variable_list_parser: Parser[Trees.Variables.LIST])
        is
            super.init();

            self.identifier_function_name_parser = identifier_function_name_parser;
            self.type_parser = type_parser;
            self.type_list_parser = type_list_parser;
            self.modifier_list_parser = modifier_list_parser;
            self.body_parser = body_parser;
            self.variable_list_parser = variable_list_parser;
        si

        parse(context: CONTEXT) -> Trees.Definitions.FUNCTION is

            // if we get here, we expect lookahead to have already seen
            // and recognized an identifier and either a '(' or a '['
            // so we don't need to speculate here, but once we start to
            // parse the arguments, we do need to speculate, because we
            // might have run off the end of an incomplete function
            // signature into the start of a following function

            if context.in_classy then
                context.in_member = true;
                context.member_indent = context.location.start_column
            else
                context.in_global_function = true;
                context.global_indent = context.location.start_column;
            fi

            let need_backtrack = false;

            try
                debug_always(">> parse function");
                debug_enter();
                
                let is_poisoned = false;
                let start = context.location;

                debug("parse function name");
                let name = identifier_function_name_parser.parse(context);
                debug("have function name {name}");

                let generic_arguments: Syntax.Trees.TypeExpressions.LIST;
    
                if context.current.token == Lexical.TOKEN.SQUARE_OPEN then
                    debug("parse generic arguments");
                    context.next_token();
    
                    generic_arguments = type_list_parser.parse(context);
                    generic_arguments.check_no_reference_types(context.logger);
    
                    is_poisoned = !generic_arguments? \/ generic_arguments.is_poisoned;

                    debug("have generic arguments, poisoned is {is_poisoned}");
                    
                    if !is_poisoned \/ context.current.token == Lexical.TOKEN.SQUARE_CLOSE then
                        debug("expecting close square bracket");
                        is_poisoned = !context.next_token(Lexical.TOKEN.SQUARE_CLOSE) \/ is_poisoned;
                        debug("after maybe square bracket, poisoned is {is_poisoned}");
                    fi

                    debug("after generic arguments poisoned is {is_poisoned}");
                fi

                debug("expect opening parenthesis {context.current.to_short_string()} poisoned is {is_poisoned}");

                let arguments_open_parenthesis_location = context.location;

                context.next_token(Lexical.TOKEN.PAREN_OPEN);

                context.tokenizer_speculate();
                context.logger_speculate();
    
                let arguments: Trees.Variables.LIST;
    
                let any_bad_arguments = false;
    
                if context.current_token != Lexical.TOKEN.PAREN_CLOSE then
                    debug("parse formal arguments");

                    if context.current.token != Lexical.TOKEN.IDENTIFIER then
                        debug("no argument name found");

                        is_poisoned = true;
                    fi
    
                    arguments = variable_list_parser.parse(context);

                    any_bad_arguments = arguments.is_poisoned;

                    debug("have arguments poisoned is {is_poisoned}");

                    let last_valid_argument_line = 0;
    
                    for ai in arguments | .index() do
                        let index = ai.index;
                        let a = ai.value;

                        if !a? \/ a.is_poisoned then
                            debug("argument #{index} is null or poisoned");
                            is_poisoned = true;
                            any_bad_arguments = true;
                        elif isa Trees.TypeExpressions.INFER(a.type_expression) then
                            debug("argument #{index} {a.name} has no type");

                            context.error(a.location, "explicit argument type required");
                            a.poison(true);
                            is_poisoned = true;
                            any_bad_arguments = true;
                        else
                            last_valid_argument_line = a.name.location.start_line;

                            debug("argument #{index} {a.name} has type");
                        fi
                    od

                    debug("after arguments: {arguments.variables.count} {arguments | .map(a => if a? then a.name.to_string() else "null" fi)}");

                    if 
                        context.current_token == Lexical.TOKEN.PAREN_OPEN \/
                        (context.current_token == Lexical.TOKEN.IDENTIFIER /\ context.location.start_line > arguments.location.end_line)
                    then
                        // we might have run off the end of an incomplete function
                        // signature into the start of a following function
                        // signature

                        debug("unexpected parenthesis at {context.location.start_line} arguments at {if arguments? then arguments.location.end_line else 0 fi}");

                        let could_be_next_function = false;

                        if !arguments? \/ arguments.variables.count == 0 then
                            debug("no arguments or bad arguments, followed by '(' - assume next function signature");
                            could_be_next_function = true;
                        elif /* arguments.variables.count == 1 /\ */ any_bad_arguments then
                            debug("bad arguments followed by '(' - assume next function signature");
                            could_be_next_function = true;
                        elif any_bad_arguments /\ context.location.start_line > arguments.location.end_line then
                            debug("bad arguments followed by '(' on next line - assume next function signature");
                            could_be_next_function = true;
                        elif last_valid_argument_line < context.location.start_line then
                            debug("unexpected '(' on line after the last valid argument - assume next function signature");
                            could_be_next_function = true;
                        fi

                        if could_be_next_function then
                            debug("possible incomplete function signature");
                            context.logger.roll_back();

                            for a in arguments do
                                if a? then
                                    a.poison(true);
                                fi
                            od

                            let roll_forwards_to_line = context.location.start_line;

                            context.tokenizer_backtrack();

                            debug("skip tokens from {context.location.start_line} to {roll_forwards_to_line} starting with {context.current.to_short_string()}");

                            while context.location.start_line < roll_forwards_to_line do
                                debug("skip token {context.current.to_short_string()} at {context.location.start_line}");
                                context.next_token();
                                debug("now at {context.location.start_line}");
                            od

                            debug("skipped to {context.location.start_line} current now {context.current.to_short_string()}");
                            debug("after body poisoned is {is_poisoned}");

                            let result =
                                new Trees.Definitions.FUNCTION(
                                    start::name.location, 
                                    name, 
                                    generic_arguments,
                                    arguments,
                                    new Trees.TypeExpressions.INFER(context.location), 
                                    new Trees.Modifiers.LIST(context.location, null, null), 
                                    new Trees.Bodies.NULL(context.location)
                                );

                            context.logger.error(
                                if arguments.variables.count > 0 then 
                                    arguments.location
                                else
                                    arguments_open_parenthesis_location
                                fi,
                                "syntax error: incomplete formal arguments"
                            );

                            return result;
                        fi

                        debug("unexpected parenthesis but probably not a possible incomplete function signature");
                    fi

                    debug("after arguments poisoned is {is_poisoned}");
                fi

                context.tokenizer_commit();
                context.logger_commit();

                if arguments == null then
                    debug("no arguments");
                    arguments = new Trees.Variables.LIST(context.location, new Collections.LIST[Trees.Variables.Variable](0));
                fi
                
                // TODO: could recognize that no closing parenthesis followed by a new line then 
                // an identifier and an opening paren is probably another function signature 

                if !is_poisoned \/ context.current.token == Lexical.TOKEN.PAREN_CLOSE then
                    debug("expecting close parenthesis");
                    is_poisoned = !context.next_token(Lexical.TOKEN.PAREN_CLOSE) \/ is_poisoned;
                    debug("after maybe close parenthesis, poisoned is {is_poisoned}");
                fi
    
                let type_expression: Trees.TypeExpressions.TypeExpression;

                if context.current.token == Lexical.TOKEN.ARROW_THIN then
                    let arrow_location = context.location;
                    debug("parse return type");
                    context.next_token();

                    context.tokenizer_speculate();

                    type_expression = type_parser.parse(context);
                    type_expression.check_is_not_reference(context.logger, "function cannot return a reference");

                    is_poisoned = is_poisoned \/ !type_expression? \/ type_expression.is_poisoned;

                    debug("after return type poisoned is {is_poisoned}");

                    if context.current_token == Lexical.TOKEN.PAREN_OPEN then
                        // we might have run off the end of an incomplete function
                        // signature into the start of a following function
                        // signature

                        debug("unexpected parenthesis at {context.location.start_line}");
    
                        // assume next function signature

                        context.tokenizer_backtrack();
    
                        let result =
                            new Trees.Definitions.FUNCTION(
                                start::name.location, 
                                name, 
                                generic_arguments,
                                arguments, 
                                new Trees.TypeExpressions.INFER(context.location), 
                                new Trees.Modifiers.LIST(context.location, null, null), 
                                new Trees.Bodies.NULL(context.location)
                            );

                        context.logger.error(arrow_location, "syntax error: incomplete return type");

                        return result;
                    fi

                    context.tokenizer_commit();
                else
                    type_expression = new Trees.TypeExpressions.INFER(context.location);
                fi

                debug("parse modifiers, next token /{context.current.to_short_string()}/ poisoned is {is_poisoned}");
                let modifiers = modifier_list_parser.parse(context);
                let expect_semicolon = context.current.token != Lexical.TOKEN.IS;

                let body: Trees.Bodies.Body;

                try 
                    debug("parse body, next token /{context.current.to_short_string()}/ poisoned is {is_poisoned}");

                    body = body_parser.parse(context)
                catch ue: UNWIND_TO_MEMBER_EXCEPTION
                    debug("caught unwind to member");
                    body = new Trees.Bodies.NULL(context.location);
                yrt

                debug("after body poisoned is {is_poisoned}");

                let result =
                    new Trees.Definitions.FUNCTION(
                        start::body.location, 
                        name, 
                        generic_arguments,
                        arguments, 
                        type_expression, 
                        modifiers, 
                        body
                    );
    
                if expect_semicolon then
                    debug("expecting semicolon {context.current.to_short_string()} poisoned is {is_poisoned}");
                    if !is_poisoned \/ context.current.token == Lexical.TOKEN.SEMICOLON then
                        debug("expecting semicolon...");
                        is_poisoned = !context.next_token(Lexical.TOKEN.SEMICOLON) \/ is_poisoned;
                        debug("after maybe semicolon poisoned is {is_poisoned}");
                    fi
                fi

                debug("about to return poisoned is {is_poisoned}");

                result.poison(is_poisoned);
    
                return result;
    
            finally
                if context.in_classy then
                    context.in_member = false;
                else
                    context.in_global_function = false;
                fi

                debug_exit();
                debug_always("<< parse function");
            yrt
        si
    si
si
