namespace Syntax.Parsers.Statements is
    use System.Exception;

    use IO.Std;
    
    use Source;

    class LIST : Base[Trees.Statements.LIST]  is
        terminators: Collections.List[Lexical.TOKEN];
        statement_parser: Parser[Trees.Statements.Statement];
        description: string => "statement list";

        init(terminators: Collections.LIST[Lexical.TOKEN], statement_parser: Parser[Trees.Statements.Statement]) is
            super.init();
            
            self.terminators = terminators;
            self.statement_parser = statement_parser;
        si

        parse(context: CONTEXT) -> Trees.Statements.LIST is
            self.terminators = terminators;

            let start = context.location;
            let end = context.location;
            let statements = new Collections.LIST[Trees.Statements.Statement]();

            while !context.is_end_of_file /\ !at_terminator(context) do
                if context.current.token == Lexical.TOKEN.SEMICOLON then
                    IoC.CONTAINER.instance.logger.warn(context.current.location, "empty statement");
                    context.next_token();
                else                
                    try
                        let statement = statement_parser.parse(context);
                        
                        if statement? then
                            statements.add(statement);                           
                        fi

                        end = statement.location;
                    catch e: Exception
                        end = context.current.location;

                        let is_current = (t2: Lexical.TOKEN) => t2 == context.current_token;

                        // FIXME: putting is_current directly within the while condition results in an empty body being emitted
                        while !terminators | .find(is_current).has_value do
                            end = context.current.location;
                            
                            context.next_token();
                        od 
                    yrt
                fi
            od

            return new Trees.Statements.LIST(start::end, statements);
        si

        at_terminator(context: CONTEXT) -> bool => terminators | .find(t1 => t1 == context.current_token).has_value;
    si
si
