namespace Syntax.Parsers.TypeExpressions is
    use IO.Std;

    
    use Source;

    class TYPE_EXPRESSION: Base[Trees.TypeExpressions.TypeExpression] is
        identifier_qualified_parser: Parser[Trees.Identifiers.Identifier];
        type_list_parser: Parser[Trees.TypeExpressions.LIST];
        description: string => "type expression";

        init(
            identifier_qualified_parser: Parser[Trees.Identifiers.Identifier],
            type_list_parser: Parser[Trees.TypeExpressions.LIST]
        )
        is
            super.init();

            self.identifier_qualified_parser = identifier_qualified_parser;
            self.type_list_parser = type_list_parser;

            add_parsers();
        si

        add_parsers() is
            add_parser(
                (context: CONTEXT) -> Trees.TypeExpressions.TypeExpression is
                    var start = context.location;
                    var identifier = identifier_qualified_parser.parse(context);

                    if identifier.is_poisoned then
                        if context.current.token == Lexical.TOKEN.IN then
                            context.next_token();
                        fi

                        let result =
                            new Trees.TypeExpressions.NAMED(identifier.location, identifier);

                        result.poison();

                        return result;
                    fi

                    if
                        !isa Trees.Identifiers.QUALIFIED(identifier) /\
                        context.current.token==Lexical.TOKEN.COLON
                    then
                        context.next_token();
                        
                        let type_expression = parse(context);
    
                        return new Trees.TypeExpressions.NAMED_TUPLE_ELEMENT(start::type_expression.location, identifier, type_expression);
                    fi

                    if context.current.token == Lexical.TOKEN.SQUARE_OPEN then
                        context.next_token();

                        let types = type_list_parser.parse(context);

                        let poison = types.is_poisoned \/ identifier.is_poisoned;

                        let result = new Trees.TypeExpressions.GENERIC(start::context.location, identifier, types);

                        if !poison \/ context.current.token == Lexical.TOKEN.SQUARE_CLOSE then
                            poison = !context.next_token(Lexical.TOKEN.SQUARE_CLOSE, syntax_error_message) \/ poison;
                        fi

                        result.poison(poison);

                        return result;
                    else
                        return new Trees.TypeExpressions.NAMED(identifier.location, identifier);
                    fi
                si,
                Lexical.TOKEN.IDENTIFIER
            );

            add_parser(
                (context: CONTEXT) -> Trees.TypeExpressions.TypeExpression is
                    var start = context.location;
                    context.next_token();
                    var types: Trees.TypeExpressions.LIST;
                    if context.current.token != Lexical.TOKEN.PAREN_CLOSE then
                        types = type_list_parser.parse(context);
                    else
                        types = new Trees.TypeExpressions.LIST(start, new Collections.LIST[Trees.TypeExpressions.TypeExpression](0));
                    fi
                    var end = context.location;
                    context.next_token(Lexical.TOKEN.PAREN_CLOSE, syntax_error_message);
                    if context.current.token == Lexical.TOKEN.ARROW_THIN then
                        context.next_token();
                        var result = parse(context);
                        end = result.location;
                        return new Trees.TypeExpressions.FUNCTION(start::end, types, result);
                    else
                        if
                            types.elements.count == 0
                        then
                            return new Trees.TypeExpressions.UNDEFINED(start::end);
                        else
                            return new Trees.TypeExpressions.TUPLE(start::end, types);
                        fi
                    fi
                si,
                Lexical.TOKEN.PAREN_OPEN
            );
        si

        parse(context: CONTEXT) -> Trees.TypeExpressions.TypeExpression is
            let inner = super.parse(context);

            let result = parse_structured(context, inner);

            return result;
        si

        parse_structured(
            context: CONTEXT,
            element: Trees.TypeExpressions.TypeExpression
        ) -> Trees.TypeExpressions.TypeExpression
        is
            var result = element;

            do
                let token = context.current.token;

                case context.current.token
                when Lexical.TOKEN.ARRAY_DEF:
                    result = new Trees.TypeExpressions.ARRAY_(result.location::context.location, result);
                    context.next_token();

                when Lexical.TOKEN.REF:
                    result = new Trees.TypeExpressions.REFERENCE(result.location::context.location, result);
                    context.next_token();

                when Lexical.TOKEN.PTR:
                    result = new Trees.TypeExpressions.POINTER(result.location::context.location, result);
                    context.next_token();

                when Lexical.TOKEN.ARROW_THIN:
                    context.next_token();

                    let return_type = parse(context);

                    let arguments = new Trees.TypeExpressions.LIST(
                        element.location,
                        new Collections.LIST[Trees.TypeExpressions.TypeExpression]([element])
                    );

                    result = new Trees.TypeExpressions.FUNCTION(result.location::return_type.location, arguments, return_type);

                default
                    return result;

                esac
            od
        si

        other_token(context: CONTEXT) -> Trees.TypeExpressions.TypeExpression is
            var should_skip_next_token = false;

            if context.current.token == Lexical.TOKEN.IN then
                should_skip_next_token = true;
            fi

            super.other_token(context);

            if should_skip_next_token then
                context.next_token();
            fi

            return new Trees.TypeExpressions.UNDEFINED(context.location);
        si
    si
si
