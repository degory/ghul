namespace Syntax.Parsers.TypeExpressions is
    
    class LIST: Base[Trees.TypeExpressions.LIST] is
        type_parser: Parser[Trees.TypeExpressions.TypeExpression];

        init(type_parser: Parser[Trees.TypeExpressions.TypeExpression]) is
            super.init();

            self.type_parser = type_parser;
        si

        parse(context: CONTEXT) -> Trees.TypeExpressions.LIST is
            var elements = new Collections.LIST[Trees.TypeExpressions.TypeExpression]();
            var start = context.location;
            var end = context.location;

            do
                var element = type_parser.parse(context);

                if element == null then
                    context.error(context.location, "bad type, bombing out");
                    break;
                fi

                elements.add(element);
                end = element.location;

                if context.is_end_of_file || context.current.token != Lexical.TOKEN.COMMA then
                    break;
                fi

                context.next_token();
            od

            return new Trees.TypeExpressions.LIST(start::end, elements);
        si
    si
si
