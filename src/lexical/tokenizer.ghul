namespace Lexical is
    use System;
    use Generic;

    use Logging;
    use Source;

    class TOKEN_BUFFER  is
        values: Collections.QUEUE[TOKEN_PAIR];

        init() is
            values = new Collections.QUEUE[TOKEN_PAIR](10);
        si

        reset() is
            init();
        si

        avail: bool => values.count > 0;

        write(t: TOKEN_PAIR) is
            values.enqueue(t);
        si

        write(tokens: Collections.Iterable[TOKEN_PAIR]) is
            for t in tokens do
                values.enqueue(t);                
            od
        si

        read() -> TOKEN_PAIR is
            return values.dequeue();
        si
    si

    class TOKENIZER_EXCEPTION: Exception  is
        init(s: String) is
            super.init(s);
        si
    si

    class TOKEN_MAP  is
        map: Collections.MAP[String,TOKEN];

        init() is
            super.init();
            map = new Collections.MAP[String,TOKEN](223);
        si

        [s: String]: TOKEN public
            is
                var t = map[s];
                if t != cast TOKEN(0) then
                    return t;
                else
                    return TOKEN.IDENTIFIER;
                fi
            si,
            = t is
                map[s] = t;
            si

    si

    class TOKEN_PAIR  is
        token: TOKEN;
        location: LOCATION;
        string: String;

        name: String => TOKEN_NAMES[token];

        toString() -> String => 
            "" + location + ": " + TOKEN_NAMES[token] + ": " + string;

        init(token: TOKEN, location: LOCATION, string: String) is
            super.init();
            self.token = token;
            self.location = location;
            self.string = string;
        si
    si

    class TOKENIZER  is
        logger: Logger;
        _token_pair: TOKEN_PAIR;
        symbol_tokens: TOKEN_MAP static;
        operator_chars: Collections.SET[char] static;
        operator_tokens: Collections.MAP[String,TOKEN] static;
        buffer: TOKEN_BUFFER;
        input: IO2.TextReader;
        end_of_file: bool;
        _prev_char: char;
        cursor: LOCATION_CURSOR;
        _token_name: String[];

        static_init() static is 
            if symbol_tokens == null then
                operator_chars = new Collections.SET[char]([
                    '!', '$', '%', '^', '&', '*', '-', '+', '=', '|', ':', '@', '~', '#', '\\', '<', '>', '.', '?', '/'
                ]: char );

                operator_tokens = new Collections.MAP[String,TOKEN]();
                operator_tokens["="] = TOKEN.ASSIGN;
                operator_tokens[":"] = TOKEN.COLON;
                operator_tokens["."] = TOKEN.DOT;
                operator_tokens["->"] = TOKEN.ARROW_THIN;
                operator_tokens["=>"] = TOKEN.ARROW_FAT;
                operator_tokens["?"] = TOKEN.QUESTION;
                operator_tokens["@"] = TOKEN.AT;
                symbol_tokens = new TOKEN_MAP();
                symbol_tokens["import"] = TOKEN.IMPORT;
                symbol_tokens["namespace"] = TOKEN.NAMESPACE;
                symbol_tokens["class"] = TOKEN.CLASS;
                symbol_tokens["struct"] = TOKEN.STRUCT;
                symbol_tokens["enum"] = TOKEN.ENUM;
                symbol_tokens["public"] = TOKEN.PUBLIC;
                symbol_tokens["protected"] = TOKEN.PROTECTED;
                symbol_tokens["private"] = TOKEN.PRIVATE;
                symbol_tokens["const"] = TOKEN.CONST;
                symbol_tokens["static"] = TOKEN.STATIC;
                symbol_tokens["native"] = TOKEN.NATIVE;
                symbol_tokens["innate"] = TOKEN.INNATE;
                symbol_tokens["if"] = TOKEN.IF;
                symbol_tokens["else"] = TOKEN.ELSE;
                symbol_tokens["while"] = TOKEN.WHILE;
                symbol_tokens["do"] = TOKEN.DO;
                symbol_tokens["for"] = TOKEN.FOR;
                symbol_tokens["in"] = TOKEN.IN;
                symbol_tokens["case"] = TOKEN.CASE;
                symbol_tokens["when"] = TOKEN.WHEN;
                symbol_tokens["default"] = TOKEN.DEFAULT;
                symbol_tokens["break"] = TOKEN.BREAK;
                symbol_tokens["continue"] = TOKEN.CONTINUE;
                symbol_tokens["ref"] = TOKEN.REF;
                symbol_tokens["ptr"] = TOKEN.PTR;
                symbol_tokens["new"] = TOKEN.NEW;
                symbol_tokens["throw"] = TOKEN.THROW;
                symbol_tokens["return"] = TOKEN.RETURN;
                symbol_tokens["cast"] = TOKEN.CAST;
                symbol_tokens["var"] = TOKEN.VAR;
                symbol_tokens["try"] = TOKEN.TRY;
                symbol_tokens["let"] = TOKEN.LET;
                symbol_tokens["catch"] = TOKEN.CATCH;
                symbol_tokens["finally"] = TOKEN.FINALLY;
                symbol_tokens["self"] = TOKEN.SELF;
                symbol_tokens["super"] = TOKEN.SUPER;
                symbol_tokens["null"] = TOKEN.NULL;
                symbol_tokens["use"] = TOKEN.USE;
                symbol_tokens["trait"] = TOKEN.TRAIT;
                symbol_tokens["isa"] = TOKEN.ISA;
                symbol_tokens["operator"] = TOKEN.OPERATOR;
                symbol_tokens["is"] = TOKEN.IS;
                symbol_tokens["si"] = TOKEN.SI;
                symbol_tokens["then"] = TOKEN.THEN;
                symbol_tokens["elif"] = TOKEN.ELIF;
                symbol_tokens["fi"] = TOKEN.FI;
                symbol_tokens["esac"] = TOKEN.ESAC;
                symbol_tokens["od"] = TOKEN.OD;
                symbol_tokens["yrt"] = TOKEN.YRT;
                symbol_tokens["true"] = TOKEN.TRUE;
                symbol_tokens["false"] = TOKEN.FALSE;
                symbol_tokens["_assert"] = TOKEN.ASSERT;
                symbol_tokens["assert"] = TOKEN.ASSERT;
            fi
        si

        init(logger: Logger, file_name: String, i: IO2.TextReader) is            
            super.init();

            self.logger = logger;

            buffer = new TOKEN_BUFFER();
            end_of_file = false;
            input = i;
            static_init();
            cursor = new LOCATION_CURSOR(file_name);
            _token_pair = new TOKEN_PAIR(TOKEN.UNKNOWN, location, null);
        si

        is_end_of_file: bool => end_of_file;

        location: LOCATION => cursor.location;

        advance_cursor(c: char) is
            cursor.next_column();

            if c == '\n' then
                cursor.next_line();
            fi
        si

        next_char() -> char is
            var c: char;

            if _prev_char != cast char(0) then
                c = _prev_char;
                advance_cursor(c);
                _prev_char = cast char(0);
                return c;
            fi

            var c0: int = input.read();

            if c0 == -1 || c0 == 12 then
                end_of_file = true;
                return ' ';
            fi

            c = cast char(c0);

            cursor.save();
            advance_cursor(c);

            return c;
        si

        prev_char(c: char) is
            cursor.restore();
            _prev_char = c;
        si

        current_string: String is
            return _token_pair.string;
        si

        read_escape() -> char is
            var c: char = next_char();
            var result: int = 0;
            if c == 't' then
                return cast char(9);
            elif c == 'n' then
                return '\n';
            elif c == 'r' then
                return cast char(13);
            elif c == '\\' then
                return '\\';
            elif c>='0' && c<='7' then
                while c >= '0' && c <= '7' do
                    result = 8 * result + cast int(c - '0');
                    c = next_char();
                od
                prev_char(c);
                return cast char(result);
            else
                return c;
            fi
        si

        current: TOKEN_PAIR => _token_pair;

        next() -> bool is
            _token_pair = read_token();
            return current.token != TOKEN.END_OF_INPUT;
        si

        skip_white_space() -> char is
            var c: char;
            do
                c = next_char();
                if !(!end_of_file&&(
                c==' '||c==cast char(9)||c=='\n')) then
                    break;
                fi
            od
            return c;
        si

        write_token(token: TOKEN_PAIR) is
            buffer.write(token);
        si

        write_tokens(tokens: Collections.Iterable[TOKEN_PAIR]) is
            buffer.write(tokens);
        si

        read_token() -> TOKEN_PAIR is
            if buffer.avail then
                return buffer.read();
            fi

            return read_token_2(false);
        si

        read_operator(c: char) -> TOKEN_PAIR is
            var buffer = new System.Text.StringBuilder();

            while operator_chars.contains(c) do
                buffer.append(c);
                c = next_char();
            od

            prev_char(c);

            if buffer.length < 1 then
                return new TOKEN_PAIR(TOKEN.FIRST, location, null);
            fi

            let s = buffer.to_string();

            var r = operator_tokens[s];

            if r == TOKEN.FIRST then
                r = TOKEN.OPERATOR;
            fi

            return new TOKEN_PAIR(r, location, s);
        si

        read_token_2(for_comment: bool) -> TOKEN_PAIR is
            var r: TOKEN;

            var c = skip_white_space();

            if end_of_file then
                return new TOKEN_PAIR(TOKEN.END_OF_INPUT, location, null);
            fi

            cursor.start();

            var buffer: System.Text.StringBuilder = null;

            if c>='0' && c<='9' then
                buffer = new System.Text.StringBuilder();
                buffer.append(c);
                c = next_char();

                if c=='x' || c=='X' then
                    buffer.append(c);
                    c = next_char();
                    while 
                    (c>='0'&&c<='9') || (c>='A'&&c<='F') || (c>='a'&&c<='f') || c=='x' || c=='w' || c=='W' || c=='l' || c=='L' do
                        buffer.append(c);
                        c = next_char();
                    od
                else
                    while 
                    (c>='0'&&c<='9') || c=='x' || c=='w' || c=='W' || c=='l' || c=='L' || c=='c' || c=='C' do
                        buffer.append(c);
                        c = next_char();
                    od
                fi

                prev_char(c);
                return new TOKEN_PAIR(TOKEN.INT_LITERAL, location, buffer.to_string());
            elif (c>='a'&&c<='z') || (c>='A'&&c<='Z') || c=='_' then
                buffer = new System.Text.StringBuilder();

                while (c>='a'&&c<='z') || (c>='A'&&c<='Z') || (c>='0'&&c<='9') || c=='_' do
                    buffer.append(c);
                    c = next_char();
                od

                prev_char(c);
                let s = buffer.to_string();
                r = symbol_tokens[s];
                return new TOKEN_PAIR(r, location, s);
            elif c == '/' then
                c = next_char();
                if c == '/' then
                    buffer = new System.Text.StringBuilder("//");
                    do
                        c = next_char();
                        if c != '\n' then
                            buffer.append(c);
                        fi
                        if !(!end_of_file&&c!='\n') then
                            break;
                        fi
                    od

                    if Shim.STR.starts_with(buffer.to_string(),"///") then
                        buffer.append("\n");
                        return read_token_2(true);
                    else
                        return read_token_2(false);
                    fi
                elif c == '*' then
                    buffer = new System.Text.StringBuilder("/*");
                    do
                        c = next_char();
                        if c == '*' then
                            c = next_char();
                            if c == '/' then
                                break;
                            fi
                            buffer.append('*');
                        fi

                        buffer.append(c);

                        if end_of_file then
                            break;
                        fi
                    od

                    if Shim.STR.starts_with(buffer.to_string(),"/**") then
                        buffer.append("*/");
                        return read_token_2(true);
                    else
                        return read_token_2(false);
                    fi
                else
                    prev_char(c);
                    return read_operator('/');
                fi
            elif operator_chars.contains(c) then
                return read_operator(c);
            fi

            case c
            when cast char(34):
                buffer = new System.Text.StringBuilder();
                c = next_char();
                while c != cast char(34) do
                    if c == cast char(92) then
                        c = read_escape();
                        buffer.append(c);
                        c = next_char();
                    elif c == '\n' then
                        logger.error(location, "newline in string literal");
                        c = next_char();
                        break;
                    else
                        buffer.append(c);
                        c = next_char();
                    fi
                    if end_of_file then
                        logger.error(location, "end of file in string literal");
                        break;
                    fi
                od
                return new TOKEN_PAIR(TOKEN.STRING_LITERAL, location, buffer.to_string());
                
            when cast char(39):
                buffer = new System.Text.StringBuilder();

                c = next_char();
                while c != cast char(39) do
                    if c == cast char(92) then
                        c = read_escape();
                        buffer.append(c);
                        c = next_char();
                    else
                        buffer.append(c);
                        c = next_char();
                    fi
                    if end_of_file then
                        logger.error(location, "end of file in character literal");
                        break;
                    fi
                od

                if buffer.length < 1 then
                    logger.error(location, "zero length character literal");
                elif buffer.length > 1 then
                    logger.error(location, "character literal is too long");
                fi
                return new TOKEN_PAIR(TOKEN.CHAR_LITERAL, location, buffer.to_string());

            when '`':
                buffer = new System.Text.StringBuilder();
                c = next_char();
                while c != '`' do
                    if c == cast char(92) then
                        c = read_escape();
                        buffer.append(c);
                        c = next_char();
                    else
                        buffer.append(c);
                        c = next_char();
                    fi
                    if end_of_file then
                        logger.error(location, "end of file in char ptr literal");
                        break;
                    fi
                od
                return new TOKEN_PAIR(TOKEN.CSTRING_LITERAL, location, buffer.to_string());

            when '(':
                return new TOKEN_PAIR(TOKEN.PAREN_OPEN, location, null);

            when ')':
                return new TOKEN_PAIR(TOKEN.PAREN_CLOSE, location, null);

            when '[':
                c = skip_white_space();
                if end_of_file then
                    return new TOKEN_PAIR(TOKEN.SQUARE_OPEN, location, null);
                elif c == ']' then
                    return new TOKEN_PAIR(TOKEN.ARRAY_DEF, location, null);
                else
                    prev_char(c);
                    return new TOKEN_PAIR(TOKEN.SQUARE_OPEN, location, null);
                fi

            when ']':
                return new TOKEN_PAIR(TOKEN.SQUARE_CLOSE, location, null);

            when ',':
                return new TOKEN_PAIR(TOKEN.COMMA, location, null);

            when ';':
                return new TOKEN_PAIR(TOKEN.SEMICOLON, location, null);
            
            esac
            
            return new TOKEN_PAIR(TOKEN.UNKNOWN, location, "" + c);
        si
    si
si
