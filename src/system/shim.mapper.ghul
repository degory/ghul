namespace Shim is
    @IF.not.v3() use object = System.Object;
    @IF.not.v3() use string = System.String;
    @IF.not.v3() use Std = System.Console;

    use Collections.Iterable;
    use Collections.Iterator;

    class MAPPER[F,T]: object,Iterable[T] is
        iterable: Iterable[F];
        mapper: (F) -> T;

        iterator: Iterator[T] => new MAPPER_ITERATOR[F,T](iterable.iterator, mapper);
        
        init(iterable: Iterable[F], mapper: (F) -> T) is
            assert iterable? else "MAPPER iterable is null";
            assert mapper? else "MAPPER mapper function is null";

            self.iterable = iterable;
            self.mapper = mapper;
        si
    si

    class MAPPER_ITERATOR[F,T]: object,Iterator[T] is
        _iterator: Iterator[F];
        _mapper: (F) -> T;

        @IL.name.read("get_Current")
        current: T => _mapper(_iterator.current);

        init(iterator: Iterator[F], mapper: (F) -> T) is
            _iterator = iterator;
            _mapper = mapper;
        si
        
        @IL.name("MoveNext")
        move_next() -> bool => _iterator.move_next();
    si
si
